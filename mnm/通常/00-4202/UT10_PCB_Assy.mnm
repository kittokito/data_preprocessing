DEVICE:53
;MODULE:UT10_PCB_Assy
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD X124A                 ; 基板嵌合RBﾁｬｯｸ閉端
TMR K1010 K5             ; 基板嵌合RBﾁｬｯｸ閉端
LD X124B                 ; 基板嵌合RBﾁｬｯｸ開端
TMR K1011 K3             ; 基板嵌合RBﾁｬｯｸ開端
;■ロボット等機器入力受け
;ｼﾌﾄﾛﾎﾞｯﾄ現在値
;Sift_RB1_Current_Position
LD X362A                 ; Sift_RB1_HEND
AND X362B                ; Sift_RB1_PEND
MPS
AND X3620                ; Sift_RB1_PM1
OUT D5528.0              ; Sift_RB1_POS1
MRD
AND X3621                ; Sift_RB1_PM2
OUT D5528.1              ; Sift_RB1_POS2
MRD
AND X3622                ; Sift_RB1_PM4
OUT D5528.2              ; Sift_RB1_POS4
MRD
AND X3623                ; Sift_RB1_PM8
OUT D5528.3              ; Sift_RB1_POS8
MRD
AND X3624                ; Sift_RB1_PM16
OUT D5528.4              ; Sift_RB1_POS16
MRD
AND X3625                ; Sift_RB1_PM32
OUT D5528.5              ; Sift_RB1_POS32
MRD
AND X3626                ; Sift_RB1_PM64
OUT D5528.6              ; Sift_RB1_POS64
MRD
AND X3627                ; Sift_RB1_PM128
OUT D5528.7              ; Sift_RB1_POS128
MPP
INV
MOV K0 D5528             ; Sift_RB1_完了POS
LD= D5528 D5526          ; Sift_RB1_完了POS  Sift_RB1_指令POS
MPS
LD>= D5526 K101          ; Sift_RB1_指令POS
AND<= D5526 K132         ; Sift_RB1_指令POS
ANL
OUT M5920                ; 基板嵌合ｼﾌﾄRB取出端
MPP
LD>= D5526 K151          ; Sift_RB1_指令POS
AND<= D5526 K182         ; Sift_RB1_指令POS
ANL
OUT M5921                ; 基板嵌合ｼﾌﾄRB嵌合端
;基板嵌合機種No
;PCB_Assy_RB_Type_No
LD CR2002                ; 常時ON
MPS
AND X3578                ; 機種選択1Ans
OUT D5551.0              ; 基板嵌合RB機種ID_Ans1
MRD
AND X3579                ; 機種選択2Ans
OUT D5551.1              ; 基板嵌合RB機種ID_Ans2
MRD
AND X357A                ; 機種選択4Ans
OUT D5551.2              ; 基板嵌合RB機種ID_Ans4
MRD
AND X357B                ; 機種選択8Ans
OUT D5551.3              ; 基板嵌合RB機種ID_Ans8
MPP
AND X357C                ; 機種選択16Ans
OUT D5551.4              ; 基板嵌合RB機種ID_Ans16
;[] ｾｯﾄ治具ID
;Set JIG ID
LD CR2002                ; 常時ON
MPS
AND X1271
OUT W0128.0              ; 基板嵌合治具_ID_0
MRD
AND X1272
OUT W0128.1              ; 基板嵌合治具_ID_1
MPP
AND X1273
OUT W0128.2              ; 基板嵌合治具_ID_2
;□ ﾕﾆｯﾄ原点
LD X124B                 ; 基板嵌合RBﾁｬｯｸ開端
OR M5756                 ; 基板嵌合原点完了　ﾜｰｸ残ﾁｪｯｸ
LD X3562                 ; 基板嵌合RB原点(42)
OR X3558                 ; 作業原点復帰完了
ANL
ANB X3540                ; Sup_RB1 EMG
AND X3541                ; Sup_RB1_CPU-OK
AND X3542                ; Sup_RB1_SV-ON
AND X3549                ; Sup_RB1_ORG
AND X354B                ; Sup_RB1_RUN
AND X361B                ; Sup_RB1_Remote_Ready
OUT M5980                ; ﾕﾆｯﾄ10原点
;□ 非常停止
LDB CR2003               ; 常時OFF
INV
OUT M5981                ; ﾕﾆｯﾄ10全異常
;□ 起動条件
LD M5980                 ; ﾕﾆｯﾄ10原点
OUT M5982                ; ﾕﾆｯﾄ10起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
OR M5986                 ; ﾕﾆｯﾄ10ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M5983                ; ﾕﾆｯﾄ10起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
LDB M82                  ; 原点復帰開始
AND CR2003               ; 常時OFF
ORL
ANB M5984                ; ﾕﾆｯﾄ10原点復帰中
OR M5984                 ; ﾕﾆｯﾄ10原点復帰中
ANB M5983                ; ﾕﾆｯﾄ10起動OFF
ANB M5799                ; ﾕﾆｯﾄ10原点終了
OUT M5984                ; ﾕﾆｯﾄ10原点復帰中
;□ 起動中
LD M73                   ; 各工程起動指令
AND M3103                ; 基板嵌合作業開始
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
AND CR2003               ; 常時OFF
ORL
AND M5982                ; ﾕﾆｯﾄ10起動条件
OR M5985                 ; ﾕﾆｯﾄ10起動中
ANB M5983                ; ﾕﾆｯﾄ10起動OFF
ANB M5599                ; ﾕﾆｯﾄ10ｻｲｸﾙ終了
OUT M5985                ; ﾕﾆｯﾄ10起動中
;□ ｻｲｸﾙ停止異常
LDB B0140                ; 基板嵌合RB異常
ANB B0141                ; 基板嵌合RBﾜｰｸ紛失異常
ANB B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
ANB B0144                ; 基板嵌合ｼﾌﾄRB異常
ANB B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
INV
OUT M5986                ; ﾕﾆｯﾄ10ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M5987                ; ﾕﾆｯﾄ10払出停止異常
;□ 警報補助
LDB M5509                ; 照合NG発生中
ANB M5510                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ発生中
ANB M5511                ; 照合部PCBﾘｰﾄﾞｴﾗｰ発生中
ANB M5523                ; 塗布検査NG発生中
LDB B0147                ; 基板取出時ﾁｬｯｸNG発生中
OR B0647
ANL
ANB M5581                ; PCB除材要求中
INV
OUT M5988                ; ﾕﾆｯﾄ10警報補助
;□ 機器準備確認
LD X3541                 ; Sup_RB1_CPU-OK
AND X3542                ; Sup_RB1_SV-ON
AND X3549                ; Sup_RB1_ORG
AND X362C                ; Sift_RB1_SV
OUT M5990                ; ﾕﾆｯﾄ10機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB B0143                ; 基板嵌合RBﾁｬｯｸ異常
INV
OUT M5991                ; ﾕﾆｯﾄ10ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LDB M5985                ; ﾕﾆｯﾄ10起動中
ANB M5984                ; ﾕﾆｯﾄ10原点復帰中
LDB M5501                ; ﾌﾚｰﾑ有動作開始
ANB M5509                ; 照合NG発生中
ANB M5510                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ発生中
ANB M5511                ; 照合部PCBﾘｰﾄﾞｴﾗｰ発生中
ANB M5530                ; 塗布検査結果OK受信確認
ANB M5535                ; 照合･検査OK基板取出可(RBへ)
ANB M5537                ; 取出時ﾁｬｯｸNG確認
INV
AND M130                 ; 全体PAUSE
ORL
TMR K1001 K20            ; ﾕﾆｯﾄ10動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D5500 D5505          ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ10動作中判定
LD T1001                 ; ﾕﾆｯﾄ10動作無し判定ﾀｲﾏ
OUT M5992                ; ﾕﾆｯﾄ10動作無し確認
;□ 払出しﾜｰｸ無し
LD CR2002                ; 常時ON
MPS
WSUM D16300 K80          ; UT10ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
CON
STA D5540                ; 基板嵌合ﾜｰｸ情報(ﾜｰｸ有無)
MPP
LD= D5540 K0             ; 基板嵌合ﾜｰｸ情報(ﾜｰｸ有無)
ANB X1242                ; 基板嵌合ﾜｰｸ確認
ANL
TMR K1005 K10            ; UNIT10払出処理ﾜｰｸ無し
LD T1005                 ; UNIT10払出処理ﾜｰｸ無し
OUT M5993                ; ﾕﾆｯﾄ10ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M5979                ; ﾕﾆｯﾄ10ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　 　UNIT10自動回路 　　□
;□□□□□□□□□□□□□□□
;<h1/>■自動回路 
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M5500 M5599         ; 基板嵌合開始  ﾕﾆｯﾄ10ｻｲｸﾙ終了
LDB M5985                ; ﾕﾆｯﾄ10起動中
MOV K0 D5500             ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
DW K0 D5700              ; UT10 main STEP monitor
LD M5985                 ; ﾕﾆｯﾄ10起動中
MOV D5500 Z1             ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
SET M5500:Z1             ; 基板嵌合開始
CAL+ D5500 K5500 D5700   ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ  UT10 main STEP monitor
;自動運転ステップ開始
;ﾌﾚｰﾑ有無判定
;ﾌﾚｰﾑが無い場合は作業完了処理
LD M5500                 ; 基板嵌合開始
MPS
LD X126A                 ; CN塗布検査部ﾜｰｸ確認
AND<> D16313 K0          ; UT10CN嵌合ﾛﾎﾞｯﾄNo.
ANL
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LDB X126A                ; CN塗布検査部ﾜｰｸ確認
ANB X1242                ; 基板嵌合ﾜｰｸ確認
OR= D16313 K0            ; UT10CN嵌合ﾛﾎﾞｯﾄNo.
ANL
MOV K57 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出要求確認
LD M5501                 ; ﾌﾚｰﾑ有動作開始
ANB M130                 ; 全体PAUSE
MPS
LD M5101                 ; 基板取出可(嵌合RBへ)
OR M5113                 ; 基板取出可ON(基板嵌合へ)
ANL
MPS
AND<> D16362 K0          ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LD= D16362 K0            ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K1011 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K81 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
AND M5121                ; 搬送異常基板除材(CV→Assy)
MOV K80 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;Hand shake to CV
LD M5580                 ; 除材処理受諾
ANB M5121                ; 搬送異常基板除材(CV→Assy)
MOV K14 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;Error Case →PCB remove
LD M5581                 ; PCB除材要求中
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANP B1023                ; 基板取出確認TP-PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5582                 ; NG_Case有 基板除材完了
ANB M5113                ; 基板取出可ON(基板嵌合へ)
MOV K57 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ 基板･ﾌﾚｰﾑ照合
;基板取出起動･基板ｺｰﾄﾞ読取位置移動
LD M5502                 ; 嵌合動作開始確認
MPS
MOV D16313 D5550         ; UT10CN嵌合ﾛﾎﾞｯﾄNo.  基板嵌合RB機種ID
MPP
AND= D5551 D5550         ; 基板嵌合RB機種ID_Ans  基板嵌合RB機種ID
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5503                 ; 機種ID照合OK動作可(RBへ)
ANB X3559                ; 基板取出完了
ANB X355A                ; 基板嵌合完了
AND X3566                ; 基板嵌合RB自動運転中
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板ｺｰﾄﾞ照合位置到着
;ﾌﾚｰﾑ､基板照合要求
LD M5504                 ; 基板嵌合RB起動確認
AND X356E                ; 基板ｺｰﾄﾞ照合可
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5505                 ; 基板嵌合部照合要求
AND W01.12               ; ｺﾈｸﾀ嵌合部照合完了
MPS
LD W01.13                ; ｺﾈｸﾀ嵌合部不一致
OR W00.9                 ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
OR W00.10                ; 照合部PCBﾘｰﾄﾞｴﾗ-
ANL
MOV K7 D5500             ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LDB W01.13               ; ｺﾈｸﾀ嵌合部不一致
ANB W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
ANB W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
ANL
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;塗布検査結果判定
;検査結果OKで基板取出動作開始
LD M5506                 ; 基板照合OK
ANB W01.12               ; ｺﾈｸﾀ嵌合部照合完了
MPS
AND M4632                ; 塗布検査結果NG(基板嵌合へ)
MOV K20 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
AND M4631                ; 塗布検査結果OK(基板嵌合へ)
MOV K30 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ ﾘｰﾄﾞｴﾗｰ＆照合NG処理
;ﾘｰﾄﾞｴﾗ-or基板照合NG･退避原点移動
LD M5507                 ; 照合NG原点移動(RB)
AND X3562                ; 基板嵌合RB原点(42)
ANB X3566                ; 基板嵌合RB自動運転中
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;NG発生ﾀﾞｲｱﾛｸﾞ表示･基板取出ﾃﾞｰﾀｸﾘｱ
LD M5508                 ; 照合NG発生確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MPS
AND W01.13               ; ｺﾈｸﾀ嵌合部不一致
DW K1000 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MRD
AND W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
DW K1008 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K10 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
AND W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
DW K1009 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K11 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5509                 ; 照合NG発生中
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
MPS
LDB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANP B1023                ; 基板取出確認TP-PB
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K13 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LD X124F                 ; 基板投入CV取出ﾜｰｸ確認
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K12 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5510                 ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ発生中
OR M5511                 ; 照合部PCBﾘｰﾄﾞｴﾗｰ発生中
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
MPS
LDB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANP B1023                ; 基板取出確認TP-PB
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K13 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LD X124F                 ; 基板投入CV取出ﾜｰｸ確認
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K12 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5512                 ; FLG.reset check
ANB M130                 ; 全体PAUSE
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
ANB W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
ANB W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K1 D5500             ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5513                 ; 照合NGﾘｾｯﾄ
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANB W01.12               ; ｺﾈｸﾀ嵌合部照合完了
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出確認
LD M5514                 ; 照合NGﾜｰｸ取出確認
AND T915                 ; 搬送位置決め開端
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出後処理部ﾜｰｸﾃﾞｰﾀにNG情報をｾｯﾄ
LD M5515                 ; PCB取出ﾃﾞｰﾀｸﾘｱ要求
AND W0101.14             ; 基板取出要求
AND W01.14               ; 基板取出完了
MPS
@MOV K0 D16362           ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
MPP
AND= D16362 K0           ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5516                 ; 照合NG_ﾜｰｸNGﾌﾗｸﾞｾｯﾄ
MPS
LD M4632                 ; 塗布検査結果NG(基板嵌合へ)
OR M4631                 ; 塗布検査結果OK(基板嵌合へ)
ANL
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LDB M4632                ; 塗布検査結果NG(基板嵌合へ)
ANB M4631                ; 塗布検査結果OK(基板嵌合へ)
AND M3152                ; 塗布検査作業完了
ANL
MOV K18 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5517                 ; 基板照合NG取出確認(PCB取出)
ANB M4632                ; 塗布検査結果NG(基板嵌合へ)
ANB M4631                ; 塗布検査結果OK(基板嵌合へ)
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5518                 ; 基板取出完了(照合NG)
ANB M5113                ; 基板取出可ON(基板嵌合へ)
MOV K57 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ 塗布検結果NG処理
;退避原点移動
LD M5520                 ; 塗布検査結果NG受信確認
ANB M4632                ; 塗布検査結果NG(基板嵌合へ)
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5521                 ; 塗布検査NG原点移動(RB)
LD X3558                 ; 作業原点復帰完了
OR X3562                 ; 基板嵌合RB原点(42)
ANL
ANB X3566                ; 基板嵌合RB自動運転中
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;NG発生ﾀﾞｲｱﾛｸﾞ表示･基板取出ﾃﾞｰﾀｸﾘｱ
LD M5522                 ; 塗布検査NG発生確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K1001 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5523                 ; 塗布検査NG発生中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
ANP B1023                ; 基板取出確認TP-PB
ANB W01.12               ; ｺﾈｸﾀ嵌合部照合完了
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5524                 ; 塗布検査NGﾘｾｯﾄ
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出確認
LD M5525                 ; 照合NGﾜｰｸ取出確認
MPS
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
MOV K22 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出後処理部ﾜｰｸﾃﾞｰﾀにNG情報をｾｯﾄ
LD M5526                 ; PCB取出ﾃﾞｰﾀｸﾘｱ要求
AND W0101.14             ; 基板取出要求
AND W01.14               ; 基板取出完了
MPS
@MOV K0 D16362           ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
MPP
AND= D16362 K0           ; UT10ﾜｰｸOK/NGﾃﾞｰﾀ
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5527                 ; ﾜｰｸNGﾌﾗｸﾞｾｯﾄ確認
ANB W0101.14             ; 基板取出要求
ANB W01.14               ; 基板取出完了
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5528                 ; 基板取出完了(塗布検査NG)
ANB M5113                ; 基板取出可ON(基板嵌合へ)
MOV K57 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ 塗布検査OK嵌合
;塗布検査OK 嵌合開始H/S for PC
LD M5530                 ; 塗布検査結果OK受信確認
ANB M130                 ; 全体PAUSE
ANB M4631                ; 塗布検査結果OK(基板嵌合へ)
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5531                 ; 検査･照合OK確認
AND W0104.0              ; 基板嵌合開始
AND W04.0                ; ｺﾈｸﾀ嵌合受付信号
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置移動
LD M5532                 ; ﾘﾆｱCV基板嵌合移動開始(平行動作)
MPS
MOV K35 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LDB M5701                ; LC基板嵌合位置移動完了
ANB M5702                ; LC基板嵌合位置移動中
ANL
SET M5700                ; LC基板嵌合位置移動
;☆ 平行動作　☆　ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置移動
LDB M5985                ; ﾕﾆｯﾄ10起動中
ZRES M5700 M5709         ; LC基板嵌合位置移動
STG M5700                ; LC基板嵌合位置移動
SET M5702                ; LC基板嵌合位置移動中
CON
JMP M5705                ; 基板嵌合開始
STG M5705                ; 基板嵌合開始
ANB X3357                ; ﾘﾆｱｺﾝﾍﾞｱ自動運転開始(SO207)
MOV K4 D3020             ; ﾘﾆｱCV動作要求No.
JMP M5706                ; ﾘﾆｱCV基板嵌合移動要求
STG M5706                ; ﾘﾆｱCV基板嵌合移動要求
AND X3357                ; ﾘﾆｱｺﾝﾍﾞｱ自動運転開始(SO207)
ANB X335C                ; ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置(SO212)
JMP M5707                ; ﾘﾆｱCV起動確認
STG M5707                ; ﾘﾆｱCV起動確認
ANB X3357                ; ﾘﾆｱｺﾝﾍﾞｱ自動運転開始(SO207)
AND X335C                ; ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置(SO212)
RES M5702                ; LC基板嵌合位置移動中
CON
SET M5701                ; LC基板嵌合位置移動完了
CON
ENDS
;☆☆☆
;嵌合RBへ取出可ON･取出完了確認
LD M5535                 ; 照合･検査OK基板取出可(RBへ)
MPS
AND X3570                ; 基板嵌合RBﾁｬｯｸNG
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LD X3559                 ; 基板取出完了
AND M5625                ; 基板ﾁｬｯｸ記憶
ANL
MOV K49 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ ﾁｬｯｸNG処理
;取出時ﾁｬｯｸNG
LD M5536                 ; 取出時ﾁｬｯｸNG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K1003 D200            ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5537                 ; 取出時ﾁｬｯｸNG確認
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
ANB X3570                ; 基板嵌合RBﾁｬｯｸNG
MPS
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
MPP
LDP B1023                ; 基板取出確認TP-PB
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K15 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5538                 ; 取出時ﾁｬｯｸNG(ﾀﾞｲｱﾛｸﾞ)
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;ﾁｬｯｸNG取出ﾘﾄﾗｲ
LD M5539                 ; 基板取出ﾘﾄﾗｲ
MPS
MOV D16313 D5550         ; UT10CN嵌合ﾛﾎﾞｯﾄNo.  基板嵌合RB機種ID
MPP
AND= D5551 D5550         ; 基板嵌合RB機種ID_Ans  基板嵌合RB機種ID
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5540                 ; 基板取出ﾘﾄﾗｲ起動
ANB X3559                ; 基板取出完了
ANB X355A                ; 基板嵌合完了
AND X3566                ; 基板嵌合RB自動運転中
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5541                 ; 基板取出ﾘﾄﾗｲ起動確認
AND X356E                ; 基板ｺｰﾄﾞ照合可
MOV K35 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;ﾘﾆｺﾝ移動完了確認
LD M5549                 ; ﾘﾆｺﾝ移動完了待ち
ANB M5702                ; LC基板嵌合位置移動中
AND M5701                ; LC基板嵌合位置移動完了
RES M5701                ; LC基板嵌合位置移動完了
CON
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板取出完了確認
LD M5550                 ; 基板取出完了(基板CVへ)
ANB X3559                ; 基板取出完了
ANB M5101                ; 基板取出可(嵌合RBへ)
ANB M5113                ; 基板取出可ON(基板嵌合へ)
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;<h1/>/ 嵌合完了･終了処理
;嵌合完了確認
LD M5551                 ; 基板取出可OFF確認
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5552                 ; 基板嵌合可ON
AND X355A                ; 基板嵌合完了
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5553                 ; 基板嵌合完了確認
ANB X355A                ; 基板嵌合完了
ANB X3566                ; 基板嵌合RB自動運転中
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5554                 ; 基板嵌合結果ON
AND W0104.1              ; 基板嵌合結果
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5555                 ; 基板嵌合開始OFF
ANB W0104.0              ; 基板嵌合開始
ANB W04.0                ; ｺﾈｸﾀ嵌合受付信号
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5556                 ; 基板嵌合結果OFF
ANB W0104.1              ; 基板嵌合結果
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;嵌合作業完了
LD M5557                 ; 基板嵌合動作完了ON
AND M3153                ; 基板嵌合作業完了
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
LD M5558                 ; 嵌合動作終了
ANB M3103                ; 基板嵌合作業開始
@INC D5500               ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;サイクル停止条件
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M5501                ; ﾌﾚｰﾑ有動作開始
AND M4986                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止異常
MOV K98 D5500            ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
;Unit AUTO cycle end
LD M5598                 ; ﾕﾆｯﾄ10ｻｲｸﾙ停止
OR M5559                 ; 基板嵌合動作完了
@MOV K99 D5500           ; ﾕﾆｯﾄ10自動運転ｽﾃｯﾌﾟ
;基板ﾁｬｯｸ記憶
;PCB Clamp flag
LD X3559                 ; 基板取出完了
OR M5625                 ; 基板ﾁｬｯｸ記憶
AND Y2407                ; 基板嵌合RBﾁｬｯｸ閉SV
AND M5985                ; ﾕﾆｯﾄ10起動中
OUT M5625                ; 基板ﾁｬｯｸ記憶
;ｼﾌﾄ取出側移動ｻﾌﾞ
LDP X356A                ; 基板嵌合RB取出ｼﾌﾄ要求
ORP M5761                ; PCB_Assy_shift_Pick up side
OR M5600                 ; ｼﾌﾄ取出移動開始
ANB M5603                ; 位置決完了確認
OUT M5600                ; ｼﾌﾄ取出移動開始
MPS
AND M30                  ; 自動ﾓｰﾄﾞ
CAL+ D16313 K100 D5520   ; UT10CN嵌合ﾛﾎﾞｯﾄNo.  Sift_RB1_Auto_POS
MRD
AND M31                  ; 手動ﾓｰﾄﾞ
CAL+ D5600 K100 D5520    ; 手動機種ID  Sift_RB1_Auto_POS
MRD
MOV D5520 D5524          ; Sift_RB1_Auto_POS  Sift_RB1_POS_確定
MPP
ANB M5602                ; 位置決完了OFF確認
OUT M5601                ; ｼﾌﾄ取出移動
LD M5601                 ; ｼﾌﾄ取出移動
ANB X362B                ; Sift_RB1_PEND
OR M5602                 ; 位置決完了OFF確認
AND M5600                ; ｼﾌﾄ取出移動開始
OUT M5602                ; 位置決完了OFF確認
LD M5602                 ; 位置決完了OFF確認
AND X362B                ; Sift_RB1_PEND
OR M5603                 ; 位置決完了確認
AND M5600                ; ｼﾌﾄ取出移動開始
OUT M5603                ; 位置決完了確認
;ｼﾌﾄ嵌合側移動ｻﾌﾞ
LDP X356B                ; 基板嵌合RB嵌合ｼﾌﾄ要求
OR M5605                 ; ｼﾌﾄ嵌合移動開始
ANB M5608                ; 位置決完了確認
OUT M5605                ; ｼﾌﾄ嵌合移動開始
MPS
AND M30                  ; 自動ﾓｰﾄﾞ
CAL+ D16313 K150 D5520   ; UT10CN嵌合ﾛﾎﾞｯﾄNo.  Sift_RB1_Auto_POS
MRD
AND M31                  ; 手動ﾓｰﾄﾞ
CAL+ D5600 K150 D5520    ; 手動機種ID  Sift_RB1_Auto_POS
MRD
MOV D5520 D5524          ; Sift_RB1_Auto_POS  Sift_RB1_POS_確定
MPP
ANB M5607                ; 位置決完了OFF確認
OUT M5606                ; ｼﾌﾄ嵌合移動
LD M5606                 ; ｼﾌﾄ嵌合移動
ANB X362B                ; Sift_RB1_PEND
OR M5607                 ; 位置決完了OFF確認
AND M5605                ; ｼﾌﾄ嵌合移動開始
OUT M5607                ; 位置決完了OFF確認
LD M5607                 ; 位置決完了OFF確認
AND X362B                ; Sift_RB1_PEND
OR M5608                 ; 位置決完了確認
AND M5605                ; ｼﾌﾄ嵌合移動開始
OUT M5608                ; 位置決完了確認
;<h1/>■工程処理 PLC→PC I/F
;照合要求
LD M5505                 ; 基板嵌合部照合要求
OR M5845                 ; MUN_照合要求
OR W0101.12              ; 基板嵌合部照合要求
ANB M5506                ; 基板照合OK
ANB M5509                ; 照合NG発生中
ANB M5510                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ発生中
ANB M5511                ; 照合部PCBﾘｰﾄﾞｴﾗｰ発生中
ANB M5846                ; 照合終了
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0101.12             ; 基板嵌合部照合要求
;基板嵌合開始･結果
LD M5531                 ; 検査･照合OK確認
OR W0104.0               ; 基板嵌合開始
ANB M5555                ; 基板嵌合開始OFF
AND M5985                ; ﾕﾆｯﾄ10起動中
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0104.0              ; 基板嵌合開始
LD M5554                 ; 基板嵌合結果ON
OR W0104.1               ; 基板嵌合結果
ANB M5556                ; 基板嵌合結果OFF
AND M5985                ; ﾕﾆｯﾄ10起動中
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0104.1              ; 基板嵌合結果
;基板取出要求
LDB M5515                ; PCB取出ﾃﾞｰﾀｸﾘｱ要求
ANB M5526                ; PCB取出ﾃﾞｰﾀｸﾘｱ要求
INV
OUT W0101.14             ; 基板取出要求
;<h1/>■原点復帰
;□□□□□□□□□□□□□□□
;□　　 UNIT10原点復帰回路 　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
ZRES M5750 M5799         ; 自動ﾓｰﾄﾞ切替  ﾕﾆｯﾄ10原点終了
LDB M5984                ; ﾕﾆｯﾄ10原点復帰中
MOV K0 D5510             ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
DW K0 D5710              ; UT10 origin STEP monitor
LD M5984                 ; ﾕﾆｯﾄ10原点復帰中
MOV D5510 Z1             ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
SET M5750:Z1             ; 自動ﾓｰﾄﾞ切替
CAL+ D5510 K5750 D5710   ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ  UT10 origin STEP monitor
;原点復帰ｻｲｸﾙ監視
LD M5984                 ; ﾕﾆｯﾄ10原点復帰中
TMR K1000 K300           ; UNIT10原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M5750                 ; 自動ﾓｰﾄﾞ切替
@FMOV K0 D16300 K100     ; UT10ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPS
MOV D5600 D5550          ; 手動機種ID  基板嵌合RB機種ID
MPP
AND X3548                ; Sup_RB1_AUTO
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5751                 ; ﾌﾟﾛｸﾞﾗﾑ停止
ANB X354B                ; Sup_RB1_RUN
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5752                 ; ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ
AND X354C                ; Sup_RB1_RESET
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5753                 ; ﾌﾟﾛｸﾞﾗﾑ起動
AND X354B                ; Sup_RB1_RUN
ANB X354C                ; Sup_RB1_RESET
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;基板嵌合RB原点復帰
LD M5754                 ; 基板嵌合RB原点復帰要求
ANB X3558                ; 作業原点復帰完了
AND X3567                ; 基板嵌合RB原点復帰中
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5755                 ; 基板嵌合RB原点復帰起動確認
AND X3558                ; 作業原点復帰完了
ANB X3567                ; 基板嵌合RB原点復帰中
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;再ﾜｰｸ残ﾁｪｯｸ
LD M5756                 ; 基板嵌合原点完了　ﾜｰｸ残ﾁｪｯｸ
AND M5980                ; ﾕﾆｯﾄ10原点
LD X124B                 ; 基板嵌合RBﾁｬｯｸ開端
LDB X124A                ; 基板嵌合RBﾁｬｯｸ閉端
ANB X124B                ; 基板嵌合RBﾁｬｯｸ開端
ORL
OR X124F                 ; 基板投入CV取出ﾜｰｸ確認
OR X126B                 ; 基板投入CVﾜｰｸ有
ANL
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;ﾁｬｯｸ開
LD M5757                 ; 基板嵌合RBﾁｬｯｸ開
AND T1011                ; 基板嵌合RBﾁｬｯｸ開端
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;Assy shift HOME
LD M5758                 ; PCB_Assy_shift_HOME
ANB X362A                ; Sift_RB1_HEND
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5759                 ; HOME end wait
AND X362A                ; Sift_RB1_HEND
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5760                 ; HOME完了ｳｪｲﾄ
ANB Y492B                ; Sift_RB1_HOME
TMH K1030 K50            ; HOME完了後ｳｪｲﾄ
CON
AND T1030                ; HOME完了後ｳｪｲﾄ
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5761                 ; PCB_Assy_shift_Pick up side
AND M5602                ; 位置決完了OFF確認
@INC D5510               ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
LD M5762                 ; Move end wait
AND M5920                ; 基板嵌合ｼﾌﾄRB取出端
@MOV K48 D5510           ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M5798                 ; ﾕﾆｯﾄ10原点確認
ANB M82                  ; 原点復帰開始
@MOV K49 D5510           ; ﾕﾆｯﾄ10原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT10手動回路 　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M5985                ; ﾕﾆｯﾄ10起動中
ANB M5984                ; ﾕﾆｯﾄ10原点復帰中
MC
;基板嵌合RBﾁｬｯｸ開閉
LD CR2002                ; 常時ON
OUT M5800                ; 基板嵌合RBﾁｬｯｸ閉可
LD B11A0                 ; UNIT10-1-1 TP-PB
AND M5800                ; 基板嵌合RBﾁｬｯｸ閉可
OR M5801                 ; 基板嵌合RBﾁｬｯｸ閉
ANPB M5803               ; 基板嵌合RBﾁｬｯｸ開
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
ANB M5821                ; 基板嵌合RB原点復帰開始
OUT M5801                ; 基板嵌合RBﾁｬｯｸ閉
LD CR2002                ; 常時ON
OUT M5802                ; 基板嵌合RBﾁｬｯｸ開可
LD B11A1                 ; UNIT10-1-2 TP-PB
AND M5802                ; 基板嵌合RBﾁｬｯｸ開可
OR M5803                 ; 基板嵌合RBﾁｬｯｸ開
ANPB M5801               ; 基板嵌合RBﾁｬｯｸ閉
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
ANB M5821                ; 基板嵌合RB原点復帰開始
OUT M5803                ; 基板嵌合RBﾁｬｯｸ開
;<h1/>/基板嵌合
;基板嵌合RB1ｻｲｸﾙ
LDB M5910                ; 基板嵌合RB_Prog_RESET
MC
LD X1242                 ; 基板嵌合ﾜｰｸ確認
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
AND X1257                ; 基板搬送戻端
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
AND X3488                ; 検査RB嵌合干渉外(50)
LD X3470                 ; 検査RB作業原点到着
OR X3471                 ; 検査RB待機位置到着
ANL
AND X335C                ; ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置(SO212)
AND X3548                ; Sup_RB1_AUTO
OUT M5810                ; 基板嵌合RB1ｻｲｸﾙ可
LDP B11A4                ; UNIT10-1-5 TP-PB
AND M5810                ; 基板嵌合RB1ｻｲｸﾙ可
OR M5811                 ; 基板嵌合RB1ｻｲｸﾙ
ANB M5817                ; 基板嵌合完了確認
MOV D5600 D5550          ; 手動機種ID  基板嵌合RB機種ID
OUT M5811                ; 基板嵌合RB1ｻｲｸﾙ
LD M5811                 ; 基板嵌合RB1ｻｲｸﾙ
AND= D5551 D5550         ; 基板嵌合RB機種ID_Ans  基板嵌合RB機種ID
OR M5812                 ; 機種ID照合OK
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
OUT M5812                ; 機種ID照合OK
LD M5812                 ; 機種ID照合OK
ANB X3559                ; 基板取出完了
ANB X355A                ; 基板嵌合完了
AND X3566                ; 基板嵌合RB自動運転中
OR M5813                 ; 基板嵌合RB起動確認
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5816                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
OUT M5813                ; 基板嵌合RB起動確認
LD M5813                 ; 基板嵌合RB起動確認
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
OR M5814                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5816                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
OUT M5814                ; 基板取出可ON(ﾀﾞﾐｰ)
LD M5814                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND X356E                ; 基板ｺｰﾄﾞ照合可
OR M5815                 ; 基板照合OK(ﾀﾞﾐｰ)
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
OUT M5815                ; 基板照合OK(ﾀﾞﾐｰ)
LD M5815                 ; 基板照合OK(ﾀﾞﾐｰ)
AND X3559                ; 基板取出完了
OR M5816                 ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5817                ; 基板嵌合完了確認
OUT M5816                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
LD M5816                 ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
AND X355A                ; 基板嵌合完了
LD M5815                 ; 基板照合OK(ﾀﾞﾐｰ)
AND X3570                ; 基板嵌合RBﾁｬｯｸNG
ORL
ANB X3566                ; 基板嵌合RB自動運転中
OR M5817                 ; 基板嵌合完了確認
AND M5811                ; 基板嵌合RB1ｻｲｸﾙ
OUT M5817                ; 基板嵌合完了確認
;<h1/>/基板照合
;基板照合RB1ｻｲｸﾙ
LD X124F                 ; 基板投入CV取出ﾜｰｸ確認
AND X1257                ; 基板搬送戻端
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
ANB M5821                ; 基板嵌合RB原点復帰開始
AND X3488                ; 検査RB嵌合干渉外(50)
LD X3470                 ; 検査RB作業原点到着
OR X3471                 ; 検査RB待機位置到着
ANL
AND X3548                ; Sup_RB1_AUTO
OUT M5840                ; 基板照合1ｻｲｸﾙ可
LDP B11A6                ; UNIT10-1-7 TP-PB
AND M5840                ; 基板照合1ｻｲｸﾙ可
OR M5841                 ; 基板照合1ｻｲｸﾙ
ANB M5850                ; 基板嵌合完了確認
MOV D5600 D5550          ; 手動機種ID  基板嵌合RB機種ID
OUT M5841                ; 基板照合1ｻｲｸﾙ
LD M5841                 ; 基板照合1ｻｲｸﾙ
AND= D5551 D5550         ; 基板嵌合RB機種ID_Ans  基板嵌合RB機種ID
OR M5842                 ; 機種ID照合OK
AND M5841                ; 基板照合1ｻｲｸﾙ
OUT M5842                ; 機種ID照合OK
LD M5842                 ; 機種ID照合OK
ANB X3559                ; 基板取出完了
ANB X355A                ; 基板嵌合完了
AND X3566                ; 基板嵌合RB自動運転中
OR M5843                 ; 基板嵌合RB起動確認
AND M5841                ; 基板照合1ｻｲｸﾙ
ANB M5849                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
OUT M5843                ; 基板嵌合RB起動確認
LD M5843                 ; 基板嵌合RB起動確認
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
OR M5844                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND M5841                ; 基板照合1ｻｲｸﾙ
ANB M5849                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
OUT M5844                ; 基板取出可ON(ﾀﾞﾐｰ)
LD M5844                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND X356E                ; 基板ｺｰﾄﾞ照合可
ANB M5848                ; 基板照合NG(ﾀﾞﾐｰ)
OUT M5839                ; 選択画面表示
;1)Reading
LD M5844                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND X356E                ; 基板ｺｰﾄﾞ照合可
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
ANB W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
ANB W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
ANB M5848                ; 基板照合NG(ﾀﾞﾐｰ)
ANP B1026                ; PCB読取PB
OR M5845                 ; MUN_照合要求
AND M5841                ; 基板照合1ｻｲｸﾙ
ANB M5846                ; 照合終了
OUT M5845                ; MUN_照合要求
LD M5845                 ; MUN_照合要求
AND W01.12               ; ｺﾈｸﾀ嵌合部照合完了
OR M5846                 ; 照合終了
AND M5841                ; 基板照合1ｻｲｸﾙ
ANB M5847                ; 照合初期化確認
OUT M5846                ; 照合終了
LD M5846                 ; 照合終了
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
ANB W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
ANB W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
OUT M5847                ; 照合初期化確認
;2)Finish
LD M5844                 ; 基板取出可ON(ﾀﾞﾐｰ)
AND X356E                ; 基板ｺｰﾄﾞ照合可
ANB W01.13               ; ｺﾈｸﾀ嵌合部不一致
ANB W00.9                ; 照合部ﾌﾚｰﾑﾘｰﾄﾞｴﾗｰ
ANB W00.10               ; 照合部PCBﾘｰﾄﾞｴﾗ-
ANB M5845                ; MUN_照合要求
ANP B1027                ; 読取ﾓｰﾄﾞ終了PB
OR M5848                 ; 基板照合NG(ﾀﾞﾐｰ)
AND M5841                ; 基板照合1ｻｲｸﾙ
OUT M5848                ; 基板照合NG(ﾀﾞﾐｰ)
LD M5848                 ; 基板照合NG(ﾀﾞﾐｰ)
AND X3562                ; 基板嵌合RB原点(42)
ANB X3566                ; 基板嵌合RB自動運転中
OR M5850                 ; 基板嵌合完了確認
AND M5841                ; 基板照合1ｻｲｸﾙ
OUT M5850                ; 基板嵌合完了確認
;<h1/>/原点復帰
;基板嵌合RB原点戻
LDB X1242                ; 基板嵌合ﾜｰｸ確認
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANB X124A                ; 基板嵌合RBﾁｬｯｸ閉端
ORB X3564                ; 基板RBZ軸退避領域(44)
ANB M5821                ; 基板嵌合RB原点復帰開始
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
AND X3548                ; Sup_RB1_AUTO
OUT M5820                ; 基板嵌合RB原点戻可
LDP B11A5                ; UNIT10-1-6 TP-PB
AND M5820                ; 基板嵌合RB原点戻可
OR M5821                 ; 基板嵌合RB原点復帰開始
ANB M5826                ; 基板嵌合RB原点終了
MPS
OUT M5821                ; 基板嵌合RB原点復帰開始
MPP
ANB M5823                ; 基板嵌合RB原点復帰起動確認
OUT M5822                ; 基板嵌合RB原点復帰要求
LD M5822                 ; 基板嵌合RB原点復帰要求
ANB X3558                ; 作業原点復帰完了
AND X3567                ; 基板嵌合RB原点復帰中
OR M5823                 ; 基板嵌合RB原点復帰起動確認
AND M5821                ; 基板嵌合RB原点復帰開始
OUT M5823                ; 基板嵌合RB原点復帰起動確認
LD M5823                 ; 基板嵌合RB原点復帰起動確認
AND X3558                ; 作業原点復帰完了
ANB X3567                ; 基板嵌合RB原点復帰中
OR M5824                 ; 基板嵌合RB原点復帰完了
AND M5821                ; 基板嵌合RB原点復帰開始
OUT M5824                ; 基板嵌合RB原点復帰完了
LD M5824                 ; 基板嵌合RB原点復帰完了
ANB X3567                ; 基板嵌合RB原点復帰中
OR M5825                 ; 基板嵌合RBﾁｬｯｸ開
AND M5821                ; 基板嵌合RB原点復帰開始
OUT M5825                ; 基板嵌合RBﾁｬｯｸ開
LD M5825                 ; 基板嵌合RBﾁｬｯｸ開
AND X124B                ; 基板嵌合RBﾁｬｯｸ開端
OR M5826                 ; 基板嵌合RB原点終了
AND M5821                ; 基板嵌合RB原点復帰開始
OUT M5826                ; 基板嵌合RB原点終了
MCR
MCR
;RB実行中AUTO_OFF処理　Controllor program reset
LDB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
ANB M5821                ; 基板嵌合RB原点復帰開始
INV
ANB X354B                ; Sup_RB1_RUN
OR M5910                 ; 基板嵌合RB_Prog_RESET
ANB X354C                ; Sup_RB1_RESET
ANB M258                 ; 動作中 TP STOP 非検出(RESET無)
MPS
TMH K1026 K20            ; Sup_RB1_自動中RUN_OFF
MPP
AND T1026                ; Sup_RB1_自動中RUN_OFF
OUT M5910                ; 基板嵌合RB_Prog_RESET
;ROBOT AUTO/MANU
LDP B11BF                ; UNIT10-2-16 TP-PB
AND M5830                ; MANUAL_ON
OUT M5831                ; MANUAL_OFF
LDP B11BF                ; UNIT10-2-16 TP-PB
ANB M5830                ; MANUAL_ON
OR M5830                 ; MANUAL_ON
ANB M5831                ; MANUAL_OFF
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M51                  ; 即停止異常(緊急停止)
OUT M5830                ; MANUAL_ON
;<h1/>■出力
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;基板嵌合RBﾁｬｯｸ開閉
LDPB X3569               ; 基板嵌合RBﾁｬｯｸ開要求
ANB M5757                ; 基板嵌合RBﾁｬｯｸ開
ANPB M5803               ; 基板嵌合RBﾁｬｯｸ開
ANB M5825                ; 基板嵌合RBﾁｬｯｸ開
INV
LD Y2406                 ; 基板嵌合RBﾁｬｯｸ開SV
ANB Y2407                ; 基板嵌合RBﾁｬｯｸ閉SV
ORL
OUT Y2406                ; 基板嵌合RBﾁｬｯｸ開SV
LDPB X3568               ; 基板嵌合RBﾁｬｯｸ閉要求
ANPB M5801               ; 基板嵌合RBﾁｬｯｸ閉
INV
LD Y2407                 ; 基板嵌合RBﾁｬｯｸ閉SV
ANB Y2406                ; 基板嵌合RBﾁｬｯｸ開SV
ORL
OUT Y2407                ; 基板嵌合RBﾁｬｯｸ閉SV
;基板嵌合RB　準備出力
;CC_LINKｲﾆｼｬﾗｲｽﾞ
LD X3618                 ; Sup_RB1_Initial_Req
MPS
TMR K1006 K2             ; 基板嵌合RBｲﾆｼｬﾙ開始
MPP
AND T1006                ; 基板嵌合RBｲﾆｼｬﾙ開始
OUT M5900                ; 基板嵌合RBｲﾆｼｬﾙ処理開始
LD M5900                 ; 基板嵌合RBｲﾆｼｬﾙ処理開始
OUT M5901                ; 基板嵌合RB_EMG&I/L_ON
LD M5901                 ; 基板嵌合RB_EMG&I/L_ON
MPS
TMR K1007 K2             ; 基板嵌合RBｲﾆｼｬﾙ完了
MPP
AND T1007                ; 基板嵌合RBｲﾆｼｬﾙ完了
OUT M5902                ; 基板嵌合RBｲﾆｼｬﾙ処理完了
LD M5902                 ; 基板嵌合RBｲﾆｼｬﾙ処理完了
OUT Y4918                ; Sup_RB1_Initial_OK
;制御出力
LD M121                  ; 非常停止正常
ANB M5901                ; 基板嵌合RB_EMG&I/L_ON
OR M5901                 ; 基板嵌合RB_EMG&I/L_ON
OUT Y4840                ; Sup_RB1 EMG
LD X3541                 ; Sup_RB1_CPU-OK
ANB X3542                ; Sup_RB1_SV-ON
AND M50                  ; 運転準備
AND M121                 ; 非常停止正常
OUT Y4841                ; Sup_RB1_SV-ON
LD M123                  ; 安全扉正常
AND T920                 ; 基板CV扉閉確認
ORB X3548                ; Sup_RB1_AUTO
ANB M5901                ; 基板嵌合RB_EMG&I/L_ON
ANB M5751                ; ﾌﾟﾛｸﾞﾗﾑ停止
OR M5901                 ; 基板嵌合RB_EMG&I/L_ON
OUT Y4849                ; Sup_RB1_IL
LD M5753                 ; ﾌﾟﾛｸﾞﾗﾑ起動
LD M123                  ; 安全扉正常
AND T920                 ; 基板CV扉閉確認
AND X3548                ; Sup_RB1_AUTO
LD M41                   ; 全ﾕﾆｯﾄ起動中
OR X3567                 ; 基板嵌合RB原点復帰中
LDB M5984                ; ﾕﾆｯﾄ10原点復帰中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M5830                ; MANUAL_ON
LDB X3566                ; 基板嵌合RB自動運転中
ANB X3567                ; 基板嵌合RB原点復帰中
OR T920                  ; 基板CV扉閉確認
ANL
ORL
ANL
ANB X354B                ; Sup_RB1_RUN
ORL
AND M50                  ; 運転準備
ANB M5910                ; 基板嵌合RB_Prog_RESET
OUT Y484A                ; Sup_RB1_Start
LD M5750                 ; 自動ﾓｰﾄﾞ切替
OUT Y484B                ; Sup_RB1_AUTO
LDB M50                  ; 運転準備
OR M5910                 ; 基板嵌合RB_Prog_RESET
ANB M51                  ; 即停止異常(緊急停止)
ANB X354C                ; Sup_RB1_RESET
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
OR M5752                 ; ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ
OUT Y484D                ; Sup_RB1_Prg_Reset
LD CR2003                ; 常時OFF
OUT Y484E                ; Sup_RB1_Manu
;動力主電源
LD M50                   ; 運転準備
OUT Y2306                ; 基板嵌合RB駆動電源ON
;基板嵌合RB　I/F出力
LDB M5754                ; 基板嵌合RB原点復帰要求
ANB M5822                ; 基板嵌合RB原点復帰要求
INV
OUT Y4858                ; 作業原点復帰要求
LDB M5503                ; 機種ID照合OK動作可(RBへ)
ANB M5540                ; 基板取出ﾘﾄﾗｲ起動
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
INV
OUT Y4859                ; 基板嵌合動作要求
LDB CR2003               ; 常時OFF
INV
OUT Y485A                ; Sup_RB1_SI(32)
LDB CR2003               ; 常時OFF
INV
OUT Y485B                ; Sup_RB1_SI(33)
;/
LDB M5101                ; 基板取出可(嵌合RBへ)
ANB M5113                ; 基板取出可ON(基板嵌合へ)
ANB M5814                ; 基板取出可ON(ﾀﾞﾐｰ)
ANB M5844                ; 基板取出可ON(ﾀﾞﾐｰ)
INV
AND X1257                ; 基板搬送戻端
OUT Y4860                ; 基板供給CV進入可(40)
LDB X335C                ; ﾘﾆｱｺﾝﾍﾞｱ基板嵌合位置(SO212)
ANB M5816                ; 基板取出OFF､嵌合進入可ON(ﾀﾞﾐｰ)
INV
OUT Y4861                ; 嵌合部進入可(41)
LDB M5503                ; 機種ID照合OK動作可(RBへ)
ANB M5540                ; 基板取出ﾘﾄﾗｲ起動
ANB M5812                ; 機種ID照合OK
ANB M5842                ; 機種ID照合OK
INV
OUT Y4862                ; 機種照合OK(42)
LDB CR2003               ; 常時OFF
INV
OUT Y4863                ; 検査OK嵌合動作要求(43)
;/
LD T1010                 ; 基板嵌合RBﾁｬｯｸ閉端
OUT Y4868                ; 基板嵌合RBﾁｬｯｸ閉端(50)
LD T1011                 ; 基板嵌合RBﾁｬｯｸ開端
OUT Y4869                ; 基板嵌合RBﾁｬｯｸ開端(51)
LD M5920                 ; 基板嵌合ｼﾌﾄRB取出端
OUT Y486A                ; 嵌合ｼﾌﾄRB取出側(52)
LD M5921                 ; 基板嵌合ｼﾌﾄRB嵌合端
OUT Y486B                ; 嵌合ｼﾌﾄ嵌合側(53)
LD T914                  ; 搬送位置決め閉端
OUT Y486C                ; 基板押さえ閉端(54)
LD T915                  ; 搬送位置決め開端
OUT Y486D                ; 基板押さえ開端(55)
LDB M5535                ; 照合･検査OK基板取出可(RBへ)
ANB M5815                ; 基板照合OK(ﾀﾞﾐｰ)
INV
OUT Y486E                ; 基板照合OK取出可(56)
LDB M5507                ; 照合NG原点移動(RB)
ANB M5521                ; 塗布検査NG原点移動(RB)
ANB M5848                ; 基板照合NG(ﾀﾞﾐｰ)
INV
OUT Y486F                ; 基板照合NG取出不可(57)
;基板嵌合RB　機種ID
LD CR2002                ; 常時ON
MPS
AND D5550.0              ; 基板嵌合RB機種ID_1
OUT Y4878                ; 機種ID_1
MRD
AND D5550.1              ; 基板嵌合RB機種ID_2
OUT Y4879                ; 機種ID_2
MRD
AND D5550.2              ; 基板嵌合RB機種ID_4
OUT Y487A                ; 機種ID_4
MRD
AND D5550.3              ; 基板嵌合RB機種ID_8
OUT Y487B                ; 機種ID_8
MPP
AND D5550.4              ; 基板嵌合RB機種ID_16
OUT Y487C                ; 機種ID_16
;基板嵌合ｼﾌﾄRB
LDB M5601                ; ｼﾌﾄ取出移動
ANB M5606                ; ｼﾌﾄ嵌合移動
INV
@MOV D5524 D5526         ; Sift_RB1_POS_確定  Sift_RB1_指令POS
MPS
TMH K1091 K10            ; Sift_RB1_POS確定ﾃﾞｨﾚｲ
MPP
AND T1091                ; Sift_RB1_POS確定ﾃﾞｨﾚｲ
OUT Y492D                ; Sift_RB1_CSTR
LD CR2002                ; 常時ON
MPS
AND D5526.0              ; Sift_RB1_指令POS1
OUT Y4920                ; Sift_RB1_PC1
MRD
AND D5526.1              ; Sift_RB1_指令POS2
OUT Y4921                ; Sift_RB1_PC2
MRD
AND D5526.2              ; Sift_RB1_指令POS4
OUT Y4922                ; Sift_RB1_PC4
MRD
AND D5526.3              ; Sift_RB1_指令POS8
OUT Y4923                ; Sift_RB1_PC8
MRD
AND D5526.4              ; Sift_RB1_指令POS16
OUT Y4924                ; Sift_RB1_PC16
MRD
AND D5526.5              ; Sift_RB1_指令POS32
OUT Y4925                ; Sift_RB1_PC32
MRD
AND D5526.6              ; Sift_RB1_指令POS64
OUT Y4926                ; Sift_RB1_PC64
MPP
AND D5526.7              ; Sift_RB1_指令POS128
OUT Y4927                ; Sift_RB1_PC128
LD M5830                 ; MANUAL_ON
OUT Y492A                ; Sift_RB1_RMOD
LDB M5758                ; PCB_Assy_shift_HOME
ANB M5759                ; HOME end wait
INV
OUT Y492B                ; Sift_RB1_HOME
LD CR2002                ; 常時ON
OUT Y492C                ; Sift_RB1_STP
LD M16                   ; ﾘｾｯﾄPB
ANB X362E                ; Sift_RB1_ALM
OUT Y492E                ; Sift_RB1_RES
LD M50                   ; 運転準備
OUT Y492F                ; Sift_RB1_SON
;<h1/>■表示
;□□　　パネル表示　　□□
LD X124A                 ; 基板嵌合RBﾁｬｯｸ閉端
LD Y2407                 ; 基板嵌合RBﾁｬｯｸ閉SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B16A0                ; UNIT10-1-1 TP-PL
LD X124B                 ; 基板嵌合RBﾁｬｯｸ開端
LD Y2406                 ; 基板嵌合RBﾁｬｯｸ開SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B16A1                ; UNIT10-1-2 TP-PL
LD M5811                 ; 基板嵌合RB1ｻｲｸﾙ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B16A4                ; UNIT10-1-5 TP-PL
LD X3562                 ; 基板嵌合RB原点(42)
ANB X3567                ; 基板嵌合RB原点復帰中
ANB M5821                ; 基板嵌合RB原点復帰開始
LD M5821                 ; 基板嵌合RB原点復帰開始
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B16A5                ; UNIT10-1-6 TP-PL
LD M5841                 ; 基板照合1ｻｲｸﾙ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B16A6                ; UNIT10-1-7 TP-PL
LD M5830                 ; MANUAL_ON
OUT B16BF                ; UNIT10-2-16 TP-PL
;<h1/>■異常
;□□　　異常検出　　□□
;B140(K321)--B14F(K336)  B150(K337)--B15F(K352)
LD X3556                 ; Sup_RB1_Error
AND Y4849                ; Sup_RB1_IL
AND T920                 ; 基板CV扉閉確認
ANB M123                 ; 安全扉正常
ANB M5901                ; 基板嵌合RB_EMG&I/L_ON
AND X3548                ; Sup_RB1_AUTO
TMR K1025 K20            ; Sup_RB1_Error
LD X3543                 ; Sup_RB1_ALM
AND T11                  ; 運転準備完了2s遅延
OR T1025                 ; Sup_RB1_Error
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0140                ; 基板嵌合RB異常
ORL
AND M50                  ; 運転準備
OUT B0140                ; 基板嵌合RB異常
@DW K320 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0640
AND B0140                ; 基板嵌合RB異常
OUT B0640
LD M5625                 ; 基板ﾁｬｯｸ記憶
ANB X124A                ; 基板嵌合RBﾁｬｯｸ閉端
MEP
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0141                ; 基板嵌合RBﾜｰｸ紛失異常
ORL
OUT B0141                ; 基板嵌合RBﾜｰｸ紛失異常
@DW K321 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0641
AND B0141                ; 基板嵌合RBﾜｰｸ紛失異常
OUT B0641
LD X3618                 ; Sup_RB1_Initial_Req
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1050 K50            ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ確認
MPP
AND T1050                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ確認
OUT B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
@DW K322 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0642
AND B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
OUT B0642
LD Y2406                 ; 基板嵌合RBﾁｬｯｸ開SV
ANB X124B                ; 基板嵌合RBﾁｬｯｸ開端
LD Y2407                 ; 基板嵌合RBﾁｬｯｸ閉SV
ANB X124A                ; 基板嵌合RBﾁｬｯｸ閉端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0143                ; 基板嵌合RBﾁｬｯｸ異常
ORL
AND M50                  ; 運転準備
AND M30                  ; 自動ﾓｰﾄﾞ
MPS
TMR K1051 K50            ; 基板嵌合RBﾁｬｯｸ確認
MPP
AND T1051                ; 基板嵌合RBﾁｬｯｸ確認
OUT B0143                ; 基板嵌合RBﾁｬｯｸ異常
@DW K323 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0643
AND B0143                ; 基板嵌合RBﾁｬｯｸ異常
OUT B0643
LDB X362E                ; Sift_RB1_ALM
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0144                ; 基板嵌合ｼﾌﾄRB異常
ORL
AND M50                  ; 運転準備
OUT B0144                ; 基板嵌合ｼﾌﾄRB異常
@DW K324 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0644
AND B0144                ; 基板嵌合ｼﾌﾄRB異常
OUT B0644
LD M17                   ; 原点復帰PB
AND X3564                ; 基板RBZ軸退避領域(44)
LD M5756                 ; 基板嵌合原点完了　ﾜｰｸ残ﾁｪｯｸ
AND M3265                ; ﾘﾆｱCV上ｷｬﾘｱﾜｰｸ確認
ORL
ANB X124B                ; 基板嵌合RBﾁｬｯｸ開端
AND X124A                ; 基板嵌合RBﾁｬｯｸ閉端
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
ORL
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OUT B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
@DW K325 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0645
AND B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
OUT B0645
LD CR2003                ; 常時OFF
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0146                ; UNIT10異常7
ORL
OUT B0146                ; UNIT10異常7
@DW K326 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0646
AND B0146                ; UNIT10異常7
OUT B0646
LD M5537                 ; 取出時ﾁｬｯｸNG確認
OUT B0147                ; 基板取出時ﾁｬｯｸNG発生中
@DW K327 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0647
AND B0147                ; 基板取出時ﾁｬｯｸNG発生中
OUT B0647
;D200警報
LD= K1000 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015A                ; Err1000:ｺﾈｸﾀ嵌合部照合NG発生
@DW H015A D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K1001 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015B                ; Err1001:塗布検査NG
@DW H015B D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K1003 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015C                ; Err1003:PCB取出時ﾁｬｯｸNG
@DW H015C D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K1008 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015D                ; Err1008:ﾌﾚｰﾑ照合部ﾘｰﾄﾞｴﾗｰ
@DW H015D D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K1009 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015E                ; Err1009:PCB照合部ﾘｰﾄﾞｴﾗｰ
@DW H015E D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K1011 D200           ; ｴﾗｰｺｰﾄﾞNo.
OUT B015F                ; Err1011:PCB除材要求
@DW H015F D0
CON
@FIFOW D0 D900           ; 異常内容
;異常ﾌﾞｻﾞｰまとめ
LDB B0140                ; 基板嵌合RB異常
OR B0640
LDB B0141                ; 基板嵌合RBﾜｰｸ紛失異常
OR B0641
ANL
LDB B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
OR B0642
ANL
LDB B0143                ; 基板嵌合RBﾁｬｯｸ異常
OR B0643
ANL
LDB B0144                ; 基板嵌合ｼﾌﾄRB異常
OR B0644
ANL
LDB B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
OR B0645
ANL
LDB B0146                ; UNIT10異常7
OR B0646
ANL
INV
OUT M5989                ; ﾕﾆｯﾄ10異常BZ補助
;Error location display
LDB B0140                ; 基板嵌合RB異常
ANB B0141                ; 基板嵌合RBﾜｰｸ紛失異常
ANB B0142                ; 基板嵌合RBｲﾆｼｬﾗｲｽﾞ異常
ANB B0143                ; 基板嵌合RBﾁｬｯｸ異常
ANB B0144                ; 基板嵌合ｼﾌﾄRB異常
ANB B0145                ; 基板嵌合原点復帰時ﾜｰｸ有異常
ANB B0146                ; UNIT10異常7
ANB B0147                ; 基板取出時ﾁｬｯｸNG発生中
ANB B0A4                 ; 原点復帰PCB嵌合ﾜｰｸ有異常
INV
OUT B090A                ; UT10 Error
END
ENDH
