DEVICE:53
;MODULE:UT4_Supply_Sift
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD CR2003                ; 常時OFF
TMR K411 K5
LD CR2003                ; 常時OFF
TMR K412 K3
LD CR2003                ; 常時OFF
TMR K413 K3
LD CR2003                ; 常時OFF
TMR K414 K3
;■ロボット等機器入力受け
;ｼﾌﾄﾛﾎﾞｯﾄ現在値
;Sup_Shift_RB_Current_Position
LD X1145                 ; 投入ｼﾌﾄﾛﾎﾞｯﾄ準備完了
AND X330B                ; Sup_Shift_RB_ORG-S
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
MPS
AND X3300                ; Sup_Shift_RB_Pout1
OUT D2528.0              ; Sup_Sift_RB_POS1
MRD
AND X3301                ; Sup_Shift_RB_Pout2
OUT D2528.1              ; Sup_Sift_RB_POS2
MRD
AND X3302                ; Sup_Shift_RB_Pout4
OUT D2528.2              ; Sup_Sift_RB_POS4
MRD
AND X3303                ; Sup_Shift_RB_Pout8
OUT D2528.3              ; Sup_Sift_RB_POS8
MRD
AND X3304                ; Sup_Shift_RB_Pout16
OUT D2528.4              ; Sup_Sift_RB_POS16
MRD
AND X3305                ; Sup_Shift_RB_Pout32
OUT D2528.5              ; Sup_Sift_RB_POS32
MRD
AND X3306                ; Sup_Shift_RB_Pout64
OUT D2528.6              ; Sup_Sift_RB_POS64
MRD
AND X3307                ; Sup_Shift_RB_Pout128
OUT D2528.7              ; Sup_Sift_RB_POS128
MPP
INV
MOV K0 D2528             ; Sup_Shift_RB_完了POS
;供給ﾛﾎﾞｯﾄ現在値
;Sup_RB_Current_Position
LD X1146                 ; 投入ｼﾌﾄ排出ﾛﾎﾞｯﾄ準備完了
AND X332B                ; Sup_RB_ORG-S
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
MPS
AND X3320                ; Sup_RB_Pout1
OUT D2538.0              ; Sup_RB_POS1
MRD
AND X3321                ; Sup_RB_Pout2
OUT D2538.1              ; Sup_RB_POS2
MRD
AND X3322                ; Sup_RB_Pout4
OUT D2538.2              ; Sup_RB_POS4
MRD
AND X3323                ; Sup_RB_Pout8
OUT D2538.3              ; Sup_RB_POS8
MRD
AND X3324                ; Sup_RB_Pout16
OUT D2538.4              ; Sup_RB_POS16
MRD
AND X3325                ; Sup_RB_Pout32
OUT D2538.5              ; Sup_RB_POS32
MRD
AND X3326                ; Sup_RB_Pout64
OUT D2538.6              ; Sup_RB_POS64
MRD
AND X3327                ; Sup_RB_Pout128
OUT D2538.7              ; Sup_RB_POS128
MPP
INV
MOV K0 D2538             ; Sup_RB_完了POS
;□
;□□ 装置状態・動作条件 □□
;□
LD= D2528 K2             ; Sup_Shift_RB_完了POS
AND= D2538 K1            ; Sup_RB_完了POS
LD X1162                 ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
AND X1169                ; 投入ｼﾌﾄ位置決端
LDB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
AND X1168                ; 投入ｼﾌﾄ位置決解除端
ORL
ANL
OUT M2980                ; ﾕﾆｯﾄ4原点
;□ 非常停止
LDB CR2003               ; 常時OFF
INV
OUT M2981                ; ﾕﾆｯﾄ4全異常
;□ 起動条件
LD M2980                 ; ﾕﾆｯﾄ4原点
OUT M2982                ; ﾕﾆｯﾄ4起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
OR M2986                 ; ﾕﾆｯﾄ4ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M2983                ; ﾕﾆｯﾄ4起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
LDB M82                  ; 原点復帰開始
AND CR2003               ; 常時OFF
ORL
ANB M2984                ; ﾕﾆｯﾄ4原点復帰中
OR M2984                 ; ﾕﾆｯﾄ4原点復帰中
ANB M2983                ; ﾕﾆｯﾄ4起動OFF
ANB M2991                ; ﾕﾆｯﾄ4ｴﾗｰ発生
ANB M2799                ; ﾕﾆｯﾄ4原点復帰完了確認
OUT M2984                ; ﾕﾆｯﾄ4原点復帰中
;□ 起動中
LD M73                   ; 各工程起動指令
AND M2982                ; ﾕﾆｯﾄ4起動条件
OR M2985                 ; ﾕﾆｯﾄ4起動中
ANB M2983                ; ﾕﾆｯﾄ4起動OFF
ANB M2589                ; ﾕﾆｯﾄ4ｻｲｸﾙ終了
OUT M2985                ; ﾕﾆｯﾄ4起動中
;□ ｻｲｸﾙ停止異常
LDB B083                 ; 投入ｼﾌﾄRB異常
ANB B084                 ; 投入RB異常
ANB B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
INV
OUT M2986                ; ﾕﾆｯﾄ4ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M2987                ; ﾕﾆｯﾄ4払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M2988                ; ﾕﾆｯﾄ4警報補助
;□ 機器準備確認
LD X1145                 ; 投入ｼﾌﾄﾛﾎﾞｯﾄ準備完了
AND X1146                ; 投入ｼﾌﾄ排出ﾛﾎﾞｯﾄ準備完了
OUT M2990                ; ﾕﾆｯﾄ4機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB B080                 ; ｷｬﾘｱ引込前後異常
ANB B081                 ; ｼﾌﾄ位置決動作異常
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
INV
OUT M2991                ; ﾕﾆｯﾄ4ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD= D2500 D2505          ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ4動作中判定
OR M130                  ; 全体PAUSE
ORB M2985                ; ﾕﾆｯﾄ4起動中
ANB M2984                ; ﾕﾆｯﾄ4原点復帰中
TMR K401 K20             ; ﾕﾆｯﾄ4動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D2500 D2505          ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ4動作中判定
LD T401                  ; ﾕﾆｯﾄ4動作無し判定ﾀｲﾏ
OUT M2992                ; ﾕﾆｯﾄ4動作無し確認
;□ 払出しﾜｰｸ無し
LD CR2002                ; 常時ON
MPS
WSUM D16100 K100         ; UT4ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
CON
STA D2550                ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
MPP
AND= D2550 K0            ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
TMR K405 K10             ; UNIT4払出処理ﾜｰｸ無
LD T405                  ; UNIT4払出処理ﾜｰｸ無
OUT M2993                ; ﾕﾆｯﾄ4ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M2979                ; ﾕﾆｯﾄ4ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　 　UNIT4自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>■自動
LD CR2002                ; 常時ON
ZRES M2500 M2589         ; ｷｬﾘｱ確認  ﾕﾆｯﾄ4ｻｲｸﾙ終了
LDB M2985                ; ﾕﾆｯﾄ4起動中
MOV K0 D2500             ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
DW K0 D2700              ; UT4 main STEP monitor
LD M2985                 ; ﾕﾆｯﾄ4起動中
MOV D2500 Z1             ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
SET M2500:Z1             ; ｷｬﾘｱ確認
CAL+ D2500 K2500 D2700   ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ  UT4 main STEP monitor
;ｷｬﾘｱ有無確認
LD M2500                 ; ｷｬﾘｱ確認
MPS
ANB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
MOV K9 D2500             ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾘﾀｰﾝCVよりｷｬﾘｱ引込
LD M2501                 ; ｷｬﾘｱ引込可
AND>= D3600 K3           ; ﾘﾀｰﾝCVｷｬﾘｱ数
AND M3702                ; ﾘﾀｰﾝ引込ｷｬﾘｱ有引込可
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2502                 ; ｷｬﾘｱ引込
AND X1160                ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾘﾀｰﾝCVよりｷｬﾘｱ引込確認
LD M2503                 ; ｷｬﾘｱ引込確認
MPS
AND X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
MOV K8 D2500             ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
ANB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｼﾌﾄ引込戻
LD M2504                 ; ｷｬﾘｱ引込戻
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｷｬﾘｱ引込NG
LD M2505                 ; ｷｬﾘｱ引込NG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K400 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2506                 ; ｷｬﾘｱ引込NG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
OUT M2950                ; Err400:ｷｬﾘｱ引込_NG
LD M2507                 ; ｷｬﾘｱ引込NG解除確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K2 D2500             ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｷｬﾘｱ引込完､ﾘﾀｰﾝCV引込可OFF確認
LD M2508                 ; ｷｬﾘｱ引込完了
AND M3722                ; ﾘﾀｰﾝCVｷｬﾘｱｶｳﾝﾀ(-)受付
@MOV D17002 D17010       ; RT3ﾌﾚｰﾑ投入ｲﾝﾃﾞｯｸｽ(投入ｼﾌﾄ用)  UT4ﾌﾚｰﾑ投入ｲﾝﾃﾞｯｸｽ(投入ｼﾌﾄ用)
@MOV K0 D17002           ; RT3ﾌﾚｰﾑ投入ｲﾝﾃﾞｯｸｽ(投入ｼﾌﾄ用)
@BMOV D15900 D16100 K100 ; RT3ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT4ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPS
@FMOV K0 D15900 K100     ; RT3ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPP
ANB M3702                ; ﾘﾀｰﾝ引込ｷｬﾘｱ有引込可
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｼﾌﾄ位置決閉
;ﾓｰﾄﾞ判定･条件分岐
LD M2509                 ; ｼﾌﾄ位置決
AND X1169                ; 投入ｼﾌﾄ位置決端
MPS
ANB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
MOV K40 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ確認
LD M2510                 ; 点検、再投入ﾜｰｸ確認
MPS
LD X1165                 ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND= D2550 K0            ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
LDB X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND<> D2550 K0           ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
ORL
ANL
MOV K19 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LD X1165                 ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND<> D2550 K0           ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
LDB X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND= D2550 K0            ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
ORL
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾃﾞｰﾀ不明ﾜｰｸ有りNG
LD M2511                 ; ﾜｰｸ有NG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K403 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2512                 ; ﾜｰｸ有NG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
LD X1165                 ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
ANP B1023                ; 基板取出確認TP-PB
ANL
MPS
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
@FMOV K0 D16100 K100     ; UT4ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPP
OUT M2953                ; Err403:ﾃﾞｰﾀ不明ﾜｰｸNG
LD M2513                 ; ﾜｰｸ有NG解除確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K10 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｲﾝﾃﾞｯｸｽNoｾｯﾄ
LD M2519                 ; ｷｬﾘｱｲﾝﾃﾞｯｸｽNo.ｾｯﾄ
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV D17010 W0140         ; UT4ﾌﾚｰﾑ投入ｲﾝﾃﾞｯｸｽ(投入ｼﾌﾄ用)  ｼｬﾄﾙ投入部ｲﾝﾃﾞｯｸｽNo.
@INC D2570
;ﾊﾟﾚｯﾄID読込
LD M2520                 ; ﾊﾟﾚｯﾄID読込要求(PCへ)
MPS
OUT W0101.4              ; 投入側ﾊﾟﾚｯﾄID読込要求
MPP
AND W01.3                ; 投入側ﾊﾟﾚｯﾄID読込完了
MPS
ANB W01.4                ; 投入側ﾊﾟﾚｯﾄID読込ｴﾗｰ
BMOV W05A D2956 K2       ; 供給側ﾊﾟﾚｯﾄID(PC)  供給側ﾊﾟﾚｯﾄID
MPS
LD X1165                 ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND= D2550 K0            ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
ANL
MOV K80 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND<> D2550 K0           ; 供給ｼﾌﾄﾜｰｸ情報(ﾜｰｸ有無)
ANL
MOV K39 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND W01.4                ; 投入側ﾊﾟﾚｯﾄID読込ｴﾗｰ
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾀﾞｲｱﾛｸﾞ表示､治具交換
LD M2521                 ; 供給ｷｬﾘｱID照合NG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K404 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2522                 ; 供給ｷｬﾘｱID照合NG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
LDP B1024                ; ﾘﾄﾗｲ確認TP-PB
AND X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
OUT M2954                ; Err404:供給ｷｬﾘｱ照合NG
LD M2523                 ; 供給ｷｬﾘｱID照合NG解除確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K19 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;投入ｼﾌﾄRB受け取り移動
LD M2580                 ; 動作条件確認
ANB X330C                ; Sup_Shift_RB_Busy
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K3 D2524             ; Sup_Shift_RB_POS_確定
LD M2581                 ; 投入ｼﾌﾄRB受取位置移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2582                 ; 位置決完了
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB X330C                ; Sup_Shift_RB_Busy
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K80 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2583                 ; ｷｬﾘｱ引込戻
AND Y2181                ; ｷｬﾘｱ引込戻SV
ANB Y2180                ; ｷｬﾘｱ引込SV
MPS
LDB M36                  ; 払出ﾓｰﾄﾞ
ORB M90                  ; 払出ﾓｰﾄﾞｼﾌﾄ動作開始
ANL
MOV K25 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LD M36                   ; 払出ﾓｰﾄﾞ
AND M90                  ; 払出ﾓｰﾄﾞｼﾌﾄ動作開始
ANL
MOV K35 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｹｰｽID照合
LD M2525                 ; 投入ｼﾌﾄ､ﾊﾟﾚｯﾄID読取完(ﾌﾞﾛｰへ)
MPS
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MPS
AND M1601                ; ｼﾌﾄID照合要求
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MRD
AND M91                  ; 投入ﾓｰﾄﾞﾘﾀｰﾝｼﾌﾄ動作開始
MOV K36 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LD M36                   ; 払出ﾓｰﾄﾞ
ANB M66                  ; 全ﾕﾆｯﾄﾜｰｸ無
AND M90                  ; 払出ﾓｰﾄﾞｼﾌﾄ動作開始
ANL
MOV K35 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MOV K65 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2526                 ; 投入ｼﾌﾄﾌﾚｰﾑ･ｷｬﾘｱID照合
AND W02.13               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合完了
MPS
ANB W02.14               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合NG
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND W02.14               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合NG
MOV K28 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2527                 ; 投入ｼﾌﾄﾌﾚｰﾑ･ｷｬﾘｱ照合OK
ANB W02.13               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合完了
ANB W02.14               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合NG
MOV K35 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2528                 ; 投入ｼﾌﾄﾌﾚｰﾑ･ｷｬﾘｱ照合NG
ANB W02.13               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合完了
ANB W02.14               ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合NG
MOV K21 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;払出ﾓｰﾄﾞ基板搬送
LD M2530                 ; 供給ｼﾌﾄ払出ﾓｰﾄﾞ基板搬送可
LD M5104                 ; 搬送可確認
LDB X124E                ; 基板投入CV読取ﾜｰｸ確認
ANB X126B                ; 基板投入CVﾜｰｸ有
ORL
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2531                 ; 払出ﾓｰﾄﾞ基板搬送開始確認
ANB M5104                ; 搬送可確認
MOV K35 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｹｰｽID照合OK
LD M2535                 ; 投入ｼﾌﾄ側 ID照合OK
ANB M1601                ; ｼﾌﾄID照合要求
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｹｰｽ投入可･完了確認
;※排出ﾓｰﾄﾞ時は投入ｼﾌﾄにｷｬﾘｱ投入されてから実行
LD M2536                 ; 投入ｼﾌﾄ側投入可(RBへ)
MPS
LD M1608                 ; ｼﾌﾄ投入完了(投入ｼﾌﾄへ)
OR M91                   ; 投入ﾓｰﾄﾞﾘﾀｰﾝｼﾌﾄ動作開始
ANL
@MOV D17010 D16155       ; UT4ﾌﾚｰﾑ投入ｲﾝﾃﾞｯｸｽ(投入ｼﾌﾄ用)  UT4投入ｲﾝﾃﾞｯｸｽNo
MPS
@BMOV D2956 D16156 K2    ; 供給側ﾊﾟﾚｯﾄID  UT4ﾊﾟﾚｯﾄID
MPP
AND= D2956 D16156        ; 供給側ﾊﾟﾚｯﾄID  UT4ﾊﾟﾚｯﾄID
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND M36                  ; 払出ﾓｰﾄﾞ
MPS
LD M90                   ; 払出ﾓｰﾄﾞｼﾌﾄ動作開始
AND X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
ANB M66                  ; 全ﾕﾆｯﾄﾜｰｸ無
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MOV K65 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2537                 ; ﾌﾚｰﾑ投入完了OFF確認
ANB M1608                ; ｼﾌﾄ投入完了(投入ｼﾌﾄへ)
MOV K40 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
FMOV K0 D2956 K2         ; 供給側ﾊﾟﾚｯﾄID
;再投入＆日常点検時　ﾊﾟﾚｯﾄID書き込み
LD M2539                 ; ﾊﾟﾚｯﾄID書き込み
@BMOV D2956 D16156 K2    ; 供給側ﾊﾟﾚｯﾄID  UT4ﾊﾟﾚｯﾄID
FMOV K0 D2956 K2         ; 供給側ﾊﾟﾚｯﾄID
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｼﾌﾄ引込戻､ｼﾌﾄ投入側移動
LD M2540                 ; NOP
ANB X330C                ; Sup_Shift_RB_Busy
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K1 D2524             ; Sup_Shift_RB_POS_確定
LD M2541                 ; 投入ｼﾌﾄRB投入側移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2542                 ; 位置決完了
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K40 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2543                 ; ｷｬﾘｱ引込戻
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2544                 ; NOP
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮き確認
LD M2545                 ; ﾜｰｸ浮き確認
MPS
LD X1166                 ; 投入ｼﾌﾀﾜｰｸ浮き確認1
AND X1167                ; 投入ｼﾌﾀﾜｰｸ浮き確認2
ANL
MOV K51 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X1166                ; 投入ｼﾌﾀﾜｰｸ浮き確認1
ORB X1167                ; 投入ｼﾌﾀﾜｰｸ浮き確認2
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮きNG
LD M2546                 ; ﾜｰｸ浮きNG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K401 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2547                 ; ﾜｰｸ浮きNG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
OUT M2951                ; Err401:ﾜｰｸ浮きNG
LD M2548                 ; ﾜｰｸ浮きNG解除確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K45 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｽﾗｲﾀﾞｱﾝﾛｯｸ
LD M2551                 ; ｷｬﾘｱ位置決開
AND X1168                ; 投入ｼﾌﾄ位置決解除端
MPS
ANB M36                  ; 払出ﾓｰﾄﾞ
MOV K70 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
AND M36                  ; 払出ﾓｰﾄﾞ
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾘﾆｺﾝ投入準備位置移動
LD M2570                 ; 投入ｼﾌﾄ投入可2(ﾘﾆｺﾝへ)
MPS
AND M3012                ; ｷｬﾘｱ投入要求(ｼﾌﾄ投入へ)
MOV K52 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB M3012                ; ｷｬﾘｱ投入要求(ｼﾌﾄ投入へ)
AND M3412                ; 投入ｼﾌﾀ投入予備動作要求
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K3 D2534             ; Sup_RB_POS_確定
LD M2571                 ; ｷｬﾘｱ投入RB送
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2572                 ; 起動確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
MOV K52 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾘﾆｺﾝ投入準備完・投入要求待ち
LD M2552                 ; 投入ｼﾌﾄ投入可(ﾘﾆｺﾝへ)
MPS
LDB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3012                ; ｷｬﾘｱ投入要求(ｼﾌﾄ投入へ)
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K2 D2534             ; Sup_RB_POS_確定
MPP
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MOV K65 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2553                 ; ｷｬﾘｱ投入RB送
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2554                 ; 起動確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ﾘﾆｺﾝ投入完了戻り
LD M2555                 ; 投入ｼﾌﾄ投入完了(ﾘﾆｺﾝへ)
ANB M3012                ; ｷｬﾘｱ投入要求(ｼﾌﾄ投入へ)
MOV K75 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K1 D2534             ; Sup_RB_POS_確定
LD M2575                 ; 供給ｼﾌﾀ_PCﾃﾞｰﾀｼﾌﾄ完了待ち
AND M3015                ; 投入ｼﾌﾄ投入完了
MOV K56 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2556                 ; ｷｬﾘｱ投入RB戻
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2557                 ; ｷｬﾘｱ投入RB戻起動確認
ANB X3352                ; ｼﾌﾄ投入要求(SO202)
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2558                 ; ｷｬﾘｱ投入RB戻確認
MPS
LD X1164                 ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
ANB W02.0                ; ﾘﾆｱｺﾝﾍﾞｱｼﾌﾄ完了
ANL
MOV K62 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
ANB X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｷｬﾘｱ飛出NG
LD M2559                 ; ｷｬﾘｱ飛出NG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K402 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2560                 ; ｷｬﾘｱ飛出NG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
ANP B1024                ; ﾘﾄﾗｲ確認TP-PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
OUT M2952                ; Err402:ｷｬﾘｱ飛出NG
LD M2561                 ; ｷｬﾘｱ飛出NG解除確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K58 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;投入要求OFF確認､ｼﾌﾄ戻
LD M2562                 ; 投入完了
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
ANB X330C                ; Sup_Shift_RB_Busy
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K2 D2524             ; Sup_Shift_RB_POS_確定
LD M2563                 ; 投入ｼﾌﾄRB引込側移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2564                 ; 位置決完了
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
MOV K89 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K62 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｷｬﾘｱ有ｻｲｸﾙ停止
LD M2565                 ; ｼﾌﾄ位置決
MPS
LD<> D2528 K2            ; Sup_Shift_RB_完了POS
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
AND X1169                ; 投入ｼﾌﾄ位置決端
ANL
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MOV K2 D2524             ; Sup_Shift_RB_POS_確定
MPP
AND= D2528 K2            ; Sup_Shift_RB_完了POS
MOV K68 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2566                 ; 投入ｼﾌﾄRB引込側移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
LD M2567                 ; 起動確認
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
@INC D2500               ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K65 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M2568                ; 位置決完了
MOV K88 D2500            ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M2588                 ; ﾕﾆｯﾄ4ｻｲｸﾙ停止処理
@MOV K89 D2500           ; ﾕﾆｯﾄ4自動運転ｽﾃｯﾌﾟ
;<h1/>■工程処理 PLC→PC I/F
LD M2526                 ; 投入ｼﾌﾄﾌﾚｰﾑ･ｷｬﾘｱID照合
AND M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
LD M3601                 ; ﾌﾚｰﾑ･ｷｬﾘｱID照合
ANB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ORL
OUT W0102.13             ; 投入ﾌﾚｰﾑ･ｷｬﾘｱ照合要求
;<h1/>■原点復帰
;□□□□□□□□□□□□□□□
;□　　 UNIT4原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
ZRES M2750 M2799         ; ｷｬﾘｱ確認  ﾕﾆｯﾄ4原点復帰完了確認
LDB M2984                ; ﾕﾆｯﾄ4原点復帰中
MOV K0 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
DW K0 D2710              ; UT4 origin STEP monitor
LD M2984                 ; ﾕﾆｯﾄ4原点復帰中
MOV D2510 Z1             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
SET M2750:Z1             ; ｷｬﾘｱ確認
CAL+ D2510 K2750 D2710   ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ  UT4 origin STEP monitor
;原点復帰ｻｲｸﾙ監視
LD M2984                 ; ﾕﾆｯﾄ4原点復帰中
TMR K400 K300            ; UNIT4原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
;ｽﾗｲﾀﾞ有無確認
LD M2750                 ; ｷｬﾘｱ確認
LDB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
LD M95                   ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
AND M1980                ; ﾕﾆｯﾄ2原点
ORL
ANL
@FMOV K0 D16100 K100     ; UT4ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPS
ANB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
AND X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
MOV K2 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｼﾌﾄ位置決解除
LD M2751                 ; ｷｬﾘｱﾛｯｸ解除
AND X1168                ; 投入ｼﾌﾄ位置決解除端
MOV K3 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｼﾌﾄ位置決
LD M2752                 ; ｷｬﾘｱﾛｯｸ
AND X1169                ; 投入ｼﾌﾄ位置決端
MOV K3 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ引込確認
LD M2753                 ; ｷｬﾘｱ引込位置確認
MPS
LDB X1160                ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
ANB X1161                ; ｷｬﾘｱ引込戻端(CV側)
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K405 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
LD X1160                 ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
OR X1161                 ; ｷｬﾘｱ引込戻端(CV側)
ANL
MOV K5 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ引込　位置不明　（作業者判断）
LD M2754                 ; ｷｬﾘｱ引込位置不定異常
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
LD B1028                 ; ｷｬﾘｱ引込 前進PB
OR Y2180                 ; ｷｬﾘｱ引込SV
ANB Y2181                ; ｷｬﾘｱ引込戻SV
ANL
OUT M2880                ; ｷｬﾘｱ引込前進PB
CON
AND X1160                ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
MOV K5 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
LD B1029                 ; ｷｬﾘｱ引込 後退PB
OR Y2181                 ; ｷｬﾘｱ引込戻SV
ANB Y2180                ; ｷｬﾘｱ引込SV
ANL
OUT M2881                ; ｷｬﾘｱ引込後退PB
CON
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
MOV K5 D2510             ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ干渉確認
LD M2755                 ; ｷｬﾘｱ干渉確認
AND X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
MOV K10 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ投入RB原点確認
LD M2760                 ; 投入ｼﾌﾄRB原点確認
MPS
ANB X330B                ; Sup_Shift_RB_ORG-S
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
AND X330B                ; Sup_Shift_RB_ORG-S
MOV K19 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2761                 ; NOP
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;投入ｼﾌﾄRB 干渉覚悟の予備原点復帰
LD M2762                 ; 投入ｼﾌﾄRB予備原点復帰
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2763                 ; 投入ｼﾌﾄRB予備原点復帰確認
ANB X330C                ; Sup_Shift_RB_Busy
MPS
LD X330D                 ; Sup_Shift_RB_END
AND X330B                ; Sup_Shift_RB_ORG-S
ANL
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MOV K3 D2524             ; Sup_Shift_RB_POS_確定
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB X330B                ; Sup_Shift_RB_ORG-S
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K11 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;投入ｼﾌﾄRB待機移動
LD M2764                 ; 投入ｼﾌﾄRB待機移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2765                 ; 位置決完了
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB X330C                ; Sup_Shift_RB_Busy
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K10 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ引込　戻し
LD M2766                 ; ｷｬﾘｱ引込戻
AND X1161                ; ｷｬﾘｱ引込戻端(CV側)
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;投入ｼﾌﾄRB原点復帰
LD M2767                 ; 投入ｼﾌﾄRB原点復帰
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2768                 ; 投入ｼﾌﾄRB原点復帰確認
ANB X330C                ; Sup_Shift_RB_Busy
MPS
LD X330D                 ; Sup_Shift_RB_END
AND X330B                ; Sup_Shift_RB_ORG-S
ANL
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB X330B                ; Sup_Shift_RB_ORG-S
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K16 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;投入ｼﾌﾄRB動作判定(ｷｬﾘｱ引込位置で判定)
LD M2769                 ; 投入ｼﾌﾄRB原点復帰完了
ANB X330C                ; Sup_Shift_RB_Busy
MOV K23 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MOV K2 D2524             ; Sup_Shift_RB_POS_確定
;投入ｼﾌﾄRB引込側移動
LD M2773                 ; 投入ｼﾌﾄRB引込側移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2774                 ; 位置決完了
ANB X330C                ; Sup_Shift_RB_Busy
MPS
AND X330D                ; Sup_Shift_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
LDB X330D                ; Sup_Shift_RB_END
ANB X330C                ; Sup_Shift_RB_Busy
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANL
MOV K23 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ投入RB原点確認
LD M2775                 ; ｷｬﾘｱ投入RB原点確認
MPS
ANB X332B                ; Sup_RB_ORG-S
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MPP
AND X332B                ; Sup_RB_ORG-S
MOV K28 D2510            ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;ｷｬﾘｱ投入RB原点復帰
LD M2776                 ; ｷｬﾘｱ投入RB原点復帰
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2777                 ; ｷｬﾘｱ投入RB原点確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
AND X332B                ; Sup_RB_ORG-S
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2778                 ; ｷｬﾘｱ原点復帰完了確認
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
MOV K1 D2534             ; Sup_RB_POS_確定
;ｷｬﾘｱ投入RB戻
LD M2779                 ; ｷｬﾘｱ投入RB戻
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2780                 ; 位置決完了
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
@INC D2510               ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M2781                 ; 原点確認
AND X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
AND M2980                ; ﾕﾆｯﾄ4原点
@MOV K48 D2510           ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
LD M2798                 ; ﾕﾆｯﾄ4原点復帰完了
ANB M82                  ; 原点復帰開始
@MOV K49 D2510           ; ﾕﾆｯﾄ4原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT4手動回路  　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M2985                ; ﾕﾆｯﾄ4起動中
ANB M2984                ; ﾕﾆｯﾄ4原点復帰中
LD M95                   ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANB X3236                ; Blow_RB_自動運転中
ORB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANL
MC
;供給ｼﾌﾄ引込
LD= D2528 K2             ; Sup_Shift_RB_完了POS
AND X1168                ; 投入ｼﾌﾄ位置決解除端
ANB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ANB X330C                ; Sup_Shift_RB_Busy
ANB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
OUT M2800                ; ｼﾌﾄ引込可
LD B10E0                 ; UNIT4-1-1 TP-PB
AND M2800                ; ｼﾌﾄ引込可
OR M2801                 ; ｼﾌﾄ引込
ANPB M2803               ; ｷｬﾘｱ引込戻
OUT M2801                ; ｼﾌﾄ引込
LD= D2528 K1             ; Sup_Shift_RB_完了POS
LD= D2528 K2             ; Sup_Shift_RB_完了POS
ANB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ORL
OR= D2528 K3             ; Sup_Shift_RB_完了POS
ORB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ANB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
OUT M2802                ; ｼﾌﾄ引込戻可
LD B10E1                 ; UNIT4-1-2 TP-PB
AND M2802                ; ｼﾌﾄ引込戻可
OR M2803                 ; ｷｬﾘｱ引込戻
ANPB M2801               ; ｼﾌﾄ引込
OUT M2803                ; ｷｬﾘｱ引込戻
;供給ｼﾌﾄﾛｯｸ開閉
LD CR2002                ; 常時ON
LD M95                   ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANB X3236                ; Blow_RB_自動運転中
ORB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANL
OUT M2804                ; ｼﾌﾄﾛｯｸ閉可
LD B10E2                 ; UNIT4-1-3 TP-PB
AND M2804                ; ｼﾌﾄﾛｯｸ閉可
OR M2805                 ; ｼﾌﾄﾛｯｸ閉
ANPB M2807               ; ｼﾌﾄﾛｯｸ開
OUT M2805                ; ｼﾌﾄﾛｯｸ閉
LD CR2002                ; 常時ON
LD M95                   ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANB X3236                ; Blow_RB_自動運転中
ORB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ANL
OUT M2806                ; ｼﾌﾄﾛｯｸ開可
LD B10E3                 ; UNIT4-1-4 TP-PB
AND M2806                ; ｼﾌﾄﾛｯｸ開可
OR M2807                 ; ｼﾌﾄﾛｯｸ開
ANPB M2805               ; ｼﾌﾄﾛｯｸ閉
OUT M2807                ; ｼﾌﾄﾛｯｸ開
;ｼﾌﾄRB投入側移動
LD= D2538 K1             ; Sup_RB_完了POS
ORB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
LD X1169                 ; 投入ｼﾌﾄ位置決端
ORB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ANL
ANB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2821                ; 投入ｼﾌﾄRB待機移動開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
AND X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
AND X330B                ; Sup_Shift_RB_ORG-S
ANB X330C                ; Sup_Shift_RB_Busy
OUT M2810                ; ｼﾌﾄRB投入移動可
LD B10E4                 ; UNIT4-1-5 TP-PB
AND M2810                ; ｼﾌﾄRB投入移動可
OR M2811                 ; 投入ｼﾌﾄRB投入移動開始
ANB M2814                ; 位置決完了確認
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
MPS
OUT M2811                ; 投入ｼﾌﾄRB投入移動開始
MPP
ANB M2813                ; 位置決完了OFF確認
OUT M2812                ; ｼﾌﾄRB投入移動
MOV K2 D2524             ; Sup_Shift_RB_POS_確定
LD M2812                 ; ｼﾌﾄRB投入移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
OR M2813                 ; 位置決完了OFF確認
AND M2811                ; 投入ｼﾌﾄRB投入移動開始
OUT M2813                ; 位置決完了OFF確認
LD M2813                 ; 位置決完了OFF確認
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
OR M2814                 ; 位置決完了確認
AND M2811                ; 投入ｼﾌﾄRB投入移動開始
OUT M2814                ; 位置決完了確認
;ｼﾌﾄRB引込側移動
LD X1160                 ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
OR X1161                 ; ｷｬﾘｱ引込戻端(CV側)
ANB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2821                ; 投入ｼﾌﾄRB待機移動開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
AND X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
AND X330B                ; Sup_Shift_RB_ORG-S
ANB X330C                ; Sup_Shift_RB_Busy
OUT M2815                ; ｼﾌﾄRB引込移動可
LD B10E5                 ; UNIT4-1-6 TP-PB
AND M2815                ; ｼﾌﾄRB引込移動可
OR M2816                 ; 投入ｼﾌﾄRB引込移動開始
ANB M2819                ; 位置決完了確認
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
MPS
OUT M2816                ; 投入ｼﾌﾄRB引込移動開始
MPP
ANB M2818                ; 位置決完了OFF確認
OUT M2817                ; ｼﾌﾄRB引込移動
MOV K1 D2524             ; Sup_Shift_RB_POS_確定
LD M2817                 ; ｼﾌﾄRB引込移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
OR M2818                 ; 位置決完了OFF確認
AND M2816                ; 投入ｼﾌﾄRB引込移動開始
OUT M2818                ; 位置決完了OFF確認
LD M2818                 ; 位置決完了OFF確認
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
OR M2819                 ; 位置決完了確認
AND M2816                ; 投入ｼﾌﾄRB引込移動開始
OUT M2819                ; 位置決完了確認
;ｼﾌﾄRB待機移動
LDB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2821                ; 投入ｼﾌﾄRB待機移動開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
AND X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
AND X330B                ; Sup_Shift_RB_ORG-S
ANB X330C                ; Sup_Shift_RB_Busy
OUT M2820                ; ｼﾌﾄRB待機移動可
LD B10E6                 ; UNIT4-1-7 TP-PB
AND M2820                ; ｼﾌﾄRB待機移動可
OR M2821                 ; 投入ｼﾌﾄRB待機移動開始
ANB M2824                ; 位置決完了確認
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
MPS
OUT M2821                ; 投入ｼﾌﾄRB待機移動開始
MPP
ANB M2823                ; 位置決完了OFF確認
OUT M2822                ; ｼﾌﾄRB待機移動
MOV K3 D2524             ; Sup_Shift_RB_POS_確定
LD M2822                 ; ｼﾌﾄRB待機移動
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
OR M2823                 ; 位置決完了OFF確認
AND M2821                ; 投入ｼﾌﾄRB待機移動開始
OUT M2823                ; 位置決完了OFF確認
LD M2823                 ; 位置決完了OFF確認
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
OR M2824                 ; 位置決完了確認
AND M2821                ; 投入ｼﾌﾄRB待機移動開始
OUT M2824                ; 位置決完了確認
;投入RB送移動
LD= D2528 K2             ; Sup_Shift_RB_完了POS
OR= D2528 K3             ; Sup_Shift_RB_完了POS
ORB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
AND X332B                ; Sup_RB_ORG-S
ANB X332C                ; Sup_RB_Busy
OUT M2825                ; ｷｬﾘｱ投入RB送可
LD B10E8                 ; UNIT4-1-9 TP-PB
AND M2825                ; ｷｬﾘｱ投入RB送可
OR M2826                 ; ｷｬﾘｱ投入RB送開始
ANB M2829                ; 位置決完了確認
MPS
OUT M2826                ; ｷｬﾘｱ投入RB送開始
MPP
ANB M2828                ; 位置決完了OFF確認
OUT M2827                ; ｷｬﾘｱ投入RB送
MOV K2 D2534             ; Sup_RB_POS_確定
LD M2827                 ; ｷｬﾘｱ投入RB送
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
OR M2828                 ; 位置決完了OFF確認
AND M2826                ; ｷｬﾘｱ投入RB送開始
OUT M2828                ; 位置決完了OFF確認
LD M2828                 ; 位置決完了OFF確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
OR M2829                 ; 位置決完了確認
AND M2826                ; ｷｬﾘｱ投入RB送開始
OUT M2829                ; 位置決完了確認
;投入RB戻移動
LD= D2528 K2             ; Sup_Shift_RB_完了POS
OR= D2528 K3             ; Sup_Shift_RB_完了POS
ORB X1162                ; 投入ｼﾌﾀ引込ｷｬﾘｱ確認
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
AND X332B                ; Sup_RB_ORG-S
ANB X332C                ; Sup_RB_Busy
OUT M2830                ; ｷｬﾘｱ投入RB戻移動可
LD B10E9                 ; UNIT4-1-10 TP-PB
AND M2830                ; ｷｬﾘｱ投入RB戻移動可
OR M2831                 ; ｷｬﾘｱ投入RB戻開始
ANB M2834                ; 位置決完了確認
MPS
OUT M2831                ; ｷｬﾘｱ投入RB戻開始
MPP
ANB M2833                ; 位置決完了OFF確認
OUT M2832                ; ｷｬﾘｱ投入RB戻
MOV K1 D2534             ; Sup_RB_POS_確定
LD M2832                 ; ｷｬﾘｱ投入RB戻
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
OR M2833                 ; 位置決完了OFF確認
AND M2831                ; ｷｬﾘｱ投入RB戻開始
OUT M2833                ; 位置決完了OFF確認
LD M2833                 ; 位置決完了OFF確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
OR M2834                 ; 位置決完了確認
AND M2831                ; ｷｬﾘｱ投入RB戻開始
OUT M2834                ; 位置決完了確認
;投入ｼﾌﾄRB原点復帰
LDB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
ANB X330C                ; Sup_Shift_RB_Busy
OUT M2835                ; 投入ｼﾌﾄRB原点復帰可
LD B10E7                 ; UNIT4-1-8 TP-PB
AND M2835                ; 投入ｼﾌﾄRB原点復帰可
OR M2836                 ; 投入ｼﾌﾄRB原点復帰開始
ANB M2839                ; 位置決完了確認
MPS
OUT M2836                ; 投入ｼﾌﾄRB原点復帰開始
MPP
ANB M2838                ; 位置決完了OFF確認
OUT M2837                ; 投入ｼﾌﾄRB原点復帰
LD M2837                 ; 投入ｼﾌﾄRB原点復帰
AND X330C                ; Sup_Shift_RB_Busy
ANB X330D                ; Sup_Shift_RB_END
OR M2838                 ; 位置決完了OFF確認
AND M2836                ; 投入ｼﾌﾄRB原点復帰開始
OUT M2838                ; 位置決完了OFF確認
LD M2838                 ; 位置決完了OFF確認
ANB X330C                ; Sup_Shift_RB_Busy
AND X330D                ; Sup_Shift_RB_END
AND X330B                ; Sup_Shift_RB_ORG-S
OR M2839                 ; 位置決完了確認
AND M2836                ; 投入ｼﾌﾄRB原点復帰開始
OUT M2839                ; 位置決完了確認
;投入RB原点復帰
LDB M2811                ; 投入ｼﾌﾄRB投入移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2816                ; 投入ｼﾌﾄRB引込移動開始
ANB M2826                ; ｷｬﾘｱ投入RB送開始
ANB M2831                ; ｷｬﾘｱ投入RB戻開始
ANB M2836                ; 投入ｼﾌﾄRB原点復帰開始
ANB M2841                ; ｷｬﾘｱ投入RB原点復帰開始
ANB X332C                ; Sup_RB_Busy
OUT M2840                ; ｷｬﾘｱ投入RB原点復帰可
LD B10EA                 ; UNIT4-1-11 TP-PB
AND M2840                ; ｷｬﾘｱ投入RB原点復帰可
OR M2841                 ; ｷｬﾘｱ投入RB原点復帰開始
ANB M2844                ; 位置決完了確認
MPS
OUT M2841                ; ｷｬﾘｱ投入RB原点復帰開始
MPP
ANB M2843                ; 位置決完了OFF確認
OUT M2842                ; ｷｬﾘｱ投入RB原点復帰
LD M2842                 ; ｷｬﾘｱ投入RB原点復帰
AND X332C                ; Sup_RB_Busy
ANB X332D                ; Sup_RB_END
OR M2843                 ; 位置決完了OFF確認
AND M2841                ; ｷｬﾘｱ投入RB原点復帰開始
OUT M2843                ; 位置決完了OFF確認
LD M2843                 ; 位置決完了OFF確認
ANB X332C                ; Sup_RB_Busy
AND X332D                ; Sup_RB_END
AND X332B                ; Sup_RB_ORG-S
OR M2844                 ; 位置決完了確認
AND M2841                ; ｷｬﾘｱ投入RB原点復帰開始
OUT M2844                ; 位置決完了確認
MCR
;<h1/>■出力回路
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;供給ｼﾌﾄ引込
LDB M2502                ; ｷｬﾘｱ引込
ANB M2801                ; ｼﾌﾄ引込
ANB M2880                ; ｷｬﾘｱ引込前進PB
INV
LD Y2180                 ; ｷｬﾘｱ引込SV
ANB Y2181                ; ｷｬﾘｱ引込戻SV
ORL
AND M123                 ; 安全扉正常
OUT Y2180                ; ｷｬﾘｱ引込SV
LDB M2504                ; ｷｬﾘｱ引込戻
ANB M2543                ; ｷｬﾘｱ引込戻
ANB M2583                ; ｷｬﾘｱ引込戻
ANB M2772                ; ｷｬﾘｱ引込戻
ANB M2803                ; ｷｬﾘｱ引込戻
ANB M2881                ; ｷｬﾘｱ引込後退PB
ANB M2766                ; ｷｬﾘｱ引込戻
INV
LD Y2181                 ; ｷｬﾘｱ引込戻SV
ANB Y2180                ; ｷｬﾘｱ引込SV
ORL
AND M123                 ; 安全扉正常
OUT Y2181                ; ｷｬﾘｱ引込戻SV
;ｷｬﾘｱﾛｯｸ開閉
LDB M2551                ; ｷｬﾘｱ位置決開
ANB M2751                ; ｷｬﾘｱﾛｯｸ解除
ANB M2807                ; ｼﾌﾄﾛｯｸ開
INV
AND M123                 ; 安全扉正常
LD Y2182                 ; 投入ｼﾌﾄ位置決解除SV
ANB Y2183                ; 投入ｼﾌﾄ位置決SV
ORL
OUT Y2182                ; 投入ｼﾌﾄ位置決解除SV
LDB M2509                ; ｼﾌﾄ位置決
ANB M2565                ; ｼﾌﾄ位置決
ANB M2752                ; ｷｬﾘｱﾛｯｸ
ANB M2805                ; ｼﾌﾄﾛｯｸ閉
INV
AND M123                 ; 安全扉正常
LD Y2183                 ; 投入ｼﾌﾄ位置決SV
ANB Y2182                ; 投入ｼﾌﾄ位置決解除SV
ORL
OUT Y2183                ; 投入ｼﾌﾄ位置決SV
;■その他制御出力
;ロボット出力
;供給ｼﾌﾄﾛﾎﾞｯﾄ
LDB M2541                ; 投入ｼﾌﾄRB投入側移動
ANB M2563                ; 投入ｼﾌﾄRB引込側移動
ANB M2566                ; 投入ｼﾌﾄRB引込側移動
ANB M2764                ; 投入ｼﾌﾄRB待機移動
ANB M2773                ; 投入ｼﾌﾄRB引込側移動
INV
LDB M2581                ; 投入ｼﾌﾄRB受取位置移動
INV
ORL
LDB M2812                ; ｼﾌﾄRB投入移動
ANB M2817                ; ｼﾌﾄRB引込移動
ANB M2822                ; ｼﾌﾄRB待機移動
INV
ORL
@MOV D2524 D2526         ; Sup_Shift_RB_POS_確定  Sup_Shift_RB_指令POS
MPS
LD M123                  ; 安全扉正常
ANB M130                 ; 全体PAUSE
ANL
TMR K490 K2              ; Sup_Shift_RB_POS確定ﾃﾞｨﾚｲ
MPP
AND T490                 ; Sup_Shift_RB_POS確定ﾃﾞｨﾚｲ
OUT Y460D                ; Sup_Sift_RB_Start
LD CR2002                ; 常時ON
MPS
AND D2526.0              ; Sup_Sift_RB_指令POS1
OUT Y4600                ; Sup_Sift_RB_Pin1
MRD
AND D2526.1              ; Sup_Sift_RB_指令POS2
OUT Y4601                ; Sup_Sift_RB_Pin2
MRD
AND D2526.2              ; Sup_Sift_RB_指令POS4
OUT Y4602                ; Sup_Sift_RB_Pin4
MRD
AND D2526.3              ; Sup_Sift_RB_指令POS8
OUT Y4603                ; Sup_Sift_RB_Pin8
MRD
AND D2526.4              ; Sup_Sift_RB_指令POS16
OUT Y4604                ; Sup_Sift_RB_Pin16
MRD
AND D2526.5              ; Sup_Sift_RB_指令POS32
OUT Y4605                ; Sup_Sift_RB_Pin32
MRD
AND D2526.6              ; Sup_Sift_RB_指令POS64
OUT Y4606                ; Sup_Sift_RB_Pin64
MPP
AND D2526.7              ; Sup_Sift_RB_指令POS128
OUT Y4607                ; Sup_Sift_RB_Pin128
LDB CR2003               ; 常時OFF
INV
OUT Y4608                ; Sup_Sift_RB_JOG+
LDB CR2003               ; 常時OFF
INV
OUT Y4609                ; Sup_Sift_RB_JOG-
LDB CR2003               ; 常時OFF
INV
OUT Y460A                ; Sup_Sift_RB_Manu
LDB M2762                ; 投入ｼﾌﾄRB予備原点復帰
ANB M2767                ; 投入ｼﾌﾄRB原点復帰
ANB M2837                ; 投入ｼﾌﾄRB原点復帰
INV
OUT Y460B                ; Sup_Sift_RB_ORG
LD M123                  ; 安全扉正常
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
OUT Y460C                ; Sup_Sift_RB_IL
LD M16                   ; ﾘｾｯﾄPB
ANB X330E                ; Sup_Shift_RB_Alarm
OUT Y460E                ; Sup_Sift_RB_Reset
LD M50                   ; 運転準備
OUT Y460F                ; Sup_Sift_RB_SV_ON
;投入ﾛﾎﾞｯﾄ
LDB M2553                ; ｷｬﾘｱ投入RB送
ANB M2556                ; ｷｬﾘｱ投入RB戻
ANB M2571                ; ｷｬﾘｱ投入RB送
ANB M2779                ; ｷｬﾘｱ投入RB戻
INV
LDB M2827                ; ｷｬﾘｱ投入RB送
ANB M2832                ; ｷｬﾘｱ投入RB戻
ANB M3310                ; 投入ｼﾌﾄ投入起動
ANB M3314                ; 完了OFF確認､ｷｬﾘｱ投入戻
INV
ORL
@MOV D2534 D2536         ; Sup_RB_POS_確定  Sup_RB_指令POS
MPS
TMR K491 K2              ; Sup_RB_POS確定ﾃﾞｨﾚｲ
MPP
AND T491                 ; Sup_RB_POS確定ﾃﾞｨﾚｲ
OUT Y462D                ; Sup_RB_Start
LD CR2002                ; 常時ON
MPS
AND D2536.0              ; Sup_RB_指令POS1
OUT Y4620                ; Sup_RB_Pin1
MRD
AND D2536.1              ; Sup_RB_指令POS2
OUT Y4621                ; Sup_RB_Pin2
MRD
AND D2536.2              ; Sup_RB_指令POS4
OUT Y4622                ; Sup_RB_Pin4
MRD
AND D2536.3              ; Sup_RB_指令POS8
OUT Y4623                ; Sup_RB_Pin8
MRD
AND D2536.4              ; Sup_RB_指令POS16
OUT Y4624                ; Sup_RB_Pin16
MRD
AND D2536.5              ; Sup_RB_指令POS32
OUT Y4625                ; Sup_RB_Pin32
MRD
AND D2536.6              ; Sup_RB_指令POS64
OUT Y4626                ; Sup_RB_Pin64
MPP
AND D2536.7              ; Sup_RB_指令POS128
OUT Y4627                ; Sup_RB_Pin128
LD CR2003                ; 常時OFF
OUT Y4628                ; Sup_RB_JOG+
OUT Y4629                ; Sup_RB_JOG-
OUT Y462A                ; Sup_RB_Manu
LDB M2776                ; ｷｬﾘｱ投入RB原点復帰
ANB M2842                ; ｷｬﾘｱ投入RB原点復帰
INV
OUT Y462B                ; Sup_RB_ORG
LD M123                  ; 安全扉正常
OUT Y462C                ; Sup_RB_IL
LD M16                   ; ﾘｾｯﾄPB
ANB X332E                ; Sup_RB_Alarm
OUT Y462E                ; Sup_RB_Reset
LD M50                   ; 運転準備
OUT Y462F                ; Sup_RB_SV_ON
;ﾛﾎﾞｯﾄｻｰﾎﾞ電源
LD M121                  ; 非常停止正常
OUT Y2150                ; 投入ｼﾌﾄﾛﾎﾞｯﾄ電源
LD M121                  ; 非常停止正常
OUT Y2151                ; 投入ｼﾌﾄ排出ﾛﾎﾞｯﾄ電源
;<h1/>■表示
;□□　　パネル表示　　□□
LD X1160                 ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
LD Y2180                 ; ｷｬﾘｱ引込SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E0                ; UNIT4-1-1 TP-PL
LD X1161                 ; ｷｬﾘｱ引込戻端(CV側)
LD Y2181                 ; ｷｬﾘｱ引込戻SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E1                ; UNIT4-1-2 TP-PL
LD X1169                 ; 投入ｼﾌﾄ位置決端
LD Y2183                 ; 投入ｼﾌﾄ位置決SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E2                ; UNIT4-1-3 TP-PL
LD X1168                 ; 投入ｼﾌﾄ位置決解除端
LD Y2182                 ; 投入ｼﾌﾄ位置決解除SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E3                ; UNIT4-1-4 TP-PL
LD= D2528 K2             ; Sup_Shift_RB_完了POS
LD= D2526 K2             ; Sup_Shift_RB_指令POS
AND X330C                ; Sup_Shift_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E4                ; UNIT4-1-5 TP-PL
LD= D2528 K1             ; Sup_Shift_RB_完了POS
LD= D2526 K1             ; Sup_Shift_RB_指令POS
AND X330C                ; Sup_Shift_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E5                ; UNIT4-1-6 TP-PL
LD= D2528 K3             ; Sup_Shift_RB_完了POS
LD= D2526 K3             ; Sup_Shift_RB_指令POS
AND X330C                ; Sup_Shift_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E6                ; UNIT4-1-7 TP-PL
LD X330B                 ; Sup_Shift_RB_ORG-S
LDB X330B                ; Sup_Shift_RB_ORG-S
AND X330C                ; Sup_Shift_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E7                ; UNIT4-1-8 TP-PL
LD= D2538 K2             ; Sup_RB_完了POS
LD= D2536 K2             ; Sup_RB_指令POS
AND X332C                ; Sup_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E8                ; UNIT4-1-9 TP-PL
LD= D2538 K1             ; Sup_RB_完了POS
LD= D2536 K1             ; Sup_RB_指令POS
AND X332C                ; Sup_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15E9                ; UNIT4-1-10 TP-PL
LD X332B                 ; Sup_RB_ORG-S
LDB X332B                ; Sup_RB_ORG-S
AND X332C                ; Sup_RB_Busy
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15EA                ; UNIT4-1-11 TP-PL
;<h1/>■異常
;□□　　異常検出　　□□
;B080(K129)--B08F(K144)  B090(K145)--B09F(K160)
LD Y2180                 ; ｷｬﾘｱ引込SV
ANB X1160                ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
LD Y2181                 ; ｷｬﾘｱ引込戻SV
ANB X1161                ; ｷｬﾘｱ引込戻端(CV側)
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B080                 ; ｷｬﾘｱ引込前後異常
ORL
MPS
TMR K450 K50             ; ｷｬﾘｱ引込前後確認
MPP
AND T450                 ; ｷｬﾘｱ引込前後確認
OUT B080                 ; ｷｬﾘｱ引込前後異常
@DW K128 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0580
AND B080                 ; ｷｬﾘｱ引込前後異常
OUT B0580
LD Y2182                 ; 投入ｼﾌﾄ位置決解除SV
ANB X1168                ; 投入ｼﾌﾄ位置決解除端
LD Y2183                 ; 投入ｼﾌﾄ位置決SV
ANB X1169                ; 投入ｼﾌﾄ位置決端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B081                 ; ｼﾌﾄ位置決動作異常
ORL
MPS
TMR K451 K50             ; ｼﾌﾄ位置決動作確認
MPP
AND T451                 ; ｼﾌﾄ位置決動作確認
OUT B081                 ; ｼﾌﾄ位置決動作異常
@DW K129 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0581
AND B081                 ; ｼﾌﾄ位置決動作異常
OUT B0581
LD X330C                 ; Sup_Shift_RB_Busy
OR T490                  ; Sup_Shift_RB_POS確定ﾃﾞｨﾚｲ
LDB M81                  ; 原点復帰動作中
OR= D2528 K2             ; Sup_Shift_RB_完了POS
ANL
LD X1160                 ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
OR X1161                 ; ｷｬﾘｱ引込戻端(CV側)
ANL
ANB X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ORL
OUT B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
@DW K130 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0582
AND B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
OUT B0582
LDB X330E                ; Sup_Shift_RB_Alarm
AND M50                  ; 運転準備
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B083                 ; 投入ｼﾌﾄRB異常
ORL
OUT B083                 ; 投入ｼﾌﾄRB異常
@DW K131 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0583
AND B083                 ; 投入ｼﾌﾄRB異常
OUT B0583
LDB X332E                ; Sup_RB_Alarm
AND M50                  ; 運転準備
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B084                 ; 投入RB異常
ORL
OUT B084                 ; 投入RB異常
@DW K132 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0584
AND B084                 ; 投入RB異常
OUT B0584
LD M2781                 ; 原点確認
ANB X1165                ; 投入ｼﾌﾀ供給位置ﾜｰｸ確認(ON=無)
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
ORL
OUT B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
@DW K133 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0585
AND B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
OUT B0585
LD X330C                 ; Sup_Shift_RB_Busy
OR T490                  ; Sup_Shift_RB_POS確定ﾃﾞｨﾚｲ
AND<> D2528 K2           ; Sup_Shift_RB_完了POS
AND M81                  ; 原点復帰動作中
OR M2755                 ; ｷｬﾘｱ干渉確認
LD X1160                 ; ｷｬﾘｱ引込端(ｼﾌﾄ側)
OR X1161                 ; ｷｬﾘｱ引込戻端(CV側)
ANL
ANB X1164                ; 投入ｼﾌﾀｷｬﾘｱ通過確認(Lon)
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B086                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾆｺﾝ側)
ORL
OUT B086                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾆｺﾝ側)
@DW K134 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0586
AND B086                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾆｺﾝ側)
OUT B0586
;D200警報
LD= K400 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09A                 ; Err400:ｷｬﾘｱ引込_NG
@DW H009A D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K401 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09B                 ; Err401:ﾜｰｸ浮きNG
@DW H009B D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K402 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09C                 ; Err402:ｷｬﾘｱ飛出NG
@DW H009C D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K403 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09D                 ; Err403:ﾃﾞｰﾀ不明ﾜｰｸNG
@DW H009D D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K404 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09E                 ; Err404:供給ｷｬﾘｱ照合NG
@DW H009E D0
CON
@FIFOW D0 D900           ; 異常内容
LD= K405 D200            ; ｴﾗｰｺｰﾄﾞNo.
OUT B09F                 ; Err405:ｷｬﾘｱ引込位置不定異常
@DW H009F D0
CON
@FIFOW D0 D900           ; 異常内容
;異常ﾌﾞｻﾞｰまとめ
LDB B080                 ; ｷｬﾘｱ引込前後異常
OR B0580
LDB B081                 ; ｼﾌﾄ位置決動作異常
OR B0581
ANL
LDB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
OR B0582
ANL
LDB B083                 ; 投入ｼﾌﾄRB異常
OR B0583
ANL
LDB B084                 ; 投入RB異常
OR B0584
ANL
LDB B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
OR B0585
ANL
LDB B086                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾆｺﾝ側)
OR B0586
ANL
INV
OUT M2989                ; ﾕﾆｯﾄ4異常BZ補助
;Error location display
LDB B080                 ; ｷｬﾘｱ引込前後異常
ANB B081                 ; ｼﾌﾄ位置決動作異常
ANB B082                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾀｰﾝCV側)
ANB B083                 ; 投入ｼﾌﾄRB異常
ANB B084                 ; 投入RB異常
ANB B085                 ; 供給ｼﾌﾄ原点復帰ﾜｰｸ有異常
ANB B086                 ; 投入ｼﾌﾀ乗移り干渉異常(ﾘﾆｺﾝ側)
INV
OUT B0904                ; UT4 Error
END
ENDH
