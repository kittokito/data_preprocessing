DEVICE:128
;MODULE:TSW
;MODULE_TYPE:0
;<h1/>手動 TSW選択
;<h1/>ﾀｯﾁｽｲｯﾁ 手動操作選択ﾌﾗｸﾞ化：B1900-B19FF
;※ｵﾙﾀﾈｲﾄやSET/RESET接点として使用したい場合はﾗﾝﾌﾟのｴﾘｱを使用する
;<h1/>--- 初期設定 ※手動操作選択ﾌﾗｸﾞｴﾘｱのﾜｰﾄﾞ数をTM200へ格納
LD CR2002                ; 常時ON
DW K16 TM200             ; 手動操作選択ﾌﾗｸﾞ ﾜｰﾄﾞ数
;<h1/>--- 画面切り替え確認
LD CR2002                ; 常時ON
MPS
AND<> D0 D42             ; TPｼｽﾃﾑ 表示中ﾍﾟｰｼﾞNO.  表示中ﾍﾟｰｼﾞNo. ｺﾋﾟｰ
OUT M450                 ; VT 画面切替あり
MPP
MOV D0 D42               ; TPｼｽﾃﾑ 表示中ﾍﾟｰｼﾞNO.  表示中ﾍﾟｰｼﾞNo. ｺﾋﾟｰ
;<h1/>--- 手動操作選択 ※有効/無効 画面が手動操作画面範囲外、画面切替発生時は無効
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
AND M33                  ; 手動ﾓｰﾄﾞ
AND>= D0 K201            ; TPｼｽﾃﾑ 表示中ﾍﾟｰｼﾞNO.
AND<= D0 K205            ; TPｼｽﾃﾑ 表示中ﾍﾟｰｼﾞNO.
ANB M450                 ; VT 画面切替あり
OUT M451                 ; 手動動作選択 有効
;<h1/>--- 手動操作選択 例外ｽｲｯﾁ ※ここに選択とは関係ないｽｲｯﾁを纏める
LD CR2003                ; 常時OFF
OUT M452                 ; 手動操作選択 例外ｽｲｯﾁ入力有り
LDB M451                 ; 手動動作選択 有効
MPS
ANB M452                 ; 手動操作選択 例外ｽｲｯﾁ入力有り
FMOV K0 B1900 TM200      ; 投入ｺﾝﾍﾞｱ回転TP-PB  手動操作選択ﾌﾗｸﾞ ﾜｰﾄﾞ数
MPP
FMOV K0 TM100 TM200      ; B1900 1W 記憶  手動操作選択ﾌﾗｸﾞ ﾜｰﾄﾞ数
;<h1/>--- 手動操作選択 検索及び、展開 ※手動操作選択ｴﾘｱの入力を1ﾜｰﾄﾞずつ前状態比較し、一致しない場合処理
LD M451                  ; 手動動作選択 有効
ANB M452                 ; 手動操作選択 例外ｽｲｯﾁ入力有り
OUT M454                 ; 手動操作選択 検索実行
;確認メイン / Check main
LD CR2002                ; 常時ON
DW K0 TM210              ; 手動操作選択ｽｲｯﾁ 検索ｱﾄﾞﾚｽｲﾝﾃﾞｯｸ
MPS
AND M454                 ; 手動操作選択 検索実行
MOV TM200 TM202          ; 手動操作選択ﾌﾗｸﾞ ﾜｰﾄﾞ数  手動操作選択 検索回数
MPP
ANB M454                 ; 手動操作選択 検索実行
MOV K0 TM202             ; 手動操作選択 検索回数
RES M455                 ; 手動選択 変化有り
FOR TM202                ; 手動操作選択 検索回数
LD CR2002                ; 常時ON
LDA TM210                ; 手動操作選択ｽｲｯﾁ 検索ｱﾄﾞﾚｽｲﾝﾃﾞｯｸ
CON
STA Z7
CON
MUL K16
CON
STA Z8
LD<> B1900:Z8 TM100:Z7   ; 投入ｺﾝﾍﾞｱ回転TP-PB  B1900 1W 記憶
AND M454                 ; 手動操作選択 検索実行
OUT M455                 ; 手動選択 変化有り
;ﾌﾗｸﾞ排他処理
LDP M455                 ; 手動選択 変化有り
MPS
AND<> B1900:Z8 K0        ; 投入ｺﾝﾍﾞｱ回転TP-PB
MPS
LDA TM100:Z7             ; B1900 1W 記憶
CON
EORA B1900:Z8            ; 投入ｺﾝﾍﾞｱ回転TP-PB
CON
DMX
CON
STA TM220                ; 手動操作選択 変化Bit Bit No.
MPP
ZRES B1900 B199F         ; 投入ｺﾝﾍﾞｱ回転TP-PB
CON
BSET B1900:Z8 TM220      ; 投入ｺﾝﾍﾞｱ回転TP-PB  手動操作選択 変化Bit Bit No.
MRD
AND= B1900:Z8 K7         ; 投入ｺﾝﾍﾞｱ回転TP-PB
ZRES B1900 B199F         ; 投入ｺﾝﾍﾞｱ回転TP-PB
MPP
BMOV B1900 TM100 TM200   ; 投入ｺﾝﾍﾞｱ回転TP-PB  B1900 1W 記憶  手動操作選択ﾌﾗｸﾞ ﾜｰﾄﾞ数
;for Next
LD CR2002                ; 常時ON
INC TM210                ; 手動操作選択ｽｲｯﾁ 検索ｱﾄﾞﾚｽｲﾝﾃﾞｯｸ
NEXT
;<h1/>--- 手動実行ｽｲｯﾁ ｸﾞﾛｰﾊﾞﾙﾌｧﾝｸｼｮﾝF8 入力許可 ※選択による手動操作の実質のﾄﾘｶﾞ !M459の接点はこのOUTより下で使用する事
LDP B1078                ; ＴＰグローバルファンクションＳＷ
AND M34                  ; ﾒｲﾝ手動操作可能条件
AND M454                 ; 手動操作選択 検索実行
ANB M455                 ; 手動選択 変化有り
OUT M459                 ; VT ｸﾞﾛｰﾊﾞﾙﾌｧﾝｸｼｮﾝSW
END
ENDH
