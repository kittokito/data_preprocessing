DEVICE:128
;MODULE:温度槽運転
;MODULE_TYPE:0
LD CR2002                ; 常時ON
OUT LR301                ; 高温槽
LDB CR2002               ; 常時ON
OUT LR300                ; 低温槽
OUT LR302                ; 冷却槽
OUT LR303                ; 硬化槽
LD LR301                 ; 高温槽
OR LR303                 ; 硬化槽
OUT LR009                ; OFF低温・冷却 ： ON高温・硬化
LD CR2002                ; 常時ON
TMR #0 #20               ; 電源ON確認
LD CR2002                ; 常時ON
TMR #1 #100              ; 電源ON10秒
LD MR200                 ; ｸﾘｰﾆﾝｸﾞ運転中
ANB T3                   ; ｸﾘｰﾆﾝｸﾞ1分ﾊﾟﾙｽ用ﾀｲﾏ
TMR #3 #600              ; ｸﾘｰﾆﾝｸﾞ1分ﾊﾟﾙｽ用ﾀｲﾏ
;<h1/><<冷凍機3分停止処理>>
LDB LR009                ; OFF低温・冷却 ： ON高温・硬化
MPS
LDB MR004                ; 冷凍機2運転
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
OUTC #1 #180             ; 冷凍機停止ｶｳﾝﾀ
MPP
LD MR004                 ; 冷凍機2運転
ANB MR203                ; 運転開始時運転不可
ANL
RES C1                   ; 冷凍機停止ｶｳﾝﾀ
;<h1/><<温度槽起動/停止>>
LDB B0460                ; IF_ﾏｽﾀ槽運転有効
LD LR000                 ; ｳｨｸﾘﾀｲﾏ無効/有効(OFF無効)
AND MR44901              ; 内部ｳｨｰｸﾘｰﾀｲﾏ1出力中
LDB LR000                ; ｳｨｸﾘﾀｲﾏ無効/有効(OFF無効)
AND B0106                ; GP)運転 PB
ORL
ANL
LD B0460                 ; IF_ﾏｽﾀ槽運転有効
AND B0461                ; IF_恒温槽運転指示
ORL
OR MR101                 ; 温度槽運転SW
ANB MR102                ; 温度槽停止SW
ANB MR301                ; 恒温異常集合
AND R1000                ; 電源異常確認
OUT MR101                ; 温度槽運転SW
LDB B0460                ; IF_ﾏｽﾀ槽運転有効
LD LR000                 ; ｳｨｸﾘﾀｲﾏ無効/有効(OFF無効)
ANB MR44901              ; 内部ｳｨｰｸﾘｰﾀｲﾏ1出力中
LDB LR000                ; ｳｨｸﾘﾀｲﾏ無効/有効(OFF無効)
AND B0107                ; GP)停止 PB
ORL
ANL
LD B0460                 ; IF_ﾏｽﾀ槽運転有効
AND B0462                ; IF_恒温槽停止指示
ORL
OR T11                   ; 温度強制停止
OUT MR102                ; 温度槽停止SW
LD MR101                 ; 温度槽運転SW
OR MR109                 ; 温度槽運転記憶
ANB MR301                ; 恒温異常集合
AND R1000                ; 電源異常確認
OUT MR103                ; 温度槽運転
LD MR103                 ; 温度槽運転
TMR #5 #1800             ; 温度槽運転記憶ﾀｲﾏ
LD T5                    ; 温度槽運転記憶ﾀｲﾏ
OR MR109                 ; 温度槽運転記憶
ANB MR206                ; 低温槽停止処理
ANB LR009                ; OFF低温・冷却 ： ON高温・硬化
ANB MR301                ; 恒温異常集合
AND R1000                ; 電源異常確認
OUT MR109                ; 温度槽運転記憶
LD MR103                 ; 温度槽運転
OR MR207                 ; ﾌｧﾝOFFﾃﾞｨﾚｲ
OUT MR308                ; 温度槽運転表示
LDB MR101                ; 温度槽運転SW
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LDB MR103                ; 温度槽運転
ANB MR207                ; ﾌｧﾝOFFﾃﾞｨﾚｲ
ORL
OUT MR309                ; 温度槽停止表示
LD R3000                 ; 冷却水圧力ｽｲｯﾁ
AND R3101                ; 冷凍機1高圧圧力ｽｲｯﾁ(低元側)
AND R3001                ; 冷凍機1低圧圧力ｽｲｯﾁ(低元側)
AND R3102                ; 冷凍機2高圧圧力ｽｲｯﾁ(高元側)
AND R3002                ; 冷凍機2低圧圧力ｽｲｯﾁ(高元側)
AND R3004                ; 冷凍機1吐出管温度
AND R3105                ; 冷凍機2吐出管温度
OUT MR202                ; 冷凍機運転可条件
LDP MR103                ; 温度槽運転
OR MR203                 ; 運転開始時運転不可
ANB MR202                ; 冷凍機運転可条件
ANB LR009                ; OFF低温・冷却 ： ON高温・硬化
ANB T147                 ; 運転開始時運転不可
OUT MR203                ; 運転開始時運転不可
TMR #147 #10             ; 運転開始時運転不可
;<h1/><<温度槽運転>>
LD MR103                 ; 温度槽運転
AND R1000                ; 電源異常確認
ANB MR203                ; 運転開始時運転不可
MC
LD MR103                 ; 温度槽運転
ANB T201                 ; ﾌｧﾝ異常
ANB MR301                ; 恒温異常集合
OUT MR105                ; ﾌｧﾝ運転
TMR #73 #80              ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
LD T73                   ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
LD R1100                 ; ｲﾝﾊﾞｰﾀ1運転確認(ﾌｧﾝ1)
AND R1102                ; ｲﾝﾊﾞｰﾀ2運転確認(ﾌｧﾝ2)
AND R1104                ; ｲﾝﾊﾞｰﾀ3運転確認(ﾌｧﾝ2)
OR MR106                 ; ﾌｧﾝ運転確認
ANL
ANB T201                 ; ﾌｧﾝ異常
AND R1101                ; ｲﾝﾊﾞｰﾀ1一括ｱﾗｰﾑ(ﾌｧﾝ1)
AND R1103                ; ｲﾝﾊﾞｰﾀ2一括ｱﾗｰﾑ(ﾌｧﾝ2)
AND R1105                ; ｲﾝﾊﾞｰﾀ3一括ｱﾗｰﾑ(ﾌｧﾝ2)
OUT MR106                ; ﾌｧﾝ運転確認
LD T73                   ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
ANB LR009                ; OFF低温・冷却 ： ON高温・硬化
LD C1                    ; 冷凍機停止ｶｳﾝﾀ
OR MR004                 ; 冷凍機2運転
ANL
OUT MR004                ; 冷凍機2運転
TMR #6 #6000             ; 冷凍機2運転低圧ｱﾗｰﾑﾃﾞｨﾚｲ
LD MR004                 ; 冷凍機2運転
LD R3114                 ; 冷凍機2運転確認(高元側)
AND R2000                ; 冷凍機2電流ｾﾝｻ
OR MR005                 ; 冷凍機2運転確認
ANL
OUT MR005                ; 冷凍機2運転確認
LD MR005                 ; 冷凍機2運転確認
LD R3005                 ; ｶｽｹｰﾄﾞｺﾝﾃﾞﾝｻ温度
OR T7                    ; 冷凍機ｶｽｹｰﾄﾞ温度到達
ANL
TMR #7 #600              ; 冷凍機ｶｽｹｰﾄﾞ温度到達
LD MR005                 ; 冷凍機2運転確認
AND T7                   ; 冷凍機ｶｽｹｰﾄﾞ温度到達
ANB C8                   ; 冷凍機1停止
OUT MR006                ; 冷凍機1運転
TMR #8 #6000             ; 冷凍機1運転低圧ｱﾗｰﾑﾃﾞｨﾚｲ
LD MR006                 ; 冷凍機1運転
TMR #140 #100            ; 冷凍機1運転10秒
TMR #141 #1200           ; 冷凍機1運転120秒
LD MR006                 ; 冷凍機1運転
LD R3013                 ; 冷凍機1運転確認(低元側)
AND R2008                ; 冷凍機1電流ｾﾝｻ
OR MR007                 ; 冷凍機1運転確認
ANL
OUT MR007                ; 冷凍機1運転確認
LD T73                   ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
LD LR009                 ; OFF低温・冷却 ： ON高温・硬化
ORB LR009                ; OFF低温・冷却 ： ON高温・硬化
ANL
MPS
TMR #15 #30              ; ﾋｰﾀ運転ﾃﾞｨﾚｲ
MPP
AND T15                  ; ﾋｰﾀ運転ﾃﾞｨﾚｲ
OUT MR108                ; ﾋｰﾀ運転
MCR
;<h1/><<冷凍機電磁弁制御>>
LDB LR009                ; OFF低温・冷却 ： ON高温・硬化
AND MR101                ; 温度槽運転SW
AND R1014                ; TC1低_電磁弁用/高_温度ｱﾗｰﾑ　EV2
AND R1114                ; TC2低_電磁弁用/高_温度ｱﾗｰﾑ　EV2
TMR #180 #3000           ; 電磁弁制御開始(5分遅延）
LD T180                  ; 電磁弁制御開始(5分遅延）
MPS
LDB T182                 ; 電磁弁制御周期(2分）
OR C30                   ; 電磁弁制御回数
ANL
TMR #181 #200            ; 電磁弁OFF時間(20秒)
TMR #182 #1200           ; 電磁弁制御周期(2分）
MRD
AND T181                 ; 電磁弁OFF時間(20秒)
OUTC #30 #5              ; 電磁弁制御回数
MPP
ANB T181                 ; 電磁弁OFF時間(20秒)
OUT MR205                ; 電磁弁OFF制御
LDB T180                 ; 電磁弁制御開始(5分遅延）
RES C30                  ; 電磁弁制御回数
;<h1/><<ｸﾘｰﾆﾝｸﾞ運転>>
LDB MR101                ; 温度槽運転SW
AND MR103                ; 温度槽運転
OUT MR200                ; ｸﾘｰﾆﾝｸﾞ運転中
LD MR200                 ; ｸﾘｰﾆﾝｸﾞ運転中
MPS
AND T3                   ; ｸﾘｰﾆﾝｸﾞ1分ﾊﾟﾙｽ用ﾀｲﾏ
OUTC #6 #60              ; ｸﾘｰﾆﾝｸﾞ時間
OUTC #8 #59              ; 冷凍機1停止
OUTC #9 #12              ; ｸﾘｰﾆﾝｸﾞ 12分
MRD
ANB T144                 ; 15秒ｶｳﾝﾀ
TMR #143 #2100           ; 3分30秒ｶｳﾝﾀ
MRD
AND T143                 ; 3分30秒ｶｳﾝﾀ
TMR #144 #150            ; 15秒ｶｳﾝﾀ
MPP
AND T144                 ; 15秒ｶｳﾝﾀ
OUTC #10 #15             ; 3.75分X15　56.25分ｶｳﾝﾀ
LDB MR200                ; ｸﾘｰﾆﾝｸﾞ運転中
RES C6                   ; ｸﾘｰﾆﾝｸﾞ時間
RES C8                   ; 冷凍機1停止
RES C9                   ; ｸﾘｰﾆﾝｸﾞ 12分
RES C10                  ; 3.75分X15　56.25分ｶｳﾝﾀ
LDB LR009                ; OFF低温・冷却 ： ON高温・硬化
ANB B0460                ; IF_ﾏｽﾀ槽運転有効
AND B0108                ; GP)強制停止 PB
TMR #11 #50              ; 温度強制停止
LD T11                   ; 温度強制停止
OR MR504                 ; 強制停止表示用
LD MR103                 ; 温度槽運転
OR MR207                 ; ﾌｧﾝOFFﾃﾞｨﾚｲ
ANL
ANB B0106                ; GP)運転 PB
OUT MR504                ; 強制停止表示用
LD B0108                 ; GP)強制停止 PB
ANB T11                  ; 温度強制停止
LD MR504                 ; 強制停止表示用
ANB CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT MR505                ; 強制停止表示
LD C6                    ; ｸﾘｰﾆﾝｸﾞ時間
OR T11                   ; 温度強制停止
OUT MR206                ; 低温槽停止処理
LD MR105                 ; ﾌｧﾝ運転
OR MR207                 ; ﾌｧﾝOFFﾃﾞｨﾚｲ
ANB T201                 ; ﾌｧﾝ異常
ANB T12                  ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
MPS
OUT MR207                ; ﾌｧﾝOFFﾃﾞｨﾚｲ
MPP
ANB MR103                ; 温度槽運転
TMR #12 #600             ; ﾌｧﾝ運転ﾃﾞｨﾚｲ
;<h1/><<温度到達>>
LD MR101                 ; 温度槽運転SW
AND R1013                ; TC1温度到達 No.1 EV
AND R1113                ; TC2温度到達 No.1 EV
TMR #13 DM14             ; 温度到達ﾀｲﾏ  温度到達時間
LD T13                   ; 温度到達ﾀｲﾏ
OR MR208                 ; 温度到達確認
AND MR101                ; 温度槽運転SW
ANB MR301                ; 恒温異常集合
MPS
LDB T200                 ; 温度異常
ANB T14                  ; 温度到達解除ﾀｲﾏ
ANL
OUT MR208                ; 温度到達確認
MPP
LD LR001                 ; 温度異常無効
LDB R1013                ; TC1温度到達 No.1 EV
ORB R1113                ; TC2温度到達 No.1 EV
ANL
ANL
TMR #14 #100             ; 温度到達解除ﾀｲﾏ
LD MR101                 ; 温度槽運転SW
LD MR208                 ; 温度到達確認
OR CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
ANB T200                 ; 温度異常
OUT MR606                ; 温度槽到達表示
;<h1/><<温度槽運転時間>>
LD T0                    ; 電源ON確認
ANB T4                   ; 1分ﾊﾟﾙｽ用ﾀｲﾏ
TMR #4 #600              ; 1分ﾊﾟﾙｽ用ﾀｲﾏ
LD CR2002                ; 常時ON
MPS
LD MR308                 ; 温度槽運転表示
AND T4                   ; 1分ﾊﾟﾙｽ用ﾀｲﾏ
ANL
OUTC #15 #60             ; 運転時間1時間ｶｳﾝ
MRD
AND C15                  ; 運転時間1時間ｶｳﾝ
@INC.D DM4               ; 積算時間(下位)
RES C15                  ; 運転時間1時間ｶｳﾝ
MPP
AND<=.D #1000000 DM4     ; 積算時間(下位)
MOV.D #999999 DM4        ; 積算時間(下位)
END
ENDH
