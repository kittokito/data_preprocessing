DEVICE:128
;MODULE:自動搬送運転
;MODULE_TYPE:0
;<h1/><<ﾏｽﾀ駆動自動運転>>
LD B0450                 ; IF_READY
OR B0350                 ; GP)ﾒﾝﾃﾅﾝｽREADY PL
LD B0453                 ; IF_ﾏｽﾀ運転開始指示
OR MR2800                ; ﾏｽﾀ運転開始
ANL
ANB B0454                ; IF_ﾏｽﾀ運転停止指示
AND MR400                ; 運転準備ON確認
ANB MR1000               ; 原点復帰開始
ANB MR5000               ; ﾃｰﾌﾞﾙ基準復帰
ANB LR100                ; 自動化/作業者(OFF自動化)
OUT MR2800               ; ﾏｽﾀ運転開始
LD MR2800                ; ﾏｽﾀ運転開始
MC
LD B0458                 ; IF_上段扉開指示
OR MR2802                ; ﾏｽﾀ上扉開指示
ANB MR2804               ; ﾏｽﾀ上扉閉指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T40                  ; 上段扉開端確認
OUT MR2802               ; ﾏｽﾀ上扉開指示
LD B0459                 ; IF_上段扉閉指示
OR MR2804                ; ﾏｽﾀ上扉閉指示
ANB MR2802               ; ﾏｽﾀ上扉開指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T41                  ; 上段扉閉端確認
OUT MR2804               ; ﾏｽﾀ上扉閉指示
LD B045A                 ; IF_中段扉開指示
OR MR2806                ; ﾏｽﾀ中扉開指示
ANB MR2808               ; ﾏｽﾀ中扉閉指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T42                  ; 中段扉開端確認
OUT MR2806               ; ﾏｽﾀ中扉開指示
LD B045B                 ; IF_中段扉閉指示
OR MR2808                ; ﾏｽﾀ中扉閉指示
ANB MR2806               ; ﾏｽﾀ中扉開指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T43                  ; 中段扉閉端確認
OUT MR2808               ; ﾏｽﾀ中扉閉指示
LD B045C                 ; IF_下段扉開指示
OR MR2900                ; ﾏｽﾀ下扉開指示
ANB MR2902               ; ﾏｽﾀ下扉閉指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T44                  ; 下段扉開端確認
OUT MR2900               ; ﾏｽﾀ下扉開指示
LD B045D                 ; IF_下段扉閉指示
OR MR2902                ; ﾏｽﾀ下扉閉指示
ANB MR2900               ; ﾏｽﾀ下扉開指示
ANB MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
ANB T45                  ; 下段扉閉端確認
OUT MR2902               ; ﾏｽﾀ下扉閉指示
LD B045E                 ; IF_ﾃｰﾌﾞﾙ回転指示
OR MR2904                ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
AND MR509                ; 扉閉確認
ANB MR1306               ; 自動ﾃｰﾌﾞﾙ動作終了
OUT MR2904               ; ﾏｽﾀﾃｰﾌﾞﾙ回転指示
MCR
;<h1/><<駆動自動運転>>
LD MR402                 ; 自動ﾓｰﾄﾞ
AND B0114                ; GP)自動開始 PB
AND MR305                ; 搬送機原点
OR MR700                 ; 自動運転開始
AND MR400                ; 運転準備ON確認
ANB MR803                ; 自動運転停止
ANB MR1000               ; 原点復帰開始
ANB MR5000               ; ﾃｰﾌﾞﾙ基準復帰
ANB MR1502               ; 駆動異常集合
LD MR208                 ; 温度到達確認
OR LR001                 ; 温度異常無効
ANL
ANB B0450                ; IF_READY
ANB B0350                ; GP)ﾒﾝﾃﾅﾝｽREADY PL
OUT MR700                ; 自動運転開始
LD MR700                 ; 自動運転開始
OR MR701                 ; 自動運転1ｻｲｸﾙ
ANB MR801                ; 1ｻｲｸﾙ動作終了
AND MR400                ; 運転準備ON確認
ANB MR1502               ; 駆動異常集合
ANB B0450                ; IF_READY
ANB B0350                ; GP)ﾒﾝﾃﾅﾝｽREADY PL
OUT MR701                ; 自動運転1ｻｲｸﾙ
LD MR701                 ; 自動運転1ｻｲｸﾙ
OUT MR804                ; 搬送自動運転中表示
LDP MR700                ; 自動運転開始
OR MR600                 ; 自動運転一回目
AND MR701                ; 自動運転1ｻｲｸﾙ
ANB MR801                ; 1ｻｲｸﾙ動作終了
OUT MR600                ; 自動運転一回目
LD MR700                 ; 自動運転開始
OR MR701                 ; 自動運転1ｻｲｸﾙ
OR MR609                 ; 1ｻｲｸﾙ動作中
OUT MR900                ; 搬送自動運転中
LDB MR900                ; 搬送自動運転中
LD CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
AND MR900                ; 搬送自動運転中
ANB MR700                ; 自動運転開始
ORL
OUT MR805                ; 搬送停止中表示
LD MR700                 ; 自動運転開始
LD CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
AND MR900                ; 搬送自動運転中
ANB MR700                ; 自動運転開始
ORL
OUT MR808                ; 搬送中ﾊﾟﾄﾗｲﾄ表示
LD MR900                 ; 搬送自動運転中
ANB MR700                ; 自動運転開始
OUT MR806                ; 停止動作中
LDP MR700                ; 自動運転開始
OR>.S +0 DM200           ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
MOV.S +0 DM200           ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
LDP T80                  ; ﾀｸﾄﾀｲﾑ
INC.S DM200              ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
LDP MR2100               ; 扉上動作開始
ORP MR2400               ; 扉中動作開始
ORP MR2700               ; 扉下動作開始
DEC.S DM200              ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
LD MR700                 ; 自動運転開始
AND<=.S +1 DM200         ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
ANB MR709                ; ﾃｰﾌﾞﾙ回転動作開始指示
ANB MR710                ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰ
OUT MR611                ; 扉動作開始
LD MR900                 ; 搬送自動運転中
AND MR400                ; 運転準備ON確認
ANB MR1502               ; 駆動異常集合
MC
LDP MR611                ; 扉動作開始
AND DM21.0               ; 上段操作
OR MR703                 ; 扉上動作開始
ANB MR704                ; 扉上動作終了
OUT MR703                ; 扉上動作開始
LD MR703                 ; 扉上動作開始
AND MR903                ; 扉上自動動作終了
OUT MR704                ; 扉上動作終了
LDP MR611                ; 扉動作開始
AND DM21.1               ; 中段操作
OR MR705                 ; 扉中動作開始
ANB MR706                ; 扉中動作終了
OUT MR705                ; 扉中動作開始
LD MR705                 ; 扉中動作開始
AND MR904                ; 扉中自動動作終了
OUT MR706                ; 扉中動作終了
LDP MR611                ; 扉動作開始
AND DM21.2               ; 下段操作
OR MR707                 ; 扉下動作開始
ANB MR708                ; 扉下動作終了
OUT MR707                ; 扉下動作開始
LD MR707                 ; 扉下動作開始
AND MR905                ; 扉下自動動作終了
OUT MR708                ; 扉下動作終了
;<h1/><<ﾃｰﾌﾞﾙ回転動作>>
LDB CR2002               ; 常時ON
ANP MR2008               ; 扉上閉確認
LDB CR2002               ; 常時ON
ANP MR2308               ; 扉中閉確認
ORL
LD CR2002                ; 常時ON
ANP MR2608               ; 扉下閉確認
ORL
OR MR709                 ; ﾃｰﾌﾞﾙ回転動作開始指示
ANB MR800                ; ﾃｰﾌﾞﾙ回転動作終了
OUT MR709                ; ﾃｰﾌﾞﾙ回転動作開始指示
LD MR1306                ; 自動ﾃｰﾌﾞﾙ動作終了
OR MR800                 ; ﾃｰﾌﾞﾙ回転動作終了
AND MR709                ; ﾃｰﾌﾞﾙ回転動作開始指示
OUT MR800                ; ﾃｰﾌﾞﾙ回転動作終了
LD CR2002                ; 常時ON
LDP MR2008               ; 扉上閉確認
ORP MR2308               ; 扉中閉確認
ORP MR2608               ; 扉下閉確認
ANL
OR MR710                 ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰ
ANB T25                  ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰﾃﾞｨﾚｲ
OUT MR710                ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰ
TMR #25 #20              ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰﾃﾞｨﾚｲ
MCR
LD MR703                 ; 扉上動作開始
OR MR705                 ; 扉中動作開始
OR MR707                 ; 扉下動作開始
OR MR709                 ; ﾃｰﾌﾞﾙ回転動作開始指示
OR MR710                 ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰ
AND MR400                ; 運転準備ON確認
ANB MR1502               ; 駆動異常集合
OUT MR609                ; 1ｻｲｸﾙ動作中
LD T80                   ; ﾀｸﾄﾀｲﾑ
LD MR700                 ; 自動運転開始
AND>.S +2 DM200          ; ﾃｰﾌﾞﾙ/ﾀｸﾄ_ｶｳﾝﾀ
LDB MR700                ; 自動運転開始
AND MR509                ; 扉閉確認
ORL
ANL
OR MR401                 ; 手動ﾓｰﾄﾞ
ANB MR709                ; ﾃｰﾌﾞﾙ回転動作開始指示
ANB MR710                ; ﾃｰﾌﾞﾙ回転ﾀﾞﾐｰ
OR MR801                 ; 1ｻｲｸﾙ動作終了
AND MR701                ; 自動運転1ｻｲｸﾙ
OUT MR801                ; 1ｻｲｸﾙ動作終了
LD MR900                 ; 搬送自動運転中
AND MR701                ; 自動運転1ｻｲｸﾙ
ANB MR401                ; 手動ﾓｰﾄﾞ
TMR #80 DM10             ; ﾀｸﾄﾀｲﾑ  ﾀｸﾄﾀｲﾑ設定値
LD CR2002                ; 常時ON
MOV.D T80 DM12           ; ﾀｸﾄﾀｲﾑ  ﾀｸﾄﾀｲﾑ表示値
LD B0115                 ; GP)自動停止 PB
OR T200                  ; 温度異常
OUT MR803                ; 自動運転停止
LD CR2002                ; 常時ON
LDP MR2008               ; 扉上閉確認
ORP MR2308               ; 扉中閉確認
ORP MR2608               ; 扉下閉確認
ANL
MPS
LDA DM21                 ; 扉開箇所記憶
CON
SLA #1
CON
STA DM21                 ; 扉開箇所記憶
MPP
AND> DM21 #4             ; 扉開箇所記憶
MOV #1 DM21              ; 扉開箇所記憶
END
ENDH
