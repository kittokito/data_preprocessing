DEVICE:128
;MODULE:搬送異常処理
;MODULE_TYPE:0
;<h1/><<非常停止>>
LD R1001                 ; 非常停止
LD R1004                 ; ﾀｯﾁﾊﾟﾈﾙ非常停止
ORB R1005                ; 中間端子台ｷｰｽｲｯﾁ
ANL
INV
OUT MR909                ; 非常停止
;<h1/><<搬送異常処理>>
LDB MR300                ; 異常ﾘｾｯﾄ
MC
LD R500                  ; 上ｽﾗｲﾄﾞ扉開
ANB T40                  ; 上段扉開端確認
LD R501                  ; 上ｽﾗｲﾄﾞ扉閉
ANB T41                  ; 上段扉閉端確認
ORL
OR T230                  ; 上段扉異常
ANB MR2509               ; ｴﾘｱｾﾝｻON
TMR #230 #100            ; 上段扉異常
LD R502                  ; 中ｽﾗｲﾄﾞ扉開
ANB T42                  ; 中段扉開端確認
LD R503                  ; 中ｽﾗｲﾄﾞ扉閉
ANB T43                  ; 中段扉閉端確認
ORL
OR T231                  ; 中段扉異常
ANB MR2509               ; ｴﾘｱｾﾝｻON
TMR #231 #100            ; 中段扉異常
LD R504                  ; 下ｽﾗｲﾄﾞ扉開
ANB T44                  ; 下段扉開端確認
LD R505                  ; 下ｽﾗｲﾄﾞ扉閉
ANB T45                  ; 下段扉閉端確認
ORL
OR T232                  ; 下段扉異常
ANB MR2509               ; ｴﾘｱｾﾝｻON
TMR #232 #100            ; 下段扉異常
LDB MR5000               ; ﾃｰﾌﾞﾙ基準復帰
MOV #550 DM350           ; ﾃｰﾌﾞﾙ1週時間
CAL/ DM350 DM22 DM352    ; ﾃｰﾌﾞﾙ1週時間  ﾃｰﾌﾞﾙ分割数  ﾃｰﾌﾞﾙ1ﾋﾟｯﾁ移動時間
CAL* DM352 DM24 DM354    ; ﾃｰﾌﾞﾙ1ﾋﾟｯﾁ移動時間  ﾃｰﾌﾞﾙ移動数  ﾃｰﾌﾞﾙ移動時間
CAL+.D DM354 #100 DM356  ; ﾃｰﾌﾞﾙ移動時間  ﾃｰﾌﾞﾙ異常時間
LD MR5000                ; ﾃｰﾌﾞﾙ基準復帰
MOV.D #700 DM356         ; ﾃｰﾌﾞﾙ異常時間
LD R1504                 ; ﾃｰﾌﾞﾙ正転
OR R1505                 ; ﾃｰﾌﾞﾙ逆転
OR T233                  ; ﾃｰﾌﾞﾙ異常
TMR #233 DM356           ; ﾃｰﾌﾞﾙ異常  ﾃｰﾌﾞﾙ異常時間
LD R1504                 ; ﾃｰﾌﾞﾙ正転
OR R1505                 ; ﾃｰﾌﾞﾙ逆転
AND R1006                ; ﾃｰﾌﾞﾙｻｰﾏﾙ
OR R1006                 ; ﾃｰﾌﾞﾙｻｰﾏﾙ
OR T234                  ; ﾃｰﾌﾞﾙｻｰﾏﾙﾄﾘｯﾌﾟ
TMR #234 #5              ; ﾃｰﾌﾞﾙｻｰﾏﾙﾄﾘｯﾌﾟ
LD R1506                 ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ戻
ANB T65                  ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ戻端確認
LD R1507                 ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ出
ANB T66                  ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ出端確認
ORL
OR T235                  ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ異常
TMR #235 #100            ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ異常
LD T0                    ; 電源ON確認
AND MR3610               ; ﾃｰﾌﾞﾙNo.ｴﾗｰ
OR T240                  ; ﾃｰﾌﾞﾙNo.ｴﾗｰ
TMR #240 #0              ; ﾃｰﾌﾞﾙNo.ｴﾗｰ
MCR
LDB T230                 ; 上段扉異常
ANB T231                 ; 中段扉異常
ANB T232                 ; 下段扉異常
ANB T233                 ; ﾃｰﾌﾞﾙ異常
ANB T234                 ; ﾃｰﾌﾞﾙｻｰﾏﾙﾄﾘｯﾌﾟ
ANB T235                 ; ﾃｰﾌﾞﾙｽﾄｯﾊﾟ異常
ANB T240                 ; ﾃｰﾌﾞﾙNo.ｴﾗｰ
INV
OUT MR1502               ; 駆動異常集合
LDB CR2002               ; 常時ON
OUT MR1509               ; 復旧異常
LD MR1502                ; 駆動異常集合
OR MR302                 ; 異常集合
OR MR303                 ; 温度異常
OR MR1509                ; 復旧異常
OR MR909                 ; 非常停止
OR MR3901                ; 警告POP表示中
MPS
ANB MR1507               ; ﾌﾞｻﾞｰ停止
MPS
OUT MR1504               ; ﾌﾞｻﾞｰ警報出力
MPP
AND T221                 ; 空圧異常
TMR #29 #200             ; 空圧異常ﾌﾞｻﾞｰ停止ﾀｲﾏ
MPP
LD B0101                 ; GP)ﾌﾞｻﾞｰ停止 PB
OR B0466                 ; IF_ﾌﾞｻﾞｰ停止指示
OR T29                   ; 空圧異常ﾌﾞｻﾞｰ停止ﾀｲﾏ
OR MR1507                ; ﾌﾞｻﾞｰ停止
ANPB MR5100              ; 新異常発生
ANL
OUT MR1507               ; ﾌﾞｻﾞｰ停止
LD MR302                 ; 異常集合
OR MR303                 ; 温度異常
OR MR1502                ; 駆動異常集合
OR MR1509                ; 復旧異常
MPS
OUT MR1505               ; GP用異常表示
MPP
LDB MR303                ; 温度異常
ANB MR1509               ; 復旧異常
ANL
OUT MR1506               ; ﾊﾟﾄﾗｲﾄ用異常表示
;<h1/><<ｴﾘｱｾﾝｻ処理処理>>
LD R2109                 ; ｴﾘｱｾﾝｻ
TMR #18 #10              ; ｴﾘｱｾﾝｻOFFﾃﾞｨﾚｲ
LDB T18                  ; ｴﾘｱｾﾝｻOFFﾃﾞｨﾚｲ
LD MR2500                ; 上ｽﾗｲﾄﾞ扉開補助
OR MR2501                ; 上ｽﾗｲﾄﾞ扉閉補助
OR MR2502                ; 中ｽﾗｲﾄﾞ扉開補助
OR MR2503                ; 中ｽﾗｲﾄﾞ扉閉補助
OR MR2504                ; 下ｽﾗｲﾄﾞ扉開補助
OR MR2505                ; 下ｽﾗｲﾄﾞ扉閉補助
ANL
LDB LR002                ; 耐久回路選択
OR MR401                 ; 手動ﾓｰﾄﾞ
ANL
ANB LR004                ; ｴﾘｱｾﾝｻ無効
AND LR100                ; 自動化/作業者(OFF自動化)
OUT MR2509               ; ｴﾘｱｾﾝｻON
END
ENDH
