DEVICE:53
;MODULE:UT9_PCB_Sup_CV
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD CR2003                ; 常時OFF
OUT M251                 ; 20141013 koike
LD X1250                 ; 搬送上流ｽﾄｯﾊﾟ下降端
TMR K910 K5              ; 搬送上流ｽﾄｯﾊﾟ下降端
LD X1251                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
TMR K911 K3              ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD X1252                 ; 搬送下流ｽﾄｯﾊﾟ下降端
TMR K912 K3              ; 搬送下流ｽﾄｯﾊﾟ下降端
LD X1253                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
TMR K913 K3              ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD X1254                 ; 搬送位置決め閉端
TMR K914 K5              ; 搬送位置決め閉端
LD X1255                 ; 搬送位置決め開端
TMR K915 K3              ; 搬送位置決め開端
LD X1256                 ; 基板搬送送端
TMR K916 K3              ; 基板搬送送端
LD X1257                 ; 基板搬送戻端
TMR K917 K3              ; 基板搬送戻端
LD X1258                 ; 基板搬送爪下降端
TMR K918 K3              ; 基板搬送爪下降端
LD X1259                 ; 基板搬送爪上昇端
TMR K919 K3              ; 基板搬送爪上昇端
LD X125D                 ; 基板CV幅変更ﾊﾝﾄﾞﾙﾄﾞｱ閉
AND X125E                ; 基板搬送CV装置側引戸
AND X125F                ; 基板搬送CV投入側引戸
TMR K920 K5              ; 基板CV扉閉確認
LDB X124D                ; 基板投入CVﾜｰｸ確認
TMR K921 K40             ; ?i?hA?g??guU???C?3?m?hF
LD X1236                 ; E-cap sensor input
TMR K922 K5              ; E-cap sensor
LDB M251                 ; 20141013 koike
MPS
AND X1235                ; EntPCB_BCR_上昇端
TMR K923 K3              ; EntPCB_BCR上昇端
MPP
AND X1237                ; EntPCB_BCR_下降端
TMR K924 K3              ; EntPCB_BCR下降端
;□ ﾕﾆｯﾄ原点
LD T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
OR CR2002                ; 常時ON
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
AND T915                 ; 搬送位置決め開端
AND T917                 ; 基板搬送戻端
AND T919                 ; 基板搬送爪上昇端
LD T923                  ; EntPCB_BCR上昇端
OR M250                  ; 20141010 koike
ANL
OUT M5480                ; ﾕﾆｯﾄ9原点
;□ 非常停止
LDB CR2003               ; 常時OFF
INV
OUT M5481                ; ﾕﾆｯﾄ9全異常
;□ 起動条件
LD M5480                 ; ﾕﾆｯﾄ9原点
OUT M5482                ; ﾕﾆｯﾄ9起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
OR M5486                 ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M5483                ; ﾕﾆｯﾄ9起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
OR M5484                 ; ﾕﾆｯﾄ9原点復帰中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
ANB M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
ANB M5299                ; ﾕﾆｯﾄ9原点終了
OUT M5484                ; ﾕﾆｯﾄ9原点復帰中
;□ 起動中
LD M73                   ; 各工程起動指令
AND M5482                ; ﾕﾆｯﾄ9起動条件
OR M5485                 ; ﾕﾆｯﾄ9起動中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
LDB M5099                ; ﾕﾆｯﾄ9-1ｻｲｸﾙ停止
ORB M5149                ; ﾕﾆｯﾄ9-3ｻｲｸﾙ終了
ANL
OUT M5485                ; ﾕﾆｯﾄ9起動中
;□ ｻｲｸﾙ停止異常
LDB B0121                ; 基板供給CV画像検査異常
ANB B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
ANB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
INV
OUT M5486                ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB M5033                ; ID_NG払出ｻｲｸﾙ停止
ANB CR2003               ; 常時OFF
INV
OUT M5487                ; ﾕﾆｯﾄ9払出停止異常
;□ 警報補助
LDB M5010                ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
INV
OUT M5488                ; ﾕﾆｯﾄ9警報補助
;□ 機器準備確認
LD B2015                 ; PA検査ﾚﾃﾞｨ
OUT M5490                ; ﾕﾆｯﾄ9機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
ANB B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
ANB B0125                ; 基板搬送CV位置決異常
ANB B0126                ; 基板搬送爪送り異常
ANB B0127                ; 基板搬送爪上下異常
LDB B012A                ; EntPCB_BCR上昇端異常
ANB B012B                ; EntPCB_BCR下降端異常
OR M251                  ; 20141013 koike
ANL
INV
OUT M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD= D5000 D5005          ; ﾕﾆｯﾄ9-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-1動作中判定
ORB M5485                ; ﾕﾆｯﾄ9起動中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
TMR K901 K20             ; ﾕﾆｯﾄ9動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D5000 D5005          ; ﾕﾆｯﾄ9-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-1動作中判定
LD T901                  ; ﾕﾆｯﾄ9動作無し判定ﾀｲﾏ
OUT M5492                ; ﾕﾆｯﾄ9動作無し確認
;□ 払出しﾜｰｸ無し
LDB X124E                ; 基板投入CV読取ﾜｰｸ確認
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
ANB X126B                ; 基板投入CVﾜｰｸ有
LD T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
OR M5004                 ; 基板ｺﾝﾍﾞｱ回転
ANL
TMR K905 K10             ; UNIT9払出処理ﾜｰｸ無し
LD T905                  ; UNIT9払出処理ﾜｰｸ無し
OUT M5493                ; ﾕﾆｯﾄ9ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M5479                ; ﾕﾆｯﾄ9ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　 　UNIT9自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>■自動回路 
;<h1/>/ 基板投入部
LD CR2002                ; 常時ON
ZRES M5000 M5099         ; 基板投入部ｻｲｸﾙ開始  ﾕﾆｯﾄ9-1ｻｲｸﾙ停止
LDB M5485                ; ﾕﾆｯﾄ9起動中
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5485                 ; ﾕﾆｯﾄ9起動中
MOV D5001 Z1             ; OA?PA9-2?c?gR?n^?g]?A?PIs
OUT M5000:Z1             ; 基板投入部ｻｲｸﾙ開始
;払出･投入抑制判定
;Discharge or Outage decision
LD M5000                 ; 基板投入部ｻｲｸﾙ開始
MPS
LDB M36                  ; 払出ﾓｰﾄﾞ
ANB M153                 ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
OR X124E                 ; 基板投入CV読取ﾜｰｸ確認
ANB M90                  ; 払出ﾓｰﾄﾞﾘﾀｰﾝｼﾌﾄ動作開始
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LDB X124E                ; 基板投入CV読取ﾜｰｸ確認
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M153                  ; Re-Issue MODE
OR M3710                 ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
OR M3711                 ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANL
ANL
MOV K90 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5090                 ; 投入抑制中
MPS
LDB M153                 ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANL
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
LD M153                  ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANL
OUT W0107.12             ; ﾗﾍﾞﾙ再発行品読取要求
MPP
AND W0A.12               ; ﾗﾍﾞﾙ再発行品読取完了
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5091                 ; ﾗﾍﾞﾙ再発行品読取完了確認
ANB W0A.12               ; ﾗﾍﾞﾙ再発行品読取完了
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;初期動作ｺﾝﾍﾞｱ回転
LD M5001                 ; ｺﾝﾍﾞｱ投入回転
MPS
AND Y230C                ; 基板供給CV正転
TMR K935 K20             ; 基板無確認
MPP
AND T935                 ; 基板無確認
MPS
ANB X124E                ; 基板投入CV読取ﾜｰｸ確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND X124E                ; 基板投入CV読取ﾜｰｸ確認
MOV K6 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ﾜｰｸ無引込
LD M5002                 ; 引込可能確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5003                 ; 基板投入ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5004                 ; 基板ｺﾝﾍﾞｱ回転
MPS
LDB M151                 ; Bonding Daily Check MODE
ANB M152                 ; Re-Enter MODE
ANB M153                 ; Re-Issue MODE
ANL
MPS
AND X124E                ; 基板投入CV読取ﾜｰｸ確認
TMR K936 K5              ; 基板到着確認
MRD
AND T936                 ; 基板到着確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LD T921                  ; ?i?hA?g??guU???C?3?m?hF
OR T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND M36                  ; 払出ﾓｰﾄﾞ
ANL
MOV K17 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LD M151                  ; Bonding Daily Check MODE
OR M152                  ; Re-Enter MODE
OR M153                  ; Re-Issue MODE
ANL
MOV K90 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ﾓｰﾄﾞ判定
;Mode decision
LD M5005                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
MPS
ANB M150                 ; CC Master Check MODE
MPS
ANB M36                  ; 払出ﾓｰﾄﾞ
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND M36                  ; 払出ﾓｰﾄﾞ
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND M150                 ; CC Master Check MODE
MOV K45 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5017                 ; 払い出しﾓｰﾄﾞｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｲﾝﾃﾞｯｸｽNoｾｯﾄ
LD M5006                 ; 搬送CVｲﾝﾃﾞｯｸｽNoｾｯﾄ
ANB W01.0                ; 基板読込完了
MPS
AND>= K0 D5030           ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
AND<= K10 D5030          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
MOV D5030 W0130          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ  基板投入部ｲﾝﾃﾞｯｸｽNo.
MRD
AND M251                 ; 20141013 koike
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB M251                 ; 20141013 koike
MOV K20 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;<h1/>/ 基板ｺｰﾄﾞ読取
;基板投入不可､IDｴﾗｰ発生時はﾀﾞｲｱﾛｸﾞ表示で処理選択
LDB M251                 ; 20141013 koike
MPS
LD M5020                 ; EntPCB読取ｼﾘﾝﾀﾞｰ下降
AND T924                 ; EntPCB_BCR下降端
ANB B012B                ; EntPCB_BCR下降端異常
ANL
MOV K7 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
LD M5007                 ; 基板ｺｰﾄﾞ読込要求
LD W01.0                 ; 基板読込完了
OR W01.2                 ; 製品IDｴﾗｰ､段取
ANL
ANL
MOV K21 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
LD M5021                 ; EntPCB読取ｼﾘﾝﾀﾞｰ上昇
AND T923                 ; EntPCB_BCR上昇端
ANB B012A                ; EntPCB_BCR上昇端異常
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND M5022                ; 基板読取り完了後判定
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
LD W07.1                 ; ?u”M?”?U’…?U“h?z?L?3
AND T922                 ; E-cap sensor
LDB W07.1                ; ?u”M?”?U’…?U“h?z?L?3
ANB T922                 ; E-cap sensor
ORL
ANL
ANL
MOV K40 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
@BMOV W020 D5400 K100    ; ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT9_ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
@MOV W07 D5463           ; 8_機種情報BITﾃﾞｰﾀ  UT9機種情報
MPP
LD W01.1                 ; 基板投入不可
LD W07.1                 ; ?u”M?”?U’…?U“h?z?L?3
ANB T922                 ; E-cap sensor
ORL
LDB W07.1                ; ?u”M?”?U’…?U“h?z?L?3
AND T922                 ; E-cap sensor
ORL
ANL
MOV K8 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LD W01.2                 ; 製品IDｴﾗｰ､段取
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K900 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K10 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M251                  ; 20141013 koike
AND M5007                ; 基板ｺｰﾄﾞ読込要求
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
LD W07.1                 ; ?u”M?”?U’…?U“h?z?L?3
AND T922                 ; E-cap sensor
LDB W07.1                ; ?u”M?”?U’…?U“h?z?L?3
ANB T922                 ; E-cap sensor
ORL
ANL
ANL
MOV K40 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
@BMOV W020 D5400 K100    ; ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT9_ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
@MOV W07 D5463           ; 8_機種情報BITﾃﾞｰﾀ  UT9機種情報
MPP
LD W01.1                 ; 基板投入不可
LD W07.1                 ; ?u”M?”?U’…?U“h?z?L?3
ANB T922                 ; E-cap sensor
ORL
LDB W07.1                ; ?u”M?”?U’…?U“h?z?L?3
AND T922                 ; E-cap sensor
ORL
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LD W01.2                 ; 製品IDｴﾗｰ､段取
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K900 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K10 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5008                 ; 投入不可確認
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;製品IDｴﾗｰ･ﾀﾞｲｱﾛｸﾞ選択
LD M5010                 ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
AND B1020                ; ﾀﾞｲｱﾛｸﾞ選択PB1
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
AND B1021                ; ﾀﾞｲｱﾛｸﾞ選択PB2
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K12 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND B1022                ; ﾀﾞｲｱﾛｸﾞ選択PB3
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K25 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;治具IDｴﾗｰ･基板取出処理(B1020)
LD M5011                 ; ﾜｰｸ取出選択
MPS
OUT W0101.2              ; ID異常時取出選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;治具IDｴﾗｰ･ﾜｰｸ治具交換選択(B1021)
LD M5012                 ; 治具交換選択
AND M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
AND M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5013                 ; 段取一時停止処理
AND M2056                ; 段取ﾌﾞﾛｰﾃｰﾌﾞﾙ1後退確認
AND M2156                ; 段取ﾌﾞﾛｰﾃｰﾌﾞﾙ2後退確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5014                 ; 段取時ﾌﾞﾛｰﾃｰﾌﾞﾙ後退確認､POSE要求
AND M130                 ; 全体Pose
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5015                 ; 一時停止確認
MPS
OUT W0101.3              ; ID異常時段取選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5016                 ; IDｴﾗｰ解除確認
ANB M130                 ; 全体Pose
MOV K7 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;治具IDｴﾗｰ･基板取出後ｻｲｸﾙ停止(B1022)
LD M5025                 ; ｻｲｸﾙ停止選択
MPS
OUT W0101.1              ; ID異常時ｻｲｸﾙ停止選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
MPS
AND X124D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB X124D                ; 基板投入CVﾜｰｸ確認
MOV K27 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5026                 ; 投入位置ﾜｰｸ有取出要求
ANB X124D                ; 基板投入CVﾜｰｸ確認
ANP X124C                ; 基板供給確認PB
MOV K25 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5027                 ; ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5028                 ; ｺﾝﾍﾞｱ排出逆転
MPS
TMR K931 K80             ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
MPP
LD T931                  ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
ORF X124D                ; 基板投入CVﾜｰｸ確認
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5029                 ; 基板取出要求
ANP X124C                ; 基板供給確認PB
ANB X124D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5030                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5031                 ; 基板取出要求ON
AND W0101.15             ; 基板投入部取出要求
AND W01.15               ; 基板投入部取出確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5032                 ; 基板取出確認
ANB W0101.15             ; 基板投入部取出要求
ANB W01.15               ; 基板投入部取出確認
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND= W0130 K0            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5033                 ; ID_NG払出ｻｲｸﾙ停止
AND M36                  ; 払出ﾓｰﾄﾞ
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;<h1/>/ ｸﾘｽﾀﾙｷｬｯﾌﾟ検査
;検査有無判定
;Inspection presence or absence
LD M5040                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
MPS
AND D5463.7              ; UT9ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB D5463.7              ; UT9ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
MOV K50 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;検査有効･無効判定
;Inspection enable or disable
LD M5041                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
MPS
LDB W08.12               ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査無効
AND W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
ANL
MPS
AND W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
MOV K50 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND W08.12               ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査無効
MOV K50 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｸﾘｽﾀﾙｷｬｯﾌﾟ検査NG 基板取出へ
;Crystal Cap Inspection NG Return
LD M5042                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査NG確認
ANB W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｸﾘｽﾀﾙｷｬｯﾌﾟ日常点検ﾓｰﾄﾞ
;Crystal Cap Daily check MODE
LD M5045                 ; ｲﾝﾃﾞｯｸｽNo. ｾｯﾄ
ANB W01.0                ; 基板読込完了
MPS
AND>= K0 D5030           ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
AND<= K10 D5030          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
MOV D5030 W0130          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ  基板投入部ｲﾝﾃﾞｯｸｽNo.
MRD
AND M251                 ; 20141013 koike
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB M251                 ; 20141013 koike
MOV K60 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LDB M251                 ; 20141013 koike
MPS
LD M5060                 ; EntPCB読取りｼﾘﾝﾀﾞｰ下降
AND T924                 ; EntPCB_BCR下降端
ANB B012B                ; EntPCB_BCR下降端異常
ANL
MOV K46 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
LD M5046                 ; 基板ｺｰﾄﾞ読込要求
LD W01.0                 ; 基板読込完了
OR W01.2                 ; 製品IDｴﾗｰ､段取
ANL
ANL
MOV K61 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MRD
LD M5061                 ; EntPCB読取りｼﾘﾝﾀﾞｰ上昇
AND T923                 ; EntPCB_BCR上昇端
ANB B012A                ; EntPCB_BCR上昇端異常
ANL
INC D5001                ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND M5062                ; 基板読取り完了後判定
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND W01.1                ; 基板投入不可
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND W01.2                ; 製品IDｴﾗｰ､段取
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M251                  ; 20141013 koike
AND M5046                ; 基板ｺｰﾄﾞ読込要求
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND W01.1                ; 基板投入不可
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND W01.2                ; 製品IDｴﾗｰ､段取
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5047                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
AND W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
LD W05.5                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
ORB W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5048                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査完了
ANB W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;<h1/>/ PA検査
LD M5050                 ; PA検査開始
MPS
AND D5463.3              ; UT9PA検査有無
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB D5463.3              ; UT9PA検査有無
MOV K67 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;PA検査Prg_No.切替
LD M5051                 ; PA検査開始ON
AND W03.0                ; PA検査開始受付
MPS
AND W08.0                ; PA画像検査無効
MOV K64 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
LDB W08.0                ; PA画像検査無効
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MOV D5414 W05C0          ; UT9_PA画像検査No.  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
LD M5052                 ; PA検査　PRO_No切替指令
AND B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5053                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5054                 ; PA検査　PRO_No切替確認
ANB B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M5055                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2010                ; PA検査結果取得可能
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;PA検査実行
LD M5056                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5057                 ; PA検査　指令
AND B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;検査完了結果判定
LD M5058                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
MPS
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
AND B2020                ; PA検査総合判定(OFF:NG ON:OK)
MOV K64 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;PA検査OK
LD M5064                 ; PA検査OK
AND W0103.1              ; PA検査結果
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5065                 ; PA検査OK動作中OFF
ANB W0103.0              ; PA検査開始
ANB W03.0                ; PA検査開始受付
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5066                 ; PA検査結果OFF
ANB W0103.1              ; PA検査結果
ANB W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;<h1/>/ ﾗﾍﾞﾙ発行&照合
;ﾌﾚｰﾑﾗﾍﾞﾙ発行
LD M5067                 ; ﾗﾍﾞﾙ発行要求
MPS
OUT W0101.7              ; ﾌﾚｰﾑﾗﾍﾞﾙ発行要求
MPP
AND W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5068                 ; ﾗﾍﾞﾙ発行確認
ANB W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｹｰｽ投入部より照合判定待ち
LD M5069                 ; ﾗﾍﾞﾙ発行完､ｹｰｽ投入可､照合判定
AND M1015                ; ﾌﾚｰﾑ照合完了､基板搬送可
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5070                 ; ｹｰｽ投入､照合完了確認
ANB M1015                ; ﾌﾚｰﾑ照合完了､基板搬送可
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
;基板搬送可･完了確認
LD M5071                 ; 照合完了搬送可(基板搬送へ)
AND M5111                ; 基板搬送完了(投入部へ)
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5072                 ; 基板搬送確認
ANB M5111                ; 基板搬送完了(投入部へ)
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;基板取出
LD M5080                 ; NG基板排出
ANB W0103.0              ; PA検査開始
MPS
AND X124D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
MPP
ANB X124D                ; 基板投入CVﾜｰｸ確認
MOV K82 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5081                 ; 投入位置ﾜｰｸ有取出要求
ANB X124D                ; 基板投入CVﾜｰｸ確認
ANP X124C                ; 基板供給確認PB
MOV K80 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5082                 ; ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5083                 ; ｺﾝﾍﾞｱ排出逆転
MPS
TMR K932 K80             ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
MPP
LD T932                  ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
ORF X124D                ; 基板投入CVﾜｰｸ確認
ANL
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5084                 ; “C??NG?a?o?v??
ANP X124C                ; 基板供給確認PB
ANB X124D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5085                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5086                 ; 基板取出要求ON
AND W0101.15             ; 基板投入部取出要求
AND W01.15               ; 基板投入部取出確認
@INC D5001               ; OA?PA9-2?c?gR?n^?g]?A?PIs
LD M5087                 ; 基板取出確認
ANB W0101.15             ; 基板投入部取出要求
ANB W01.15               ; 基板投入部取出確認
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND= W0130 K0            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MOV K0 D5001             ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M5000                 ; 基板投入部ｻｲｸﾙ開始
OR M5004                 ; 基板ｺﾝﾍﾞｱ回転
OR M5071                 ; 照合完了搬送可(基板搬送へ)
ANL
MOV K98 D5001            ; OA?PA9-2?c?gR?n^?g]?A?PIs
;ｻｲｸﾙ終了
LD M5098                 ; ﾕﾆｯﾄ9-1ｻｲｸﾙ停止処理
@MOV K99 D5001           ; OA?PA9-2?c?gR?n^?g]?A?PIs
;<h1/>□ 搬送ｺﾝﾍﾞｱ
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M5100 M5149         ; 基板CV搬送開始  ﾕﾆｯﾄ9-3ｻｲｸﾙ終了
LDB M5485                ; ﾕﾆｯﾄ9起動中
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5485                 ; ﾕﾆｯﾄ9起動中
MOV D5002 Z1             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
OUT M5100:Z1             ; 基板CV搬送開始
;自動運転ステップ開始
;取出部基板確認
LD M5100                 ; 基板CV搬送開始
MPS
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
MOV K3 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板有取出要求
LD M5101                 ; 基板取出可(嵌合RBへ)
AND M5550                ; 基板取出完了(基板CVへ)
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5102                 ; 基板取出確認
ANB M5550                ; 基板取出完了(基板CVへ)
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5103                 ; 搬送可待ち
LD M5071                 ; 照合完了搬送可(基板搬送へ)
OR M3650                 ; 再投入ﾓｰﾄﾞ基板搬送可
OR M3680                 ; 点検投入ﾓｰﾄﾞ基板搬送可
LD X124E                 ; 基板投入CV読取ﾜｰｸ確認
OR X126B                 ; 基板投入CVﾜｰｸ有
AND M3690                ; 払出ﾓｰﾄﾞ基板搬送可
ORL
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5104                 ; 搬送可確認
ANB M3650                ; 再投入ﾓｰﾄﾞ基板搬送可
ANB M3680                ; 点検投入ﾓｰﾄﾞ基板搬送可
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板搬送動作
LD M5105                 ; 搬送爪閉
AND T918                 ; 基板搬送爪下降端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5106                 ; 基板CV下流ｽﾄｯﾊﾟ下降
AND T912                 ; 搬送下流ｽﾄｯﾊﾟ下降端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;搬送爪送りでｲﾝﾃﾞｯｸｽｶｳﾝﾄｱｯﾌﾟとｲﾝﾃﾞｯｸｽﾃﾞｰﾀをｼﾌﾄ
LD M5107                 ; 搬送爪送
@INC D5030               ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
@BMOV W0130 W0131 K6     ; 基板投入部ｲﾝﾃﾞｯｸｽNo.  基板搬送1ｲﾝﾃﾞｯｸｽNo.
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND T916                 ; 基板搬送送端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5108                 ; 基板CV下流ｽﾄｯﾊﾟ上昇
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5109                 ; 搬送爪開
AND T919                 ; 基板搬送爪上昇端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5110                 ; 搬送爪戻
AND T917                 ; 基板搬送戻端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板投入へ基板搬送完了ON
LD M5111                 ; 基板搬送完了(投入部へ)
ANB M5071                ; 照合完了搬送可(基板搬送へ)
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5112                 ; 取出部ﾜｰｸ確認
MPS
AND X124F                ; 基板投入CV取出ﾜｰｸ確認
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
ANB X124F                ; 基板投入CV取出ﾜｰｸ確認
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板取出可･確認
LD M5113                 ; 基板取出可ON(基板嵌合へ)
LD M5518                 ; 基板取出完了(照合NG)
OR M5528                 ; 基板取出完了(塗布検査NG)
OR M5550                 ; 基板取出完了(基板CVへ)
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
@MOV K0 W0135            ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
LD M5114                 ; 基板取出完了確認
ANB M5518                ; 基板取出完了(照合NG)
ANB M5528                ; 基板取出完了(塗布検査NG)
ANB M5550                ; 基板取出完了(基板CVへ)
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M5103                ; 搬送可待ち
MOV K48 D5002            ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M5148                 ; ﾕﾆｯﾄ9-3ｻｲｸﾙ停止
@MOV K49 D5002           ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;<h1/>■工程処理 PLC→PC I/F
;Code Read / CC Check
LDB M251                 ; 20141013 koike
ANB M5007                ; 基板ｺｰﾄﾞ読込要求
ANB M5021                ; EntPCB読取ｼﾘﾝﾀﾞｰ上昇
ANB M5022                ; 基板読取り完了後判定
ANB M5046                ; 基板ｺｰﾄﾞ読込要求
ANB M5061                ; EntPCB読取りｼﾘﾝﾀﾞｰ上昇
ANB M5062                ; 基板読取り完了後判定
INV
LD M251                  ; 20141013 koike
LD M5007                 ; 基板ｺｰﾄﾞ読込要求
OR M5046                 ; 基板ｺｰﾄﾞ読込要求
ANL
ORL
OUT W0101.0              ; 基板読込要求
LD M5041                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
OR M5047                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
OUT W0105.4              ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査開始
;PA検査開始･結果
LDB M5031                ; 基板取出要求ON
ANB M5086                ; 基板取出要求ON
INV
OUT W0101.15             ; 基板投入部取出要求
LD M5051                 ; PA検査開始ON
OR W0103.0               ; PA検査開始
ANB M5065                ; PA検査OK動作中OFF
ANB M5080                ; NG基板排出
AND M5485                ; ﾕﾆｯﾄ9起動中
OUT W0103.0              ; PA検査開始
LD M5064                 ; PA検査OK
OR W0103.1               ; PA検査結果
ANB M5066                ; PA検査結果OFF
AND M5485                ; ﾕﾆｯﾄ9起動中
OUT W0103.1              ; PA検査結果
;“?“u??‰A?\?|I×，T
LD M253                  ; 20141021　koike
LD M5005                 ; ｽﾄｯﾊﾟ上昇
OR M5200                 ; 基板投入不可
ANL
ANB M5064                ; PA検査OK
ANB M5072                ; 基板搬送確認
ANB M5087                ; 基板取出確認
AND M5485                ; ﾕﾆｯﾄ9起動中
LD> D5001 K4             ; OA?PA9-2?c?gR?n^?g]?A?PIs
AND<= D5001 K68          ; OA?PA9-2?c?gR?n^?g]?A?PIs
ORL
OUT M5200                ; 基板投入不可
;<h1/>■原点復帰
;□□□□□□□□□□□□□□□
;□　　 UNIT9原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
ZRES M5250 M5299         ; 原点復帰可能確認  ﾕﾆｯﾄ9原点終了
LDB M5484                ; ﾕﾆｯﾄ9原点復帰中
MOV K0 D5010             ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5484                 ; ﾕﾆｯﾄ9原点復帰中
MOV D5010 Z1             ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
OUT M5250:Z1             ; 原点復帰可能確認
;原点復帰ｻｲｸﾙ監視
LD M5484                 ; ﾕﾆｯﾄ9原点復帰中
TMR K900 K300            ; UNIT9原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M5250                 ; 原点復帰可能確認
LD T923                  ; EntPCB_BCR上昇端
OR M251                  ; 20141013 koike
ANL
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5251                 ; 基板搬送爪開
AND T919                 ; 基板搬送爪上昇端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5252                 ; 基板搬送爪戻
AND T917                 ; 基板搬送戻端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5253                 ; 位置決開
AND T915                 ; 搬送位置決め開端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5254                 ; 基板CVｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M5255                 ; 基板搬送原点完了
AND M5480                ; ﾕﾆｯﾄ9原点
@MOV K48 D5010           ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5298                 ; ﾕﾆｯﾄ9原点確認
ANB M82                  ; 原点復帰開始
@MOV K49 D5010           ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT9手動回路  　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND T920                 ; 基板CV扉閉確認
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M5485                ; ﾕﾆｯﾄ9起動中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
MC
;基板CV上流ｽﾄｯﾊﾟ上下
LD X1253                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
OUT M5300                ; 基板CV上流ｽﾄｯﾊﾟ下降可
LD B1180                 ; UNIT9-1-1 TP-PB
AND M5300                ; 基板CV上流ｽﾄｯﾊﾟ下降可
OR M5301                 ; 基板CV上流ｽﾄｯﾊﾟ下昇
ANPB M5303               ; 基板CV上流ｽﾄｯﾊﾟ上昇
OUT M5301                ; 基板CV上流ｽﾄｯﾊﾟ下昇
LD CR2002                ; 常時ON
OUT M5302                ; 基板CV上流ｽﾄｯﾊﾟ上昇可
LD B1181                 ; UNIT9-1-2 TP-PB
AND M5302                ; 基板CV上流ｽﾄｯﾊﾟ上昇可
OR M5303                 ; 基板CV上流ｽﾄｯﾊﾟ上昇
ANPB M5301               ; 基板CV上流ｽﾄｯﾊﾟ下昇
OUT M5303                ; 基板CV上流ｽﾄｯﾊﾟ上昇
;基板CV下流ｽﾄｯﾊﾟ上下
LD X1251                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
OUT M5304                ; 基板CV下流ｽﾄｯﾊﾟ下降可
LD B1182                 ; UNIT9-1-3 TP-PB
AND M5304                ; 基板CV下流ｽﾄｯﾊﾟ下降可
OR M5305                 ; 基板CV下流ｽﾄｯﾊﾟ下昇
ANPB M5307               ; 基板CV下流ｽﾄｯﾊﾟ上昇
OUT M5305                ; 基板CV下流ｽﾄｯﾊﾟ下昇
LD X1256                 ; 基板搬送送端
OR X1257                 ; 基板搬送戻端
OR X1259                 ; 基板搬送爪上昇端
OUT M5306                ; 基板CV下流ｽﾄｯﾊﾟ上昇可
LD B1183                 ; UNIT9-1-4 TP-PB
AND M5306                ; 基板CV下流ｽﾄｯﾊﾟ上昇可
OR M5307                 ; 基板CV下流ｽﾄｯﾊﾟ上昇
ANPB M5305               ; 基板CV下流ｽﾄｯﾊﾟ下昇
OUT M5307                ; 基板CV下流ｽﾄｯﾊﾟ上昇
;基板搬送爪上下
LD CR2002                ; 常時ON
OUT M5308                ; 基板搬送爪閉可
LD B1184                 ; UNIT9-1-5 TP-PB
AND M5308                ; 基板搬送爪閉可
OR M5309                 ; 基板搬送爪閉
ANPB M5311               ; 基板搬送爪開
OUT M5309                ; 基板搬送爪閉
LD CR2002                ; 常時ON
OUT M5310                ; 基板搬送爪開可
LD B1185                 ; UNIT9-1-6 TP-PB
AND M5310                ; 基板搬送爪開可
OR M5311                 ; 基板搬送爪開
ANPB M5309               ; 基板搬送爪閉
OUT M5311                ; 基板搬送爪開
;基板搬送爪送戻
LDB X124F                ; 基板投入CV取出ﾜｰｸ確認
AND X1255                ; 搬送位置決め開端
AND X1252                ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1259                 ; 基板搬送爪上昇端
AND X3562                ; 基板嵌合RB原点(43)
OUT M5312                ; 基板搬送爪送可
LD B1186                 ; UNIT9-1-7 TP-PB
AND M5312                ; 基板搬送爪送可
OR M5313                 ; 基板搬送爪送
ANPB M5315               ; 基板搬送爪戻
OUT M5313                ; 基板搬送爪送
LD X1252                 ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1259                 ; 基板搬送爪上昇端
AND X3562                ; 基板嵌合RB原点(43)
OUT M5314                ; 基板搬送爪戻可
LD B1187                 ; UNIT9-1-8 TP-PB
AND M5314                ; 基板搬送爪戻可
OR M5315                 ; 基板搬送爪戻
ANPB M5313               ; 基板搬送爪送
OUT M5315                ; 基板搬送爪戻
;基板CV正転･逆転
LD CR2002                ; 常時ON
OUT M5316                ; 基板搬送CV位置決閉可
LD B1188                 ; UNIT9-1-9 TP-PB
AND M5316                ; 基板搬送CV位置決閉可
OR M5317                 ; 基板搬送CV位置決閉
ANPB M5319               ; 基板搬送CV位置決開
OUT M5317                ; 基板搬送CV位置決閉
LD CR2002                ; 常時ON
OUT M5318                ; 基板搬送CV位置決開可
LD B1189                 ; UNIT9-1-10 TP-PB
AND M5318                ; 基板搬送CV位置決開可
OR M1519
ANPB M5317               ; 基板搬送CV位置決閉
OUT M5319                ; 基板搬送CV位置決開
;基板CV正転･逆転
LD CR2002                ; 常時ON
ANB M5323                ; 基板CV逆転
OUT M5320                ; 基板CV正転可
LD B118A                 ; UNIT9-1-11 TP-PB
AND M5320                ; 基板CV正転可
OUT M5321                ; 基板CV正転
LD CR2002                ; 常時ON
ANB M5321                ; 基板CV正転
OUT M5322                ; 基板CV逆転可
LD B118B                 ; UNIT9-1-12 TP-PB
AND M5322                ; 基板CV逆転可
OUT M5323                ; 基板CV逆転
;PA検査
LD CR2002                ; 常時ON
AND B2015                ; PA検査ﾚﾃﾞｨ
OUT M5330                ; PA検査開始可
LDP B118C                ; UNIT9-1-13 TP-PB
AND M5330                ; PA検査開始可
OR M5331                 ; PA検査　PRO_No切替指令
ANB M5332                ; PA検査　PRO_No切替確認
MOV D5100 W05C0          ; 手動PA検査No  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
OUT M5331                ; PA検査　PRO_No切替指令
LD M5331                 ; PA検査　PRO_No切替指令
AND B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
OR M5332                 ; PA検査　PRO_No切替確認
ANB M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
OUT M5332                ; PA検査　PRO_No切替確認
LD M5332                 ; PA検査　PRO_No切替確認
ANB B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5333                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB M5334                ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
OUT M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M5333                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2010                ; PA検査結果取得可能
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
OR M5334                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB M5335                ; PA検査　指令
OUT M5334                ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
;PA検査実行
LD M5334                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5335                 ; PA検査　指令
ANB M5336                ; PA検査　BUSY確認
OUT M5335                ; PA検査　指令
LD M5335                 ; PA検査　指令
AND B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5336                 ; PA検査　BUSY確認
ANB M5337                ; PA検査完了
OUT M5336                ; PA検査　BUSY確認
;検査完了結果判定
LD M5336                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
LDB B2020                ; PA検査総合判定(OFF:NG ON:OK)
OR B2020                 ; PA検査総合判定(OFF:NG ON:OK)
ANL
OUT M5337                ; PA検査完了
LDP B118D                ; UNIT9-1-14 TP-PB
OR M5324                 ; EntPCB_BCR上昇ﾏﾆｭ
ANB B118E                ; UNIT9-1-15 TP-PB
ANB M5250                ; 原点復帰可能確認
OUT M5324                ; EntPCB_BCR上昇ﾏﾆｭ
MCR
;<h1/>■出力
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;基板CV上流ｽﾄｯﾊﾟ上下
LDB M5003                ; 基板投入ｽﾄｯﾊﾟ下降
ANB M5027                ; ｽﾄｯﾊﾟ下降
ANB M5082                ; ｽﾄｯﾊﾟ下降
ANB M5301                ; 基板CV上流ｽﾄｯﾊﾟ下昇
INV
OR Y2400                 ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB M5005                ; ｽﾄｯﾊﾟ上昇
ANB M5030                ; ｽﾄｯﾊﾟ上昇
ANB M5085                ; ｽﾄｯﾊﾟ上昇
ANB M5254                ; 基板CVｽﾄｯﾊﾟ上昇
ANB M5303                ; 基板CV上流ｽﾄｯﾊﾟ上昇
ANB M5017                ; 払い出しﾓｰﾄﾞｽﾄｯﾊﾟ上昇
OUT Y2400                ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
;基板CV下流ｽﾄｯﾊﾟ上下
LDB M5106                ; 基板CV下流ｽﾄｯﾊﾟ下降
ANB M5305                ; 基板CV下流ｽﾄｯﾊﾟ下昇
INV
OR Y2401                 ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB M5108                ; 基板CV下流ｽﾄｯﾊﾟ上昇
ANB M5254                ; 基板CVｽﾄｯﾊﾟ上昇
ANB M5307                ; 基板CV下流ｽﾄｯﾊﾟ上昇
OUT Y2401                ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
;基板CV位置決開閉
LDB CR2003               ; 常時OFF
ANB M5317                ; 基板搬送CV位置決閉
ANPB X356C               ; 基板押さえ閉要求
INV
OR Y2402                 ; 基板搬送CV位置決出
ANB M5523                ; 塗布検査NG発生中
ANB M5253                ; 位置決開
ANB M5319                ; 基板搬送CV位置決開
ANB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
ANB M5512                ; 照合NG発生中
ANPB X356D               ; 基板押さえ開要求
OUT Y2402                ; 基板搬送CV位置決出
;基板搬送爪送戻
LDB M5107                ; 搬送爪送
ANB M5313                ; 基板搬送爪送
INV
LD Y2403                 ; 基板搬送送りSV
ANB Y2404                ; 基板搬送戻りSV
ORL
AND T920                 ; 基板CV扉閉確認
ANB M130                 ; 全体Pose
LD T923                  ; EntPCB_BCR上昇端
OR M251                  ; 20141013 koike
ANL
OUT Y2403                ; 基板搬送送りSV
LDB M5110                ; 搬送爪戻
ANB M5252                ; 基板搬送爪戻
ANB M5315                ; 基板搬送爪戻
INV
LD Y2404                 ; 基板搬送戻りSV
ANB Y2403                ; 基板搬送送りSV
ORL
AND T920                 ; 基板CV扉閉確認
ANB M130                 ; 全体Pose
LD T923                  ; EntPCB_BCR上昇端
OR M251                  ; 20141013 koike
ANL
OUT Y2404                ; 基板搬送戻りSV
;基板搬送爪開閉
LDB M5105                ; 搬送爪閉
ANB M5309                ; 基板搬送爪閉
INV
OR Y2405                 ; 基板搬送爪閉SV
ANB M5109                ; 搬送爪開
ANB M5251                ; 基板搬送爪開
ANB M5311                ; 基板搬送爪開
LD T923                  ; EntPCB_BCR上昇端
OR M251                  ; 20141013 koike
ANL
OUT Y2405                ; 基板搬送爪閉SV
;基板CV
LDB M5001                ; ｺﾝﾍﾞｱ投入回転
ANB M5004                ; 基板ｺﾝﾍﾞｱ回転
ANB M5321                ; 基板CV正転
INV
AND T920                 ; 基板CV扉閉確認
OUT Y230C                ; 基板供給CV正転
LDB M5028                ; ｺﾝﾍﾞｱ排出逆転
ANB M5083                ; ｺﾝﾍﾞｱ排出逆転
ANB M5323                ; 基板CV逆転
INV
AND T920                 ; 基板CV扉閉確認
OUT Y230F                ; 基板供給CV逆転
;バーコードリーダー上下
LDB M251                 ; 20141013 koike
MPS
LDB M5020                ; EntPCB読取ｼﾘﾝﾀﾞｰ下降
ANB M5007                ; 基板ｺｰﾄﾞ読込要求
ANB M5060                ; EntPCB読取りｼﾘﾝﾀﾞｰ下降
ANB M5046                ; 基板ｺｰﾄﾞ読込要求
ANB M5324                ; EntPCB_BCR上昇ﾏﾆｭ
ANL
INV
OUT Y2311                ; EntPCB_BCR_DOWN
MPP
ANB Y2311                ; EntPCB_BCR_DOWN
OUT Y2312                ; EntPCB_BCR_UP
;基板投入部表示･BZ
LDB M5081                ; 投入位置ﾜｰｸ有取出要求
ANB M5084                ; “C??NG?a?o?v??
ANB M5029                ; 基板取出要求
INV
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2320                ; 基板投入確認PL
LDB B0120                ; 払出ﾓｰﾄﾞ基板投入不可
ANB M5081                ; 投入位置ﾜｰｸ有取出要求
ANB M5084                ; “C??NG?a?o?v??
ANB M5029                ; 基板取出要求
INV
LD M5200                 ; 基板投入不可
OR M36                   ; 払出ﾓｰﾄﾞ
AND X124D                ; 基板投入CVﾜｰｸ確認
ORL
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2321                ; 基板投入ﾌﾞｻﾞｰ
LD M5485                 ; ﾕﾆｯﾄ9起動中
MPS
LDB M36                  ; 払出ﾓｰﾄﾞ
ANB M5200                ; 基板投入不可
ANL
OUT Y2322                ; 基板投入OK_PL
MPP
LD M5200                 ; 基板投入不可
OR M36                   ; 払出ﾓｰﾄﾞ
ANL
OUT Y2323                ; 基板投入NG_PL
;PA検査ｶﾒﾗ
LDB M5057                ; PA検査　指令
ANB M5335                ; PA検査　指令
INV
OUT B2040                ; PA検査ﾄﾘｶﾞ要求
LDB M5052                ; PA検査　PRO_No切替指令
ANB M5331                ; PA検査　PRO_No切替指令
INV
OUT B2042                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替要求
LDB CR2003               ; 常時OFF
INV
OUT B2043                ; PA検査ﾜｰﾆﾝｸﾞｸﾘｱ要求
LDB M5055                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
INV
OUT B2045                ; PA検査ﾊﾞｯﾌｧｸﾘｱ要求
;<h1/>■表示
;□□　　パネル表示　　□□
LD X1250                 ; 搬送上流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1680                ; UNIT9-1-1 TP-PL
LD X1251                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1681                ; UNIT9-1-2 TP-PL
LD X1252                 ; 搬送下流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1682                ; UNIT9-1-3 TP-PL
LD X1253                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1683                ; UNIT9-1-4 TP-PL
LD X1258                 ; 基板搬送爪下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1684                ; UNIT9-1-5 TP-PL
LD X1259                 ; 基板搬送爪上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1685                ; UNIT9-1-6 TP-PL
LD X1256                 ; 基板搬送送端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1686                ; UNIT9-1-7 TP-PL
LD X1257                 ; 基板搬送戻端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1687                ; UNIT9-1-8 TP-PL
LD X1254                 ; 搬送位置決め閉端
OUT B1688                ; UNIT9-1-9 TP-PL
LD X1255                 ; 搬送位置決め開端
OUT B1689                ; UNIT9-1-10 TP-PL
LD Y230C                 ; 基板供給CV正転
OUT B168A                ; UNIT9-1-11 TP-PL
LD Y230F                 ; 基板供給CV逆転
OUT B168B                ; UNIT9-1-12 TP-PL
LDB M251                 ; 20141013 koike
MPS
AND X1235                ; EntPCB_BCR_上昇端
OUT B168E                ; UNIT9-1-15 TP-PL
MPP
AND X1237                ; EntPCB_BCR_下降端
OUT B168D                ; UNIT9-1-14 TP-PL
;<h1/>■異常
;□□　　異常検出　　□□
;B120(K289)--B12F(K304)  B130(K305)--B13F(K320)
LD X124D                 ; 基板投入CVﾜｰｸ確認
AND M5000                ; 基板投入部ｻｲｸﾙ開始
AND M36                  ; 払出ﾓｰﾄﾞ
OUT B0120                ; 払出ﾓｰﾄﾞ基板投入不可
@DW K288 D0
CON
@FIFOW D0 D900           ; 異常内容
LD X123A                 ; 塗布検査画像ｴﾗｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0121                ; 基板供給CV画像検査異常
ORL
AND M50                  ; 運転準備
OUT B0121                ; 基板供給CV画像検査異常
@DW K289 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0621
AND B0121                ; 基板供給CV画像検査異常
OUT B0621
LD B201F                 ; PA検査ｴﾗｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
ORL
AND M50                  ; 運転準備
OUT B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
@DW K290 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0622
AND B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
OUT B0622
LD Y2400                 ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB X1250                ; 搬送上流ｽﾄｯﾊﾟ下降端
LDB Y2400                ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB X1251                ; 搬送上流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K950 K50             ; 基板搬送CV上流ｽﾄｯﾊﾟ確認
MPP
AND T950                 ; 基板搬送CV上流ｽﾄｯﾊﾟ確認
OUT B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
@DW K291 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0623
AND B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
OUT B0623
LD Y2401                 ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB X1252                ; 搬送下流ｽﾄｯﾊﾟ下降端
LDB Y2401                ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB X1253                ; 搬送下流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K951 K50             ; 基板搬送CV下流ｽﾄｯﾊﾟ確認
MPP
AND T951                 ; 基板搬送CV下流ｽﾄｯﾊﾟ確認
OUT B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
@DW K292 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0624
AND B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
OUT B0624
LD Y2402                 ; 基板搬送CV位置決出
ANB X1254                ; 搬送位置決め閉端
LDB Y2402                ; 基板搬送CV位置決出
ANB X1255                ; 搬送位置決め開端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0125                ; 基板搬送CV位置決異常
ORL
AND M50                  ; 運転準備
MPS
TMR K952 K50             ; 基板搬送CV位置決確認
MPP
AND T952                 ; 基板搬送CV位置決確認
OUT B0125                ; 基板搬送CV位置決異常
@DW K293 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0625
AND B0125                ; 基板搬送CV位置決異常
OUT B0625
LD Y2403                 ; 基板搬送送りSV
ANB X1256                ; 基板搬送送端
LD Y2404                 ; 基板搬送戻りSV
ANB X1257                ; 基板搬送戻端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0126                ; 基板搬送爪送り異常
ORL
AND M50                  ; 運転準備
MPS
TMR K953 K50             ; 基板搬送爪送り確認
MPP
AND T953                 ; 基板搬送爪送り確認
OUT B0126                ; 基板搬送爪送り異常
@DW K294 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0626
AND B0126                ; 基板搬送爪送り異常
OUT B0626
LD Y2405                 ; 基板搬送爪閉SV
ANB X1258                ; 基板搬送爪下降端
LDB Y2405                ; 基板搬送爪閉SV
ANB X1259                ; 基板搬送爪上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0127                ; 基板搬送爪上下異常
ORL
AND M50                  ; 運転準備
MPS
TMR K954 K50             ; 基板搬送爪上下確認
MPP
AND T954                 ; 基板搬送爪上下確認
OUT B0127                ; 基板搬送爪上下異常
@DW K295 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0627
AND B0127                ; 基板搬送爪上下異常
OUT B0627
LD M17                   ; 原点復帰PB
LD X124E                 ; 基板投入CV読取ﾜｰｸ確認
OR X124F                 ; 基板投入CV取出ﾜｰｸ確認
OR X126B                 ; 基板投入CVﾜｰｸ有
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
ORL
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OUT B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
@DW K296 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0628
AND B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
OUT B0628
LDB T920                 ; 基板CV扉閉確認
AND M30                  ; 自動ﾓｰﾄﾞ
OUT B0129                ; 基板搬送CV扉開
@DW K297 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0629                 ; 基板搬送CV扉開BZ停止
AND B0129                ; 基板搬送CV扉開
OUT B0629                ; 基板搬送CV扉開BZ停止
LDB M251                 ; 20141013 koike
MPS
LD Y2312                 ; EntPCB_BCR_UP
ANB T923                 ; EntPCB_BCR上昇端
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B012A                ; EntPCB_BCR上昇端異常
ORL
AND M30                  ; 自動ﾓｰﾄﾞ
ANL
MPS
TMR K955 K50             ; EntPCB_BCR上昇確認
MPP
AND T955                 ; EntPCB_BCR上昇確認
OUT B012A                ; EntPCB_BCR上昇端異常
@DW K298 D0
CON
@FIFOW D0 D900           ; 異常内容
MPP
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B062A                 ; EntPCB_BCR上昇端異常ﾌﾞｻﾞｰ停止
AND B012A                ; EntPCB_BCR上昇端異常
ANL
OUT B062A                ; EntPCB_BCR上昇端異常ﾌﾞｻﾞｰ停止
LDB M251                 ; 20141013 koike
MPS
LD Y2311                 ; EntPCB_BCR_DOWN
ANB T924                 ; EntPCB_BCR下降端
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B012B                ; EntPCB_BCR下降端異常
ORL
AND M30                  ; 自動ﾓｰﾄﾞ
ANL
MPS
TMR K956 K50             ; EntPCB_BCR下降確認
MPP
AND T956                 ; EntPCB_BCR下降確認
OUT B012B                ; EntPCB_BCR下降端異常
@DW K299 D0
CON
@FIFOW D0 D900           ; 異常内容
MPP
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B062B                 ; EntPCB_BCR下降端異常ﾌﾞｻﾞｰ停止
AND B012B                ; EntPCB_BCR下降端異常
ANL
OUT B062B                ; EntPCB_BCR下降端異常ﾌﾞｻﾞｰ停止
LD M5084                 ; “C??NG?a?o?v??
OUT B027                 ; Board reject
;異常ﾌﾞｻﾞｰまとめ
LDB B0121                ; 基板供給CV画像検査異常
OR B0621
LDB B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
OR B0622
ANL
LDB B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
OR B0623
ANL
LDB B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
OR B0624
ANL
LDB B0125                ; 基板搬送CV位置決異常
OR B0625
ANL
LDB B0126                ; 基板搬送爪送り異常
OR B0626
ANL
LDB B0127                ; 基板搬送爪上下異常
OR B0627
ANL
LDB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
OR B0628
ANL
LDB B012A                ; EntPCB_BCR上昇端異常
OR B062A                 ; EntPCB_BCR上昇端異常ﾌﾞｻﾞｰ停止
OR M251                  ; 20141013 koike
ANL
LDB B012B                ; EntPCB_BCR下降端異常
OR B062B                 ; EntPCB_BCR下降端異常ﾌﾞｻﾞｰ停止
OR M251                  ; 20141013 koike
ANL
INV
OUT M5489                ; ﾕﾆｯﾄ9異常BZ補助
END
ENDH
