DEVICE:53
;MODULE:KE_CPU_POS_Y
;MODULE_TYPE:1
;ﾓｰﾀｰ	ｵﾘｴﾝﾀﾙ　CSK513PAP
;ﾄﾞﾗｲﾊﾞ	ｵﾘｴﾝﾀﾙ	SD5103P3
;***CPUﾊﾟﾙｽ出力Y軸位置決め****
;
;IN/OUTｱﾄﾞﾚｽ　先頭ﾋﾞｯﾄｱﾄﾞﾚｽから16bit占有
;				IN　先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+00　位置決め起動
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+01　予備
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+02　位置決めInter Lock
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+03　位置決めﾘｾｯﾄSW
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+04〜7　予備
;				OUT 先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+08　位置決め中
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+09　位置決め完了
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+10　異常
;					先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+11〜15　予備
;					
;ERR_CODE	 先頭ﾜｰﾄﾞｱﾄﾞﾚｽから4Word占有
;				先頭ﾜｰﾄﾞｱﾄﾞﾚｽ+00　異常Noを格納
;				先頭ﾜｰﾄﾞｱﾄﾞﾚｽ+02　異常内容のｺｰﾄﾞNoを格納
;IN
LD P0:#0                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR000               ; 位置決め起動
LD P0:#1                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR001
LD P0:#2                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR002               ; 位置決めInter Lock
LD P0:#3                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR003               ; 位置決めﾘｾｯﾄSW
;ﾌﾟﾛｸﾞﾗﾐﾝｸﾞｴﾘｱ
LD @MR000                ; 位置決め起動
AND @MR002               ; 位置決めInter Lock
ANB @MR010               ; 異常
OUT @MR004               ; 出.位置決め
LD @MR004                ; 出.位置決め
MPS
ANB @MR005               ; Y軸位置決め中
PLSY #1
MPP
LD CR3109                ; Y軸ﾊﾟﾙｽ出力中/緊急停止
OR @MR005                ; Y軸位置決め中
ANL
OUT @MR005               ; Y軸位置決め中
LD @MR005                ; Y軸位置決め中
AND CR3106               ; Y軸位置決め完了ﾘﾚｰ
ANB CR3109               ; Y軸ﾊﾟﾙｽ出力中/緊急停止
OUT @MR006               ; Y軸位置決め完了
;ERR
;位置決め異常
LD CR3107                ; Y軸ｴﾗｰ/ｴﾗｰｸﾘｱ
OR CR3108                ; Y軸ﾜｰﾆﾝｸﾞ/ﾜｰﾆﾝｸﾞｸﾘｱ
OUT @MR011               ; Y軸位置決ﾕﾆｯﾄ異常
BSET @DM0 #1             ; ERR_Code格納
LD @MR011                ; Y軸位置決ﾕﾆｯﾄ異常
MOV CM2054 @DM2          ; Y軸ｴﾗｰｺｰﾄﾞ  軸ERR_Code格納
LDB @MR011               ; Y軸位置決ﾕﾆｯﾄ異常
MOV #0 @DM2              ; 軸ERR_Code格納
LD @MR003                ; 位置決めﾘｾｯﾄSW
MPS
AND CR3107               ; Y軸ｴﾗｰ/ｴﾗｰｸﾘｱ
RES CR3107               ; Y軸ｴﾗｰ/ｴﾗｰｸﾘｱ
MPP
AND CR3108               ; Y軸ﾜｰﾆﾝｸﾞ/ﾜｰﾆﾝｸﾞｸﾘｱ
RES CR3108               ; Y軸ﾜｰﾆﾝｸﾞ/ﾜｰﾆﾝｸﾞｸﾘｱ
;ERRまとめ
LD @MR011                ; Y軸位置決ﾕﾆｯﾄ異常
OUT @MR010               ; 異常
;出力
LD @MR005                ; Y軸位置決め中
OUT P0:#8                ; IN/OUTｱﾄﾞﾚｽ
LD @MR006                ; Y軸位置決め完了
OUT P0:#9                ; IN/OUTｱﾄﾞﾚｽ
LD @MR010                ; 異常
OUT P0:#10               ; IN/OUTｱﾄﾞﾚｽ
BMOV.D @DM0 P1:#0 #2     ; ERR_Code格納  ERR_CODE
LDB @MR010               ; 異常
FMOV.D #0 @DM0 #2        ; ERR_Code格納
FMOV.D #0 P1:#0 #2       ; ERR_CODE
LDB CR2002               ; 常時ON
MEND
END
ENDH
