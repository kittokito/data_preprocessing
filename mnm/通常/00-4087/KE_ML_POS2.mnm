DEVICE:53
;MODULE:KE_ML_POS2
;MODULE_TYPE:1
;ﾕﾆｯﾄ	ｷｰｴﾝｽ	KV-ML16V
;****各軸位置決め入出力転送****
;軸No　		#1〜＃16
;ML16Vｱﾄﾞﾚｽ	ﾕﾆｯﾄｴﾃﾞｨﾀによるKV-ML16Vの設定値　先頭ｱﾄﾞﾚｽ
;				共通	1軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+4000〜4315
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+4400〜4715
;						2軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+4800〜5115
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+5200〜5515
;						3軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+5600〜5115
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+6000〜5515
;		　　　位置決用　1軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+000〜007
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+800〜807
;						2軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+008〜015
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+808〜815
;						3軸	OUT	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+100〜107
;							IN	先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+900〜907
;IN/OUTｱﾄﾞﾚｽ　先頭ﾋﾞｯﾄｱﾄﾞﾚｽから16bit占有
;				先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+00〜07(IN)
;				先頭ﾋﾞｯﾄｱﾄﾞﾚｽ+08〜15(OUT)
;ERR_CODE	 先頭ﾜｰﾄﾞｱﾄﾞﾚｽから1Word占有
;				異常内容のｺｰﾄﾞNoを格納
LD= V0 #1                ; 軸No
BMOV UR0:#704 @MR500 #4  ; MC20Vｱﾄﾞﾚｽ  ML-IN 軸ｴﾗｰ中
PMOV UR0:#128 #0 @MR1000 #0 #8;MC20Vｱﾄﾞﾚｽ  ML-IN 位置決め 制御開始完了
LD= V0 #2                ; 軸No
BMOV UR0:#832 @MR500 #4  ; MC20Vｱﾄﾞﾚｽ  ML-IN 軸ｴﾗｰ中
PMOV UR0:#136 #0 @MR1000 #0 #8;MC20Vｱﾄﾞﾚｽ  ML-IN 位置決め 制御開始完了
;入力
LD P0:#0                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR000               ; 位置決め起動
LD P0:#1                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR001
LD P0:#2                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR002               ; 位置決めIL
LD P0:#3                 ; IN/OUTｱﾄﾞﾚｽ
OUT @MR003               ; 位置決め ﾘｾｯﾄSW
;ﾌﾟﾛｸﾞﾗﾐﾝｸﾞｴﾘｱ
LD @MR000                ; 位置決め起動
AND @MR002               ; 位置決めIL
ANB @MR009               ; 異常
OUT @MR004               ; 出.位置決め
LD @MR004                ; 出.位置決め
ANB @MR005               ; 位置決め中
OUT @MR900               ; ML-OUT 位置決め 制御開始
LD @MR004                ; 出.位置決め
LD @MR504                ; ML-IN 軸制御中
AND @MR1000              ; ML-IN 位置決め 制御開始完了
OR @MR005                ; 位置決め中
ANL
OUT @MR005               ; 位置決め中
LD @MR005                ; 位置決め中
ANB @MR504               ; ML-IN 軸制御中
LD @MR1002               ; ML-IN 位置決め完了
OR @MR006                ; 位置決め完了
ANL
OUT @MR006               ; 位置決め完了
LD @MR006                ; 位置決め完了
OUT @MR902               ; ML-OUT 位置決め 完了ｸﾘｱ
LD @MR006                ; 位置決め完了
ANB @MR1002              ; ML-IN 位置決め完了
OUT @MR007               ; 位置決め完了ｸﾘｱ
;ERR
;ﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
LD @MR805                ; ML-IN ﾄﾞﾗｲﾊﾞｱﾗｰﾑ
AND @MR004               ; 出.位置決め
OUT @MR010               ; ﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
BSET @DM0 #0             ; ERR_Code格納
LD @MR003                ; 位置決め ﾘｾｯﾄSW
OR @MR405                ; ML-OUT ﾄﾞﾗｲﾊﾞｱﾗｰﾑ ﾘｾｯﾄ
AND @MR805               ; ML-IN ﾄﾞﾗｲﾊﾞｱﾗｰﾑ
OUT @MR405               ; ML-OUT ﾄﾞﾗｲﾊﾞｱﾗｰﾑ ﾘｾｯﾄ
;軸ｴﾗｰ
LD @MR500                ; ML-IN 軸ｴﾗｰ中
OUT @MR011               ; 軸ｴﾗｰ
BSET @DM0 #1             ; ERR_Code格納
LD @MR003                ; 位置決め ﾘｾｯﾄSW
OR @MR100                ; ML-OUT 軸ｴﾗｰｸﾘｱ
AND @MR500               ; ML-IN 軸ｴﾗｰ中
OUT @MR100               ; ML-OUT 軸ｴﾗｰｸﾘｱ
LD @MR500                ; ML-IN 軸ｴﾗｰ中
U_RDAERC UV0 V0 @DM2     ; MC20Vｱﾄﾞﾚｽ  軸No  軸ERR_Code格納
MPS
AND= V0 #1               ; 軸No
UREAD #1 #1248 @DM4 #2   ; 軸ERR詳細情報
MPP
AND= V0 #2               ; 軸No
UREAD #1 #1250 @DM4 #2   ; 軸ERR詳細情報
;ERRまとめ
LD @MR010                ; ﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
OR @MR011                ; 軸ｴﾗｰ
OUT @MR009               ; 異常
;出力
LD @MR005                ; 位置決め中
OUT P0:#8                ; IN/OUTｱﾄﾞﾚｽ
LD @MR007                ; 位置決め完了ｸﾘｱ
OUT P0:#9                ; IN/OUTｱﾄﾞﾚｽ
LD @MR009                ; 異常
OUT P0:#10               ; IN/OUTｱﾄﾞﾚｽ
BMOV.D @DM0 P1:#0 #3     ; ERR_Code格納  ERR_CODE
LDB @MR009               ; 異常
FMOV.D #0 @DM0 #3        ; ERR_Code格納
FMOV.D #0 P1:#0 #3       ; ERR_CODE
LD @MR100                ; ML-OUT 軸ｴﾗｰｸﾘｱ
OUT P0:#11               ; IN/OUTｱﾄﾞﾚｽ
LD @MR405                ; ML-OUT ﾄﾞﾗｲﾊﾞｱﾗｰﾑ ﾘｾｯﾄ
OUT P0:#12               ; IN/OUTｱﾄﾞﾚｽ
LD @MR900                ; ML-OUT 位置決め 制御開始
OUT P0:#13               ; IN/OUTｱﾄﾞﾚｽ
LD @MR902                ; ML-OUT 位置決め 完了ｸﾘｱ
OUT P0:#14               ; IN/OUTｱﾄﾞﾚｽ
LDB CR2002               ; 常時ON
MEND
END
ENDH
