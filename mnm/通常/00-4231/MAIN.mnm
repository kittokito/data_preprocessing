DEVICE:53
;MODULE:MAIN
;MODULE_TYPE:0
;□□　　ダミー接点　　□□
LD CR2003                ; 常時OFF
OUT M0                   ; 常時OFF
LD CR2003                ; 常時OFF
OUT M997                 ; 0422ｺｼﾊﾗ変更
LD CR3905                ; 重度ｴﾗｰ発生中
INV
OUT M1                   ; CPU正常
LD CR2008                ; 運転開始時1ｽｷｬﾝON
OUT M2                   ; 1ｽｷｬﾝON
LD B101F                 ; PC_OFFLINE_MODE
OUT M3                   ; OFFLINE_MODE
LD CR2002                ; 常時ON
ANB T2                   ; 0.3Secｸﾛｯｸ
TMH K1 K30               ; 0.3Secｸﾛｯｸ
LD T1                    ; 0.3Secｸﾛｯｸ
TMH K2 K30               ; 0.3Secｸﾛｯｸ
LD CR2002                ; 常時ON
ANB T5                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
TMH K4 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1_0.5sec
LD T4                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1_0.5sec
TMH K5 K25               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
LD CR2002                ; 常時ON
ANB T7                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
TMH K6 K40               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
LD T6                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
TMH K7 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
LD CR2002                ; 常時ON
ANB T9                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
TMR K8 K10               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3_1.0sec
LD T8                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3_1.0sec
TMR K9 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
LD CR2002                ; 常時ON
ANB T37                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
TMR K36 K50              ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ_10.0sec
LD T36                   ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ_10.0sec
TMR K37 K5               ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
LD CR2002                ; 常時ON
ANB T39                  ; ﾜｰｸ投入可音程ﾀｲﾏ1
TMH K38 K30              ; ﾜｰｸ投入可音程ﾀｲﾏ
LD T38                   ; ﾜｰｸ投入可音程ﾀｲﾏ
TMH K39 K100             ; ﾜｰｸ投入可音程ﾀｲﾏ1
;暫定で設定するものを記述する
;★必要無くなったときは削除を忘れないこと
LDB M0                   ; 常時OFF
OUT M900                 ; 暫定で設定するもの
MOV K10 D1560            ; 搬送_2_ワーク在荷_CNT
;<h1/>□_安全関連入力受け
;安全系の入力は一旦ここで受ける。
LD X03                   ; 圧空元圧正常
OUT M120                 ; 圧空正常
LD X01                   ; 非常停止正常
OUT M121                 ; 非常停止正常
LD CR2002                ; 常時ON
OUT M122                 ; 外部非常停止正常
LD CR2002                ; 常時ON
OUT M123                 ; 安全扉正常
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
AND X05                  ; 供給部ﾗｲﾄｶｰﾃﾝOUT2
AND X0A                  ; 装置背面ﾗｲﾄｶｰﾃﾝOUT1
AND X0B                  ; 装置背面ﾗｲﾄｶｰﾃﾝOUT1
OUT M124                 ; ｴﾘｱｾﾝｻ正常
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
AND X05                  ; 供給部ﾗｲﾄｶｰﾃﾝOUT2
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
TMR K60 K5               ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LDB X04                  ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
TMR K66 K5               ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
TMR K61 K5               ; ST１-2部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LD X08                   ; ST3-4部ﾗｲﾄｶｰﾃﾝOUT1
TMR K62 K5               ; ST3-4部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
TMR K63 K5               ; 装置背面ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LDB M120                 ; 圧空正常
ANB M121                 ; 非常停止正常
ANB M122                 ; 外部非常停止正常
ANB M123                 ; 安全扉正常
ANB M124                 ; ｴﾘｱｾﾝｻ正常
ANB M125
ANB M126
ANB M127
ANB M128
OUB M129                 ; 安全入力OK
;<h1/>□_スイッチ変換
;操作系の入力は一旦ここで受ける。
LD B1016                 ; DRY_RUN_PB
OUT M7                   ; 空運転PB
LD B1017                 ; ROBOT_TEST_TP-PB
OUT M8                   ; 能力拡張_テスト
LD B1001                 ; 自動/手動PB
OUT M10                  ; 自動/手動PB
;今回は運転準備PB無し装置電源入で常時ON
LD X00                   ; 装置電源入
OUT M12                  ; 運転準備入PB
LD CR2003                ; 常時OFF
OUT M13                  ; 運転準備切PB
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
OUT M15                  ; ﾌﾞｻﾞｰ停止PB
LD B1011                 ; ｴﾗｰﾘｾｯﾄPB
OUT M16                  ; ﾘｾｯﾄPB
LD B1000                 ; 原点復帰PB
OUT M17                  ; 原点復帰PB
LD B1002                 ; ｽﾀｰﾄPB
OUT M18                  ; 起動PB
LD B1003                 ; ｻｲｸﾙ停止PB
TMR K15 K50              ; 強制停止
OUT M19                  ; 停止PB
LD B1004                 ; 段取りPB
OUT M22                  ; 段取りPB
;用途に応じて使用する
LD CR2003                ; 常時OFF
MPS
TMR K16 K20              ; 終了PB_ONﾃﾞｨﾚｲ
MPP
AND T16                  ; 終了PB_ONﾃﾞｨﾚｲ
OUT M20                  ; 終了PB
LD CR2003                ; 常時OFF
MPS
TMR K17 K10              ; 払出PB_ONﾃﾞｨﾚｲ
MPP
AND T17                  ; 払出PB_ONﾃﾞｨﾚｲ
OUT M21                  ; 払出PB
;<h1/>□_各モード選択
;自動・手動モード
LDP M10                  ; 自動/手動PB
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M30                  ; 自動ﾓｰﾄﾞ
MEP
LD M30                   ; 自動ﾓｰﾄﾞ
ANPB M31                 ; 手動ﾓｰﾄﾞ
ORL
ANB M51                  ; 即停止異常(緊急停止)
OUT M30                  ; 自動ﾓｰﾄﾞ
LDP M10                  ; 自動/手動PB
ANB M31                  ; 手動ﾓｰﾄﾞ
ANB M41                  ; ﾕﾆｯﾄ_起動中
MEP
OR T51                   ; 手動ﾓｰﾄﾞ移行ﾀｲﾏ
OR CR2008                ; 運転開始時1ｽｷｬﾝON
LD M31                   ; 手動ﾓｰﾄﾞ
ANPB M30                 ; 自動ﾓｰﾄﾞ
ORL
ANB M71                  ; 自動運転開始BZ
OUT M31                  ; 手動ﾓｰﾄﾞ
LDB M40                  ; 全ﾕﾆｯﾄ原点
ANB M71                  ; 自動運転開始BZ
ORB M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M41                  ; ﾕﾆｯﾄ_起動中
TMR K51 K10              ; 手動ﾓｰﾄﾞ移行ﾀｲﾏ
;この手動操作可条件はメインのみの条件
;ユニット毎に必要な条件は各ユニットへ追加する
LD M120                  ; 圧空正常
AND M121                 ; 非常停止正常
AND M122                 ; 外部非常停止正常
LD M123                  ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ANL
OUT M32                  ; ﾒｲﾝ手動操作可能条件
LD M40                   ; 全ﾕﾆｯﾄ原点
OUT M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
;空運転､払出モードは必要に応じて使用
LD M7                    ; 空運転PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K34 K50              ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M41                  ; ﾕﾆｯﾄ_起動中
ANP T34                  ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LD M35                   ; 空運転ﾓｰﾄﾞ
ANPB T34                 ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
ORL
OUT M35                  ; 空運転ﾓｰﾄﾞ
LDP M19                  ; 停止PB
OR M13309                ; PC→自動運転停止ﾌﾗｸﾞ
OR M59                   ; 払出停止異常まとめ
ORP M1404                ; ID異常時ｻｲｸﾙ停止選択
ANB M130                 ; 一時停止
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M36                   ; 払出ﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M71                  ; 自動運転開始BZ
OUT M36                  ; 払出ﾓｰﾄﾞ
;ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD B1015                 ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙPB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K35 K50              ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
LDP T35                  ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LDPB T35                 ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
AND M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M41                  ; ﾕﾆｯﾄ_起動中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
OUT M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
;エリアセンサー無効化
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K41 K30              ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
LDP T41                  ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
ANB M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
LDPB T41                 ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
AND M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ORL
ANB M41                  ; ﾕﾆｯﾄ_起動中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
ANB B1001                ; 自動/手動PB
ANB B1002                ; ｽﾀｰﾄPB
ANPB B1011               ; ｴﾗｰﾘｾｯﾄPB
ANB M51                  ; 即停止異常(緊急停止)
OUT M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
LD M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B151C                ; ｴﾘｱｾﾝｻｰ無効
;段取りﾓｰﾄﾞ　（未使用）
LDP M22                  ; 段取りPB
ANB M38                  ; 段取りﾓｰﾄﾞ
LD M38                   ; 段取りﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M41                  ; ﾕﾆｯﾄ_起動中
OUT M38                  ; 段取りﾓｰﾄﾞ
;□□　　装置状態集約　　□□
;■装置原点
LD M1480                 ; ﾕﾆｯﾄ1原点
AND M3480                ; 搬送ST2_原点
LD M5480
AND M5980                ; ﾕﾆｯﾄ10原点_OK_NG_CV
ORB M99                  ; 能拡後対応FLG
ANL
AND M6480                ; ﾘｰｸST2ﾕﾆｯﾄ11原点
AND M6980                ; ﾌｧｲﾅﾙST2ﾕﾆｯﾄ12原点
LD M7480
ORB M99                  ; 能拡後対応FLG
ANL
LD M7980
AND M8480
AND M8980
AND M9480
AND M9980
ORB M99                  ; 能拡後対応FLG
ANL
OUT M40                  ; 全ﾕﾆｯﾄ原点
;■起動中
LDB M1485                ; ﾕﾆｯﾄ1起動中
ANB M3485                ; ﾕﾆｯﾄ2起動中
ANB M5485
ANB M5985                ; ﾕﾆｯﾄ10起動中
ANB M6485                ; ﾕﾆｯﾄ11起動中
ANB M6985                ; ﾕﾆｯﾄ12起動中
ANB M7485
ANB M7985
ANB M8485
ANB M8985
ANB M9485
ANB M9985
INV
OUT M41                  ; ﾕﾆｯﾄ_起動中
;■原点復帰中
LDB M1484                ; ﾕﾆｯﾄ1原点復帰中
ANB M3484                ; ﾕﾆｯﾄ2原点復帰中
ANB M5484
ANB M5984                ; ﾕﾆｯﾄ10原点復帰中
ANB M6484                ; ﾕﾆｯﾄ11原点復帰中
ANB M6984                ; ﾕﾆｯﾄ12原点復帰中
ANB M7484
ANB M7984
ANB M8484
ANB M8984
ANB M9484
ANB M9984
INV
OUT M42                  ; 全ﾕﾆｯﾄ原点復帰中
;■ﾛﾎﾞｯﾄ準備完了
LD M1490                 ; ﾕﾆｯﾄ1機器準備完了
ORB M99                  ; 能拡後対応FLG
AND M3490                ; ﾕﾆｯﾄ2機器準備完了
LD M5490
AND M5990                ; ﾕﾆｯﾄ10機器準備完了
ORB M99                  ; 能拡後対応FLG
ANL
AND M6490                ; ﾕﾆｯﾄ11機器準備完了
AND M6990                ; ﾕﾆｯﾄ12機器準備完了
LD M7490
ORB M99                  ; 能拡後対応FLG
ANL
LD M7990
AND M8490
AND M8990
AND M9490
AND M9990
ORB M99                  ; 能拡後対応FLG
ANL
OUT M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
;■運転準備
LDB M0                   ; 常時OFF
TMR K0 K20               ; 電源投入0Nﾃﾞｨﾚｲ
TMR K11 K50              ; タッチパネル_ロゴ表示終了
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
AND M12                  ; 運転準備入PB
OR M5                    ; 運転準備補助
LDB M13                  ; 運転準備切PB
OR M41                   ; ﾕﾆｯﾄ_起動中
ANL
ANB M51                  ; 即停止異常(緊急停止)
OUT M5                   ; 運転準備補助
LD M5                    ; 運転準備補助
TMR K10 K20              ; 運転準備ﾃﾞｨﾚｲ
MPS
AND T10                  ; 運転準備ﾃﾞｨﾚｲ
OUT M50                  ; 運転準備
MPP
AND M50                  ; 運転準備
TMR K12 K30              ; 運転準備ONﾃﾞｨﾚｲ(3S)
;<h1/>□_異常検出まとめ
;■即停止異常(全ﾕﾆｯﾄ即停止)
LDB B00                  ; 0)_非常停止異常
ANB B01                  ; 1)_圧空低下異常
ANB B02                  ; 2)_安全扉開異常
ANB B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ANB B04                  ; 4)_外部非常停止
ANB B05                  ; 5)_PLC異常
ANB B06                  ; 6)_ﾘﾝｸ異常
ANB M58                  ; 非常停止異常まとめ
ANB B0A                  ; 10)_PC通信異常
ANB B0B                  ; 11)_PCｴﾗｰ
ANB B017                 ; 23)_EMG_STOP(ﾊﾝﾃﾞｨ)
ANB B018                 ; 24)_EMG_STOP(操作ﾊﾟﾈﾙ)
ANB B019                 ; 25)_EMG_STOP(ﾜｰｸ投入部)
ANB B01A                 ; 26)_EMG_STOP(ﾘｰｸ検査ST2-2)
ANB B01B                 ; 27)_EMG_STOP(ﾘｰｸ検査ST3-4)
ANB B01C                 ; 28)_EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)
ANB B01D                 ; 29)_EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)
INV
OUT M51                  ; 即停止異常(緊急停止)
;■ﾕﾆｯﾄ異常検出(該当ﾕﾆｯﾄ以外ｻｲｸﾙ停止)
LDB M1486                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
ANB M3486                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ANB M5486
ANB M5986                ; ﾕﾆｯﾄ10ｻｲｸﾙ停止異常
ANB M6486                ; ﾕﾆｯﾄ11ｻｲｸﾙ停止異常
ANB M6986                ; ﾕﾆｯﾄ12ｻｲｸﾙ停止異常
ANB M7486
ANB M7986
ANB M8486
ANB M8986
ANB M9486
ANB M9986
INV
OUT M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
;■ﾕﾆｯﾄ毎各種警報検出(ﾜｰｸ不足･ｵﾍﾟｺｰﾙ等)
LDB M1488                ; ﾕﾆｯﾄ1警報補助
ANB M3488                ; ﾕﾆｯﾄ2警報補助
ANB M5488
ANB M5988                ; ﾕﾆｯﾄ10警報補助
ANB M6488                ; ﾕﾆｯﾄ11警報補助
ANB M6988                ; ﾕﾆｯﾄ12警報補助
ANB M7488
ANB M7988
ANB M8488
ANB M8988
ANB M9488
ANB M9988
INV
OUT M53                  ; 警報(ﾜｰｸ不足等)
;■ﾕﾆｯﾄ毎異常ﾌﾞｻﾞｰ検出
LDB M200                 ; ﾒｲﾝ異常BZ　まとめ
ANB M1489                ; ﾕﾆｯﾄ1異常BZ補助
ANB M3489                ; ﾕﾆｯﾄ2異常BZ補助
ANB M5489
ANB M5989                ; ﾕﾆｯﾄ10異常BZ補助
ANB M6489                ; ﾕﾆｯﾄ11異常BZ補助
ANB M6989                ; ﾕﾆｯﾄ12異常BZ補助
ANB M7489
ANB M7989
ANB M8489
ANB M8989
ANB M9489
ANB M9989
ANB M13489               ; ﾕﾆｯﾄ25異常BZ補助
INV
OUT M54                  ; 異常ﾌﾞｻﾞｰ
;□□　　自動運転停止指令(ｻｲｸﾙ停止)　　□□
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR T15                   ; 強制停止
OR M65                   ; 自動起動停止指令(払出停止)
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M73                  ; 各工程起動指令
OUT M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
;□□　　払出完了運転停止指令(ｻｲｸﾙ停止)　　□□
LD M36                   ; 払出ﾓｰﾄﾞ
LD M41                   ; ﾕﾆｯﾄ_起動中
OR CR2002                ; 常時ON
ANL
AND M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M3493                ; ﾕﾆｯﾄ2ﾜｰｸ無し(払出時)
LD M5493
AND M5993                ; ﾕﾆｯﾄ10ﾜｰｸ無し(払出時)
ORB M99                  ; 能拡後対応FLG
ANL
AND M6493                ; ﾕﾆｯﾄ11ﾜｰｸ無し(払出時)
AND M6993                ; ﾕﾆｯﾄ12ﾜｰｸ無し
LD M7493
AND M7993
AND M8493
AND M8993
AND M9493
AND M9993
ORB M99                  ; 能拡後対応FLG
ANL
ANB M1485                ; ﾕﾆｯﾄ1起動中
ANB M3485                ; ﾕﾆｯﾄ2起動中
ANB M5485
ANB M5985                ; ﾕﾆｯﾄ10起動中
ANB M6485                ; ﾕﾆｯﾄ11起動中
ANB M6985                ; ﾕﾆｯﾄ12起動中
ANB M7485
ANB M7985
ANB M8485
ANB M8985
ANB M9485
ANB M9985
OR M57                   ; 全ﾕﾆｯﾄ払出完了
AND M36                  ; 払出ﾓｰﾄﾞ
OUT M57                  ; 全ﾕﾆｯﾄ払出完了
LD M57                   ; 全ﾕﾆｯﾄ払出完了
LDB L10                  ; ﾘｰｸ検査有無(0_有/1_無)
AND M6350                ; ﾘｰｸﾁｬﾝﾊﾞｰ下降完了
LD L10                   ; ﾘｰｸ検査有無(0_有/1_無)
LD M6350                 ; ﾘｰｸﾁｬﾝﾊﾞｰ下降完了
ORB L1100                ; ﾘｰｸst2_有効状態
ANL
ORL
ANL
OUT M65                  ; 自動起動停止指令(払出停止)
;緊急停止異常まとめ
LDB M1481                ; ﾕﾆｯﾄ1即停止異常
ANB M3481                ; ﾕﾆｯﾄ2即停止異常
ANB M5481
ANB M5981                ; ﾕﾆｯﾄ10即停止異常
ANB M6481                ; ﾕﾆｯﾄ11即停止異常
ANB M6981                ; ﾕﾆｯﾄ12即停止異常
ANB M7481
ANB M7981
ANB M8481
ANB M8981
ANB M9481
ANB M9981
INV
OUT M58                  ; 非常停止異常まとめ
;払出停止異常まとめ
LDB M1487                ; ﾕﾆｯﾄ1払出停止異常
ANB M3487                ; ﾕﾆｯﾄ2払出停止異常
ANB M5487
ANB M5987                ; ﾕﾆｯﾄ10払出停止異常
ANB M6487                ; ﾕﾆｯﾄ11払出停止異常
ANB M6987                ; ﾕﾆｯﾄ12払出停止異常
ANB M7487
ANB M7987
ANB M8487
ANB M8987
ANB M9487
ANB M9987
INV
OUT M59                  ; 払出停止異常まとめ
;■各ﾕﾆｯﾄｴﾗｰ検出
LDB B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
ANB B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
ANB M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M3491                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M5491
ANB M5991                ; ﾕﾆｯﾄ10ｴﾗｰ発生
ANB M6491                ; ﾕﾆｯﾄ11ｴﾗｰ発生
ANB M6991                ; ﾕﾆｯﾄ12ｴﾗｰ発生
ANB M7491
ANB M7991
ANB M8491
ANB M8991
ANB M9491
ANB M9991
INV
OUT M60                  ; ｴﾗｰ発生
;一時停止処理
;アラーム発生時に使用
LD M73                   ; 各工程起動指令
OR M41                   ; ﾕﾆｯﾄ_起動中
ANB M149                 ; 一時停止処理終了
ANB M133                 ; 一時停止　ｷｬﾝｾﾙ
MC
LDP B1012                ; 一時停止PB
LD CR2003                ; 常時OFF
ANP M60                  ; ｴﾗｰ発生
ORL
OR M130                  ; 一時停止
OUT M130                 ; 一時停止
LD M1492                 ; ﾕﾆｯﾄ1動作無し確認
AND M3492                ; ﾕﾆｯﾄ2動作無し確認
LD M5492
OR CR2002                ; 常時ON
ANL
AND M5992                ; ﾕﾆｯﾄ10動作無し確認
AND M6492                ; ﾕﾆｯﾄ11動作無し確認
AND M6992                ; ﾕﾆｯﾄ12動作無し確認
LD M7492
AND M7992
AND M8492
AND M8992
AND M9492
AND M9992
ORB M99                  ; 能拡後対応FLG
ANL
OUB M131                 ; PACK中
LD M130                  ; 一時停止
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OR M133                  ; 一時停止　ｷｬﾝｾﾙ
MPS
LDB M133                 ; 一時停止　ｷｬﾝｾﾙ
ANB M131                 ; PACK中
ANL
OUT M132                 ; 全ﾕﾆｯﾄ停止OK
MPP
LDB M132                 ; 全ﾕﾆｯﾄ停止OK
AND M131                 ; PACK中
ANP B1002                ; ｽﾀｰﾄPB
ANL
OUT M133                 ; 一時停止　ｷｬﾝｾﾙ
LD M132                  ; 全ﾕﾆｯﾄ停止OK
OR M140                  ; ﾄﾞｱﾛｯｸ開
ANB M141                 ; ﾄﾞｱ閉確認
OUT M140                 ; ﾄﾞｱﾛｯｸ開
LD M140                  ; ﾄﾞｱﾛｯｸ開
AND M123                 ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
ANP B1002                ; ｽﾀｰﾄPB
OR M141                  ; ﾄﾞｱ閉確認
OUT M141                 ; ﾄﾞｱ閉確認
LD M141                  ; ﾄﾞｱ閉確認
AND M123                 ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
OR M148                  ; 状態復帰完了
OUT M148                 ; 状態復帰完了
LD M148                  ; 状態復帰完了
OR M149                  ; 一時停止処理終了
OUT M149                 ; 一時停止処理終了
MCR
;<h1/>□_自動起動回路
LD M30                   ; 自動ﾓｰﾄﾞ
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
AND M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; ﾕﾆｯﾄ_起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M50                  ; 運転準備
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M51                  ; 即停止異常(緊急停止)
LDB W00.8                ; PC自動抑制中
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
LD L1100                 ; ﾘｰｸst2_有効状態
OR L10                   ; ﾘｰｸ検査有無(0_有/1_無)
ANL
LD L1200                 ; FINALST2_有効状態
OR L11                   ; ﾌｧｲﾅﾙ検査有無(0_有/1_無)
ANL
OUT M70                  ; 自動起動条件
;<h1/>__運転開始_PCチェック
LDP M18                  ; 起動PB
AND M70                  ; 自動起動条件
OR M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB M71                  ; 自動運転開始BZ
ANB M76                  ; PCより起動不可ON
AND M50                  ; 運転準備
MPS
OUT M75                  ; 自動運転開始PCﾁｪｯｸ開始
MPP
LDB M3                   ; OFFLINE_MODE
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
OUT W0100.6              ; 起動確認
LD M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB W00.6                ; 起動可能
AND W00.5                ; 起動確認完了
ANB M3                   ; OFFLINE_MODE
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT M76                  ; PCより起動不可ON
LD W00.6                 ; 起動可能
AND W00.5                ; 起動確認完了
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
AND M75                  ; 自動運転開始PCﾁｪｯｸ開始
OUT M77                  ; PCより起動可能ON
;<h1/>__運転開始
LD M77                   ; PCより起動可能ON
OR M71                   ; 自動運転開始BZ
ANB M51                  ; 即停止異常(緊急停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M71                  ; 自動運転開始BZ
@MOV K0 D5520            ; NG_CV_Status
INV
MOV K0 D200              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
;起動時ﾌﾞｻﾞｰ
LD M71                   ; 自動運転開始BZ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
MPS
ANB T21                  ; 自動起動ﾃﾞｨﾚｲ
TMR K20 K10              ; 起動前BZ_ONﾀｲﾏ
MPP
AND T20                  ; 起動前BZ_ONﾀｲﾏ
TMR K21 K5               ; 自動起動ﾃﾞｨﾚｲ
@CAL+ D200 K1 D200       ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ  起動時ﾌﾞｻﾞｰｶｳﾝﾄ
LD M71                   ; 自動運転開始BZ
AND= D200 K3             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M72                  ; 自動運転開始
LD M72                   ; 自動運転開始
OUT M73                  ; 各工程起動指令
;<h1/>__電特再検査品装置内有ﾌﾗｸﾞ
LD= D1205 K1             ; 投入部1_再検査品st
OR= D1205 K2             ; 投入部1_再検査品st
OUT M189                 ; 投入部_再検査品有(ST1‐2)
;____搬送1側
LD<> D1705 K0            ; 搬送ﾊﾝﾄﾞ1_再検査品st
OR<> D1805 K0            ; 搬送ﾊﾝﾄﾞ2_再検査品st
OUT M181                 ; 搬送RB1_再投入品_有
LD<> D6205 K0            ; Leak_ST2_再検査品st
OUT M182                 ; Leak_ST2_再投入品_有
LD<> D6705 K0            ; FINAL_ST2_再検査品st
OUT M183                 ; Final_ST2_再投入品_有
LD<> D7205 K0            ; Leak_ST1_Leak_ST1_再検査品st
OUT M184                 ; Leak_ST1_再投入品_有
LD<> D7705 K0            ; FINAL_ST1_再検査品st
OUT M185                 ; Final_ST1_再投入品_有
LDB M181                 ; 搬送RB1_再投入品_有
ANB M182                 ; Leak_ST2_再投入品_有
ANB M183                 ; Final_ST2_再投入品_有
ANB M184                 ; Leak_ST1_再投入品_有
ANB M185                 ; Final_ST1_再投入品_有
OUB M180                 ; 搬送1側_再投入品有
;____搬送2側
LD= D1205 K3             ; 投入部1_再検査品st
OR= D1205 K4             ; 投入部1_再検査品st
OUT M199                 ; 投入部_再検査品有(ST3‐4)
LD<> D3705 K0
OR<> D3805 K0
OUT M191                 ; 搬送RB2_再投入品_有
LD<> D8205 K0
OUT M192                 ; Leak_ST3_再投入品_有
LD<> D8705 K0
OUT M193                 ; Final_ST3_再投入品_有
LD<> D9205 K0
OUT M194                 ; Leak_ST4_再投入品_有
LD<> D9705 K0
OUT M195                 ; Final_ST4_再投入品_有
LDB M191                 ; 搬送RB2_再投入品_有
ANB M192                 ; Leak_ST3_再投入品_有
ANB M193                 ; Final_ST3_再投入品_有
ANB M194                 ; Leak_ST4_再投入品_有
ANB M195                 ; Final_ST4_再投入品_有
OUB M190                 ; 搬送2側_再投入品有
;<h1/>__リーク上治具_休息管理
LD> D212 K99             ; 休息設定時間
MOV K1 D212              ; 休息設定時間
LD>= D212 K1             ; 休息設定時間
AND<= D212 K99           ; 休息設定時間
AND<> D212 K0            ; 休息設定時間
OUT M160                 ; ﾘｰｸ上治具_休息待ち時間有効
LD= D1701 K0             ; 搬送ﾊﾝﾄﾞ1_ﾜｰｸ有無記憶
AND= D1801 K0            ; 搬送ﾊﾝﾄﾞ2_ﾜｰｸ有無記憶
OUT M161                 ; ST1-2_搬送ﾍｯﾄﾞ_ﾜｰｸ無し
LD= D6201 K0             ; Leak_ST2_ﾜｰｸ有無記憶
AND L1100                ; ﾘｰｸst2_有効状態
OUT M162                 ; ST1-2_ﾘｰｸ_ﾜｰｸ無し
LD= D6701 K0             ; FINAL_ST2_ﾜｰｸ有無記憶
OUT M163                 ; ST1-2_ﾌｧｲﾅﾙ_ﾜｰｸ無し
LD M160                  ; ﾘｰｸ上治具_休息待ち時間有効
AND M161                 ; ST1-2_搬送ﾍｯﾄﾞ_ﾜｰｸ無し
AND M162                 ; ST1-2_ﾘｰｸ_ﾜｰｸ無し
AND M163                 ; ST1-2_ﾌｧｲﾅﾙ_ﾜｰｸ無し
ANB X1280                ; 給材部ﾜｰｸ有無確認(ON=有)
OUT M169                 ; ST1-2_ﾜｰｸ無し確認
LD M1371                 ; ﾘｰｸ休息ﾓｰﾄﾞｶｳﾝﾄ開始ﾌﾗｸﾞ
AND M169                 ; ST1-2_ﾜｰｸ無し確認
ANB M6071                ; ﾕﾆｯﾄ11-1全異常
ANB M172                 ; ﾘｰｸ上治具_休息指令中
OUT M170                 ; ﾘｰｸ上治具_休息待ちCNT_開始
@MOV K0 D210             ; 休息時間_秒カウント
@MOV K0 D211             ; 休息時間_分カウント
LD M170                  ; ﾘｰｸ上治具_休息待ちCNT_開始
ANP CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB M172                 ; ﾘｰｸ上治具_休息指令中
MPS
INC D210                 ; 休息時間_秒カウント
MPP
AND>= D210 K60           ; 休息時間_秒カウント
INC D211                 ; 休息時間_分カウント
MOV K0 D210              ; 休息時間_秒カウント
LD M170                  ; ﾘｰｸ上治具_休息待ちCNT_開始
AND>= D211 D212          ; 休息時間_分カウント  休息設定時間
OR M6037                 ; U11-1_S37_下降中にｻｲｸﾙ停止中止
OR M172                  ; ﾘｰｸ上治具_休息指令中
AND M71                  ; 自動運転開始BZ
ANB M174                 ; ﾘｰｸ上治具_休息終了
OUT M172                 ; ﾘｰｸ上治具_休息指令中
OUT B0387                ; 903)_ﾘｰｸ上治具_休息中
LD B1002                 ; ｽﾀｰﾄPB
OR M1370                 ; ﾘｰｸ休息ﾓｰﾄﾞ中止ﾌﾗｸﾞ
OR M36                   ; 払出ﾓｰﾄﾞ
OR M173                  ; ﾘｰｸ上治具_休息終了_待ち
AND M172                 ; ﾘｰｸ上治具_休息指令中
MPS
TMR K26 K2               ; ﾘｰｸ上治具_休息終了_DLY
MPP
AND T26                  ; ﾘｰｸ上治具_休息終了_DLY
OUT M173                 ; ﾘｰｸ上治具_休息終了_待ち
LD M173                  ; ﾘｰｸ上治具_休息終了_待ち
AND X1303                ; ST2_ﾘｰｸ検査_上治具_上昇端
OUT M174                 ; ﾘｰｸ上治具_休息終了
LD M172                  ; ﾘｰｸ上治具_休息指令中
ANB M173                 ; ﾘｰｸ上治具_休息終了_待ち
OUT M179                 ; ﾘｰｸ上治具_休息指令中
;<h1/>□_原点復帰回路
LDB M72                  ; 自動運転開始
ANB M41                  ; ﾕﾆｯﾄ_起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
AND M31                  ; 手動ﾓｰﾄﾞ
OUT M80                  ; 原点復帰可能条件
LD M17                   ; 原点復帰PB
AND M80                  ; 原点復帰可能条件
OR M81                   ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
ANB T15                  ; 強制停止
OUT M81                  ; 原点復帰動作中
MPS
TMR K22 K5               ; 原点復帰開始ﾃﾞｨﾚｲ
MPP
AND CR2003               ; 常時OFF
TMR K23 K300             ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LDP T22                  ; 原点復帰開始ﾃﾞｨﾚｲ
OR M82                   ; 原点復帰開始
AND M81                  ; 原点復帰動作中
ANB M84                  ; 原点復帰起動確認
OUT M82                  ; 原点復帰開始
LD M82                   ; 原点復帰開始
AND M1484                ; ﾕﾆｯﾄ1原点復帰中
AND M3484                ; ﾕﾆｯﾄ2原点復帰中
LD M5484
ORB M99                  ; 能拡後対応FLG
ANL
LD M5984                 ; ﾕﾆｯﾄ10原点復帰中
ORB M99                  ; 能拡後対応FLG
ANL
AND M6484                ; ﾕﾆｯﾄ11原点復帰中
AND M6984                ; ﾕﾆｯﾄ12原点復帰中
LD M7484
AND M7984
AND M8484
AND M8984
AND M9484
AND M9984
ORB M99                  ; 能拡後対応FLG
ANL
OUT M83                  ; 原点復帰全起動確認補助
LD M82                   ; 原点復帰開始
AND M83                  ; 原点復帰全起動確認補助
OR M84                   ; 原点復帰起動確認
AND M81                  ; 原点復帰動作中
ANB M85                  ; 原点復帰完了
OUT M84                  ; 原点復帰起動確認
LD M84                   ; 原点復帰起動確認
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
OR M85                   ; 原点復帰完了
AND M81                  ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
OUT M85                  ; 原点復帰完了
LD M85                   ; 原点復帰完了
AND M40                  ; 全ﾕﾆｯﾄ原点
OR M86                   ; 装置全原点確認
AND M81                  ; 原点復帰動作中
OUT M86                  ; 装置全原点確認
;<h1/>□_ﾌﾞｻﾞｰ補助回路
LD M54                   ; 異常ﾌﾞｻﾞｰ
MPS
ANB T30                  ; 異常ﾌﾞｻﾞｰ自動停止
OUT M100                 ; 異常ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K30 K100             ; 異常ﾌﾞｻﾞｰ自動停止
LD M53                   ; 警報(ﾜｰｸ不足等)
MPS
ANB T31                  ; 警報ﾌﾞｻﾞｰ自動停止
OUT M101                 ; 警報ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K31 K100             ; 警報ﾌﾞｻﾞｰ自動停止
LDP M15                  ; ﾌﾞｻﾞｰ停止PB
OUT M110                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LDP M16                  ; ﾘｾｯﾄPB
LDP X309                 ; ﾜｰｸ供給部NG_SW
AND M1020                ; _1-1_S20_NGﾗﾝﾌﾟ点灯
ORL
OUT M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
;<h1/>□_その他補助回路
;ﾕﾆｯﾄ別動作停止、ﾕﾆｯﾄﾊﾟｽ設定､生産ｶｳﾝﾀ等装置固有の回路がある場合はここ。
;RCX240_CC-LINK初期通信処理
LD CR2002                ; 常時ON
ANB R8400                ; ｴﾗｰ発生
AND R8401                ; ﾕﾆｯﾄﾚﾃﾞｨ
OUT M500                 ; CC-LINK_UNIT_OK
;□□　　出力回路　　□□
LD X00                   ; 装置電源入
OUT Y50                  ; 電源入表示
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
AND X04                  ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
AND X04                  ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
INV
AND M16                  ; ﾘｾｯﾄPB
AND CR2003               ; 常時OFF
OUT Y51                  ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M51                   ; 即停止異常(緊急停止)
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANL
LDB M54                  ; 異常ﾌﾞｻﾞｰ
AND M6414                ; ｾｯﾄ不可ﾌﾞｻﾞｰ(ｴﾘｱｾﾝｻ検出中)
ORL
OUT M502                 ; 異常停止_連続ブザー
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M59                   ; 払出停止異常まとめ
OR M60                   ; ｴﾗｰ発生
ANL
OUT M503                 ; エラー発生_ブザー_0.5
LD M54                   ; 異常ﾌﾞｻﾞｰ
AND M53                  ; 警報(ﾜｰｸ不足等)
ANB M502                 ; 異常停止_連続ブザー
ANB M503                 ; エラー発生_ブザー_0.5
OUT M504                 ; 警報_ブザー_0.5
LD M54                   ; 異常ﾌﾞｻﾞｰ
ANB M502                 ; 異常停止_連続ブザー
ANB M503                 ; エラー発生_ブザー_0.5
ANB M504                 ; 警報_ブザー_0.5
OUT M505                 ; その他_ブザー_1.0
LDB M54                  ; 異常ﾌﾞｻﾞｰ
AND M30                  ; 自動ﾓｰﾄﾞ
AND B0320                ; 800)_安全扉開
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OUT M510                 ; 自動時_安全ドア開_ブザー_1.0
LDB M54                  ; 異常ﾌﾞｻﾞｰ
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ANL
OUT M511                 ; IL解除中_ブザー_10.0
LD M71                   ; 自動運転開始BZ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M520                 ; 自動運転開始_ブザー_1.0
LDB M502                 ; 異常停止_連続ブザー
INV
LDB M503                 ; エラー発生_ブザー_0.5
ANB M504                 ; 警報_ブザー_0.5
INV
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1_0.5sec
ORL
LDB M505                 ; その他_ブザー_1.0
ANB M510                 ; 自動時_安全ドア開_ブザー_1.0
INV
AND T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3_1.0sec
ORL
LDB M511                 ; IL解除中_ブザー_10.0
INV
AND T36                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ_10.0sec
ORL
LDB M520                 ; 自動運転開始_ブザー_1.0
INV
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
ORL
OUT Y31B                 ; ﾒｲﾝﾊﾟﾈﾙ_ﾌﾞｻﾞｰ
OUT Y31C                 ; ｼｸﾞﾅﾙﾀﾜｰ_ﾌﾞｻﾞｰ
LD CR2003                ; 常時OFF
TMR K45 K600             ; 投入要求ﾌﾞｻﾞｰ自動停止
;<h1/>□_ｼｸﾞﾅﾙﾀﾜｰ
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
ANB M59                  ; 払出停止異常まとめ
INV
OUT Y31D                 ; ｼｸﾞﾅﾙﾀﾜｰ_赤
OUT W0100.8              ; ﾀﾜｰﾗｲﾄ赤
LD M53                   ; 警報(ﾜｰｸ不足等)
OUT Y31E                 ; ｼｸﾞﾅﾙﾀﾜｰ_黄
OUT W0100.9              ; ﾀﾜｰﾗｲﾄ黄
LD M71                   ; 自動運転開始BZ
AND M71                  ; 自動運転開始BZ
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OR M73                   ; 各工程起動指令
OR M41                   ; ﾕﾆｯﾄ_起動中
LD M42                   ; 全ﾕﾆｯﾄ原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y31F                 ; ｼｸﾞﾅﾙﾀﾜｰ_緑
OUT W0100.10             ; ﾀﾜｰﾗｲﾄ緑
;　　ﾓｰﾄﾞ切替異常
LD L10                   ; ﾘｰｸ検査有無(0_有/1_無)
AND L11                  ; ﾌｧｲﾅﾙ検査有無(0_有/1_無)
RES L10                  ; ﾘｰｸ検査有無(0_有/1_無)
CON
RES L11                  ; ﾌｧｲﾅﾙ検査有無(0_有/1_無)
;<h1/>□_画面切替管理
;※画面切替管理は別ﾓｼﾞｭｰﾙを作成しても良い。
;□□　　ﾀｯﾁﾊﾟﾈﾙ表示　　□□
LDB M0                   ; 常時OFF
MPS
@MOV K0 D58              ; TpM-K_切替ページNo．
MPP
AND T11                  ; タッチパネル_ロゴ表示終了
@MOV K1 D58              ; TpM-K_切替ページNo．
LD M40                   ; 全ﾕﾆｯﾄ原点
LD M81                   ; 原点復帰動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1500                ; 原点復帰TP-PL
LD M30                   ; 自動ﾓｰﾄﾞ
OUT B1501                ; 自動/手動TP-PL
LD M30                   ; 自動ﾓｰﾄﾞ
LD M71                   ; 自動運転開始BZ
OR M41                   ; ﾕﾆｯﾄ_起動中
ANL
ANB M179                 ; ﾘｰｸ上治具_休息指令中
LD M179                  ; ﾘｰｸ上治具_休息指令中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1502                ; 起動TP-PL
LDB M41                  ; ﾕﾆｯﾄ_起動中
ANB M71                  ; 自動運転開始BZ
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M36                   ; 払出ﾓｰﾄﾞ
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M41                   ; ﾕﾆｯﾄ_起動中
OR M71                   ; 自動運転開始BZ
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1503                ; 停止TP-PL
LDB M53                  ; 警報(ﾜｰｸ不足等)
OR CR2002                ; 常時ON
ANB M54                  ; 異常ﾌﾞｻﾞｰ
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1510                ; ﾌﾞｻﾞｰ停止PL
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
ANB M13300               ; PCﾌﾞｻﾞｰ要求(PC_Busy)
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1511                ; ｴﾗｰﾘｾｯﾄPL
LD M130                  ; 一時停止
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OUT B1512                ; 一時停止PL
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1515                ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙ中PL
LD M35                   ; 空運転ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1516                ; DRY_RUN_PL
LD M38                   ; 段取りﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1517                ; 段取り_PL
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M57                  ; 全ﾕﾆｯﾄ払出完了
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
OUB B1518                ; ｴﾗｰsw表示
;メッセージ表示
LDB M41                  ; ﾕﾆｯﾄ_起動中
ANB M71                  ; 自動運転開始BZ
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M57                  ; 全ﾕﾆｯﾄ払出完了
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
MPS
ANB M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
OUT B0384                ; 900)_ｲﾆｼｬﾗｲｽﾞ中表示
MPP
LDB M90                  ; ｲﾆｼｬﾗｲｽﾞ中表示
ANB M81                  ; 原点復帰動作中
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANB B0384                ; 900)_ｲﾆｼｬﾗｲｽﾞ中表示
ANL
OUT B0385                ; 901)_原点復帰要求
LD M81                   ; 原点復帰動作中
OUT B0386                ; 902)_原点復帰中
;<h1/>□_主幹異常
;ﾒｲﾝ回路に関連する異常はここにまとめる。
LDB M121                 ; 非常停止正常
LD CR2003                ; 常時OFF
ANB B0500                ; 非常停止異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B00                  ; 0)_非常停止異常
ORL
OUT B00                  ; 0)_非常停止異常
LDP B00                  ; 0)_非常停止異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1001 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0500                 ; 非常停止異常BZ停止
AND B00                  ; 0)_非常停止異常
OUT B0500                ; 非常停止異常BZ停止
LD M50                   ; 運転準備
ANB M120                 ; 圧空正常
LD CR2003                ; 常時OFF
ANB B0501                ; 圧空低下異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01                  ; 1)_圧空低下異常
ORL
MPS
TMR K80 K50              ; 圧空低下確認
MPP
AND T80                  ; 圧空低下確認
OUT B01                  ; 1)_圧空低下異常
LDP B01                  ; 1)_圧空低下異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1002 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0501                 ; 圧空低下異常BZ停止
AND B01                  ; 1)_圧空低下異常
OUT B0501                ; 圧空低下異常BZ停止
LD M1492                 ; ﾕﾆｯﾄ1動作無し確認
AND M3492                ; ﾕﾆｯﾄ2動作無し確認
LD M5492
ORB M99                  ; 能拡後対応FLG
ANL
AND M5992                ; ﾕﾆｯﾄ10動作無し確認
AND M6492                ; ﾕﾆｯﾄ11動作無し確認
AND M6992                ; ﾕﾆｯﾄ12動作無し確認
LD M7492
AND M7992
ORB M99                  ; 能拡後対応FLG
ANL
LD M8492
AND M8992
AND M9492
AND M9992
ORB M99                  ; 能拡後対応FLG
ANL
INV
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ANB M123                 ; 安全扉正常
LD CR2003                ; 常時OFF
ANB B0502                ; 安全扉開異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02                  ; 2)_安全扉開異常
ORL
OUT B02                  ; 2)_安全扉開異常
LDP B02                  ; 2)_安全扉開異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1003 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0502                 ; 安全扉開異常BZ停止
AND B02                  ; 2)_安全扉開異常
OUT B0502                ; 安全扉開異常BZ停止
LD T23                   ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LD CR2003                ; 常時OFF
ANB B0503                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ORL
OUT B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
LDP B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1004 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0503                 ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
AND B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OUT B0503                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
LD M50                   ; 運転準備
ANB M122                 ; 外部非常停止正常
LD CR2003                ; 常時OFF
ANB B0504                ; 外部非常停止BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04                  ; 4)_外部非常停止
ORL
OUT B04                  ; 4)_外部非常停止
LDP B04                  ; 4)_外部非常停止
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1005 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0504                 ; 外部非常停止BZ停止
AND B04                  ; 4)_外部非常停止
OUT B0504                ; 外部非常停止BZ停止
LD CR3905                ; 重度ｴﾗｰ発生中
LD CR2003                ; 常時OFF
ANB B0505                ; PLC異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B05                  ; 5)_PLC異常
ORL
OUT B05                  ; 5)_PLC異常
LDP B05                  ; 5)_PLC異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1006 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0505                 ; PLC異常BZ停止
AND B05                  ; 5)_PLC異常
OUT B0505                ; PLC異常BZ停止
LD M5                    ; 運転準備補助
AND R8400                ; ｴﾗｰ発生
LDB R9200                ; 他局通信状態
ANB R9201                ; 他局WDTｴﾗｰ状態
ANB R9202                ; 他局ﾋｭｰｽﾞ断状態
INV
AND M5                   ; 運転準備補助
AND R8401                ; ﾕﾆｯﾄﾚﾃﾞｨ
ORL
LD CR2003                ; 常時OFF
ANB B0506                ; ﾘﾝｸ異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B06                  ; 6)_ﾘﾝｸ異常
ORL
OUT B06                  ; 6)_ﾘﾝｸ異常
LDP B06                  ; 6)_ﾘﾝｸ異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1007 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0506                 ; ﾘﾝｸ異常BZ停止
AND B06                  ; 6)_ﾘﾝｸ異常
OUT B0506                ; ﾘﾝｸ異常BZ停止
LD CR2306                ; ﾊﾞｯｸｱｯﾌﾟ電池異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LD CR2003                ; 常時OFF
ANB B0507                ; PLCﾊﾞｯﾃﾘ低下BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
ORL
MPS
TMR K50 K18000           ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
MPP
AND T50                  ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
OUT B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
LDP B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K8 D13500            ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0507                 ; PLCﾊﾞｯﾃﾘ低下BZ停止
AND B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
OUT B0507                ; PLCﾊﾞｯﾃﾘ低下BZ停止
LDP M10                  ; 自動/手動PB
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; ﾕﾆｯﾄ_起動中
ANB M71                  ; 自動運転開始BZ
LD CR2003                ; 常時OFF
ANB B0509                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
ORL
OUT B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
LDP B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K10 D13500           ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0509                 ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
AND B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
OUT B0509                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
ANB M300                 ; PC通信正常
AND M30                  ; 自動ﾓｰﾄﾞ
LD CR2003                ; 常時OFF
ANB B050A                ; 10)_PC通信異常BZOFF
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A                  ; 10)_PC通信異常
ORL
ANB M3                   ; OFFLINE_MODE
OUT B0A                  ; 10)_PC通信異常
LDP B0A                  ; 10)_PC通信異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1011 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B050A                 ; 10)_PC通信異常BZOFF
AND B0A                  ; 10)_PC通信異常
OUT B050A                ; 10)_PC通信異常BZOFF
LD W00.1                 ; PC Error
LD CR2003                ; 常時OFF
ANB B050B                ; 11)_PC_Error_BZOFF
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0B                  ; 11)_PCｴﾗｰ
ORL
ANB M3                   ; OFFLINE_MODE
OUT B0B                  ; 11)_PCｴﾗｰ
LDP B0B                  ; 11)_PCｴﾗｰ
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1012 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B050B                 ; 11)_PC_Error_BZOFF
AND B0B                  ; 11)_PCｴﾗｰ
OUT B050B                ; 11)_PC_Error_BZOFF
LDB X302
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B0517                ; EMG_STOP(ﾊﾝﾃﾞｨ)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B017                 ; 23)_EMG_STOP(ﾊﾝﾃﾞｨ)
ORL
OUT B017                 ; 23)_EMG_STOP(ﾊﾝﾃﾞｨ)
LDP B017                 ; 23)_EMG_STOP(ﾊﾝﾃﾞｨ)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1024 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0517                 ; EMG_STOP(ﾊﾝﾃﾞｨ)BZ停止
AND B017                 ; 23)_EMG_STOP(ﾊﾝﾃﾞｨ)
OUT B0517                ; EMG_STOP(ﾊﾝﾃﾞｨ)BZ停止
LDB X303                 ; EMG_ON(操作ﾊﾟﾈﾙ)
LD CR2003                ; 常時OFF
ANB B0518                ; EMG_STOP(操作ﾊﾟﾈﾙ)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B018                 ; 24)_EMG_STOP(操作ﾊﾟﾈﾙ)
ORL
OUT B018                 ; 24)_EMG_STOP(操作ﾊﾟﾈﾙ)
LDP B018                 ; 24)_EMG_STOP(操作ﾊﾟﾈﾙ)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1025 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0518                 ; EMG_STOP(操作ﾊﾟﾈﾙ)BZ停止
AND B018                 ; 24)_EMG_STOP(操作ﾊﾟﾈﾙ)
OUT B0518                ; EMG_STOP(操作ﾊﾟﾈﾙ)BZ停止
LDB X304                 ; EMG_ON(ﾜｰｸ投入部)
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B0519                ; EMG_STOP(ﾜｰｸ投入部)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B019                 ; 25)_EMG_STOP(ﾜｰｸ投入部)
ORL
OUT B019                 ; 25)_EMG_STOP(ﾜｰｸ投入部)
LDP B019                 ; 25)_EMG_STOP(ﾜｰｸ投入部)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1026 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0519                 ; EMG_STOP(ﾜｰｸ投入部)BZ停止
AND B019                 ; 25)_EMG_STOP(ﾜｰｸ投入部)
OUT B0519                ; EMG_STOP(ﾜｰｸ投入部)BZ停止
LDB X305                 ; EMG_ON(ﾘｰｸ検査ST1-2)
LD CR2003                ; 常時OFF
ANB B051A                ; EMG_STOP(ﾘｰｸ検査ST2-2)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01A                 ; 26)_EMG_STOP(ﾘｰｸ検査ST2-2)
ORL
OUT B01A                 ; 26)_EMG_STOP(ﾘｰｸ検査ST2-2)
LDP B01A                 ; 26)_EMG_STOP(ﾘｰｸ検査ST2-2)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1027 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051A                 ; EMG_STOP(ﾘｰｸ検査ST2-2)BZ停止
AND B01A                 ; 26)_EMG_STOP(ﾘｰｸ検査ST2-2)
OUT B051A                ; EMG_STOP(ﾘｰｸ検査ST2-2)BZ停止
LDB X306                 ; EMG_ON(ﾘｰｸ検査ST3-4)
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B051B                ; EMG_STOP(ﾘｰｸ検査ST3-4)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01B                 ; 27)_EMG_STOP(ﾘｰｸ検査ST3-4)
ORL
OUT B01B                 ; 27)_EMG_STOP(ﾘｰｸ検査ST3-4)
LDP B01B                 ; 27)_EMG_STOP(ﾘｰｸ検査ST3-4)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1028 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051B                 ; EMG_STOP(ﾘｰｸ検査ST3-4)BZ停止
AND B01B                 ; 27)_EMG_STOP(ﾘｰｸ検査ST3-4)
OUT B051B                ; EMG_STOP(ﾘｰｸ検査ST3-4)BZ停止
LDB X307                 ; EMG_ON(ﾌｧｲﾅﾙ検査ST1-2)
LD CR2003                ; 常時OFF
ANB B051C                ; EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C                 ; 28)_EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)
ORL
OUT B01C                 ; 28)_EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)
LDP B01C                 ; 28)_EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1029 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051C                 ; EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)BZ停止
AND B01C                 ; 28)_EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)
OUT B051C                ; EMG_STOP(ﾌｧｲﾅﾙ検査ST2-2)BZ停止
LDB X308                 ; EMG_ON(ﾌｧｲﾅﾙ検査ST3-4)
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B051D                ; EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)BZ停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01D                 ; 29)_EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)
ORL
OUT B01D                 ; 29)_EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)
LDP B01D                 ; 29)_EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1030 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051D                 ; EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)BZ停止
AND B01D                 ; 29)_EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)
OUT B051D                ; EMG_STOP(ﾌｧｲﾅﾙ検査ST3-4)BZ停止
LD X04                   ; 供給部ﾗｲﾄｶｰﾃﾝOUT1
AND M124                 ; ｴﾘｱｾﾝｻ正常
INV
LD<> D50 K20             ; TpM-K_表示中ページNo.
OR M30                   ; 自動ﾓｰﾄﾞ
ANL
OUT B0320                ; 800)_安全扉開
;異常ﾌﾞｻﾞｰまとめ
LDB B00                  ; 0)_非常停止異常
OR B0500                 ; 非常停止異常BZ停止
LDB B01                  ; 1)_圧空低下異常
OR B0501                 ; 圧空低下異常BZ停止
ANL
LDB B02                  ; 2)_安全扉開異常
OR B0502                 ; 安全扉開異常BZ停止
ANL
LDB B03                  ; 3)_原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OR B0503                 ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
ANL
LDB B04                  ; 4)_外部非常停止
OR B0504                 ; 外部非常停止BZ停止
ANL
LDB B05                  ; 5)_PLC異常
OR B0505                 ; PLC異常BZ停止
ANL
LDB B06                  ; 6)_ﾘﾝｸ異常
OR B0506                 ; ﾘﾝｸ異常BZ停止
ANL
LDB B07                  ; 7)_PLCﾊﾞｯﾃﾘ低下
OR B0507                 ; PLCﾊﾞｯﾃﾘ低下BZ停止
ANL
LDB B09                  ; 9)_原点未完ﾓｰﾄﾞ切替不可異常
OR B0509                 ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
ANL
LDB B0A                  ; 10)_PC通信異常
OR B050A                 ; 10)_PC通信異常BZOFF
ANL
LDB B0B                  ; 11)_PCｴﾗｰ
OR B050B                 ; 11)_PC_Error_BZOFF
ANL
LDB B0C                  ; 12)_
OR B050C
ANL
LDB B0D                  ; 13)_
OR B050D
ANL
LDB B0E                  ; 14)_
OR B050E
ANL
LDB B012                 ; 891)_
OR B0512
ANL
LDB B013                 ; 19)_
OR B0513
ANL
LDB B014                 ; 20)_
OR B0514
ANL
LDB B015                 ; 21)_
OR B0515
ANL
LDB B016                 ; 22)_
OR B0516
ANL
INV
OUT M200                 ; ﾒｲﾝ異常BZ　まとめ
;<h1/>□_手動_JIGNO.指定
LD CR2002                ; 常時ON
MPS
AND M31                  ; 手動ﾓｰﾄﾞ
LDA D500                 ; 手動_JIG_NO.指定
CON
MUL K10
CON
STA D504                 ; RB1_POSｵﾌｾｯﾄ量計算
MPP
AND M30                  ; 自動ﾓｰﾄﾞ
LDA D502                 ; 自動_JIG_NO.指定
CON
MUL K10
CON
STA D504                 ; RB1_POSｵﾌｾｯﾄ量計算
LD CR2002                ; 常時ON
MPS
AND CR2003               ; 常時OFF
LDA D504                 ; RB1_POSｵﾌｾｯﾄ量計算
CON
ADD K1
CON
STA D1030                ; RB1_ﾜｰｸ投入位置Pos指定
MRD
AND CR2002               ; 常時ON
DW K2 D1030              ; RB1_ﾜｰｸ投入位置Pos指定
MPP
LDA D504                 ; RB1_POSｵﾌｾｯﾄ量計算
CON
ADD K0
CON
STA D1031                ; RB1_ｺｰﾄﾞ読取位置Pos指定
;□JIGIDﾁｪｯｸ
LD= D1050 D1210          ; 投入部JIG_ID  投入部1_投入ﾊﾟﾚｯﾄID
OUT M150                 ; 投入部JIG_ID_OK
LD= D6100 D1211          ; ﾘｰｸST2_上治具ID  投入部1_ﾘｰｸ上治具ID
AND= D6101 D1212         ; ﾘｰｸST2_下治具ID  投入部1_ﾘｰｸ下治具ID
OUT M151                 ; ﾘｰｸST2部JIG_ID_OK
LD= D6600 D1214          ; ﾌｧｲﾅﾙST2_ｺﾝﾀｸﾄﾍｯﾄﾞID  投入部1_電特ﾃｽﾄﾍｯﾄﾞID
AND= D6601 D1213         ; ﾌｧｲﾅﾙST2_ﾊﾟﾚｯﾄID  投入部1_電特ﾊﾟﾚｯﾄID
OUT M152                 ; ﾌｧｲﾅﾙST2部JIG_ID_OK
LD= D1210 K0             ; 投入部1_投入ﾊﾟﾚｯﾄID
OUT M155                 ; 投入部治具OK_NGﾏｽｸ
;□ｻｲｸﾙﾀｲﾑ計測
LD M1620                 ; U2_S120_供給→搬送ﾃﾞｰﾀｼﾌﾄ
OUT M210                 ; ｻｲｸﾙﾀｲﾑ計測開始
LD M71                   ; 自動運転開始BZ
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC E0                  ; ｻｲｸﾙﾀｲﾑ計測時間
LDP M210                 ; ｻｲｸﾙﾀｲﾑ計測開始
BMOV E0 E1 K99           ; ｻｲｸﾙﾀｲﾑ計測時間  ｻｲｸﾙﾀｲﾑ
MOV K0 E0                ; ｻｲｸﾙﾀｲﾑ計測時間
MPS
AND> K100 E110           ; ｻｲｸﾙﾀｲﾑ計測回数
INC E110                 ; ｻｲｸﾙﾀｲﾑ計測回数
MPP
AVG E1 E110              ; ｻｲｸﾙﾀｲﾑ  ｻｲｸﾙﾀｲﾑ計測回数
CON
STA E112
LD B1002                 ; ｽﾀｰﾄPB
AND B1003                ; ｻｲｸﾙ停止PB
FMOV K0 E0 K120          ; ｻｲｸﾙﾀｲﾑ計測時間
;PC製品ﾃﾞｰﾀｸﾘｱ要求
LDB M5525                ; U10-1_S25_OK製品ﾃﾞｰﾀｸﾘｱ要求
ANB M1310                ; OK品ﾃﾞｰﾀｸﾘｱ要求
ANB M1311                ; NG品ﾃﾞｰﾀｸﾘｱ要求(OKと共通)
INV
OUT W0101.8              ; OK排出製品ﾃﾞｰﾀｸﾘｱ要求
LDB M5605                ; U10-2_S25_NG製品ﾃﾞｰﾀｸﾘｱ要求
INV
OUT W0101.9              ; NG排出製品ﾃﾞｰﾀｸﾘｱ要求
END
ENDH
