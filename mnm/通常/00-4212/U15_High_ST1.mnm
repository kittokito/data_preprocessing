DEVICE:53
;MODULE:U15_High_ST1
;MODULE_TYPE:0
;<h1/>□_入力受け
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X1438                 ; 高温検査正面上流安全扉正常
AND X1439                ; 高温検査正面下流安全扉正常
AND X143A                ; 高温検査背面上流安全扉正常
AND X143B                ; 高温検査背面下流安全扉正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
TMR K1501 K5             ; HiST1扉閉
LD X1442                 ; HiST1_部_ﾜｰｸ押_下降端
TMH K1502 K50            ; HiST1_部_ﾜｰｸ押_下降端
LD CR2003                ; 常時OFF
TMR K1503 K3
LD X1441                 ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
TMR K1504 K5             ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
;ｺﾝﾀｸﾄﾍｯﾄﾞID
LD X1446                 ; HiST1_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_1
OUT D8106.0
LD X1447                 ; HiST1_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_2
OUT D8106.1
LD X1448                 ; HiST1_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_4
OUT D8106.2
;ﾊﾟﾚｯﾄID
LD X1449                 ; HiST1_部_ﾊﾟﾚｯﾄID_1
OUT D8107.0
LD X144A                 ; HiST1_部_ﾊﾟﾚｯﾄID_2
OUT D8107.1
LD X144B                 ; HiST1_部_ﾊﾟﾚｯﾄID_4
OUT D8107.2
;ＩＤ保持
LD M71                   ; 自動運転開始BZ
AND L1500                ; HiST1_有効状態
LD M9412                 ; HiST1_ID保持ﾄﾘｶﾞ
ANB M9410                ; HiST1_無効中
ORL
AND M71                  ; 自動運転開始BZ
OUT M9412                ; HiST1_ID保持ﾄﾘｶﾞ
;____ｺﾝﾀｸﾄﾍｯﾄﾞＩＤ保持
;※PC送信用は生ﾃﾞｰﾀに変更
LD L1500                 ; HiST1_有効状態
MPS
TMR K1506 K5             ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
MPP
AND T1506                ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
@MOV D8106 D8100         ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID
LD CR2002                ; 常時ON
MOV D8106 W013A          ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  ﾃｽﾄﾍｯﾄﾞID#1
LDB L1500                ; HiST1_有効状態
MOV K0 D8100             ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID
;____ﾊﾟﾚｯﾄＩＤ保持
LD L1500                 ; HiST1_有効状態
LDB Y2500                ; HiST1ｽﾃｰｼﾞ前後前進
AND T1504                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OR T1507                 ; HiST1_ﾊﾟﾚｯﾄID_更新_DLY
ANL
TMR K1507 K5             ; HiST1_ﾊﾟﾚｯﾄID_更新_DLY
@MOV D8107 D8101         ; HiST1_ﾊﾟﾚｯﾄID_ｾﾝｻ状態  HiST1_ﾊﾟﾚｯﾄID
LD CR2002                ; 常時ON
MOV D8101 W0136          ; HiST1_ﾊﾟﾚｯﾄID  ﾊﾟﾚｯﾄID#1
LDB L1500                ; HiST1_有効状態
MOV K0 D8101             ; HiST1_ﾊﾟﾚｯﾄID
;<h1/>__ST_有効/無効選択
;段取り状態中ﾌﾗｸﾞの追加
;自動中は段取り状態中に、ST上のﾜｰｸ無、RBﾊﾝﾄﾞﾜｰｸ無し、供給部ﾜｰｸ無しにて無効状態OKとなる
LDPB X144F               ; ST2電特検査_有効/無効SW
ANPB B124A               ; HiST1検査_有効/無効_Tpb
INV
ANB L1500                ; HiST1_有効状態
OR M9404                 ; HiST1検査_有効確認要求
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
ANB M9405                ; HiST1検査_有効確認OK
OUT M9404                ; HiST1検査_有効確認要求
LD M9404                 ; HiST1検査_有効確認要求
AND W0107.3              ; Hi_ST1ﾁｪｯｶ-確認要求
LD W07.3                 ; Hi_ST1ﾁｪｯｶ-確認完了
OR M3                    ; OFFLINE_MODE
ANL
OUT M9405                ; HiST1検査_有効確認OK
LD M9405                 ; HiST1検査_有効確認OK
ANB W05.4                ; ｽﾃｰｼﾞ#1 無効切替要求
LDPB X144F               ; ST2電特検査_有効/無効SW
ANPB B124A               ; HiST1検査_有効/無効_Tpb
ANB W05.4                ; ｽﾃｰｼﾞ#1 無効切替要求
INV
AND L1500                ; HiST1_有効状態
ORL
OUT M9406                ; HiST1検査_有効/無効要求
LD M71                   ; 自動運転開始BZ
LDB M8482                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M8486                ; ﾊﾝﾄﾞ1_Hi検査ST1再検品
OR M8483                 ; ﾊﾝﾄﾞ1_Hi検査完了品(排出ﾜｰｸ)
ANL
LDB M8492                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M8496                ; ﾊﾝﾄﾞ2_Hi検査ST1再検品
OR M8493                 ; ﾊﾝﾄﾞ2_Hi検査完了品(排出ﾜｰｸ)
ANL
AND= D8201 K0            ; HiST1 ﾜｰｸ有無記憶
LDB M9485                ; ﾕﾆｯﾄ15起動中
LD M9001                 ; U15-1_S01
ANB M8401                ; 【搬送】搬送→Hi_ST1搬送開始
ORL
ANL
ANB M8530                ; ﾊﾝﾄﾞ1_HiST1給材中ﾌﾗｸﾞ
ANB M8531                ; ﾊﾝﾄﾞ2_HiST1給材中ﾌﾗｸﾞ
OUT M9407                ; HiST1_自動中無効条件OK
LDB M71                  ; 自動運転開始BZ
OR M9407                 ; HiST1_自動中無効条件OK
OUT M9408                ; HiST1_無効OK条件
LD M9409                 ; HiST1_無効予約(自動運転)
ANB M9410                ; HiST1_無効中
ANP M9406                ; HiST1検査_有効/無効要求
OUT M9411                ; HiST1_予約ｷｬﾝｾﾙ
LDP M9406                ; HiST1検査_有効/無効要求
AND L1500                ; HiST1_有効状態
AND M71                  ; 自動運転開始BZ
ANB M9409                ; HiST1_無効予約(自動運転)
LD M9409                 ; HiST1_無効予約(自動運転)
ANB M9410                ; HiST1_無効中
ANB M9411                ; HiST1_予約ｷｬﾝｾﾙ
ORL
OUT M9409                ; HiST1_無効予約(自動運転)
LD M9409                 ; HiST1_無効予約(自動運転)
AND M9408                ; HiST1_無効OK条件
LDP M9406                ; HiST1検査_有効/無効要求
ANB M41                  ; ﾕﾆｯﾄ_起動中
ORL
LDB B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
ANB B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
INV
MEP
ORL
LD M9410                 ; HiST1_無効中
ANB L1500                ; HiST1_有効状態
ORL
OUT M9410                ; HiST1_無効中
;<h1/>__ST_有効処理
LDP M9406                ; HiST1検査_有効/無効要求
LD<> D8106 K0            ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND<> D8107 K0           ; HiST1_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8106 K0            ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND= D8107 K0            ; HiST1_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
ORL
ANL
ANB L1500                ; HiST1_有効状態
LD L1500                 ; HiST1_有効状態
ANB M9410                ; HiST1_無効中
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
ORL
OUT L1500                ; HiST1_有効状態
;JIG_ID一致確認
LD L1500                 ; HiST1_有効状態
MPS
LD= D8100 D7711          ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID  ﾊﾝﾄﾞ1 高温検査ﾃｽﾄﾍｯﾄﾞID
AND= D8101 D7710         ; HiST1_ﾊﾟﾚｯﾄID  ﾊﾝﾄﾞ1 高温検査ﾊﾟﾚｯﾄID
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8100 K0            ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID
AND= D8101 K0            ; HiST1_ﾊﾟﾚｯﾄID
ORL
ANL
OUT M9401                ; HiST1-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
MPP
LD= D8100 D7811          ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID  ﾊﾝﾄﾞ2 高温検査ﾃｽﾄﾍｯﾄﾞID
AND= D8101 D7810         ; HiST1_ﾊﾟﾚｯﾄID  ﾊﾝﾄﾞ2 高温検査ﾊﾟﾚｯﾄID
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8100 K0            ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID
AND= D8101 K0            ; HiST1_ﾊﾟﾚｯﾄID
ORL
ANL
OUT M9402                ; HiST1-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
;<h1/>__ロボット等機器入力受け
;<h1/>□_装置状態・動作条件
;ﾕﾆｯﾄまとめ
LD M9070                 ; U15-1原点
AND M9230                ; U15-3原点
OUT M9480                ; ﾕﾆｯﾄ15原点
;□ 非常停止
LDB M9071                ; U15-1全異常
ANB M9231                ; U15-3全異常
INV
OUT M9481                ; ﾕﾆｯﾄ15即停止異常
;□ 起動条件
LD CR2003                ; 常時OFF
OUT M9482                ; ﾕﾆｯﾄ15起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M9484                ; ﾕﾆｯﾄ15原点復帰中
ORL
OR M9486                 ; ﾕﾆｯﾄ15ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M9483                ; ﾕﾆｯﾄ15起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M9484                ; ﾕﾆｯﾄ15原点復帰中
OR M9484                 ; ﾕﾆｯﾄ15原点復帰中
ANB M9483                ; ﾕﾆｯﾄ15起動OFF
ANB M9491                ; ﾕﾆｯﾄ15ｴﾗｰ発生
ANB M9297                ; OriStepEND
OUT M9484                ; ﾕﾆｯﾄ15原点復帰中
;□ 起動中
LDB M9074                ; U15-1起動中
ANB M9234                ; U15-3(検査SUB)起動中
INV
OUT M9485                ; ﾕﾆｯﾄ15起動中
;□ ｻｲｸﾙ停止異常
LDB M9075                ; U15-1ｻｲｸﾙ停止異常
ANB M9235                ; U15-3ｻｲｸﾙ停止異常
INV
OUT M9486                ; ﾕﾆｯﾄ15ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9487                ; ﾕﾆｯﾄ15払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M9488                ; ﾕﾆｯﾄ15警報補助
;□ 機器準備確認
LD CR2002                ; 常時ON
OUT M9490                ; ﾕﾆｯﾄ15機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB M9076                ; U15-1ｴﾗｰ発生
ANB M9236                ; U15-3ｴﾗｰ発生
INV
OUT M9491                ; ﾕﾆｯﾄ15ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD M9077                 ; U15-1動作無し
AND M9237                ; U15-3動作無し
OUT M9492                ; ﾕﾆｯﾄ15動作無し確認
;□ 払出しﾜｰｸ無し
LDB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
ANB M8482                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M8486                ; ﾊﾝﾄﾞ1_Hi検査ST1再検品
ANB M8492                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M8496                ; ﾊﾝﾄﾞ2_Hi検査ST1再検品
TMR K1514 K10
LD T1514
OUT M9493                ; ﾕﾆｯﾄ15ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
AND T1501                ; HiST1扉閉
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M57                  ; 全ﾕﾆｯﾄ払出完了
ANB M59                  ; 払出停止異常まとめ
ANB M9491                ; ﾕﾆｯﾄ15ｴﾗｰ発生
LDB M130                 ; 一時停止
ORB M9017                ; U15-1_S17
ANL
OUT M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
;<h1/>□_自動回路
;<h1/>◇_状態・動作条件(補助)
LD X1443                 ; HiST1_部_ﾜｰｸ押_上昇端
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
ANB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
AND= D8201 K0            ; HiST1 ﾜｰｸ有無記憶
OUT M9070                ; U15-1原点
LDB B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
ANB B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
ANB B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
ANB B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
ANB B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
INV
OUT M9071                ; U15-1全異常
LD M9070                 ; U15-1原点
ANB M9074                ; U15-1起動中
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
LD M9401                 ; HiST1-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
OR M9402                 ; HiST1-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
ANL
ANB M9408                ; HiST1_無効OK条件
OUT M9072                ; U15-1起動条件
LDB M50                  ; 運転準備
ORB L1500                ; HiST1_有効状態
OR M9486                 ; ﾕﾆｯﾄ15ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M9073                ; U15-1起動OFF
LD M73                   ; 各工程起動指令
AND M9072                ; U15-1起動条件
OR M9074                 ; U15-1起動中
ANB M9073                ; U15-1起動OFF
ANB M9063                ; U15-1 StepEnd
OUT M9074                ; U15-1起動中
;ﾕﾆｯﾄ1-1 ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9075                ; U15-1ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-1 ｴﾗｰ発生まとめ
LDB B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
ANB B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
ANB B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
ANB B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
INV
OUT M9076                ; U15-1ｴﾗｰ発生
;ﾕﾆｯﾄ1-1 停止判定
LD= D8000 D8005          ; HiST1-1自動運転ｽﾃｯﾌﾟ  HiST1-1動作中判定
ORB M9074                ; U15-1起動中
TMR K1510 K20            ; 停止完了判定ﾀｲﾏｰ
LD CR2002                ; 常時ON
MOV D8000 D8005          ; HiST1-1自動運転ｽﾃｯﾌﾟ  HiST1-1動作中判定
LD T1510                 ; 停止完了判定ﾀｲﾏｰ
OUT M9077                ; U15-1動作無し
;□
;□　ﾕﾆｯﾄ1-1 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M9000 K4         ; U15-1_S00
LDB M9074                ; U15-1起動中
MOV K0 D8000             ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9074                 ; U15-1起動中
MOV D8000 Z1             ; HiST1-1自動運転ｽﾃｯﾌﾟ
SET M9000:Z1             ; U15-1_S00
;ﾜｰｸ有無判定等
LD M9000                 ; U15-1_S00
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9001                 ; U15-1_S01
MPS
ANB M9493                ; ﾕﾆｯﾄ15ﾜｰｸ無し
MPS
AND M8401                ; 【搬送】搬送→Hi_ST1搬送開始
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
LD M9401                 ; HiST1-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
OR M9402                 ; HiST1-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
ANB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
ANL
OUT M9420                ; HiST1→搬送ﾜｰｸ供給要求
MPP
LD M9493                 ; ﾕﾆｯﾄ15ﾜｰｸ無し
AND M36                  ; 払出ﾓｰﾄﾞ
ANL
MOV K63 D8000            ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9002                 ; U15-1_S02
LD M8405                 ; 【搬送】搬送→Hi_ST1搬送完了
OR M9422                 ; HiST1→搬送ﾜｰｸ受取り完了
ANL
MPS
LDB M8401                ; 【搬送】搬送→Hi_ST1搬送開始
ANB M8405                ; 【搬送】搬送→Hi_ST1搬送完了
ANL
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M9422                ; HiST1→搬送ﾜｰｸ受取り完了
LD M9003                 ; U15-1_S03
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND= D8201 K1            ; HiST1 ﾜｰｸ有無記憶
MPS
LD X1444                 ; HiST1_部_ﾜｰｸ確認(ON=有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
MOV K60 D8000            ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9060                 ; U15-1_S60 HiST1_ﾜｰｸ受取異常
AND B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
MOV K63 D8000            ; HiST1-1自動運転ｽﾃｯﾌﾟ
;　　ｸﾗﾝﾌﾟ指令
LD M9004                 ; U15-1_S04_ｸﾗﾝﾌﾟ指令
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND X1442                ; HiST1_部_ﾜｰｸ押_下降端
ANB B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;　　Z軸上昇確認
LD M9005                 ; U15-1_S05_開始確認待ち
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
LD L1452                 ; Hi搬送高さ移動完了(HIGH)
OR L1451                 ; Hi搬送高さ移動完了(MIDLE)
ANL
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
@INC D8020               ; HiST1投入回数
LD M9006                 ; U15-1_S06
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9007                 ; U15-1_S07
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9008                 ; U15-1_S08
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9009                 ; U15-1_S09
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;　　■電特検査前処理
LD M9010                 ; U15-1_S10
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9011                 ; U15-1_S11
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9012                 ; U15-1_S12_
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9013                 ; U15-1_S13
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9014                 ; U15-1_S14_ﾊﾟﾚｯﾄ前進指令
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND X1440                ; HiST1_部_ﾊﾟﾚｯﾄ_前進端
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S15_電特検査処理開始
LD M9015                 ; U15-1_S15
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
ANB M9234                ; U15-3(検査SUB)起動中
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9016                 ; U15-1_S16_検査SUB開始ﾌﾗｸﾞ
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND M9234                ; U15-3(検査SUB)起動中
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9017                 ; U15-1_S17
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
ANB M9234                ; U15-3(検査SUB)起動中
ANB M130                 ; 一時停止
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9018                 ; U15-1_S18
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9019                 ; U15-1_S19
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;　　ｽﾃｰｼﾞ後退
LD M9020                 ; U15-1_S20_ｽﾃｰｼﾞ後退命令
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;　　ｸﾗﾝﾌﾟ解除
LD M9021                 ; U15-1_S21_
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9022                 ; U15-1_S22
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9023                 ; U15-1_S23
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9024                 ; U15-1_S24
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S25_ﾜｰｸ除材要求
LDB M8482                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M8486                ; ﾊﾝﾄﾞ1_Hi検査ST1再検品
ANB M8492                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M8496                ; ﾊﾝﾄﾞ2_Hi検査ST1再検品
ANB M8520                ; ﾊﾝﾄﾞ1HiST1除材中ﾌﾗｸﾞ
ANB M8521                ; ﾊﾝﾄﾞ2HiST1除材中ﾌﾗｸﾞ
ANB M9409                ; HiST1_無効予約(自動運転)
ANB M9909                ; HiST2_無効予約(自動運転)
ANB M10409               ; HiST3_無効予約(自動運転)
ANB M10909               ; HiST4_無効予約(自動運転)
INV
OUT M9425                ; HiST1_ﾜｰｸ除去要求発行許可
;M9　M1304を上流仮置きよりﾜｰｸ除材要求なしに入れ替え
LD M9025                 ; U15-1_S25
MPS
AND M8411                ; 【搬送】HiST1除材開始
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
LDB M7511                ; 仮置き台ﾜｰｸ取出可
OR M9425                 ; HiST1_ﾜｰｸ除去要求発行許可
ANL
OUT M9421                ; HiST1→搬送1除材要求
LD M9026                 ; U15-1_S26
LD M8415                 ; 【搬送】HiST1除材完了
OR M9423                 ; HiST1→搬送除材完了受取
ANL
MPS
LDB M8411                ; 【搬送】HiST1除材開始
ANB M8415                ; 【搬送】HiST1除材完了
ANL
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M9423                ; HiST1→搬送除材完了受取
LD M9027                 ; U15-1_S27
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND= D8201 K0            ; HiST1 ﾜｰｸ有無記憶
LD L1452                 ; Hi搬送高さ移動完了(HIGH)
OR L1451                 ; Hi搬送高さ移動完了(MIDLE)
OR L1450                 ; ﾜｰｸ入替高さ移動完了(HiW)
ANL
MPS
ANB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
OUT M9430                ; HiST1_ﾜｰｸ受渡し異常
LD M9028                 ; U15-1_S28
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8000                ; HiST1-1自動運転ｽﾃｯﾌﾟ
LD M9029                 ; U15-1_S29
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
MOV K63 D8000            ; HiST1-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M9062                 ; U15-1_S62
OR CR2003                ; 常時OFF
@MOV K63 D8000           ; HiST1-1自動運転ｽﾃｯﾌﾟ
;<h1/>◇_電特検査SUB 状態・動作条件(補助)
LD CR2002                ; 常時ON
OUT M9230                ; U15-3原点
LDB CR2003               ; 常時OFF
INV
OUT M9231                ; U15-3全異常
LD M9230                 ; U15-3原点
ANB M9234                ; U15-3(検査SUB)起動中
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND M300                 ; PC通信正常
OUT M9232                ; U15-3起動条件
LDB M50                  ; 運転準備
OR M9486                 ; ﾕﾆｯﾄ15ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M9233                ; U15-3起動OFF
LD M9016                 ; U15-1_S16_検査SUB開始ﾌﾗｸﾞ
AND M9232                ; U15-3起動条件
OR M9234                 ; U15-3(検査SUB)起動中
ANB M9233                ; U15-3起動OFF
ANB M9223                ; U15-3 StepEnd
OUT M9234                ; U15-3(検査SUB)起動中
;tp表示
LD CR2002                ; 常時ON
DW K0 D8400              ; Hi検査_ｽﾃｰﾀｽ表示
LD M9234                 ; U15-3(検査SUB)起動中
DW K1 D8400              ; Hi検査_ｽﾃｰﾀｽ表示
LD= D8204 K1             ; HiST1 Hi検査結果(1=OK_2=NG)
DW K2 D8400              ; Hi検査_ｽﾃｰﾀｽ表示
LD= D8204 K2             ; HiST1 Hi検査結果(1=OK_2=NG)
DW K3 D8400              ; Hi検査_ｽﾃｰﾀｽ表示
LDB L1500                ; HiST1_有効状態
DW K4 D8400              ; Hi検査_ｽﾃｰﾀｽ表示
;ﾕﾆｯﾄ1-3ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9235                ; U15-3ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-3ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M9236                ; U15-3ｴﾗｰ発生
;ﾕﾆｯﾄ1-3停止判定
LD= D8002 D8007          ; HiST1-3自動運転ｽﾃｯﾌﾟ  HiST1-3動作中判定
TMR K1512 K20
LD CR2002                ; 常時ON
MOV D8002 D8007          ; HiST1-3自動運転ｽﾃｯﾌﾟ  HiST1-3動作中判定
LD T1512
ORB M9234                ; U15-3(検査SUB)起動中
OUT M9237                ; U15-3動作無し
;□
;□　ﾕﾆｯﾄ1-3自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M9160 K4         ; U15-3_S0
LDB M9234                ; U15-3(検査SUB)起動中
MOV K0 D8002             ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9234                 ; U15-3(検査SUB)起動中
MOV D8002 Z1             ; HiST1-3自動運転ｽﾃｯﾌﾟ
SET M9160:Z1             ; U15-3_S0
;<h1/>__S0_ﾜｰｸ有無判定
LD M9160                 ; U15-3_S0
MPS
LD T1501                 ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
OR M3                    ; OFFLINE_MODE
ANL
MPS
AND T1530                ; Hi検査時間(仮)
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
MPP
TMR K1530 K50            ; Hi検査時間(仮)
LD M9161                 ; U15-3_S1Hi検査ｽﾀｰﾄ要求
AND W0105.8              ; 電気特性検査開始要求#1
LD W05.8                 ; 電気特性検査ｽﾃｰｼﾞ状態#1
ANB W05.12               ; 電気特性検査結果#1
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9162                 ; U15-3_S2
LDB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#1
OR M3                    ; OFFLINE_MODE
ANL
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9163                 ; U15-3_S3
MPS
AND<> D8204 K0           ; HiST1 Hi検査結果(1=OK_2=NG)
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
MRD
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
MPS
AND W05.12               ; 電気特性検査結果#1
MOV K1 D8204             ; HiST1 Hi検査結果(1=OK_2=NG)
MPP
ANB W05.12               ; 電気特性検査結果#1
MOV K2 D8204             ; HiST1 Hi検査結果(1=OK_2=NG)
MPP
LD M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MPS
ANB B1038                ; B3C8_968)Bz停止
MOV K1 D8204             ; HiST1 Hi検査結果(1=OK_2=NG)
MPP
AND B1038                ; B3C8_968)Bz停止
MOV K2 D8204             ; HiST1 Hi検査結果(1=OK_2=NG)
LD M9164                 ; U15-3_S4
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9165                 ; U15-3_S5
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9166                 ; U15-3_S6
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9167                 ; U15-3_S7
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9168                 ; U15-3_S8
INC D8002                ; HiST1-3自動運転ｽﾃｯﾌﾟ
LD M9169                 ; U15-3_S9
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
MOV K63 D8002            ; HiST1-3自動運転ｽﾃｯﾌﾟ
;<h1/>□_原点復帰回路
LD CR2002                ; 常時ON
FMOV K0 M9250 K3         ; OriStep0
LDB M9484                ; ﾕﾆｯﾄ15原点復帰中
MOV K0 D8010             ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9484                 ; ﾕﾆｯﾄ15原点復帰中
MOV D8010 Z1             ; HiST1 原点復帰ｽﾃｯﾌﾟ
SET M9250:Z1             ; OriStep0
;原点復帰ｻｲｸﾙ監視
LD M9484                 ; ﾕﾆｯﾄ15原点復帰中
TMR K1500 K200           ; UNIT15原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M9250                 ; OriStep0
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND M66                  ; Lo_Hi_RB & Chamber原点
DW K0 D8020              ; HiST1投入回数
CON
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9251                 ; OriStep1
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
ANB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#1
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9252                 ; OriStep2
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9253                 ; OriStep3_ｽﾃｰｼﾞ戻り指令
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9254                 ; OriStep4
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9255                 ; OriStep5_
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND L1452                ; Hi搬送高さ移動完了(HIGH)
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9256                 ; OriStep6_ｸﾗﾝﾌﾟ上昇指令
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND X1443                ; HiST1_部_ﾜｰｸ押_上昇端
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ有無ﾁｪｯｸ
LD M9257                 ; OriStep7
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
MPS
ANB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
MOV K10 D8010            ; HiST1 原点復帰ｽﾃｯﾌﾟ
MPP
AND X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ除去要求
LD M9258                 ; OriStep8_ORGﾜｰｸ除去要求
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
AND B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
MOV K46 D8010            ; HiST1 原点復帰ｽﾃｯﾌﾟ
;　　
LD M9259                 ; OriStep9
AND T1501                ; HiST1扉閉
AND M9479                ; ﾕﾆｯﾄ15ｽﾃｯﾌﾟ動作
INC D8010                ; HiST1 原点復帰ｽﾃｯﾌﾟ
LD M9260                 ; OriStep10
MPS
AND M9480                ; ﾕﾆｯﾄ15原点
MOV K46 D8010            ; HiST1 原点復帰ｽﾃｯﾌﾟ
MPP
FMOV K0 D8200 K20        ; HiST1 INDEX NO.
LD M9296                 ; OriStep46
ANB M82                  ; 原点復帰開始
MOV K47 D8010            ; HiST1 原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M9485                ; ﾕﾆｯﾄ15起動中
ANB M9484                ; ﾕﾆｯﾄ15原点復帰中
MC
;ｺﾝﾀｸﾄﾍｯﾄﾞ前後
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND M8302                ; U14-3_RCX240起動条件
AND M8311                ; RCX240動作完了
AND L1452                ; Hi搬送高さ移動完了(HIGH)
AND X1442                ; HiST1_部_ﾜｰｸ押_下降端
OUT M9300                ; HiST1ﾊﾟﾚｯﾄ前進_OK
LDP B1240                ; UNIT9-2-1 TP-PB
LD M9312                 ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANP X1209                ; Lo#1_上段扉閉端
ANB X1208                ; Lo#1_上段扉開端
ORL
ORP M9324                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND M9300                ; HiST1ﾊﾟﾚｯﾄ前進_OK
LD M9301                 ; HiST1ﾊﾟﾚｯﾄ前進指令
ANB M9303                ; HiST1ﾊﾟﾚｯﾄ後退指令
ORL
OUT M9301                ; HiST1ﾊﾟﾚｯﾄ前進指令
LD CR2002                ; 常時ON
ANB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#1
AND L1452                ; Hi搬送高さ移動完了(HIGH)
OUT M9302                ; HiST1ﾊﾟﾚｯﾄ後退_OK
LDP B1241                ; UNIT9-2-2 TP-PB
LD M9312                 ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANP X1208                ; Lo#1_上段扉開端
ANB X1209                ; Lo#1_上段扉閉端
ORL
ORP M9332                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND M9302                ; HiST1ﾊﾟﾚｯﾄ後退_OK
LD M9303                 ; HiST1ﾊﾟﾚｯﾄ後退指令
ANB M9301                ; HiST1ﾊﾟﾚｯﾄ前進指令
ORL
OUT M9303                ; HiST1ﾊﾟﾚｯﾄ後退指令
;　　ｸﾗﾝﾌﾟ上下
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND M8302                ; U14-3_RCX240起動条件
AND M8311                ; RCX240動作完了
AND L1452                ; Hi搬送高さ移動完了(HIGH)
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OUT M9304                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_起動OK
LDP B1242                ; UNIT9-2-3 TP-PB
ORP M9322                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND M9304                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_起動OK
LD M9305                 ; HiST1ｸﾗﾝﾌﾟ開閉_閉_指令
ANB M9307                ; HiST1ｸﾗﾝﾌﾟ開閉_開__指令
ORL
OUT M9305                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_指令
LD CR2002                ; 常時ON
ANB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#1
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OUT M9306                ; HiST1ｸﾗﾝﾌﾟ開閉_開_起動OK
LDP B1243                ; UNIT9-2-4 TP-PB
ORP M9334                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND M9306                ; HiST1ｸﾗﾝﾌﾟ開閉_開_起動OK
LD M9307                 ; HiST1ｸﾗﾝﾌﾟ開閉_開__指令
ANB M9305                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_指令
ORL
OUT M9307                ; HiST1ｸﾗﾝﾌﾟ開閉_開__指令
;ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進
LD M129                  ; 安全入力OK
AND M8302                ; U14-3_RCX240起動条件
AND M8311                ; RCX240動作完了
AND L1452                ; Hi搬送高さ移動完了(HIGH)
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OUT M9320                ; HiST1ｸﾗﾝﾌﾟ&前進指令条件
LD M9313                 ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANP X1209                ; Lo#1_上段扉閉端
ANB X1208                ; Lo#1_上段扉開端
AND M9304                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_起動OK
AND M9320                ; HiST1ｸﾗﾝﾌﾟ&前進指令条件
OR M9321                 ; HiST1ｸﾗﾝﾌﾟ&前進開始
ANB M9326                ; HiST1ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進動作完了
ANB B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
ANB B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
AND M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT M9321                ; HiST1ｸﾗﾝﾌﾟ&前進開始
LD M9321                 ; HiST1ｸﾗﾝﾌﾟ&前進開始
AND M9304                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_起動OK
OR M9322                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND M9321                ; HiST1ｸﾗﾝﾌﾟ&前進開始
OUT M9322                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
LD M9322                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND T1502                ; HiST1_部_ﾜｰｸ押_下降端
OR M9323                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
AND M9321                ; HiST1ｸﾗﾝﾌﾟ&前進開始
OUT M9323                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
LD M9323                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
AND M9300                ; HiST1ﾊﾟﾚｯﾄ前進_OK
OR M9324                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND M9321                ; HiST1ｸﾗﾝﾌﾟ&前進開始
OUT M9324                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
LD M9324                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND X1440                ; HiST1_部_ﾊﾟﾚｯﾄ_前進端
OR M9325                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
AND M9321                ; HiST1ｸﾗﾝﾌﾟ&前進開始
OUT M9325                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
LD M9325                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
OUT M9326                ; HiST1ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進動作完了
;ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ解除
LD M129                  ; 安全入力OK
AND M8302                ; U14-3_RCX240起動条件
AND M8311                ; RCX240動作完了
AND L1452                ; Hi搬送高さ移動完了(HIGH)
OUT M9330                ; HiST1ｸﾗﾝﾌﾟ解除&ﾊﾟﾚｯﾄ後退_指令条
LD M9313                 ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANP X1208                ; Lo#1_上段扉開端
ANB X1209                ; Lo#1_上段扉閉端
AND M9302                ; HiST1ﾊﾟﾚｯﾄ後退_OK
AND M9330                ; HiST1ｸﾗﾝﾌﾟ解除&ﾊﾟﾚｯﾄ後退_指令条
OR M9331                 ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
ANB M9336                ; HiST1ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ開動作完了
ANB B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
ANB B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
AND M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT M9331                ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
LD M9331                 ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
AND M9302                ; HiST1ﾊﾟﾚｯﾄ後退_OK
OR M9332                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND M9331                ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M9332                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
LD M9332                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OR M9333                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
AND M9331                ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M9333                ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
LD M9333                 ; HiST1ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
AND M9306                ; HiST1ｸﾗﾝﾌﾟ開閉_開_起動OK
OR M9334                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND M9331                ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M9334                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
LD M9334                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND X1443                ; HiST1_部_ﾜｰｸ押_上昇端
OR M9335                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
AND M9331                ; HiST1ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M9335                ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
LD M9335                 ; HiST1ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
OUT M9336                ; HiST1ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ開動作完了
MCR
LDP B1246                ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
ANB M9312                ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
LDPB B1246               ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
AND M9312                ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANB M6312                ; Hi_RBﾁｬｯｸ開可
ANB M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
AND M29                  ; 未使用
OUT M9312                ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
LDP B1247                ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
ANB M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
LDPB B1247               ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
AND M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANB M9312                ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANB M6312                ; Hi_RBﾁｬｯｸ開可
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
AND M29                  ; 未使用
OUT M9313                ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
;<h1/>□_出力回路
;ソレノイドバルブ出力
;ｽﾃｰｼﾞ(ﾊﾟﾚｯﾄ前後)
LD M9301                 ; HiST1ﾊﾟﾚｯﾄ前進指令
AND M9300                ; HiST1ﾊﾟﾚｯﾄ前進_OK
LDB M9014                ; U15-1_S14_ﾊﾟﾚｯﾄ前進指令
INV
ORL
LD Y2500                 ; HiST1ｽﾃｰｼﾞ前後前進
ANB Y2501                ; HiST1ｽﾃｰｼﾞ前後後退
ORL
AND M50                  ; 運転準備
OUT Y2500                ; HiST1ｽﾃｰｼﾞ前後前進
LD M9303                 ; HiST1ﾊﾟﾚｯﾄ後退指令
AND M9302                ; HiST1ﾊﾟﾚｯﾄ後退_OK
LDB M9253                ; OriStep3_ｽﾃｰｼﾞ戻り指令
ANB M9020                ; U15-1_S20_ｽﾃｰｼﾞ後退命令
INV
ORL
LD Y2501                 ; HiST1ｽﾃｰｼﾞ前後後退
ANB Y2500                ; HiST1ｽﾃｰｼﾞ前後前進
ORL
AND M50                  ; 運転準備
OUT Y2501                ; HiST1ｽﾃｰｼﾞ前後後退
;ｸﾗﾝﾌﾟ/ｱﾝｸﾗﾝﾌﾟ
LD M9305                 ; HiST1ｸﾗﾝﾌﾟ開閉_閉_指令
AND M9304                ; HiST1ｸﾗﾝﾌﾟ開閉_閉_起動OK
OR M8454                 ; HiST1 ｸﾗﾝﾌﾟ指令(Hi給材)
OR M9004                 ; U15-1_S04_ｸﾗﾝﾌﾟ指令
LD Y2502                 ; HiST1ﾜｰｸ押さえ下降
ANB Y2503                ; HiST1ﾜｰｸ押さえ上昇
ORL
AND M50                  ; 運転準備
OUT Y2502                ; HiST1ﾜｰｸ押さえ下降
LD M9307                 ; HiST1ｸﾗﾝﾌﾟ開閉_開__指令
AND M9306                ; HiST1ｸﾗﾝﾌﾟ開閉_開_起動OK
OR M8464                 ; HiST1_ｱﾝｸﾗﾝﾌﾟ指令(Hi除材)
OR M9256                 ; OriStep6_ｸﾗﾝﾌﾟ上昇指令
LD Y2503                 ; HiST1ﾜｰｸ押さえ上昇
ANB Y2502                ; HiST1ﾜｰｸ押さえ下降
ORL
AND M50                  ; 運転準備
OUT Y2503                ; HiST1ﾜｰｸ押さえ上昇
;■その他制御出力
LD M9161                 ; U15-3_S1Hi検査ｽﾀｰﾄ要求
OUT W0105.8              ; 電気特性検査開始要求#1
LD L1500                 ; HiST1_有効状態
OUT W0105.12             ; 電気特性検査ｽﾃｰｼﾞ有効/無効#1
LD M9404                 ; HiST1検査_有効確認要求
OUT W0107.3              ; Hi_ST1ﾁｪｯｶ-確認要求
LD L1500                 ; HiST1_有効状態
ANB M9409                ; HiST1_無効予約(自動運転)
LD M9409                 ; HiST1_無効予約(自動運転)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y2480                ; HiST1__有効/無効表示
OUT B174A                ; HiST1__有効/無効表示
;□□　　パネル表示　　□□
LD Y2500                 ; HiST1ｽﾃｰｼﾞ前後前進
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1440                 ; HiST1_部_ﾊﾟﾚｯﾄ_前進端
OUT B1740                ; UNIT9-2-1 TP-PL
LD Y2501                 ; HiST1ｽﾃｰｼﾞ前後後退
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1441                 ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
OUT B1741                ; UNIT9-2-2 TP-PL
LD Y2502                 ; HiST1ﾜｰｸ押さえ下降
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1442                 ; HiST1_部_ﾜｰｸ押_下降端
OUT B1742                ; UNIT9-2-3 TP-PL
LD Y2503                 ; HiST1ﾜｰｸ押さえ上昇
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1443                 ; HiST1_部_ﾜｰｸ押_上昇端
OUT B1743                ; UNIT9-2-4 TP-PL
LD M9312                 ; HiST1SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
OUT B1746                ; SwBoxMode_ﾊﾟﾚｯﾄFlag_ONPL
LD M9313                 ; HiST1SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT B1747                ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞONPL
;<h1/>□_異常検出
;[][]    Error Detect    [][]
;B1E0(K481)--B1EF(K496)  B1E0(K497)--B1EF(K512)
LD Y2500                 ; HiST1ｽﾃｰｼﾞ前後前進
ANB X1440                ; HiST1_部_ﾊﾟﾚｯﾄ_前進端
LD Y2501                 ; HiST1ｽﾃｰｼﾞ前後後退
ANB X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
ORL
LD X1440                 ; HiST1_部_ﾊﾟﾚｯﾄ_前進端
AND X1441                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
ORL
LD CR2003                ; 常時OFF
ANB B06E0                ; B1E0_480)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
ORL
MPS
AND T1590                ; CLSﾀｲﾑｱｳﾄ
OUT B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
MPP
TMR K1590 K100           ; CLSﾀｲﾑｱｳﾄ
LDP B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K481 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E0                 ; B1E0_480)Bz停止
AND B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
OUT B06E0                ; B1E0_480)Bz停止
;　　
LD Y2502                 ; HiST1ﾜｰｸ押さえ下降
ANB X1442                ; HiST1_部_ﾜｰｸ押_下降端
LD Y2503                 ; HiST1ﾜｰｸ押さえ上昇
ANB X1443                ; HiST1_部_ﾜｰｸ押_上昇端
ORL
LD X1442                 ; HiST1_部_ﾜｰｸ押_下降端
AND X1443                ; HiST1_部_ﾜｰｸ押_上昇端
ORL
LD CR2003                ; 常時OFF
ANB B06E1                ; B1E1_481)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
ORL
MPS
AND T1591                ; CLSﾀｲﾑｱｳﾄ
OUT B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
MPP
TMR K1591 K100           ; CLSﾀｲﾑｱｳﾄ
LDP B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K482 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E1                 ; B1E1_481)Bz停止
AND B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
OUT B06E1                ; B1E1_481)Bz停止
;
LD T1506                 ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
AND L1500                ; HiST1_有効状態
AND<> D8100 D8106        ; HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID  HiST1_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
LD CR2003                ; 常時OFF
ANB B06E2                ; B1E2_482)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
ORL
OUT B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
LDP B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K483 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E2                 ; B1E2_482)Bz停止
AND B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
OUT B06E2                ; B1E2_482)Bz停止
;
LD T1507                 ; HiST1_ﾊﾟﾚｯﾄID_更新_DLY
AND L1500                ; HiST1_有効状態
AND<> D8101 D8107        ; HiST1_ﾊﾟﾚｯﾄID  HiST1_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
ANB Y2500                ; HiST1ｽﾃｰｼﾞ前後前進
AND T1504                ; HiST1_部_ﾊﾟﾚｯﾄ_後退端
LD CR2003                ; 常時OFF
ANB B06E3                ; B1E3_483)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
ORL
OUT B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
LDP B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K484 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E3                 ; B1E3_483)Bz停止
AND B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
OUT B06E3                ; B1E3_483)Bz停止
;
LD M9060                 ; U15-1_S60 HiST1_ﾜｰｸ受取異常
LD CR2003                ; 常時OFF
ANB B06E4                ; B1E4_484)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
ORL
OUT B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
LDP B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K485 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E4                 ; B1E4_484)Bz停止
AND B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
OUT B06E4                ; B1E4_484)Bz停止
;
LD M9430                 ; HiST1_ﾜｰｸ受渡し異常
LD CR2003                ; 常時OFF
ANB B06E5                ; B1E5_485)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
ORL
OUT B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
LDP B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K486 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E5                 ; B1E5_485)Bz停止
AND B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
OUT B06E5                ; B1E5_485)Bz停止
;
LD D8201.0
ANB X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
LDB D8201.0
AND X1444                ; HiST1_部_ﾜｰｸ確認(ON=有)
ORL
ANB M8401                ; 【搬送】搬送→Hi_ST1搬送開始
ANB M8405                ; 【搬送】搬送→Hi_ST1搬送完了
ANB M8411                ; 【搬送】HiST1除材開始
ANB M8415                ; 【搬送】HiST1除材完了
ANB M9053                ; U15-1_S53
ANB M9054                ; U15-1_S54
AND M71                  ; 自動運転開始BZ
LD CR2003                ; 常時OFF
ANB B06E6                ; B1E6_486)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
ORL
OUT B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
LDP B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV K487 D0              ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06E6                 ; B1E6_486)Bz停止
AND B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
OUT B06E6                ; B1E6_486)Bz停止
;　　
LD M9258                 ; OriStep8_ORGﾜｰｸ除去要求
LD CR2003                ; 常時OFF
ANB B06FE                ; B1FE_510)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
ORL
OUT B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06FE                 ; B1FE_510)Bz停止
AND B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
OUT B06FE                ; B1FE_510)Bz停止
;電特検査中
LD W05.8                 ; 電気特性検査ｽﾃｰｼﾞ状態#1
AND M31                  ; 手動ﾓｰﾄﾞ
AND M9251                ; OriStep1
LD CR2003                ; 常時OFF
ANB B06FF                ; B1FF_511)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
ORL
OUT B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06FF                 ; B1FF_511)Bz停止
AND B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
OUT B06FF                ; B1FF_511)Bz停止
;異常ﾌﾞｻﾞｰまとめ
LDB B01E0                ; B1E0_480)HiST1ｽﾃｰｼﾞ前後CL異常
OR B06E0                 ; B1E0_480)Bz停止
LDB B01E1                ; B1E1_481)HiST1ｸﾗﾝﾌﾟ開閉CLS異常
OR B06E1                 ; B1E1_481)Bz停止
ANL
LDB B01E2                ; B1E2_482)HiST1_ﾃｽﾄﾍｯﾄﾞID_不一致
OR B06E2                 ; B1E2_482)Bz停止
ANL
LDB B01E3                ; B1E3_483)HiST1_ﾊﾟﾚｯﾄID_不一致
OR B06E3                 ; B1E3_483)Bz停止
ANL
LDB B01E4                ; B1E4_484)HiST1_ﾜｰｸ受取異常
OR B06E4                 ; B1E4_484)Bz停止
ANL
LDB B01E5                ; B1E5_485)HiST1_ﾜｰｸ受渡し異常
OR B06E5                 ; B1E5_485)Bz停止
ANL
LDB B01E6                ; B1E6_486)HiST1 ﾜｰｸ情報不一致
OR B06E6                 ; B1E6_486)Bz停止
ANL
LDB B01E7                ; B1E7_487)
OR B06E7                 ; B1E7_487)Bz停止
ANL
LDB B01E8                ; B1E8_488)
OR B06E8                 ; B1E8_488)Bz停止
ANL
LDB B01E9                ; B1E9_489)
OR B06E9                 ; B1E9_489)Bz停止
ANL
LDB B01EA                ; B1EA_490)
OR B06EA                 ; B1EA_490)Bz停止
ANL
LDB B01EB                ; B1EB_491)
OR B06EB                 ; B1EB_491)Bz停止
ANL
LDB B01EC                ; B1EC_492)
OR B06EC                 ; B1EC_492)Bz停止
ANL
LDB B01ED                ; B1ED_493)
OR B06ED                 ; B1ED_493)Bz停止
ANL
LDB B01EE                ; B1EE_494)
OR B06EE                 ; B1EE_494)Bz停止
ANL
LDB B01EF                ; B1EF_495)
OR B06EF                 ; B1EF_495)Bz停止
ANL
LDB B01F0                ; B1F0_496)
OR B06F0                 ; B1F0_496)Bz停止
ANL
LDB B01F1                ; B1F1_497)
OR B06F1                 ; B1F1_497)Bz停止
ANL
LDB B01F2                ; B1F2_498)
OR B06F2                 ; B1F2_498)Bz停止
ANL
LDB B01F3                ; B1F3_499)
OR B06F3                 ; B1F3_499)Bz停止
ANL
LDB B01F4                ; B1F4_500)
OR B06F4                 ; B1F4_500)Bz停止
ANL
LDB B01F5                ; B1F5_501)
OR B06F5                 ; B1F5_501)Bz停止
ANL
LDB B01F6                ; B1F6_502)
OR B06F6                 ; B1F6_502)Bz停止
ANL
LDB B01F7                ; B1F7_503)
OR B06F7                 ; B1F7_503)Bz停止
ANL
LDB B01F8                ; B1F8_504)
OR B06F8                 ; B1F8_504)Bz停止
ANL
LDB B01F9                ; B1F9_505)
OR B06F9                 ; B1F9_505)Bz停止
ANL
LDB B01FA                ; B1FA_506)
OR B06FA                 ; B1FA_506)Bz停止
ANL
LDB B01FB                ; B1FB_507)
OR B06FB                 ; B1FB_507)Bz停止
ANL
LDB B01FC                ; B1FC_508)
OR B06FC                 ; B1FC_508)Bz停止
ANL
LDB B01FD                ; B1FD_509)
OR B06FD                 ; B1FD_509)Bz停止
ANL
LDB B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
OR B06FE                 ; B1FE_510)Bz停止
ANL
LDB B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
OR B06FF                 ; B1FF_511)Bz停止
ANL
LDB B01FE                ; B1FE_510)HiST1ﾜｰｸ除去要求
OR B06FE                 ; B1FE_510)Bz停止
ANL
LDB B01FF                ; B1FF_511)HiST1ﾜｰｸ検査中
OR B06FF                 ; B1FF_511)Bz停止
ANL
INV
OUT M9489                ; ﾕﾆｯﾄ15異常BZ補助
END
ENDH
