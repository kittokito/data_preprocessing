DEVICE:53
;MODULE:UT01_Case_Supply
;MODULE_TYPE:0
;[][]    Received input    [][]
LD X1021                 ; ﾜｰｸ投入側ﾜｰｸ確認
TMR K110 K3              ; 投入部ﾜｰｸ確認
LD X1022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
TMR K111 K3              ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
LD X1023                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ浮き確認1
TMH K112 K10             ; 給材ﾜｰｸ浮き確認1
LD X1024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ浮き確認2
TMH K113 K10             ; 給材ﾜｰｸ浮き確認2
;[] ｾｯﾄ治具ID
;Set JIG ID
LD M1001                 ; ﾌﾚｰﾑ投入要求､確認
MPS
AND X1025                ; 給材ﾃｰﾌﾞﾙ治具ID-1
SET D1050.0              ; 投入ﾊﾟﾚｯﾄ_ID_0
MRD
ANB X1025                ; 給材ﾃｰﾌﾞﾙ治具ID-1
RES D1050.0              ; 投入ﾊﾟﾚｯﾄ_ID_0
MRD
AND X1026                ; 給材ﾃｰﾌﾞﾙ治具ID-2
SET D1050.1              ; 投入ﾊﾟﾚｯﾄ_ID_1
MRD
ANB X1026                ; 給材ﾃｰﾌﾞﾙ治具ID-2
RES D1050.1              ; 投入ﾊﾟﾚｯﾄ_ID_1
MRD
AND X1027                ; 給材ﾃｰﾌﾞﾙ治具ID-3
SET D1050.2              ; 投入ﾊﾟﾚｯﾄ_ID_2
MPP
ANB X1027                ; 給材ﾃｰﾌﾞﾙ治具ID-3
RES D1050.2              ; 投入ﾊﾟﾚｯﾄ_ID_2
;[]
;[][] Device status and operation conditions [][]
;[]
;□ ﾕﾆｯﾄ原点
LDB X1022                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
OR M81                   ; 原点復帰動作中
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD X1029                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ORL
ANL
OUT M1480                ; ﾕﾆｯﾄ1原点
;[] 非常停止
;Emergency stop
LDB B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
INV
OUT M1481                ; ﾕﾆｯﾄ1全異常
;[] 起動条件
;Start-up conditions
LD M1480                 ; ﾕﾆｯﾄ1原点
ANB M36                  ; 払出ﾓｰﾄﾞ
OUT M1482                ; ﾕﾆｯﾄ1起動条件
;[] 起動OFF
;Start OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1484                ; ﾕﾆｯﾄ1原点復帰中
ORL
OR M1486                 ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M1483                ; ﾕﾆｯﾄ1起動OFF
;[] 原点復帰
;Origin
LD M82                   ; 原点復帰開始
LDB M82                  ; 原点復帰開始
ANP B1099                ; UNIT1-2-10 TP-PB
ORL
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OR M1484                 ; ﾕﾆｯﾄ1原点復帰中
ANB M1483                ; ﾕﾆｯﾄ1起動OFF
ANB M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M1299                ; ﾕﾆｯﾄ1原点復帰完了確認
MPS
OUT M1484                ; ﾕﾆｯﾄ1原点復帰中
MPP
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1599                ; UNIT1-2-10 TP-PL
;[] 起動中
;Running
LD M73                   ; 各工程起動指令
AND M1482                ; ﾕﾆｯﾄ1起動条件
OR M1485                 ; ﾕﾆｯﾄ1起動中
ANB M1483                ; ﾕﾆｯﾄ1起動OFF
LDB M1049                ; ﾕﾆｯﾄ1-1ｻｲｸﾙ終了
ORB M1099                ; ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ終了
ANL
OUT M1485                ; ﾕﾆｯﾄ1起動中
;[] ｻｲｸﾙ停止異常
;Cycle stop error
LDB CR2003               ; 常時OFF
INV
OUT M1486                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
;[] 払出停止異常
;Payout stop error
LDB M1018                ; ID_NG払出ｻｲｸﾙ停止
ANB CR2003               ; 常時OFF
INV
OUT M1487                ; ﾕﾆｯﾄ1払出停止異常
;[] 警報補助
;Auxiliary alarm
LDB B021                 ; ﾌﾚｰﾑ投入不可異常
OR B0521
LDB B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
OR B0525
ANL
ANB M1003                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M1016                ; ｻｲｸﾙ停止ﾌﾚｰﾑ取出要求ON(BZ､PL)
INV
OUT M1488                ; ﾕﾆｯﾄ1警報補助
;[] 機器準備確認
;Equipment ready
LD CR2002                ; 常時ON
OUT M1490                ; ﾕﾆｯﾄ1機器準備完了
;[] ｴﾗｰ発生　一時停止
;Error, pause
LDB B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
ANB B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
INV
OUT M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
;[] 動作中ﾕﾆｯﾄ無し
;No action unit
LD CR2002                ; 常時ON
ORB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
TMR K101 K20             ; ﾕﾆｯﾄ1-1動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D1000 D1005          ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD T101                  ; ﾕﾆｯﾄ1-1動作無し判定ﾀｲﾏ
OUT M1492                ; ﾕﾆｯﾄ1動作無し確認
;[] 払出しﾜｰｸ無し
;Without paying out work
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD X1029                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ORL
ANB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
ANB X1022                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
TMR K105 K10             ; UNIT1払出処理ﾜｰｸ無
LD T105                  ; UNIT1払出処理ﾜｰｸ無
OUT M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
;[] ｽﾃｯﾌﾟ起動
;Step start-up
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
AND M123                 ; 安全扉正常
ANB M60                  ; ｴﾗｰ発生
ANB M130                 ; 一時停止
OUT M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
;<h1/>■自動回路
;[][][][][][][][][][][][][][][]
;[]　　 　UNIT1自動回路  　　[]
;[][][][][][][][][][][][][][][]
;/ ﾌﾚｰﾑ供給
LD CR2002                ; 常時ON
ZRES M1000 M1049         ; ﾌﾚｰﾑ投入部ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ1-1ｻｲｸﾙ終了
LDB M1485                ; ﾕﾆｯﾄ1起動中
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1485                 ; ﾕﾆｯﾄ1起動中
MOV D1000 Z1             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
OUT M1000:Z1             ; ﾌﾚｰﾑ投入部ｽﾃｯﾌﾟ開始
LD M1000                 ; ﾌﾚｰﾑ投入部ｽﾃｯﾌﾟ開始
AND M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
ANB T110                 ; 投入部ﾜｰｸ確認
ANB M36                  ; 払出ﾓｰﾄﾞ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾌﾚｰﾑ供給確認
;Frame supply check
LD M1001                 ; ﾌﾚｰﾑ投入要求､確認
AND M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
MPS
LD T110                  ; 投入部ﾜｰｸ確認
AND X102E                ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
ANL
TMR K115 K10             ; ﾌﾚｰﾑ投入確認ﾃﾞｨﾚｲ
MRD
AND T115                 ; ﾌﾚｰﾑ投入確認ﾃﾞｨﾚｲ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB T110                 ; 投入部ﾜｰｸ確認
AND M36                  ; 払出ﾓｰﾄﾞ
ANL
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>/ﾌﾚｰﾑｺｰﾄﾞ読取
;Frame code Read
LD M1002                 ; ﾌﾚｰﾑ投入確認､ｺｰﾄﾞ照合要求
OUT W0101.0              ; 投入部読込要求
MPS
LD W01.0                 ; 投入部読込完了
OR M3                    ; OFFLINE_MODE
ANL
MPS
LDB W01.1                ; 投入部投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
ANL
BMOV W020 D1100 K2       ; 投入ﾊﾟﾚｯﾄID  投入ﾊﾟﾚｯﾄID(投入)
MPS
AND M3                   ; OFFLINE_MODE
DW K7 D1100              ; 投入ﾊﾟﾚｯﾄID(投入)
CON
DW K7 D1101              ; 投入部ﾛﾎﾞｯﾄNo.(投入)
MPP
MOV K20 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND W01.1                ; 投入部投入不可
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD W01.2                 ; 製品IDｴﾗｰ､段取
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K10 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;投入不可 (確認PB B1025)
LD M1003                 ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
AND X102E                ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ処理選択(ﾀﾞｲｱﾛｸﾞ表示)
LD M1010                 ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
LD B1020                 ; ﾀﾞｲｱﾛｸﾞ選択PB1
ANB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MRD
AND B1021                ; ﾀﾞｲｱﾛｸﾞ選択PB2
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K13 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND B1022                ; ﾀﾞｲｱﾛｸﾞ選択PB3
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K16 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･ﾜｰｸ取出処理(B1020)
LD M1011                 ; ﾌﾚｰﾑ取出選択(BZ､PL)
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1012                 ; ﾌﾚｰﾑ取出確認
AND W0101.2              ; ID異常時取出選択
ANB W01.2                ; 製品IDｴﾗｰ､段取
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･ﾜｰｸ治具交換選択(B1021)
LD M1013                 ; 治具交換選択
AND W0101.3              ; ID異常時段取選択
ANB W01.2                ; 製品IDｴﾗｰ､段取
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･ﾌﾚｰﾑ取出後ｻｲｸﾙ停止(B1022)
LD M1016                 ; ｻｲｸﾙ停止ﾌﾚｰﾑ取出要求ON(BZ､PL)
ANB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
AND X102E                ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1017                 ; ﾌﾚｰﾑ取出確認
AND W0101.1              ; ID異常時ｻｲｸﾙ停止選択
ANB W01.2                ; 製品IDｴﾗｰ､段取
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1018                 ; ID_NG払出ｻｲｸﾙ停止
AND M36                  ; 払出ﾓｰﾄﾞ
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾃｰﾌﾞﾙ回転
;Table rotation
LD M1020                 ; 取出部ﾌﾚｰﾑ無､要求確認
AND M1051                ; ﾌﾚｰﾑ供給要求
AND M1725                ; Lo_Sup_RB_初期起動完了ﾌﾗｸﾞ
AND M6225                ; Hi_Sup_RB_初期起動完了ﾌﾗｸﾞ
AND M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
MPS
LDB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K22 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1021                 ; ﾃｰﾌﾞﾙ1供給回転
AND X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
MOV K23 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1022                 ; ﾃｰﾌﾞﾙ2供給回転
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮き確認
;Work float check
LD M1023                 ; 給材ﾃｰﾌﾞﾙ旋回確認
AND M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
MPS
AND X1022                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
MPS
TMR K120 K5              ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
MPP
AND T120                 ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
MPS
LD T112                  ; 給材ﾜｰｸ浮き確認1
AND T113                 ; 給材ﾜｰｸ浮き確認2
ANL
MOV K30 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB T112                 ; 給材ﾜｰｸ浮き確認1
ORB T113                 ; 給材ﾜｰｸ浮き確認2
ANL
MPS
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K25 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB X1022                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
MPS
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K35 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K36 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ無NG
;Work Detect NG
LD M1035                 ; ﾃｰﾌﾞﾙ1ﾜｰｸ無しNG回転戻
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K37 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1036                 ; ﾃｰﾌﾞﾙ2ﾜｰｸ無しNG回転戻
AND X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1037                 ; ﾌﾚｰﾑ無しNG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
ANP B1025                ; 確認TP_PB
ANB T110                 ; 投入部ﾜｰｸ確認
AND X102E                ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1038                 ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去
AND W0101.4              ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去要求
AND W01.4                ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去確認
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1039                 ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去確認
ANB W0101.4              ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去要求
ANB W01.4                ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去確認
MOV K1 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮きNG
;Work float NG
LD M1024                 ; ﾃｰﾌﾞﾙ1浮きNG回転戻
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
MOV K26 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1025                 ; ﾃｰﾌﾞﾙ2浮きNG回転戻
AND X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾌﾚｰﾑ浮きNG再ｾｯﾄ要求
;Frame float NG re-set request
LD M1026                 ; ﾌﾚｰﾑ浮きNG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1027                 ; ﾌﾚｰﾑ浮きNG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
ANP B1025                ; 確認TP_PB
ANB T110                 ; 投入部ﾜｰｸ確認
AND X102E                ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1028                 ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
AND W0101.4              ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去要求
AND W01.4                ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去確認
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1029                 ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去確認
ANB W0101.4              ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去要求
ANB W01.4                ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去確認
MOV K1 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾃﾞｰﾀｼﾌﾄ要求(PLC→PC)
;Shift data request
LD M1030                 ; 浮きOKﾃﾞｰﾀｼﾌﾄ要求
AND W0101.8              ; 投入部反転要求
LD W01.8                 ; 反転部反転完了
OR M3                    ; OFFLINE_MODE
ANL
@BMOV D1100 D1110 K10    ; 投入ﾊﾟﾚｯﾄID(投入)  投入ﾊﾟﾚｯﾄID(反転)
@FMOV K0 D1100 K10       ; 投入ﾊﾟﾚｯﾄID(投入)
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1031                 ; ﾃｰﾌﾞﾙ反転､ﾌﾚｰﾑ供給完了
ANB W01.8                ; 反転部反転完了
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1032                 ; ﾌﾚｰﾑ供給要求OFF確認
ANB M1051                ; ﾌﾚｰﾑ供給要求
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1000                ; ﾌﾚｰﾑ投入部ｽﾃｯﾌﾟ開始
MOV K48 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
;Cycle end
LD M1048                 ; ﾕﾆｯﾄ1-1ｻｲｸﾙ停止処理
@MOV K49 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>/ ﾌﾚｰﾑ取出
;[] UNIT1-2 ﾌﾚｰﾑ取出自動
;UNIT1-2 Frame extraction, automatic
LD CR2002                ; 常時ON
ZRES M1050 M1099         ; ﾌﾚｰﾑ取出部ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ終了
LDB M1485                ; ﾕﾆｯﾄ1起動中
MOV K0 D1001
LD M1485                 ; ﾕﾆｯﾄ1起動中
MOV D1001 Z1
OUT M1050:Z1             ; ﾌﾚｰﾑ取出部ｽﾃｯﾌﾟ開始
;ﾌﾚｰﾑ有無判定
;Frame with or without check
LD M1050                 ; ﾌﾚｰﾑ取出部ｽﾃｯﾌﾟ開始
ANB T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
@INC D1001
;ﾌﾚｰﾑ供給要求､完了確認
;Frame supply demand, supply check
LD M1051                 ; ﾌﾚｰﾑ供給要求
AND M1031                ; ﾃｰﾌﾞﾙ反転､ﾌﾚｰﾑ供給完了
@INC D1001
LD M1052                 ; ﾌﾚｰﾑ供給完了OFF確認
ANB M1031                ; ﾃｰﾌﾞﾙ反転､ﾌﾚｰﾑ供給完了
@INC D1001
;ﾌﾚｰﾑ取出可､完了確認
;Frame extraction permit, take-out completion check
LD M1053                 ; 給材部ﾌﾚｰﾑ取出可ON(RBへ)
MPS
LD M1580                 ; 取出完了確認
ANB X3220                ; Low_RB_給材取出侵入中(800)
ANL
@INC D1001
MPP
LDB M1985                ; ﾕﾆｯﾄ2起動中
ANB X3201                ; Low_Sup_RB_RUN
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANL
@MOV K49 D1001
LD M1054                 ; ﾌﾚｰﾑ取出完了確認
ANB M1580                ; 取出完了確認
MPS
AND T116                 ; ﾌﾚｰﾑ取出完了ﾜｰｸﾁｪｯｸﾃﾞｨﾚｲ
@INC D1001
MPP
TMR K116 K8              ; ﾌﾚｰﾑ取出完了ﾜｰｸﾁｪｯｸﾃﾞｨﾚｲ
LD M1055                 ; ﾌﾚｰﾑ無確認
ANB T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
MOV K0 D1001
;ｻｲｸﾙ停止処理
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1051                ; ﾌﾚｰﾑ供給要求
MOV K48 D1001
;ｻｲｸﾙ終了
;Cycle end
LD M1098                 ; ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ停止処理
@MOV K49 D1001
;<h1/>■工程処理 PLC→PC I/F
;各種要求･選択
LD M1017                 ; ﾌﾚｰﾑ取出確認
OUT W0101.1              ; ID異常時ｻｲｸﾙ停止選択
LD M1012                 ; ﾌﾚｰﾑ取出確認
OUT W0101.2              ; ID異常時取出選択
LD M1013                 ; 治具交換選択
OUT W0101.3              ; ID異常時段取選択
LDB M1028                ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去
ANB M1038                ; ﾌﾚｰﾑ取出確認ﾃﾞｰﾀ消去
INV
OUT W0101.4              ; 投入部投入ﾜｰｸﾃﾞｰﾀ消去要求
LD M1030                 ; 浮きOKﾃﾞｰﾀｼﾌﾄ要求
OUT W0101.8              ; 投入部反転要求
LD CR2002                ; 常時ON
MOV D1050 W0120          ; 投入ﾊﾟﾚｯﾄID  投入ﾊﾟﾚｯﾄID
;<h1/>■原点復帰回路
;[][][][][][][][][][][][][][][]
;[][]    UNIT1 原点復帰    [][]
;[][][][][][][][][][][][][][][]
LD CR2002                ; 常時ON
ZRES M1250 M1299         ; ﾕﾆｯﾄ1原点復帰開始  ﾕﾆｯﾄ1原点復帰完了確認
LDB M1484                ; ﾕﾆｯﾄ1原点復帰中
MOV K0 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1484                 ; ﾕﾆｯﾄ1原点復帰中
MOV D1010 Z1             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
OUT M1250:Z1             ; ﾕﾆｯﾄ1原点復帰開始
;原点復帰ｻｲｸﾙ監視
;Origin cycle monitoring
LD M1484                 ; ﾕﾆｯﾄ1原点復帰中
AND M66                  ; Lo_Hi_RB & Chamber原点
TMR K100 K300            ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
;Origin step start
LD M1250                 ; ﾕﾆｯﾄ1原点復帰開始
AND M123                 ; 安全扉正常
AND M1980                ; ﾕﾆｯﾄ2原点
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
@FMOV K0 D1100 K10       ; 投入ﾊﾟﾚｯﾄID(投入)
;旋回ﾃｰﾌﾞﾙ回転端確認
;Turntable rotation end check
LD M1251                 ; 旋回ﾃｰﾌﾞﾙ確認
AND M123                 ; 安全扉正常
MPS
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
OR X1029                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K3 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
LDB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1252                 ; ﾃｰﾌﾞﾙ1側旋回
AND M123                 ; 安全扉正常
AND X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1253                 ; ﾜｰｸ有無確認
AND M123                 ; 安全扉正常
MPS
LD X1021                 ; ﾜｰｸ投入側ﾜｰｸ確認
OR X1022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANL
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
LDB X1021                ; ﾜｰｸ投入側ﾜｰｸ確認
ANB X1022                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANL
MOV K5 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1254                 ; ﾜｰｸ有取出要求
AND M123                 ; 安全扉正常
AND B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
MOV K1 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
;Origin end
LD M1255                 ; ﾜｰｸ無確認
AND M1480                ; ﾕﾆｯﾄ1原点
@MOV K48 D1010           ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1298                 ; ﾕﾆｯﾄ1原点復帰完了
ANB M82                  ; 原点復帰開始
@MOV K49 D1010           ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;[][][][][][][][][][][][][][][]
;[] 　　   UNIT1 手動　      []
;[][][][][][][][][][][][][][][]
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
MC
;旋回ﾃｰﾌﾞﾙ回転
;Turntable rotation
LD T60                   ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB M1841                ; 供給1ｻｲｸﾙ(手動)
ANB M1841                ; 供給1ｻｲｸﾙ(手動)
ANB M1821                ; 取出1ｻｲｸﾙ(手動)
OUT M1300                ; ﾃｰﾌﾞﾙ1供給旋回可
LD B1080                 ; UNIT1-1-1 TP-PB
AND M1300                ; ﾃｰﾌﾞﾙ1供給旋回可
OR M1301                 ; ﾃｰﾌﾞﾙ1供給旋回
ANPB M1303               ; ﾃｰﾌﾞﾙ2供給旋回
OUT M1301                ; ﾃｰﾌﾞﾙ1供給旋回
LD T60                   ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB M1841                ; 供給1ｻｲｸﾙ(手動)
ANB M1841                ; 供給1ｻｲｸﾙ(手動)
ANB M1821                ; 取出1ｻｲｸﾙ(手動)
OUT M1302                ; ﾃｰﾌﾞﾙ2供給旋回可
LD B1081                 ; UNIT1-1-2 TP-PB
AND M1302                ; ﾃｰﾌﾞﾙ2供給旋回可
OR M1303                 ; ﾃｰﾌﾞﾙ2供給旋回
ANPB M1301               ; ﾃｰﾌﾞﾙ1供給旋回
OUT M1303                ; ﾃｰﾌﾞﾙ2供給旋回
MCR
;<h1/>■出力回路
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;旋回ﾃｰﾌﾞﾙ回転
LDB M1021                ; ﾃｰﾌﾞﾙ1供給回転
ANB M1025                ; ﾃｰﾌﾞﾙ2浮きNG回転戻
ANB M1036                ; ﾃｰﾌﾞﾙ2ﾜｰｸ無しNG回転戻
ANB M1252                ; ﾃｰﾌﾞﾙ1側旋回
ANB M1301                ; ﾃｰﾌﾞﾙ1供給旋回
INV
LDP M50                  ; 運転準備
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LDB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
ANL
ORL
ANB X3220                ; Low_RB_給材取出侵入中(800)
OR M1320                 ; 投入ﾃｰﾌﾞﾙ時計周補助
ANPB M1321               ; 投入ﾃｰﾌﾞﾙ反時計周補助
OUT M1320                ; 投入ﾃｰﾌﾞﾙ時計周補助
LD M1320                 ; 投入ﾃｰﾌﾞﾙ時計周補助
LD M123                  ; 安全扉正常
AND T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB X3220                ; Low_RB_給材取出侵入中(800)
OR X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANL
OUT Y2038                ; 投入ﾃｰﾌﾞﾙ1側供給旋回SV
LDB M1022                ; ﾃｰﾌﾞﾙ2供給回転
ANB M1024                ; ﾃｰﾌﾞﾙ1浮きNG回転戻
ANB M1035                ; ﾃｰﾌﾞﾙ1ﾜｰｸ無しNG回転戻
ANB M1303                ; ﾃｰﾌﾞﾙ2供給旋回
INV
LDP M50                  ; 運転準備
AND X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
ANB X3220                ; Low_RB_給材取出侵入中(800)
OR M1321                 ; 投入ﾃｰﾌﾞﾙ反時計周補助
ANPB M1320               ; 投入ﾃｰﾌﾞﾙ時計周補助
OUT M1321                ; 投入ﾃｰﾌﾞﾙ反時計周補助
LD M1321                 ; 投入ﾃｰﾌﾞﾙ反時計周補助
LD M123                  ; 安全扉正常
AND T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB X3220                ; Low_RB_給材取出侵入中(800)
OR X1029                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
OUT Y2039                ; 投入ﾃｰﾌﾞﾙ2側供給旋回SV
LDB M1003                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M1016                ; ｻｲｸﾙ停止ﾌﾚｰﾑ取出要求ON(BZ､PL)
ANB M1027                ; ﾌﾚｰﾑ浮きNG中
ANB M1037                ; ﾌﾚｰﾑ無しNG中
INV
AND T1                   ; 0.3Secｸﾛｯｸ
LD M1485                 ; ﾕﾆｯﾄ1起動中
ANB M1001                ; ﾌﾚｰﾑ投入要求､確認
ANB M1003                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M1016                ; ｻｲｸﾙ停止ﾌﾚｰﾑ取出要求ON(BZ､PL)
ANB M1027                ; ﾌﾚｰﾑ浮きNG中
ANB M1037                ; ﾌﾚｰﾑ無しNG中
ORL
OUT Y2031                ; ﾜｰｸ投入部投入不可PL
LD M1001                 ; ﾌﾚｰﾑ投入要求､確認
OUT Y2032                ; ﾜｰｸ投入部投入可PL
;<h1/>■表示
;□□　　パネル表示　　□□
LD M1010                 ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
OUT M1100                ; Label reissue Window
LD X1028                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1580                ; UNIT1-1-1 TP-PL
LD X1029                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1581                ; UNIT1-1-2 TP-PL
;<h1/>■異常
;□□    異常検出    □□
;B020(K33)--B02F(K48)  B030(K49)--B03F(K64)
LD Y2038                 ; 投入ﾃｰﾌﾞﾙ1側供給旋回SV
ANB X1028                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD Y2039                 ; 投入ﾃｰﾌﾞﾙ2側供給旋回SV
ANB X1029                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
ORL
MPS
TMR K150 K80             ; 給材ﾃｰﾌﾞﾙ旋回確認
MPP
AND T150                 ; 給材ﾃｰﾌﾞﾙ旋回確認
OUT B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
LDP B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW K33 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0520
AND B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
OUT B0520
LD M1485                 ; ﾕﾆｯﾄ1起動中
AND M1000                ; ﾌﾚｰﾑ投入部ｽﾃｯﾌﾟ開始
AND T110                 ; 投入部ﾜｰｸ確認
OUT B021                 ; ﾌﾚｰﾑ投入不可異常
LDP B021                 ; ﾌﾚｰﾑ投入不可異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW K34 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0521
AND B021                 ; ﾌﾚｰﾑ投入不可異常
OUT B0521
LD M1050                 ; ﾌﾚｰﾑ取出部ｽﾃｯﾌﾟ開始
OR M1055                 ; ﾌﾚｰﾑ無確認
AND T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
ORL
OUT B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
LDP B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW K35 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0522
AND B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
OUT B0522
LD M1254                 ; ﾜｰｸ有取出要求
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
ORL
OUT B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
LDP B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
AND< D900 K64            ; FIFO_DATA_VALUE
DW K36 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0523
AND B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
OUT B0523
LD CR2003                ; 常時OFF
OUT B024                 ; 異常5
LDP B024                 ; 異常5
AND< D900 K64            ; FIFO_DATA_VALUE
DW K37 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0524
AND B024                 ; 異常5
OUT B0524
LDB T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
MPS
TMR K151 K80             ; 投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
MPP
AND T151                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
OUT B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
LDP B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
AND< D900 K64            ; FIFO_DATA_VALUE
DW K38 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0525
AND B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
OUT B0525
LD CR2003                ; 常時OFF
OUT B026                 ; UNIT1異常7
LDP B026                 ; UNIT1異常7
AND< D900 K64            ; FIFO_DATA_VALUE
DW K39 D0                ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0526
AND B026                 ; UNIT1異常7
OUT B0526
;異常ﾌﾞｻﾞｰまとめ
LDB B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
OR B0520
LDB B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
OR B0522
ANL
LDB B023                 ; 原点復帰給材部ﾜｰｸ有除去要求
OR B0523
ANL
LDB B024                 ; 異常5
OR B0524
ANL
INV
OUT M1489                ; ﾕﾆｯﾄ1異常BZ補助
END
ENDH
