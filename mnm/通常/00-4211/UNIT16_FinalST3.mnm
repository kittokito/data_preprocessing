DEVICE:53
;MODULE:UNIT16_FinalST3
;MODULE_TYPE:0
;<h1/>□_入力受け
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X08                   ; ST3-4部ﾗｲﾄｶｰﾃﾝOUT1
AND X09                  ; ST3-4部ﾗｲﾄｶｰﾃﾝOUT2
AND X0C                  ; ST3-4背面ﾗｲﾄｶｰﾃﾝOUT1
AND X0D                  ; ST3-4背面ﾗｲﾄｶｰﾃﾝOUT1
OR M39                   ; ?LO?}?Y?a???nd??O??AT
TMR K1601 K5             ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
LD X1354                 ; ST3_電特部_ﾜｰｸ押_下降端
TMH K1602 K50            ; ST3_電特部_ﾜｰｸ押_下降端
LD CR2003                ; 常時OFF
TMR K1603 K3
LD X1353                 ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
TMR K1604 K5             ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
;ｺﾝﾀｸﾄﾍｯﾄﾞID
LD X1358                 ; ST3_電特部_ｺﾝﾀｸﾄﾍｯﾄﾞID_1
OUT D8606.0
LD X1359                 ; ST3_電特部_ｺﾝﾀｸﾄﾍｯﾄﾞID_2
OUT D8606.1
LD X135A                 ; ST3_電特部_ｺﾝﾀｸﾄﾍｯﾄﾞID_4
OUT D8606.2
;ﾊﾟﾚｯﾄID
LD X135B                 ; ST3_電特部_ﾊﾟﾚｯﾄID_1
OUT D8607.0
LD X135C                 ; ST3_電特部_ﾊﾟﾚｯﾄID_2
OUT D8607.1
LD X135D                 ; ST3_電特部_ﾊﾟﾚｯﾄID_4
OUT D8607.2
;ＩＤ保持
LD M71                   ; 自動運転開始BZ
AND L1600                ; FINALST3_有効状態
LD M8912                 ; FinalST3_ID保持ﾄﾘｶﾞ
ANB M8910                ; FainlST3_無効中
ORL
AND M71                  ; 自動運転開始BZ
OUT M8912                ; FinalST3_ID保持ﾄﾘｶﾞ
;____ｺﾝﾀｸﾄﾍｯﾄﾞＩＤ保持
;※PC送信用は生ﾃﾞｰﾀに変更
LD L1600                 ; FINALST3_有効状態
MPS
TMR K1606 K5             ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
MPP
AND T1606                ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
@MOV D8606 D8600         ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
LD CR2002                ; 常時ON
MOV D8606 W0136          ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  電特ﾃｽﾄﾍｯﾄﾞID#3
LDB L1600                ; FINALST3_有効状態
MOV K0 D8600             ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
;____ﾊﾟﾚｯﾄＩＤ保持
LD L1600                 ; FINALST3_有効状態
LDB Y2444                ; 電特ST3ｽﾃｰｼﾞ前後前進
AND T1604                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OR T1607                 ; FinalST3_ﾊﾟﾚｯﾄID_更新_DLY
ANL
TMR K1607 K5             ; FinalST3_ﾊﾟﾚｯﾄID_更新_DLY
@MOV D8607 D8601         ; FinalST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態  FinalST3_ﾊﾟﾚｯﾄID
LD CR2002                ; 常時ON
MOV D8601 W0132          ; FinalST3_ﾊﾟﾚｯﾄID  電特ﾊﾟﾚｯﾄID#3
LDB L1600                ; FINALST3_有効状態
MOV K0 D8601             ; FinalST3_ﾊﾟﾚｯﾄID
;<h1/>__FINAL_ST_治具有効/無効選択
;段取り状態中ﾌﾗｸﾞの追加
;自動中は段取り状態中に、ST上のﾜｰｸ無、RBﾊﾝﾄﾞﾜｰｸ無し、供給部ﾜｰｸ無しにて無効状態OKとなる
LDB X124F                ; ST3電特検査_有効/無効SW
ANB B126A                ; ST1電特検査_有効/無効_Tpb
INV
OUT M8904                ; ST3電特検査_有効/無効SW
LDB M3102                ; ﾊﾝﾄﾞ1_ﾌｧｲﾅﾙ検査品
ANB M3113                ; ﾊﾝﾄﾞ1_ﾌｧｲﾅﾙST3再検品
ANB M3152                ; ﾊﾝﾄﾞ2_ﾌｧｲﾅﾙ検査品
ANB M3163                ; ﾊﾝﾄﾞ2_ﾌｧｲﾅﾙST3再検品
LD= D6201 K0             ; Leak_ST2_ﾜｰｸ有無記憶
AND= D7201 K0            ; Leak_ST1_ﾜｰｸ有無記憶
AND= D8201 K0            ; Leak_ST3_ﾜｰｸ有無記憶
OR L1200                 ; FINALST2_有効状態
OR L1400                 ; FINALST1_有効状態
ANL
ANB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
AND= D8701 K0            ; Final_ST3_ﾜｰｸ有無記憶
LDB M8574                ; U16-1起動中
LD M8501                 ; U16-1_S1_ﾜｰｸ供給要求
ANB M2972                ; 【搬送】搬送→FINAL_ST3搬送開始
ORL
ANL
ANB M3220                ; ﾊﾝﾄﾞ1_ﾌｧｲﾅﾙST3給材中ﾌﾗｸﾞ
ANB M3221                ; ﾊﾝﾄﾞ2_ﾌｧｲﾅﾙST3給材中ﾌﾗｸﾞ
OUT M8907                ; FainlST3_自動中無効条件OK
LDB M71                  ; 自動運転開始BZ
LD M8909                 ; FainlST3_無効予約(自動運転)
AND M8907                ; FainlST3_自動中無効条件OK
ORL
OUT M8908                ; FainlST3_無効OK条件
LD M8909                 ; FainlST3_無効予約(自動運転)
ANB M8910                ; FainlST3_無効中
ANP M8904                ; ST3電特検査_有効/無効SW
ANB W09.10               ; 電特ステージ#3 無効切替要求
OUT M8911                ; FainlST3_予約ｷｬﾝｾﾙ
LDP M8904                ; ST3電特検査_有効/無効SW
ORP W09.10               ; 電特ステージ#3 無効切替要求
AND L1600                ; FINALST3_有効状態
AND M71                  ; 自動運転開始BZ
ANB M8909                ; FainlST3_無効予約(自動運転)
LD M8909                 ; FainlST3_無効予約(自動運転)
ANB M8910                ; FainlST3_無効中
ANB M8911                ; FainlST3_予約ｷｬﾝｾﾙ
ORL
OUT M8909                ; FainlST3_無効予約(自動運転)
LD M8909                 ; FainlST3_無効予約(自動運転)
AND M8908                ; FainlST3_無効OK条件
LDP M8904                ; ST3電特検査_有効/無効SW
ANB W09.10               ; 電特ステージ#3 無効切替要求
ORP W09.10               ; 電特ステージ#3 無効切替要求
ANB M41                  ; ﾕﾆｯﾄ_起動中
ORL
LDB B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
ANB B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
INV
MEP
ORL
LDB M300                 ; PC通信正常
ANB M3                   ; OFFLINE_MODE
ORL
LD M8910                 ; FainlST3_無効中
ANB L1600                ; FINALST3_有効状態
ORL
OUT M8910                ; FainlST3_無効中
;<h1/>__Final ST_有効処理
LDP M8904                ; ST3電特検査_有効/無効SW
LD<> D8606 K0            ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND<> D8607 K0           ; FinalST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8606 K0            ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND= D8607 K0            ; FinalST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
ORL
ANL
ANB L1600                ; FINALST3_有効状態
LD M8915                 ; FinalST3有効要求
ANB M8917                ; ST有効OK
ANB M8918                ; ST有効化NG
ORL
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
OUT M8915                ; FinalST3有効要求
LD M8915                 ; FinalST3有効要求
ANB M3                   ; OFFLINE_MODE
ANB M9                   ; ﾁｪｯｶｰﾁｪｯｸ有効ﾓｰﾄﾞOFF(step2でOFF)
OUT W0108.2              ; ﾁｪｯｶｰ#3確認要求
LD M8915                 ; FinalST3有効要求
LD W0108.2               ; ﾁｪｯｶｰ#3確認要求
OR M3                    ; OFFLINE_MODE
OR M9                    ; ﾁｪｯｶｰﾁｪｯｸ有効ﾓｰﾄﾞOFF(step2でOFF)
ANL
OR M8916                 ; ST有効確認要求
AND M8915                ; FinalST3有効要求
OUT M8916                ; ST有効確認要求
;有効化ok
LD M8916                 ; ST有効確認要求
LD W0A.2                 ; ﾁｪｯｶｰ#3確認完了
ANB W09.10               ; 電特ステージ#3 無効切替要求
OR M3                    ; OFFLINE_MODE
OR M9                    ; ﾁｪｯｶｰﾁｪｯｸ有効ﾓｰﾄﾞOFF(step2でOFF)
ANL
OR M8917                 ; ST有効OK
AND M8915                ; FinalST3有効要求
OUT M8917                ; ST有効OK
LD M8917                 ; ST有効OK
LD L1600                 ; FINALST3_有効状態
ANB M8910                ; FainlST3_無効中
ANB M8918                ; ST有効化NG
ORL
OUT L1600                ; FINALST3_有効状態
;有効化不可
LD M8916                 ; ST有効確認要求
ANB M3                   ; OFFLINE_MODE
ANB M9                   ; ﾁｪｯｶｰﾁｪｯｸ有効ﾓｰﾄﾞOFF(step2でOFF)
AND W0A.2                ; ﾁｪｯｶｰ#3確認完了
AND W09.10               ; 電特ステージ#3 無効切替要求
OR M8918                 ; ST有効化NG
AND M8915                ; FinalST3有効要求
OUT M8918                ; ST有効化NG
;JIG_ID一致確認
LD L1600                 ; FINALST3_有効状態
MPS
LD= D8600 D1714          ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  搬送ﾊﾝﾄﾞ1_電特ﾃｽﾄﾍｯﾄﾞID
AND= D8601 D1713         ; FinalST3_ﾊﾟﾚｯﾄID  搬送ﾊﾝﾄﾞ1_電特ﾊﾟﾚｯﾄID
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
OUT M8901                ; FinalST3-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
MPP
LD= D8600 D1814          ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  搬送ﾊﾝﾄﾞ2_電特ﾃｽﾄﾍｯﾄﾞID
AND= D8601 D1813         ; FinalST3_ﾊﾟﾚｯﾄID  搬送ﾊﾝﾄﾞ2_電特ﾊﾟﾚｯﾄID
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
OUT M8902                ; FinalST3-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
;<h1/>□_装置状態・動作条件
;ﾕﾆｯﾄまとめ
LD M8570                 ; U16-1原点
AND M8730                ; U16-3原点
OUT M8980                ; FinalST3U16原点
;□ 非常停止
LDB M8571                ; U16-1全異常
ANB M8731                ; U16-3全異常
INV
OUT M8981                ; U16即停止異常
;□ 起動条件
LD CR2003                ; 常時OFF
OUT M8982                ; U16起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M8984                ; U16原点復帰中
ORL
OR T15                   ; 強制停止
OUT M8983                ; U16起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M8984                ; U16原点復帰中
OR M8984                 ; U16原点復帰中
ANB M8983                ; U16起動OFF
ANB M8991                ; U16ｴﾗｰ発生
ANB M8797                ; STEPEND
OUT M8984                ; U16原点復帰中
;□ 起動中
LDB M8574                ; U16-1起動中
ANB M8734                ; U16-3(電特検査SUB)起動中
INV
OUT M8985                ; U16起動中
;□ ｻｲｸﾙ停止異常
LDB M8575                ; U16-1ｻｲｸﾙ停止異常
ANB M8735                ; U16-3ｻｲｸﾙ停止異常
INV
OUT M8986                ; U16ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M8987                ; U16払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M8988                ; U16警報補助
;□ 機器準備確認
LD CR2002                ; 常時ON
OUT M8990                ; U16機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB M8576                ; U16-1ｴﾗｰ発生
ANB M8736                ; U16-3ｴﾗｰ発生
INV
OUT M8991                ; U16ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD M8577                 ; U16-1動作無し
AND M8737                ; U16-3動作無し
OUT M8992                ; U16動作無し確認
;□ 払出しﾜｰｸ無し
LDB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
ANB M8421                ; LeakST3_ﾜｰｸ除材要求
ANB M7421                ; LeakST1_ﾜｰｸ除材要求
ANB M2574                ; ﾕﾆｯﾄ2-1起動中
AND M36                  ; 払出ﾓｰﾄﾞ
TMR K1614 K10            ; U16_ﾜｰｸ無し(払出時)
LD T1614                 ; U16_ﾜｰｸ無し(払出時)
OUT M8993                ; U16ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
ANB M130                 ; 一時停止
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M57                  ; 自動起動停止指令(払出停止)
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
OUT M8979                ; U16ｽﾃｯﾌﾟ動作
;<h1/>□_UNIT_8_自動回路
;<h1/>◇_ﾕﾆｯﾄ8-1状態・動作条件(補助)
LD X1355                 ; ST3_電特部_ﾜｰｸ押_上昇端
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
ANB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
AND= D8701 K0            ; Final_ST3_ﾜｰｸ有無記憶
OUT M8570                ; U16-1原点
LDB B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
ANB B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
ANB B0204                ; 516)FinalST3ﾜｰｸ受取異常
ANB B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
INV
OUT M8571                ; U16-1全異常
LD M8570                 ; U16-1原点
ANB M8574                ; U16-1起動中
AND M8979                ; U16ｽﾃｯﾌﾟ動作
ANB L11                  ; ﾌｧｲﾅﾙ検査有無(0_有/1_無)
ANB M8908                ; FainlST3_無効OK条件
ANB M8993                ; U16ﾜｰｸ無し
OUT M8572                ; U16-1起動条件
LDB M50                  ; 運転準備
ORB L1600                ; FINALST3_有効状態
OR M8986                 ; U16ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M8573                ; U16-1起動OFF
LD M73                   ; 各工程起動指令
AND M8572                ; U16-1起動条件
OR M8574                 ; U16-1起動中
ANB M8573                ; U16-1起動OFF
ANB M8563                ; U16-1ｻｲｸﾙ終了
OUT M8574                ; U16-1起動中
;ﾕﾆｯﾄ1-1 ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M8575                ; U16-1ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-1 ｴﾗｰ発生まとめ
LDB B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
ANB B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
ANB B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
ANB B0332                ; 818)電特ST3検査中
INV
OUT M8576                ; U16-1ｴﾗｰ発生
;ﾕﾆｯﾄ1-1 停止判定
LD= D8500 D8505          ; Unit16-1自動運転ｽﾃｯﾌﾟ  Unit16-1動作中判定
ORB M8574                ; U16-1起動中
TMR K1610 K20            ; U16-1_停止中
LD CR2002                ; 常時ON
MOV D8500 D8505          ; Unit16-1自動運転ｽﾃｯﾌﾟ  Unit16-1動作中判定
LD T1610                 ; U16-1_停止中
OUT M8577                ; U16-1動作無し
;□
;□　ﾕﾆｯﾄ1-1 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M8500 K4         ; U16-1_S0
LDB M8574                ; U16-1起動中
MOV K0 D8500             ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8574                 ; U16-1起動中
MOV D8500 Z1             ; Unit16-1自動運転ｽﾃｯﾌﾟ
SET M8500:Z1             ; U16-1_S0
;　ｶｳﾝﾄﾘｾｯﾄ
LDP M73                  ; 各工程起動指令
ORP L1600                ; FINALST3_有効状態
MOV K0 D8520             ; ﾜｰｸ投入数(ST3)
MOV K0 D8521             ; ﾜｰｸ排出数(ST3)
;　ｻｲｸﾙ開始
LD M8500                 ; U16-1_S0
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
ANB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S01_ﾜｰｸ給材要求
LD M8501                 ; U16-1_S1_ﾜｰｸ供給要求
MPS
LD L1600                 ; FINALST3_有効状態
ANB M8993                ; U16ﾜｰｸ無し
ANL
MPS
AND M2972                ; 【搬送】搬送→FINAL_ST3搬送開始
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
LD M8901                 ; FinalST3-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
OR M8902                 ; FinalST3-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
ANL
OUT M8920                ; FinalST3→搬送ﾜｰｸ供給要求
MPP
LD M8993                 ; U16ﾜｰｸ無し
LD M8910                 ; FainlST3_無効中
ANB L1600                ; FINALST3_有効状態
ORL
ANL
MOV K63 D8500            ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8502                 ; U16-1_S2
LD M2977                 ; 【搬送】搬送→FINAL_ST3搬送完了
OR M8922                 ; FinalST3→搬送ﾜｰｸ受取り完了
ANL
MPS
LDB M2972                ; 【搬送】搬送→FINAL_ST3搬送開始
ANB M2977                ; 【搬送】搬送→FINAL_ST3搬送完了
ANL
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M8922                ; FinalST3→搬送ﾜｰｸ受取り完了
LD M8503                 ; U16-1_S3
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND= D8701 K1            ; Final_ST3_ﾜｰｸ有無記憶
MPS
LD X1356                 ; ST3_電特部_ﾜｰｸ確認(ON=有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
MOV K60 D8500            ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8560                 ; U16-1_S60_FinalST3_ﾜｰｸ受取異常
AND B0204                ; 516)FinalST3ﾜｰｸ受取異常
MOV K63 D8500            ; Unit16-1自動運転ｽﾃｯﾌﾟ
;　　ｸﾗﾝﾌﾟ指令
LD M8504                 ; U16-1_S4_ｸﾗﾝﾌﾟ指令
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1354                ; ST3_電特部_ﾜｰｸ押_下降端
ANB B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;　　Z軸上昇確認
LD M8505                 ; U16-1_S5_電特開始確認待ち
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
LD L309                  ; Z_搬送高さ(P9)
OR L308                  ; Z_搬送高さ(P8)
ANL
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8506                 ; U16-1_S6
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LDP M8506                ; U16-1_S6
AND= D8705 K0            ; Final_ST3_再検査品st
LD= D8203 K2             ; Leak_ST3_Leak検査結果(1=OK_2=NG)
ANB M98                  ; Break品優先排出変更ﾌﾗｸﾞ
AND= D8207 K0            ; Leak_ST3_BreakMode品(ONで)
MEP
ORL
@INC D8520               ; ﾜｰｸ投入数(ST3)
LD M8507                 ; U16-1_S7
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8508                 ; U16-1_S8
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8509                 ; U16-1_S9
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;　　■電特検査前処理
LD M8510                 ; U16-1_S10
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8511                 ; U16-1_S11
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8512                 ; U16-1_S12_
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8513                 ; U16-1_S13
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
MPS
LD= D8703 K1             ; Final_ST3_Leak検査結果(1=OK_2=NG
OR<> D8705 K0            ; Final_ST3_再検査品st
OR L10                   ; ﾘｰｸ検査有無(0_有/1_無)
ANL
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
LD= D8703 K2             ; Final_ST3_Leak検査結果(1=OK_2=NG
AND<> D8705 K0           ; Final_ST3_再検査品st
ANB L10                  ; ﾘｰｸ検査有無(0_有/1_無)
ANL
MOV K20 D8500            ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8514                 ; U16-1_S14_ﾊﾟﾚｯﾄ前進指令
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1352                ; ST3_電特部_ﾊﾟﾚｯﾄ_前進端
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S15_電特検査処理開始
LD M8515                 ; U16-1_S15
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8516                 ; U16-1_S16_電特検査SUB開始ﾌﾗｸﾞ
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND M8734                ; U16-3(電特検査SUB)起動中
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8517                 ; U16-1_S17
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
ANB M8734                ; U16-3(電特検査SUB)起動中
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8518                 ; U16-1_S18
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8519                 ; U16-1_S19
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;　　ｽﾃｰｼﾞ後退
LD M8520                 ; U16-1_S20_ｽﾃｰｼﾞ後退命令
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;　　ｸﾗﾝﾌﾟ解除
LD M8521                 ; U16-1_S21_
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8522                 ; U16-1_S22
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8523                 ; U16-1_S23
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LD M8524                 ; U16-1_S24
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S25_ﾜｰｸ除材要求
LD M8525                 ; U16-1_S25
MPS
AND M2992                ; 【搬送】ﾌｧｲﾅﾙST3除材開始
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
LDB M3101                ; ﾊﾝﾄﾞ1_ﾘｰｸ検査品
ANB M3151                ; ﾊﾝﾄﾞ2_ﾘｰｸ検査品
OR<> D8705 K0            ; Final_ST3_再検査品st
OR L10                   ; ﾘｰｸ検査有無(0_有/1_無)
OR M3228                 ; ﾊﾝﾄﾞ1ﾌｧｲﾅﾙST3除材中ﾌﾗｸﾞ
ANL
OUT M8921                ; FinalST3→搬送1除材要求
LD M8526                 ; U16-1_S26
LD M2997                 ; 【搬送】ﾌｧｲﾅﾙST3除材完了
OR M8923                 ; FinalST3→搬送除材完了受取
ANL
MPS
LDB M2992                ; 【搬送】ﾌｧｲﾅﾙST3除材開始
ANB M2997                ; 【搬送】ﾌｧｲﾅﾙST3除材完了
ANL
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M8923                ; FinalST3→搬送除材完了受取
LD M8527                 ; U16-1_S27
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND= D8701 K0            ; Final_ST3_ﾜｰｸ有無記憶
LD L309                  ; Z_搬送高さ(P9)
OR L308                  ; Z_搬送高さ(P8)
OR L307                  ; Z_ﾜｰｸ入替高さ(搬送禁止)
ANL
MPS
ANB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
OUT M8930                ; Final_ST3_ﾜｰｸ受渡し異常
LD M8528                 ; U16-1_S28
INC D8500                ; Unit16-1自動運転ｽﾃｯﾌﾟ
LDP M8528                ; U16-1_S28
AND= D8705 K0            ; Final_ST3_再検査品st
LD= D8203 K2             ; Leak_ST3_Leak検査結果(1=OK_2=NG)
MEP
ORL
@INC D8521               ; ﾜｰｸ排出数(ST3)
LD M8529                 ; U16-1_S29
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
MOV K63 D8500            ; Unit16-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M8562                 ; U16-1_S62
OR CR2003                ; 常時OFF
@MOV K63 D8500           ; Unit16-1自動運転ｽﾃｯﾌﾟ
;<h1/>◇_ﾕﾆｯﾄ8-3(電特検査SUB)状態・動作条件(補助)
LD CR2002                ; 常時ON
OUT M8730                ; U16-3原点
LDB CR2003               ; 常時OFF
INV
OUT M8731                ; U16-3全異常
LD M8730                 ; U16-3原点
ANB M8734                ; U16-3(電特検査SUB)起動中
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND M300                 ; PC通信正常
OUT M8732                ; U16-3起動条件
LDB M50                  ; 運転準備
OR M8986                 ; U16ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M8733                ; U16-3起動OFF
LD M8516                 ; U16-1_S16_電特検査SUB開始ﾌﾗｸﾞ
AND M8732                ; U16-3起動条件
OR M8734                 ; U16-3(電特検査SUB)起動中
ANB M8733                ; U16-3起動OFF
ANB M8723                ; U16-3ｻｲｸﾙ終了
OUT M8734                ; U16-3(電特検査SUB)起動中
;tp表示
LD CR2002                ; 常時ON
DW K0 D8900              ; Final検査_ｽﾃｰﾀｽ表示
LD M8734                 ; U16-3(電特検査SUB)起動中
DW K1 D8900              ; Final検査_ｽﾃｰﾀｽ表示
LD= D8704 K1             ; Final_ST3_Final検査結果(1=OK_2=N
DW K2 D8900              ; Final検査_ｽﾃｰﾀｽ表示
LD= D8704 K2             ; Final_ST3_Final検査結果(1=OK_2=N
DW K3 D8900              ; Final検査_ｽﾃｰﾀｽ表示
LDB L1600                ; FINALST3_有効状態
DW K4 D8900              ; Final検査_ｽﾃｰﾀｽ表示
;ﾕﾆｯﾄ1-3ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M8735                ; U16-3ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-3ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M8736                ; U16-3ｴﾗｰ発生
;ﾕﾆｯﾄ1-3停止判定
LD= D8502 D8507          ; Unit16-3自動運転ｽﾃｯﾌﾟ  Unit16-3動作中判定
TMR K1612 K20            ; U16-3_停止中
LD CR2002                ; 常時ON
MOV D8502 D8507          ; Unit16-3自動運転ｽﾃｯﾌﾟ  Unit16-3動作中判定
LD T1612                 ; U16-3_停止中
ORB M8734                ; U16-3(電特検査SUB)起動中
OUT M8737                ; U16-3動作無し
;□
;□　ﾕﾆｯﾄ1-3自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M8660 K4         ; U16-3_S0
LDB M8734                ; U16-3(電特検査SUB)起動中
MOV K0 D8502             ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8734                 ; U16-3(電特検査SUB)起動中
MOV D8502 Z1             ; Unit16-3自動運転ｽﾃｯﾌﾟ
SET M8660:Z1             ; U16-3_S0
;<h1/>__S0_ﾜｰｸ有無判定
LD M8660                 ; U16-3_S0
MPS
LD T1601                 ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
INC D8502                ; Unit16-3自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
OR M3                    ; OFFLINE_MODE
ANL
MPS
AND T1630                ; Final検査時間(仮)
INC D8502                ; Unit16-3自動運転ｽﾃｯﾌﾟ
MPP
TMR K1630 K450           ; Final検査時間(仮)
LD M8661                 ; U16-3_S1_Final検査ｽﾀｰﾄ要求
AND W0107.2              ; 電気特性検査開始要求#3
LD W07.2                 ; 電気特性検査ｽﾃｰｼﾞ状態#3
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8662                 ; U16-3_S2
LDB W07.2                ; 電気特性検査ｽﾃｰｼﾞ状態#3
OR M3                    ; OFFLINE_MODE
ANL
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8663                 ; U16-3_S3
MPS
AND<> D8704 K0           ; Final_ST3_Final検査結果(1=OK_2=N
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
MRD
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
MPS
AND W07.10               ; 電気特性検査結果#3
MOV K1 D8704             ; Final_ST3_Final検査結果(1=OK_2=N
MPP
ANB W07.10               ; 電気特性検査結果#3
MOV K2 D8704             ; Final_ST3_Final検査結果(1=OK_2=N
MPP
LD M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MPS
ANB B1038                ; ﾌｧｲﾅﾙST2_強制NG_TEST
MOV K1 D8704             ; Final_ST3_Final検査結果(1=OK_2=N
MPP
AND B1038                ; ﾌｧｲﾅﾙST2_強制NG_TEST
MOV K2 D8704             ; Final_ST3_Final検査結果(1=OK_2=N
LD M8664                 ; U16-3_S4
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8665                 ; U16-3_S5
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8666                 ; U16-3_S6
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8667                 ; U16-3_S7
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8668                 ; U16-3_S8
@INC D8502               ; Unit16-3自動運転ｽﾃｯﾌﾟ
LD M8669                 ; U16-3_S9
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
MOV K63 D8502            ; Unit16-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D8502            ; Unit16-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M8722                 ; U16-3_S62
OR CR2003                ; 常時OFF
@MOV K63 D8502           ; Unit16-3自動運転ｽﾃｯﾌﾟ
;<h1/>□_UNIT_8_原点復帰回路
LD CR2002                ; 常時ON
FMOV K0 M8750 K3         ; STEP0
LDB M8984                ; U16原点復帰中
MOV K0 D8510             ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8984                 ; U16原点復帰中
MOV D8510 Z1             ; Unit16原点復帰ｽﾃｯﾌﾟ
SET M8750:Z1             ; STEP0
;原点復帰ｻｲｸﾙ監視
LD M8984                 ; U16原点復帰中
TMR K1600 K200
;原点復帰ステップ開始
LD M8750                 ; STEP0
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8751                 ; STEP1
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND L309                 ; Z_搬送高さ(P9)
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8752                 ; STEP2_ｽﾃｰｼﾞ戻り指令
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8753                 ; STEP3_ｸﾗﾝﾌﾟ上昇指令
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND X1355                ; ST3_電特部_ﾜｰｸ押_上昇端
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8754                 ; STEP4
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8755                 ; STEP5_
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8756                 ; STEP6_
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ有無ﾁｪｯｸ
LD M8757                 ; STEP7
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
MPS
ANB X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
@MOV K10 D8510           ; Unit16原点復帰ｽﾃｯﾌﾟ
MPP
AND X1356                ; ST3_電特部_ﾜｰｸ確認(ON=有)
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ除去要求
LD M8758                 ; STEP8_ORGﾜｰｸ除去要求
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
AND B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
@MOV K46 D8510           ; Unit16原点復帰ｽﾃｯﾌﾟ
;　　
LD M8759                 ; STEP9
AND T1601                ; ST3-4間ﾗｲﾄｶｰﾃﾝOK
AND M8979                ; U16ｽﾃｯﾌﾟ動作
@INC D8510               ; Unit16原点復帰ｽﾃｯﾌﾟ
LD M8760                 ; STEP10
MPS
AND M8980                ; FinalST3U16原点
@MOV K46 D8510           ; Unit16原点復帰ｽﾃｯﾌﾟ
MRD
FMOV K0 D8700 K100       ; Final_ST3_INDEX NO.
MPP
AND M8                   ; 能力拡張_ﾃｽﾄ
FMOV K0 D7700 K100       ; Final_ST1_INDEX NO.
LD M8796                 ; STEP46
ANB M82                  ; 原点復帰開始
@MOV K47 D8510           ; Unit16原点復帰ｽﾃｯﾌﾟ
;<h1/>□_UNIT_8_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M8985                ; U16起動中
ANB M8984                ; U16原点復帰中
MC
;ｺﾝﾀｸﾄﾍｯﾄﾞ前後
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND M2732                ; ﾕﾆｯﾄ2-3_RCX240起動条件
AND M2801                ; RCX240動作完了
AND L309                 ; Z_搬送高さ(P9)
AND X1354                ; ST3_電特部_ﾜｰｸ押_下降端
OUT M8800                ; ﾊﾟﾚｯﾄ前進_OK
LDP B1260                ; ﾊﾟﾚｯﾄ前進TpSW
LD M8812                 ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
ANP X1249                ; ﾊﾝﾄﾞ2吸着OFF_SW
ANB X1248                ; ﾊﾝﾄﾞ1吸着OFF_SW
ORL
ORP M8824                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND M8800                ; ﾊﾟﾚｯﾄ前進_OK
LD M8801                 ; ﾊﾟﾚｯﾄ前進指令
ANB M8803                ; ﾊﾟﾚｯﾄ後退指令
ORL
OUT M8801                ; ﾊﾟﾚｯﾄ前進指令
LD CR2002                ; 常時ON
ANB W07.2                ; 電気特性検査ｽﾃｰｼﾞ状態#3
AND L309                 ; Z_搬送高さ(P9)
AND X1354                ; ST3_電特部_ﾜｰｸ押_下降端
OUT M8802                ; ﾊﾟﾚｯﾄ後退_OK
LDP B1261                ; ﾊﾟﾚｯﾄ後退TpSW
LD M8812                 ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
ANP X1248                ; ﾊﾝﾄﾞ1吸着OFF_SW
ANB X1249                ; ﾊﾝﾄﾞ2吸着OFF_SW
ORL
ORP M8832                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND M8802                ; ﾊﾟﾚｯﾄ後退_OK
LD M8803                 ; ﾊﾟﾚｯﾄ後退指令
ANB M8801                ; ﾊﾟﾚｯﾄ前進指令
ORL
OUT M8803                ; ﾊﾟﾚｯﾄ後退指令
;　　ｸﾗﾝﾌﾟ上下
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND M2732                ; ﾕﾆｯﾄ2-3_RCX240起動条件
AND M2801                ; RCX240動作完了
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OUT M8804                ; ｸﾗﾝﾌﾟ開閉_閉_起動OK
LDP B1262                ; ｸﾗﾝﾌﾟ下降TpSW
ORP M8822                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND M8804                ; ｸﾗﾝﾌﾟ開閉_閉_起動OK
LD M8805                 ; ｸﾗﾝﾌﾟ開閉_閉_指令
ANB M8807                ; ｸﾗﾝﾌﾟ開閉_開__指令
ORL
OUT M8805                ; ｸﾗﾝﾌﾟ開閉_閉_指令
LD CR2002                ; 常時ON
ANB W07.2                ; 電気特性検査ｽﾃｰｼﾞ状態#3
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OUT M8806                ; ｸﾗﾝﾌﾟ開閉_開_起動OK
LDP B1263                ; ｸﾗﾝﾌﾟ上昇TpSW
ORP M8834                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND M8806                ; ｸﾗﾝﾌﾟ開閉_開_起動OK
LD M8807                 ; ｸﾗﾝﾌﾟ開閉_開__指令
ANB M8805                ; ｸﾗﾝﾌﾟ開閉_閉_指令
ORL
OUT M8807                ; ｸﾗﾝﾌﾟ開閉_開__指令
;ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進
LD M129                  ; 安全入力OK
AND M2732                ; ﾕﾆｯﾄ2-3_RCX240起動条件
AND M2801                ; RCX240動作完了
AND L309                 ; Z_搬送高さ(P9)
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OUT M8820                ; 電特ｸﾗﾝﾌﾟ&前進指令条件
LD M8813                 ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANP X1249                ; ﾊﾝﾄﾞ2吸着OFF_SW
ANB X1248                ; ﾊﾝﾄﾞ1吸着OFF_SW
AND M8804                ; ｸﾗﾝﾌﾟ開閉_閉_起動OK
AND M8820                ; 電特ｸﾗﾝﾌﾟ&前進指令条件
OR M8821                 ; 電特ｸﾗﾝﾌﾟ&前進開始
ANB M8826                ; ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進動作完了
ANB B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
ANB B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
AND M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT M8821                ; 電特ｸﾗﾝﾌﾟ&前進開始
LD M8821                 ; 電特ｸﾗﾝﾌﾟ&前進開始
AND M8804                ; ｸﾗﾝﾌﾟ開閉_閉_起動OK
OR M8822                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND M8821                ; 電特ｸﾗﾝﾌﾟ&前進開始
OUT M8822                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
LD M8822                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ閉指令
AND T1602                ; ST3_電特部_ﾜｰｸ押_下降端
OR M8823                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
AND M8821                ; 電特ｸﾗﾝﾌﾟ&前進開始
OUT M8823                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
LD M8823                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ確認
AND M8800                ; ﾊﾟﾚｯﾄ前進_OK
OR M8824                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND M8821                ; 電特ｸﾗﾝﾌﾟ&前進開始
OUT M8824                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
LD M8824                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進指令
AND X1352                ; ST3_電特部_ﾊﾟﾚｯﾄ_前進端
OR M8825                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
AND M8821                ; 電特ｸﾗﾝﾌﾟ&前進開始
OUT M8825                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
LD M8825                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ前進確認
OUT M8826                ; ｸﾗﾝﾌﾟ閉→ﾊﾟﾚｯﾄ前進動作完了
;ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ解除
LD M129                  ; 安全入力OK
AND M2732                ; ﾕﾆｯﾄ2-3_RCX240起動条件
AND M2801                ; RCX240動作完了
AND L309                 ; Z_搬送高さ(P9)
OUT M8830                ; 電特ｸﾗﾝﾌﾟ解除&ﾊﾟﾚｯﾄ後退_指令条件
LD M8813                 ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANP X1248                ; ﾊﾝﾄﾞ1吸着OFF_SW
ANB X1249                ; ﾊﾝﾄﾞ2吸着OFF_SW
AND M8802                ; ﾊﾟﾚｯﾄ後退_OK
AND M8830                ; 電特ｸﾗﾝﾌﾟ解除&ﾊﾟﾚｯﾄ後退_指令条件
OR M8831                 ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
ANB M8836                ; ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ開動作完了
ANB B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
ANB B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
AND M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT M8831                ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
LD M8831                 ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
AND M8802                ; ﾊﾟﾚｯﾄ後退_OK
OR M8832                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND M8831                ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M8832                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
LD M8832                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退指令
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OR M8833                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
AND M8831                ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M8833                ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
LD M8833                 ; 電特ｸﾗﾝﾌﾟ&前進_ﾊﾟﾚｯﾄ後退確認
AND M8806                ; ｸﾗﾝﾌﾟ開閉_開_起動OK
OR M8834                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND M8831                ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M8834                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
LD M8834                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開指令
AND X1355                ; ST3_電特部_ﾜｰｸ押_上昇端
OR M8835                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
AND M8831                ; 電特ﾊﾟﾚｯﾄ後退&ｸﾗﾝﾌﾟ解除開始
OUT M8835                ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
LD M8835                 ; 電特ｸﾗﾝﾌﾟ&前進_ｸﾗﾝﾌﾟ開確認
OUT M8836                ; ﾊﾟﾚｯﾄ後退→ｸﾗﾝﾌﾟ開動作完了
MCR
LDP B1266                ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
ANB M8812                ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
LDPB B1266               ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
AND M8812                ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
ANB M8312                ; SwBoxMode_LeakﾁｬﾝﾊﾞｰFlag
ANB M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
OUT M8812                ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
LDP B1267                ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
ANB M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
LDPB B1267               ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
AND M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANB M8812                ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
ANB M8312                ; SwBoxMode_LeakﾁｬﾝﾊﾞｰFlag
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
OUT M8813                ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
;<h1/>□_出力回路
;ソレノイドバルブ出力
;ｽﾃｰｼﾞ(ﾊﾟﾚｯﾄ前後)
LD M8801                 ; ﾊﾟﾚｯﾄ前進指令
AND M8800                ; ﾊﾟﾚｯﾄ前進_OK
LDB M8514                ; U16-1_S14_ﾊﾟﾚｯﾄ前進指令
INV
ORL
LD Y2444                 ; 電特ST3ｽﾃｰｼﾞ前後前進
ANB Y2445                ; 電特ST3ｽﾃｰｼﾞ前後後退
ORL
AND M50                  ; 運転準備
OUT Y2444                ; 電特ST3ｽﾃｰｼﾞ前後前進
LD M8803                 ; ﾊﾟﾚｯﾄ後退指令
AND M8802                ; ﾊﾟﾚｯﾄ後退_OK
LDB M8752                ; STEP2_ｽﾃｰｼﾞ戻り指令
ANB M8520                ; U16-1_S20_ｽﾃｰｼﾞ後退命令
INV
ORL
LD Y2445                 ; 電特ST3ｽﾃｰｼﾞ前後後退
ANB Y2444                ; 電特ST3ｽﾃｰｼﾞ前後前進
ORL
AND M50                  ; 運転準備
OUT Y2445                ; 電特ST3ｽﾃｰｼﾞ前後後退
LD M8805                 ; ｸﾗﾝﾌﾟ開閉_閉_指令
AND M8804                ; ｸﾗﾝﾌﾟ開閉_閉_起動OK
LD M1919                 ; U2-S419_電特部ｸﾗﾝﾌﾟ指令
LD M3221                 ; ﾊﾝﾄﾞ2_ﾌｧｲﾅﾙST3給材中ﾌﾗｸﾞ
OR M3220                 ; ﾊﾝﾄﾞ1_ﾌｧｲﾅﾙST3給材中ﾌﾗｸﾞ
ANL
ORL
OR M8504                 ; U16-1_S4_ｸﾗﾝﾌﾟ指令
LD Y2446                 ; 電特ST3ﾜｰｸ押さえ下降
ANB Y2447                ; 電特ST3ﾜｰｸ押さえ上昇
ORL
AND M50                  ; 運転準備
OUT Y2446                ; 電特ST3ﾜｰｸ押さえ下降
LD M8807                 ; ｸﾗﾝﾌﾟ開閉_開__指令
AND M8806                ; ｸﾗﾝﾌﾟ開閉_開_起動OK
LDB M8753                ; STEP3_ｸﾗﾝﾌﾟ上昇指令
ANB M3288                ; ST3_電特部_ﾜｰｸ押_上昇指令
INV
ORL
LD Y2447                 ; 電特ST3ﾜｰｸ押さえ上昇
ANB Y2446                ; 電特ST3ﾜｰｸ押さえ下降
ORL
AND M50                  ; 運転準備
OUT Y2447                ; 電特ST3ﾜｰｸ押さえ上昇
;■その他制御出力
LD M8661                 ; U16-3_S1_Final検査ｽﾀｰﾄ要求
OUT W0107.2              ; 電気特性検査開始要求#3
LD L1600                 ; FINALST3_有効状態
OUT W0107.10             ; 電気特性検査ｽﾃｰｼﾞ有効/無効#3
LD L1600                 ; FINALST3_有効状態
ANB M8909                ; FainlST3_無効予約(自動運転)
LD M8909                 ; FainlST3_無効予約(自動運転)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD M8916                 ; ST有効確認要求
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y225F                ; ST3_電特_有効/無効表示
OUT B176A                ; ST1電特検査_有効/無効_Tpb
;□□　　パネル表示　　□□
LD Y2444                 ; 電特ST3ｽﾃｰｼﾞ前後前進
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1352                 ; ST3_電特部_ﾊﾟﾚｯﾄ_前進端
OUT B1760                ; ﾊﾟﾚｯﾄ前進TpPL
LD Y2445                 ; 電特ST3ｽﾃｰｼﾞ前後後退
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1353                 ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
OUT B1761                ; ﾊﾟﾚｯﾄ後退TpPL
LD Y2446                 ; 電特ST3ﾜｰｸ押さえ下降
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1354                 ; ST3_電特部_ﾜｰｸ押_下降端
OUT B1762                ; ｸﾗﾝﾌﾟ下降TpPL
LD Y2447                 ; 電特ST3ﾜｰｸ押さえ上昇
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1355                 ; ST3_電特部_ﾜｰｸ押_上昇端
OUT B1763                ; ｸﾗﾝﾌﾟ上昇TpPL
LD M8812                 ; SwBoxMode_電特ﾊﾟﾚｯﾄ前後Flag
OUT B1766                ; PLBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞPL
LD M8813                 ; SwBoxMode_電特ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT B1767                ; PLBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞPL
;<h1/>□_異常検出
LD Y2444                 ; 電特ST3ｽﾃｰｼﾞ前後前進
ANB X1352                ; ST3_電特部_ﾊﾟﾚｯﾄ_前進端
LD Y2445                 ; 電特ST3ｽﾃｰｼﾞ前後後退
ANB X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
ORL
LD X1352                 ; ST3_電特部_ﾊﾟﾚｯﾄ_前進端
AND X1353                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
ORL
LD CR2003                ; 常時OFF
ANB B0700                ; 512)_FinalST3ｽﾃｰｼﾞ前後CL異常BzSt
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
ORL
MPS
AND T1690                ; CLSﾀｲﾑｱｳﾄ
OUT B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
MPP
TMR K1690 K100           ; CLSﾀｲﾑｱｳﾄ
LDP B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K512 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0700                 ; 512)_FinalST3ｽﾃｰｼﾞ前後CL異常BzSt
AND B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
OUT B0700                ; 512)_FinalST3ｽﾃｰｼﾞ前後CL異常BzSt
;　　
LD Y2446                 ; 電特ST3ﾜｰｸ押さえ下降
ANB X1354                ; ST3_電特部_ﾜｰｸ押_下降端
LD Y2447                 ; 電特ST3ﾜｰｸ押さえ上昇
ANB X1355                ; ST3_電特部_ﾜｰｸ押_上昇端
ORL
LD X1354                 ; ST3_電特部_ﾜｰｸ押_下降端
AND X1355                ; ST3_電特部_ﾜｰｸ押_上昇端
ORL
LD CR2003                ; 常時OFF
ANB B0701                ; 513)_FinalST3ｸﾗﾝﾌﾟ開閉CLS異常BzS
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
ORL
MPS
AND T1691                ; CLSﾀｲﾑｱｳﾄ
OUT B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
MPP
TMR K1691 K100           ; CLSﾀｲﾑｱｳﾄ
LDP B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K513 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0701                 ; 513)_FinalST3ｸﾗﾝﾌﾟ開閉CLS異常BzS
AND B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
OUT B0701                ; 513)_FinalST3ｸﾗﾝﾌﾟ開閉CLS異常BzS
;
LD T1606                 ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
AND L1600                ; FINALST3_有効状態
AND<> D8600 D8606        ; FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  FinalST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
LD CR2003                ; 常時OFF
ANB B0702                ; 514)_FinalST3_ﾃｽﾄﾍｯﾄﾞID_不BzStop
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
ORL
OUT B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
LDP B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1514 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0702                 ; 514)_FinalST3_ﾃｽﾄﾍｯﾄﾞID_不BzStop
AND B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
OUT B0702                ; 514)_FinalST3_ﾃｽﾄﾍｯﾄﾞID_不BzStop
;
LD T1607                 ; FinalST3_ﾊﾟﾚｯﾄID_更新_DLY
AND L1600                ; FINALST3_有効状態
AND<> D8601 D8607        ; FinalST3_ﾊﾟﾚｯﾄID  FinalST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
ANB Y2444                ; 電特ST3ｽﾃｰｼﾞ前後前進
AND T1604                ; ST3_電特部_ﾊﾟﾚｯﾄ_後退端
LD CR2003                ; 常時OFF
ANB B0703                ; 515)_FinalST3_ﾊﾟﾚｯﾄID_不一BzStop
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
ORL
OUT B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
LDP B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1515 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0703                 ; 515)_FinalST3_ﾊﾟﾚｯﾄID_不一BzStop
AND B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
OUT B0703                ; 515)_FinalST3_ﾊﾟﾚｯﾄID_不一BzStop
;
LD M8560                 ; U16-1_S60_FinalST3_ﾜｰｸ受取異常
LD CR2003                ; 常時OFF
ANB B0704                ; 516)_FinalST3_ﾜｰｸ受取異常BzStop
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0204                ; 516)FinalST3ﾜｰｸ受取異常
ORL
OUT B0204                ; 516)FinalST3ﾜｰｸ受取異常
LDP B0204                ; 516)FinalST3ﾜｰｸ受取異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1516 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0704                 ; 516)_FinalST3_ﾜｰｸ受取異常BzStop
AND B0204                ; 516)FinalST3ﾜｰｸ受取異常
OUT B0704                ; 516)_FinalST3_ﾜｰｸ受取異常BzStop
;
LD M8930                 ; Final_ST3_ﾜｰｸ受渡し異常
LD CR2003                ; 常時OFF
ANB B0705                ; 517)_FinalST3_ﾜｰｸ受渡し異常BzSto
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
ORL
OUT B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
LDP B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1517 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0705                 ; 517)_FinalST3_ﾜｰｸ受渡し異常BzSto
AND B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
OUT B0705                ; 517)_FinalST3_ﾜｰｸ受渡し異常BzSto
;　　
LD M8758                 ; STEP8_ORGﾜｰｸ除去要求
LD M2020                 ; U2_S520_吸着ﾐｽ発報orﾜｰｸ除去要求
LD M3229                 ; ﾊﾝﾄﾞ2ﾌｧｲﾅﾙST3除材中ﾌﾗｸﾞ
OR M3228                 ; ﾊﾝﾄﾞ1ﾌｧｲﾅﾙST3除材中ﾌﾗｸﾞ
ANL
AND>= D1540 K2           ; 吸着ﾘﾄﾗｲ回数ｶｳﾝﾀ
ORL
LD CR2003                ; 常時OFF
ANB B0831                ; 817)_ﾌｧｲﾅﾙST3ﾜｰｸ除去要求Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
ORL
OUT B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
LDP B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K817 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0831                 ; 817)_ﾌｧｲﾅﾙST3ﾜｰｸ除去要求Bz停止
AND B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
OUT B0831                ; 817)_ﾌｧｲﾅﾙST3ﾜｰｸ除去要求Bz停止
;
LD W07.2                 ; 電気特性検査ｽﾃｰｼﾞ状態#3
AND M31                  ; 手動ﾓｰﾄﾞ
ANP M17                  ; 原点復帰PB
LD CR2003                ; 常時OFF
ANB B0832                ; 818)_電特ST3検査中Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0332                ; 818)電特ST3検査中
ORL
OUT B0332                ; 818)電特ST3検査中
LDP B0332                ; 818)電特ST3検査中
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K818 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0832                 ; 818)_電特ST3検査中Bz停止
AND B0332                ; 818)電特ST3検査中
OUT B0832                ; 818)_電特ST3検査中Bz停止
;異常ﾌﾞｻﾞｰまとめ
LDB B0200                ; 512)FinalST3ｽﾃｰｼﾞ前後CL異常
OR B0700                 ; 512)_FinalST3ｽﾃｰｼﾞ前後CL異常BzSt
LDB B0201                ; 513)FinalST3ｸﾗﾝﾌﾟ開閉CLS異常
OR B0701                 ; 513)_FinalST3ｸﾗﾝﾌﾟ開閉CLS異常BzS
ANL
LDB B0202                ; 514)FinalST3ﾃｽﾄﾍｯﾄﾞID不
OR B0702                 ; 514)_FinalST3_ﾃｽﾄﾍｯﾄﾞID_不BzStop
ANL
LDB B0203                ; 515)FinalST3ﾊﾟﾚｯﾄID不一
OR B0703                 ; 515)_FinalST3_ﾊﾟﾚｯﾄID_不一BzStop
ANL
LDB B0204                ; 516)FinalST3ﾜｰｸ受取異常
OR B0704                 ; 516)_FinalST3_ﾜｰｸ受取異常BzStop
ANL
LDB B0205                ; 517)FinalST3ﾜｰｸ受渡し異常
OR B0705                 ; 517)_FinalST3_ﾜｰｸ受渡し異常BzSto
ANL
LDB B0206                ; 518)
OR B0706                 ; 390)_U12E7Bz停止
ANL
LDB B0207                ; 519)
OR B0707                 ; 391)_U12E8Bz停止
ANL
LDB B0208                ; 520)
OR B0708                 ; 392)_U12E9Bz停止
ANL
LDB B0209                ; 521)
OR B0709                 ; 393)_U12E10Bz停止
ANL
LDB B020A                ; 522)
OR B070A                 ; 394)_U12E11Bz停止
ANL
LDB B020B                ; 523)
OR B070B                 ; 395)_U12E12Bz停止
ANL
LDB B020C                ; 524)
OR B070C                 ; 396)_U12E13Bz停止
ANL
LDB B020D                ; 525)
OR B070D                 ; 397)_U12E14Bz停止
ANL
LDB B020E                ; 526)
OR B070E                 ; 398)_U12E15Bz停止
ANL
LDB B020F                ; 527)
OR B070F                 ; 399)_U12E16Bz停止
ANL
LDB B0210                ; 528)
OR B0710                 ; 400)_U12E17Bz停止
ANL
LDB B0211                ; 529)
OR B0711                 ; 401)_U12E18Bz停止
ANL
LDB B0212                ; 530)
OR B0712                 ; 402)_U12E19Bz停止
ANL
LDB B0213                ; 531)
OR B0713                 ; 403)_U12E20Bz停止
ANL
LDB B01F4                ; 500)
OR B06F4                 ; 500)BzStop
ANL
LDB B01F5                ; 501)
OR B06F5                 ; 501)BzStop
ANL
LDB B01F6                ; 502)
OR B06F6                 ; 502)BzStop
ANL
LDB B01F7                ; 503)
OR B06F7                 ; 503)BzStop
ANL
LDB B01F8                ; 504)
OR B06F8                 ; 504)BzStop
ANL
LDB B01F9                ; 505)
OR B06F9                 ; 505)BzStop
ANL
LDB B01FA                ; 506)
OR B06FA                 ; 506)BzStop
ANL
LDB B01FB                ; 507)
OR B06FB                 ; 507)BzStop
ANL
LDB B01FC                ; 508)
OR B06FC                 ; 508)BzStop
ANL
LDB B01FD                ; 509)
OR B06FD                 ; 509)BzStop
ANL
LDB B01FE                ; 510)
OR B06FE                 ; 510)BzStop
ANL
LDB B01FF                ; 511)
OR B06FF                 ; 511)BzStop
ANL
LDB B0331                ; 817)ﾌｧｲﾅﾙST3ﾜｰｸ除去要求
OR B0831                 ; 817)_ﾌｧｲﾅﾙST3ﾜｰｸ除去要求Bz停止
ANL
LDB B0332                ; 818)電特ST3検査中
OR B0832                 ; 818)_電特ST3検査中Bz停止
ANL
INV
OUT M8989                ; U16異常BZ補助
END
ENDH
