DEVICE:53
;MODULE:UNIT6_CV
;MODULE_TYPE:0
;<h1/>□_入力受け
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LDB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
TMR K601 K100            ; OKｺﾝﾍﾞﾔ満杯時間
LDB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
TMR K602 K100            ; NGｺﾝﾍﾞﾔ満杯時間
LDB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
TMR K603 K8              ; OKｺﾝﾍﾞﾔ到達確認時間
LDB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
TMR K604 K8              ; NGｺﾝﾍﾞﾔ到着確認時間
;<h1/>__ロボット等機器入力受け
;<h1/>□_装置状態・動作条件
;ﾕﾆｯﾄまとめ
LD M3570                 ; ﾕﾆｯﾄ6-1原点_OK_CV
AND M3650                ; ﾕﾆｯﾄ6-2原点_NG_CV
AND M3730                ; ﾕﾆｯﾄ6-3原点_現在未使用
OUT M3980                ; ﾕﾆｯﾄ6原点_OK_NG_CV
;□ 非常停止
LDB M3571                ; ﾕﾆｯﾄ6-1全異常
ANB M3651                ; ﾕﾆｯﾄ6-2異常
ANB M3731                ; ﾕﾆｯﾄ6-3全異常
INV
OUT M3981                ; ﾕﾆｯﾄ6即停止異常
;□ 起動条件
LD CR2003                ; 常時OFF
OUT M3982                ; ﾕﾆｯﾄ6起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
ORL
OR T15                   ; 強制停止
OUT M3983                ; ﾕﾆｯﾄ6起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
OR M3984                 ; ﾕﾆｯﾄ6原点復帰中
ANB M3983                ; ﾕﾆｯﾄ6起動OFF
ANB M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
ANB M3797                ; STEPEND
OUT M3984                ; ﾕﾆｯﾄ6原点復帰中
;□ 起動中
LDB M3574                ; ﾕﾆｯﾄ6-1起動中
ANB M3654                ; ﾕﾆｯﾄ6-2起動中
ANB M3734                ; ﾕﾆｯﾄ6-3起動中
INV
OUT M3985                ; ﾕﾆｯﾄ6起動中
;□ ｻｲｸﾙ停止異常
LDB M3575                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ停止異常
ANB M3655                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ停止異常
ANB M3735                ; ﾕﾆｯﾄ6-3ｻｲｸﾙ停止異常
INV
OUT M3986                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3987                ; ﾕﾆｯﾄ6払出停止異常
;□ 警報補助
LDB B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
ANB B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
INV
OUT M3988                ; ﾕﾆｯﾄ6警報補助
;□ 機器準備確認
LDB CR2003               ; 常時OFF
OUT M3990                ; ﾕﾆｯﾄ6機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB M3576                ; ﾕﾆｯﾄ6-1ｴﾗｰ発生
ANB M3656                ; ﾕﾆｯﾄ6-2ｴﾗｰ発生
ANB M3736                ; ﾕﾆｯﾄ6-3ｴﾗｰ発生
INV
OUT M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD M3577                 ; ﾕﾆｯﾄ6-1動作無し
AND M3657                ; ﾕﾆｯﾄ6-2動作無し
AND M3737                ; ﾕﾆｯﾄ6-3動作無し
OUT M3992                ; ﾕﾆｯﾄ6動作無し確認
;□ 払出しﾜｰｸ無し
LD X1288                 ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
AND X128C                ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し(払出時)
ANB M1574                ; ﾕﾆｯﾄ2-1起動中
AND M36                  ; 払出ﾓｰﾄﾞ
TMR K614 K20             ; ｺﾝﾍﾞﾔ部ﾜｰｸ無し
LD T614                  ; ｺﾝﾍﾞﾔ部ﾜｰｸ無し
OUT M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
ANB M130                 ; 一時停止
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M57                  ; 自動起動停止指令(払出停止)
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
OUT M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
;<h1/>□_UNIT_6_自動回路
;<h1/>◇_OKｺﾝﾍﾞﾔ（ﾕﾆｯﾄ6-1)状態・動作条件(補助)
LDB Y55                  ; OK品ｺﾝﾍﾞｱ電源ON
AND X1288                ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
OUT M3570                ; ﾕﾆｯﾄ6-1原点_OK_CV
LDB CR2003               ; 常時OFF
INV
OUT M3571                ; ﾕﾆｯﾄ6-1全異常
LD M3570                 ; ﾕﾆｯﾄ6-1原点_OK_CV
ANB M3574                ; ﾕﾆｯﾄ6-1起動中
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
ANB M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
AND M1659                ; 初期起動時吸着ｾﾝｻ確認完了記憶
OUT M3572                ; ﾕﾆｯﾄ6-1起動条件
LDB M50                  ; 運転準備
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3573                ; ﾕﾆｯﾄ6-1起動OFF
LD M73                   ; 各工程起動指令
AND M3572                ; ﾕﾆｯﾄ6-1起動条件
OR M3574                 ; ﾕﾆｯﾄ6-1起動中
ANB M3573                ; ﾕﾆｯﾄ6-1起動OFF
ANB M3563                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ終了
OUT M3574                ; ﾕﾆｯﾄ6-1起動中
LD M2191                 ; U2-S691_ﾃﾞｰﾀｼﾌﾄ指令
DW K0 D3520              ; NG_CV_Status
MPS
AND= D3803 K2            ; NGｺﾝﾍﾞﾔ_ﾘｰｸ検査結果(1=OK_2=NG)
DW K1 D3520              ; NG_CV_Status
MPP
AND= D3804 K2            ; NGｺﾝﾍﾞﾔ_ﾌｧｲﾅﾙ検査結果(1=OK_2=NG)
DW K2 D3520              ; NG_CV_Status
;ﾕﾆｯﾄ1-1 ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3575                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-1 ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M3576                ; ﾕﾆｯﾄ6-1ｴﾗｰ発生
;ﾕﾆｯﾄ1-1 停止判定
LD= D3500 D3505          ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-1動作中判定
ORB M3574                ; ﾕﾆｯﾄ6-1起動中
TMR K610 K20             ; ﾕﾆｯﾄ6動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D3500 D3505          ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-1動作中判定
LD T610                  ; ﾕﾆｯﾄ6動作無し判定ﾀｲﾏ
OUT M3577                ; ﾕﾆｯﾄ6-1動作無し
;□
;□　ﾕﾆｯﾄ1-1 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3500 K4         ; U6-1_S0_
LDB M3574                ; ﾕﾆｯﾄ6-1起動中
MOV K0 D3500             ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3574                 ; ﾕﾆｯﾄ6-1起動中
MOV D3500 Z1             ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
OUT M3500:Z1             ; U6-1_S0_
;条件分岐
LD M3500                 ; U6-1_S0_
AND X1288                ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S1_ﾜｰｸ搭載待ち
LD M3501                 ; U6-1_S1_
MPS
ANB M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
MPS
AND M2310                ; OKｺﾝﾍﾞﾔ給材開始
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1288                ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
OUT M3900                ; OKｺﾝﾍﾞﾔ部_ﾜｰｸ供給要求
MPP
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
MOV K20 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3502                 ; U6-1_S2_OK_CV_ワーク受取中
ANB M2184                ; U2-S684_ｺﾝﾍﾞﾔ供給OK待ち
LDB X1288                ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
AND= D3701 K1            ; OKｺﾝﾍﾞﾔ_ﾜｰｸ有無記憶
LD M1840                 ; Z_搬送高さ(HIGH)_搬送1
OR M1855                 ; Z_搬送高さ(MIDLE)_搬送1
ANL
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3503                 ; U6-1_S3_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3504                 ; U6-1_S4_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3505                 ; U6-1_S5_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3506                 ; U6-1_S6_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3507                 ; U6-1_S7_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3508                 ; U6-1_S8_
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3509                 ; U6-1_S9_OKｺﾝﾍﾞﾔ部満杯確認部
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANB B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;　　ｺﾝﾍﾞﾔﾓｰﾀｰ回転
LD M3510                 ; U6-1_S10_OKｺﾝﾍﾞﾔ回転
MPS
AND T620                 ; OK_CV_ピッチ送り時間
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
LDB T603                 ; OKｺﾝﾍﾞﾔ到達確認時間
AND X1288                ; OKｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
ANL
TMR K620 K15             ; OK_CV_ピッチ送り時間
LD M3511                 ; U6-1_S11_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
FMOV K0 D3700 K100       ; OKｺﾝﾍﾞﾔ_INDEX NO.
LD M3512                 ; U6-1_S12_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3513                 ; U6-1_S13_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3514                 ; U6-1_S14_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3515                 ; U6-1_S15_
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
MOV K63 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S20_払出し停止用動作
LD M3520                 ; U6-1_S20_
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
@MOV K0 D3514            ; OK_CV_払出回転時間
LD M3521                 ; U6-1_S21_ﾓｰﾀｰ回転指令
MPS
ANB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
LDP CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MPS
INC D3514                ; OK_CV_払出回転時間
MPP
LD>= D3514 K70           ; OK_CV_払出回転時間
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MOV K24 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3522                 ; U6-1_S22_OK_CV_再起動
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
MPS
AND T622                 ; OK_CV_再起動_DLY
MOV K21 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
TMR K622 K10             ; OK_CV_再起動_DLY
;　　ﾃﾞｰﾀ消去
LD M3524                 ; U6-1_S24_
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
INC D3500                ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3525                 ; U6-1_S25_
MPS
LD W01.8                 ; OK排出製品ﾃﾞｰﾀｸﾘｱ完了
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ORB M0                   ; 常時OFF
ANL
MOV K63 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
AND M0                   ; 常時OFF
OUT W0101.8              ; OK排出製品ﾃﾞｰﾀｸﾘｱ要求
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M3562                 ; U6-1_S62_
OR CR2003                ; 常時OFF
@MOV K63 D3500           ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;<h1/>◇_NGｺﾝﾍﾞﾔ（ﾕﾆｯﾄ6-2状態・動作条件(補助)
LDB Y56                  ; NG品ｺﾝﾍﾞｱ電源ON
AND X128C                ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
OUT M3650                ; ﾕﾆｯﾄ6-2原点_NG_CV
LDB CR2003               ; 常時OFF
INV
OUT M3651                ; ﾕﾆｯﾄ6-2異常
LD M3650                 ; ﾕﾆｯﾄ6-2原点_NG_CV
ANB M3654                ; ﾕﾆｯﾄ6-2起動中
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
ANB M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
AND M1659                ; 初期起動時吸着ｾﾝｻ確認完了記憶
OUT M3652                ; ﾕﾆｯﾄ6-2起動条件
LDB M50                  ; 運転準備
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3653                ; ﾕﾆｯﾄ6-2起動OFF
LD M73                   ; 各工程起動指令
AND M3652                ; ﾕﾆｯﾄ6-2起動条件
OR M3654                 ; ﾕﾆｯﾄ6-2起動中
ANB M3653                ; ﾕﾆｯﾄ6-2起動OFF
ANB M3643                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ終了
OUT M3654                ; ﾕﾆｯﾄ6-2起動中
;ﾕﾆｯﾄ1-2ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3655                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-2ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M3656                ; ﾕﾆｯﾄ6-2ｴﾗｰ発生
;ﾕﾆｯﾄ1-2停止判定
LD= D3501 D3506          ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-2動作中判定
ORB M3654                ; ﾕﾆｯﾄ6-2起動中
TMR K611 K20
LD CR2002                ; 常時ON
MOV D3501 D3506          ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-2動作中判定
LD T611
LD CR2003                ; 常時OFF
AND M31                  ; 手動ﾓｰﾄﾞ
ORL
OUT M3657                ; ﾕﾆｯﾄ6-2動作無し
;□
;□　ﾕﾆｯﾄ1-2ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3580 K4         ; U6-2_S0
LDB M3654                ; ﾕﾆｯﾄ6-2起動中
MOV K0 D3501             ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3654                 ; ﾕﾆｯﾄ6-2起動中
MOV D3501 Z1             ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
OUT M3580:Z1             ; U6-2_S0
;<h1/>__S0_ﾜｰｸ搭載待ち
LD M3580                 ; U6-2_S0
MPS
ANB M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
MPS
AND M2311                ; NGｺﾝﾍﾞﾔ給材開始
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
AND X128C                ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
OUT M3901                ; NGｺﾝﾍﾞﾔ部_ﾜｰｸ供給要求
MPP
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
MOV K20 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3581                 ; U6-2_S1_NG_CV_ワーク受取中
ANB M2184                ; U2-S684_ｺﾝﾍﾞﾔ供給OK待ち
LDB X128C                ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
AND= D3801 K1            ; NGｺﾝﾍﾞﾔ_ﾜｰｸ有無記憶
LD M1840                 ; Z_搬送高さ(HIGH)_搬送1
OR M1855                 ; Z_搬送高さ(MIDLE)_搬送1
ANL
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3582                 ; U6-2_S2_U6-2_S1_
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3583                 ; U6-2_S3
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3584                 ; U6-2_S4
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3585                 ; U6-2_S5
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3586                 ; U6-2_S6
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3587                 ; U6-2_S7
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3588                 ; U6-2_S8
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3589                 ; U6-2_S9
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANB B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;　　ｺﾝﾍﾞﾔﾓｰﾀｰ回転
LD M3590                 ; U6-2_S10_NGｺﾝﾍﾞﾔ起動指令
MPS
LD T621                  ; NG _CV_ワーク到着_DLY
OR T625                  ; ﾜｰｸ到着確認前にﾜｰｸをとられたﾀｲﾏ
ANL
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MRD
LD X128C                 ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
ANB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
TMR K621 K8              ; NG _CV_ワーク到着_DLY
MPP
LD X128C                 ; NGｺﾝﾍﾞﾔ_除材部ﾜｰｸ確認(1:無/0:有)
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
TMR K625 K100            ; ﾜｰｸ到着確認前にﾜｰｸをとられたﾀｲﾏ
LD M3591                 ; U6-2_S11_NG_CV_ワーク取出待ち
MPS
AND T624                 ; NG_ワーク取出_完了
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
TMR K624 K10             ; NG_ワーク取出_完了
LD M3592                 ; U6-2_S12
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3593                 ; U6-2_S13
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3594                 ; U6-2_S14
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3595                 ; U6-2_S15
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
MOV K63 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;<h1/>__S20_払出し停止用動作
LD M3600                 ; U6-2_S20_
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
@MOV K0 D3516            ; NG_CV_払出回転時間
LD M3601                 ; U6-2_S21_NGｺﾝﾍﾞﾔ起動指令
MPS
ANB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
LDP CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MPS
INC D3516                ; NG_CV_払出回転時間
MPP
AND>= D3516 K60          ; NG_CV_払出回転時間
MOV K24 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3602                 ; U6-2_S22
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
MPS
AND T623                 ; NG_CV_再起動_DLY
MOV K21 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
TMR K623 K10             ; NG_CV_再起動_DLY
;　　ﾃﾞｰﾀ消去
LD M3604                 ; U6-2_S24
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
INC D3501                ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3605                 ; U6-2_S25
MPS
LD W01.9                 ; NG排出製品ﾃﾞｰﾀｸﾘｱ完了
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ORB M0                   ; 常時OFF
ANL
MOV K63 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
AND M0                   ; 常時OFF
OUT W0101.9              ; NG排出製品ﾃﾞｰﾀｸﾘｱ要求
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M3642                 ; U6-2_S62
OR CR2003                ; 常時OFF
@MOV K63 D3501           ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;<h1/>◇_未使用_ﾕﾆｯﾄ6-3状態・動作条件(補助)
LDB M0                   ; 常時OFF
OUT M3730                ; ﾕﾆｯﾄ6-3原点_現在未使用
LDB CR2003               ; 常時OFF
INV
OUT M3731                ; ﾕﾆｯﾄ6-3全異常
LD M3730                 ; ﾕﾆｯﾄ6-3原点_現在未使用
ANB M3734                ; ﾕﾆｯﾄ6-3起動中
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
OUT M3732                ; ﾕﾆｯﾄ6-3起動条件
LDB M50                  ; 運転準備
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3733                ; ﾕﾆｯﾄ6-3起動OFF
LD M73                   ; 各工程起動指令
AND CR2003               ; 常時OFF
AND M3732                ; ﾕﾆｯﾄ6-3起動条件
OR M3734                 ; ﾕﾆｯﾄ6-3起動中
ANB M3733                ; ﾕﾆｯﾄ6-3起動OFF
ANB M3723                ; ﾕﾆｯﾄ6-3ｻｲｸﾙ終了
OUT M3734                ; ﾕﾆｯﾄ6-3起動中
;ﾕﾆｯﾄ1-3ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3735                ; ﾕﾆｯﾄ6-3ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-3ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M3736                ; ﾕﾆｯﾄ6-3ｴﾗｰ発生
;ﾕﾆｯﾄ1-3停止判定
LD= D3502 D3507          ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-3動作中判定
TMR K612 K20
LD CR2002                ; 常時ON
MOV D3502 D3507          ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-3動作中判定
LD T612
ORB M3734                ; ﾕﾆｯﾄ6-3起動中
OUT M3737                ; ﾕﾆｯﾄ6-3動作無し
;□
;□　ﾕﾆｯﾄ1-3自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3660 K4         ; STEP0
LDB M3734                ; ﾕﾆｯﾄ6-3起動中
MOV K0 D3502             ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
LD M3734                 ; ﾕﾆｯﾄ6-3起動中
MOV D3502 Z1             ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
OUT M3660:Z1             ; STEP0
;ﾜｰｸ有無判定
LD M3660                 ; STEP0
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
MPS
AND CR2003               ; 常時OFF
@INC D3502               ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
MPP
AND CR2003               ; 常時OFF
@MOV K4 D3502            ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D3502            ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M3722                 ; ﾕﾆｯﾄ6-3ｻｲｸﾙ停止処理
OR CR2003                ; 常時OFF
@MOV K63 D3502           ; ﾕﾆｯﾄ6-3自動運転ｽﾃｯﾌﾟ
;<h1/>□_UNIT_6_原点復帰回路
LD CR2002                ; 常時ON
FMOV K0 M3750 K3         ; STEP0_OK_NG_CV_Org_先頭
LDB M3984                ; ﾕﾆｯﾄ6原点復帰中
MOV K0 D3510             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3984                 ; ﾕﾆｯﾄ6原点復帰中
MOV D3510 Z1             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
OUT M3750:Z1             ; STEP0_OK_NG_CV_Org_先頭
;原点復帰ｻｲｸﾙ監視
LD M3984                 ; ﾕﾆｯﾄ6原点復帰中
TMR K600 K200            ; ﾗｯｸ排出CV原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M3750                 ; STEP0_OK_NG_CV_Org_先頭
MPS
AND M1840                ; Z_搬送高さ(HIGH)_搬送1
INC D3510                ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
MRD
@MOV K0 D3512            ; OK_NG_CV_Org_回転時間
MRD
@MOV K0 D3520            ; NG_CV_Status
MPP
@FMOV K0 D3700 K200      ; OKｺﾝﾍﾞﾔ_INDEX NO.
LD M3751                 ; STEP1_OK_NG_CV_満杯チェック
MPS
LDB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ORB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
INC D3510                ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
MPP
LD X1289                 ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MOV K4 D3510             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3752                 ; STEP2_OK_NG_CV_満杯
AND X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
MPS
TMR K606 K5              ; OK_NG_CV_除材確認
MPP
AND T606                 ; OK_NG_CV_除材確認
MOV K1 D3510             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3754                 ; STEP4_OK_NG_CV_回転指令_Org
MPS
LDB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ORB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MOV K1 D3510             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
MPP
LD X1289                 ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
AND X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ANL
MPS
ANP CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
INC D3512                ; OK_NG_CV_Org_回転時間
MPP
AND>= D3512 K110         ; OK_NG_CV_Org_回転時間
MOV K10 D3510            ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3760                 ; STEP10
AND M3980                ; ﾕﾆｯﾄ6原点_OK_NG_CV
MOV K46 D3510            ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M3796                 ; STEP46
ANB M82                  ; 原点復帰開始
MOV K47 D3510            ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;<h1/>□_UNIT_6_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
MC
;OKｺﾝﾍﾞﾔ起動
LD M121                  ; 非常停止正常
AND M122                 ; 外部非常停止正常
LD M1818                 ; ROB_XYZ_復帰完了_Flg
ANB M1832                ; XY_OKｺﾝﾍﾞﾔ位置
OR M1840                 ; Z_搬送高さ(HIGH)_搬送1
ANL
OUT M3800                ; OKｺﾝﾍﾞﾔ起動OK
LDP B1120                ; UNIT6-1-1 TP-PB
ORP X30A                 ; OKｺﾝﾍﾞﾔ起動
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
ANB M3803                ; OKｺﾝﾍﾞﾔ起動OFF指令
ORL
AND M3800                ; OKｺﾝﾍﾞﾔ起動OK
OUT M3801                ; OKｺﾝﾍﾞﾔ起動指令
LD CR2002                ; 常時ON
OUT M3802                ; OKｺﾝﾍﾞﾔ起動OFF_OK
LDP B1121                ; UNIT6-1-2 TP-PB
LDB X30A                 ; OKｺﾝﾍﾞﾔ起動
MEP
ORL
AND M3802                ; OKｺﾝﾍﾞﾔ起動OFF_OK
LD M3803                 ; OKｺﾝﾍﾞﾔ起動OFF指令
ANB M3801                ; OKｺﾝﾍﾞﾔ起動指令
ORL
OUT M3803                ; OKｺﾝﾍﾞﾔ起動OFF指令
;NGｺﾝﾍﾞﾔ起動
LD M121                  ; 非常停止正常
AND M122                 ; 外部非常停止正常
LD M1818                 ; ROB_XYZ_復帰完了_Flg
ANB M1833                ; XY_NGｺﾝﾍﾞﾔ位置
OR M1840                 ; Z_搬送高さ(HIGH)_搬送1
ANL
OUT M3804                ; NGｺﾝﾍﾞﾔ起動OK
LDP B1122                ; UNIT6-1-3 TP-PB
ORP X30B                 ; NGｺﾝﾍﾞﾔ起動
LD M3805                 ; NGｺﾝﾍﾞﾔ起動指令
ANB M3807                ; NGｺﾝﾍﾞﾔ起動OFF指令
ORL
AND M3804                ; NGｺﾝﾍﾞﾔ起動OK
OUT M3805                ; NGｺﾝﾍﾞﾔ起動指令
LD CR2002                ; 常時ON
OUT M3806                ; NGｺﾝﾍﾞﾔ起動OFF_OK
LDP B1123                ; UNIT6-1-4 TP-PB
LDB X30B                 ; NGｺﾝﾍﾞﾔ起動
MEP
ORL
AND M3806                ; NGｺﾝﾍﾞﾔ起動OFF_OK
LD M3807                 ; NGｺﾝﾍﾞﾔ起動OFF指令
ANB M3805                ; NGｺﾝﾍﾞﾔ起動指令
ORL
OUT M3807                ; NGｺﾝﾍﾞﾔ起動OFF指令
MCR
;<h1/>□_出力回路
;ソレノイドバルブ出力
LDB M3801                ; OKｺﾝﾍﾞﾔ起動指令
ANB M3754                ; STEP4_OK_NG_CV_回転指令_Org
INV
LD M3510                 ; U6-1_S10_OKｺﾝﾍﾞﾔ回転
ANB T603                 ; OKｺﾝﾍﾞﾔ到達確認時間
OR M3521                 ; U6-1_S21_ﾓｰﾀｰ回転指令
LD X30A                  ; OKｺﾝﾍﾞﾔ起動
ANB T603                 ; OKｺﾝﾍﾞﾔ到達確認時間
ANB M3502                ; U6-1_S2_OK_CV_ワーク受取中
AND M72                  ; 自動運転開始
ORL
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
ORL
AND M50                  ; 運転準備
OUT Y55                  ; OK品ｺﾝﾍﾞｱ電源ON
LDB M3805                ; NGｺﾝﾍﾞﾔ起動指令
ANB M3754                ; STEP4_OK_NG_CV_回転指令_Org
INV
LD M3590                 ; U6-2_S10_NGｺﾝﾍﾞﾔ起動指令
OR M3601                 ; U6-2_S21_NGｺﾝﾍﾞﾔ起動指令
LD X30B                  ; NGｺﾝﾍﾞﾔ起動
ANB T604                 ; NGｺﾝﾍﾞﾔ到着確認時間
ANB M3581                ; U6-2_S1_NG_CV_ワーク受取中
AND M72                  ; 自動運転開始
ORL
AND M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
ORL
AND M50                  ; 運転準備
OUT Y56                  ; NG品ｺﾝﾍﾞｱ電源ON
LDB M3522                ; U6-1_S22_OK_CV_再起動
INV
OR T603                  ; OKｺﾝﾍﾞﾔ到達確認時間
ANB B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
LD B0323                 ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y316                 ; OKｺﾝﾍﾞﾔﾋﾟｯｷﾝｸﾞﾗﾝﾌﾟ
LDB M3522                ; U6-1_S22_OK_CV_再起動
ANB M3591                ; U6-2_S11_NG_CV_ワーク取出待ち
INV
OR T604                  ; NGｺﾝﾍﾞﾔ到着確認時間
ANB B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
LD B0324                 ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y317                 ; NGｺﾝﾍﾞﾔ_ﾋﾟｯｷﾝｸﾞﾗﾝﾌﾟ
;■その他制御出力
;□□　　パネル表示　　□□
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
OUT B1620                ; UNIT6-1-1 TP-PL
LDB M3801                ; OKｺﾝﾍﾞﾔ起動指令
OUT B1621                ; UNIT6-1-2 TP-PL
LD M3805                 ; NGｺﾝﾍﾞﾔ起動指令
OUT B1622                ; UNIT6-1-3 TP-PL
LDB M3805                ; NGｺﾝﾍﾞﾔ起動指令
OUT B1623                ; UNIT6-1-4 TP-PL
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
LD Y55                   ; OK品ｺﾝﾍﾞｱ電源ON
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD X30A                  ; OKｺﾝﾍﾞﾔ起動
AND Y55                  ; OK品ｺﾝﾍﾞｱ電源ON
ANB M3502                ; U6-1_S2_OK_CV_ワーク受取中
AND M30                  ; 自動ﾓｰﾄﾞ
ORL
OUT Y314                 ; OKｺﾝﾍﾞﾔ起動表示
LD M3805                 ; NGｺﾝﾍﾞﾔ起動指令
LD Y56                   ; NG品ｺﾝﾍﾞｱ電源ON
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD X30B                  ; NGｺﾝﾍﾞﾔ起動
AND Y56                  ; NG品ｺﾝﾍﾞｱ電源ON
ANB M3581                ; U6-2_S1_NG_CV_ワーク受取中
AND M30                  ; 自動ﾓｰﾄﾞ
ORL
OUT Y315                 ; NGｺﾝﾍﾞﾔ起動表示
;<h1/>□_異常検出
LD T601                  ; OKｺﾝﾍﾞﾔ満杯時間
LD M3752                 ; STEP2_OK_NG_CV_満杯
ANB X1289                ; OKｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ORL
AND M50                  ; 運転準備
OUT B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0823                 ; 803)_U6E1_ブザー停止
AND B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
OUT B0823                ; 803)_U6E1_ブザー停止
LD T602                  ; NGｺﾝﾍﾞﾔ満杯時間
LD M3752                 ; STEP2_OK_NG_CV_満杯
ANB X128D                ; NGｺﾝﾍﾞﾔ_満杯検出(1:無/0:有)
ORL
AND M50                  ; 運転準備
OUT B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0824                 ; 804)_U6E2_ブザー定S
AND B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
OUT B0824                ; 804)_U6E2_ブザー定S
;異常ﾌﾞｻﾞｰまとめ
LDB B0323                ; 803)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
OR B0823                 ; 803)_U6E1_ブザー停止
LDB B0324                ; 804)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
OR B0824                 ; 804)_U6E2_ブザー定S
ANL
INV
OUT M3989                ; ﾕﾆｯﾄ6異常BZ補助
END
ENDH
