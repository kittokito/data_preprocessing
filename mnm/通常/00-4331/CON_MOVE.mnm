DEVICE:53
;MODULE:CON_MOVE
;MODULE_TYPE:1
;OneSystem SupportMacro
;
;-----------------------------------------
;Macro Name 	: 	IAI_CON_MOVE
;Macro Type	:	Self Holding
;Ver.		:	1.1
;Function	:	IAI CON系直値移動ﾏｸﾛ
;-----------------------------------------
;引数：
;	V0	=	目標位置						V0	=	Target Position Value
;	V1	=	位置決め幅						V1	=	Pos Wide
;	V2 	=	速度							V2	=	Speed
;	V3	=	加減速							V3	=	G
;
;	P0	=	RB入力先頭番地	DM番号で指定	P0	=	Input Leading Address for RB
;	P1	=	RB出力先頭番地	DM番号で指定	P1	=	Output Leading Address for RB
;	P2	=	完了コード						P2	=	Macro Complete Code
;			#20			=	正常終了				#20 		=	No Error
;			#21			=	RB応答なし				#21 		=	Non Responce
;			#22			=	ｽﾃｰﾀｽ異常				#22 		=	Status Abnormal
;			Alarmcode	=	Alarm					#Alarmcode =	Alarm
;
;機能：CON系 MSEP 数直移動指令ﾏｸﾛ/ Robot Move Macro for Numerical Move Mode
;IN
BLD P0:#7 #0             ; RB_IN
OUT PEND                 ; IN PEND
BLD P0:#7 #1             ; RB_IN
OUT HEND                 ; IN HEND
BLD P0:#7 #2             ; RB_IN
OUT MOVE                 ; IN MOVE
BLD P0:#7 #3             ; RB_IN
OUT ALM                  ; IN ALM
BLD P0:#7 #4             ; RB_IN
OUT SV                   ; IN SV
BLD P0:#7 #5             ; RB_IN
OUT PSFL                 ; IN PSFL
BLD P0:#7 #7             ; RB_IN
OUT ALML                 ; IN ALML
BLD P0:#7 #8             ; RB_IN
OUT M_END                ; IN MEND
BLD P0:#7 #12            ; RB_IN
OUT ZONE1                ; IN ZONE1
BLD P0:#7 #13            ; RB_IN
OUT ZONE2                ; IN ZONE2
BLD P0:#7 #14            ; RB_IN
OUT CRDY                 ; IN CRDY
BLD P0:#7 #15            ; RB_IN
OUT EMGS                 ; IN EMGS
;OUT
LD CSTR                  ; OUT START
BOUT P1:#7 #0            ; RB_OUT
LD PUSH                  ; OUT Push
BOUT P1:#7 #12           ; RB_OUT
LD DIR                   ; OUT Push ON=+Dir
BOUT P1:#7 #13           ; RB_OUT
LD Incre                 ; OUT ON=INC
BOUT P1:#7 #14           ; RB_OUT
;
LD MR19915               ; _運転ﾓｰﾄﾞ指定
ORP R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
@SET @R110               ; 終了JMPへ
STG @R110                ; 終了JMPへ
JMP @R012                ; 終了処理
;初期化　/ Initialize
LD @CR2008
SET @R000
;RBｽﾃｰﾀｽﾓﾆﾀｰ / Status Monitoring
LDB EMGS                 ; IN EMGS
AND SV                   ; IN SV
AND CRDY                 ; IN CRDY
AND HEND                 ; IN HEND
ANB MOVE                 ; IN MOVE
OUT @R014                ; RB Status OK
;Move Parameter storage
STG @R000
MPS
LDA.D V0                 ; POS
CON
STA.D P1:#0              ; RB_OUT
CON
LDA.D #10
CON
STA.D P1:#2              ; RB_OUT
CON
LDA V2                   ; SPEED
CON
STA P1:#4                ; RB_OUT
CON
LDA V3                   ; G
CON
STA P1:#5                ; RB_OUT
MRD
RES DIR                  ; OUT Push ON=+Dir
CON
RES PUSH                 ; OUT Push
MPP
TMH @0 #3
CON
AND @T0
JMP @R001
STG @R001
MPS
LD @R014                 ; RB Status OK
ANB ALM                  ; IN ALM
BANB P1:#7 #2            ; RB_OUT
ANL
SET CSTR                 ; OUT START
CON
JMP @R002                ; ﾛﾎﾞｯﾄ_START_ON
MRD
ANB @R014                ; RB Status OK
DW #22 @DM0
CON
JMP @R012                ; 終了処理
MPP
AND ALM                  ; IN ALM
JMP @R011                ; Alarm
STG @R002                ; ﾛﾎﾞｯﾄ_START_ON
MPS
BLDB P1:#7 #2            ; RB_OUT
ANB PSFL                 ; IN PSFL
ANB PEND                 ; IN PEND
ANB ALM                  ; IN ALM
ANB @T2
ANL
RES CSTR                 ; OUT START
CON
JMP @R003                ; ﾛﾎﾞｯﾄ_位置決完了待ち
MRD
LD PSFL                  ; IN PSFL
AND MOVE                 ; IN MOVE
ANB ALM                  ; IN ALM
ANB @T2
ANL
TMH @4 #10
CON
AND @T4
RES CSTR                 ; OUT START
CON
JMP @R003                ; ﾛﾎﾞｯﾄ_位置決完了待ち
MRD
AND ALM                  ; IN ALM
RES CSTR                 ; OUT START
CON
JMP @R011                ; Alarm
MPP
BANB P1:#7 #2            ; RB_OUT
TMR @2 #900
CON
AND @T2
RES CSTR                 ; OUT START
CON
DW #21 @DM0
CON
JMP @R012                ; 終了処理
STG @R003                ; ﾛﾎﾞｯﾄ_位置決完了待ち
MPS
LDB ALM                  ; IN ALM
ANB @T3
AND PEND                 ; IN PEND
ANL
DW #20 @DM0
CON
JMP @R012                ; 終了処理
MRD
AND ALM                  ; IN ALM
JMP @R011                ; Alarm
MPP
BANB P1:#7 #2            ; RB_OUT
TMR @3 #900
CON
AND @T3
DW #21 @DM0
CON
JMP @R012                ; 終了処理
;Alarm code Stroge
STG @R011                ; Alarm
LDA P0:#6                ; RB_IN
CON
STA @DM0
CON
JMP @R012                ; 終了処理
;終了処理
STG @R012                ; 終了処理
LDA @DM0
CON
STA P2                   ; C_CODE
CON
MPS
ZRES @R001 @R100         ; ﾏｸﾛ異常発生
CON
ENDS
MPP
MEND
END
ENDH
