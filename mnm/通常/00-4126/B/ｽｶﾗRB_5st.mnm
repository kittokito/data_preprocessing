DEVICE:53
;MODULE:ｽｶﾗRB_5st
;MODULE_TYPE:0
;<h1/>入力
LD MR714                 ; PLC全ﾕﾆｯﾄ正常
MPS
AND R50800               ; 5stRB 非常停止入力状態
OUT MR52000              ; 5stRB 非常停止入力状態
MRD
AND R50801               ; 5stRB CPU OK状態
OUT MR52001              ; 5stRB CPU OK状態
MRD
AND R50802               ; 5stRB ｻｰﾎﾞON
OUT MR52002              ; 5stRB ｻｰﾎﾞON
MRD
AND R50803               ; 5stRB ｱﾗｰﾑ
OUT MR52003              ; 5stRB ｱﾗｰﾑ
MRD
ANB R50808               ; 5stRB 自動ﾓｰﾄﾞ
OUT MR52010              ; 5stRB 手動ﾓｰﾄﾞ
MRD
ANB R50809               ; 5stRB 原点復帰完了
OUT MR52011              ; 5st 原点復帰未完
MRD
AND R50907               ; 5stRB ﾊﾞｯﾃﾘｰｱﾗｰﾑ
OUT MR52013              ; 5stRB ﾊﾞｯﾃﾘｰｱﾗｰﾑ
MPP
AND R50903               ; 5st ｱｰﾑ手系正常
OUT MR52014              ; 5st ｱｰﾑ手系正常
LD T11                   ; ﾄﾞﾗｲﾊﾞ電源ON確認DLY
AND R1005                ; 5stRB 主電源投入準備完了
ANB B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
AND @T5                  ; 5stRB ｲﾝﾀｰﾛｯｸOFF
OUT MR52015              ; 5stRB 主電源投入準備完了
;ﾃｨｰﾁﾝｸﾞ
LDP B0A1                 ; TP5stRB ﾃｨｰﾁﾝｸﾞPB
ANB B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
LD B10A1                 ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
ANPB B0A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPB
ORL
AND MR1200               ; 手動ﾓｰﾄﾞ
OUT B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
TMR @4 #10               ; 5stRB ｲﾝﾀｰﾛｯｸOFF
LDB B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
TMR @5 #10               ; 5stRB ｲﾝﾀｰﾛｯｸOFF
;異常
LD T1520                 ; 5stRB ｻｰﾎﾞONﾀｲﾑｵｰﾊﾞｰ
OUT MR52100              ; 5st RB ｻｰﾎﾞONﾀｲﾑｵｰﾊﾞｰ
LD R50811                ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
ANB MR52207              ; 5stRB ｽﾀｰﾄ指令
LDB R50811               ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
AND MR52207              ; 5stRB ｽﾀｰﾄ指令
ORL
ANB B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
TMR #1521 #10            ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行異常
OUT MR52101              ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行異常
LD T1522                 ; 5stRB 動作ﾀｲﾑｵｰﾊﾞｰ
OUT MR52102              ; 5st RB 動作ﾀｲﾑｵｰﾊﾞｰ
;ｲﾆｼｬﾙﾃﾞｰﾀ処理
LD R51508                ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理要求
OR MR52200               ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理
AND MR714                ; PLC全ﾕﾆｯﾄ正常
ANB MR52201              ; 5stRB ﾘﾓｰﾄREADY
OUT MR52200              ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理
LD R51511                ; 5stRB ﾘﾓｰﾄREADY
OUT MR52201              ; 5stRB ﾘﾓｰﾄREADY
;ｺﾝﾄﾛｰﾗ正常
LD MR52001               ; 5stRB CPU OK状態
ANB MR52003              ; 5stRB ｱﾗｰﾑ
ANB MR52013              ; 5stRB ﾊﾞｯﾃﾘｰｱﾗｰﾑ
ANB MR52011              ; 5st 原点復帰未完
ANB MR52010              ; 5stRB 手動ﾓｰﾄﾞ
ANB MR52100              ; 5st RB ｻｰﾎﾞONﾀｲﾑｵｰﾊﾞｰ
ANB MR52101              ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行異常
ANB MR52102              ; 5st RB 動作ﾀｲﾑｵｰﾊﾞｰ
AND MR52014              ; 5st ｱｰﾑ手系正常
OUT MR52202              ; 5stRB ｺﾝﾄﾛｰﾗ正常
;ｺﾝﾄﾛｰﾗ準備完了
LD MR52015               ; 5stRB 主電源投入準備完了
ANB MR52000              ; 5stRB 非常停止入力状態
AND MR52002              ; 5stRB ｻｰﾎﾞON
AND @T3                  ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
OUT MR52203              ; 5stRB 準備完了
;ｻｰﾎﾞON
LD MR52015               ; 5stRB 主電源投入準備完了
ANB MR52000              ; 5stRB 非常停止入力状態
AND MR52001              ; 5stRB CPU OK状態
ANB MR52013              ; 5stRB ﾊﾞｯﾃﾘｰｱﾗｰﾑ
ANB MR52002              ; 5stRB ｻｰﾎﾞON
ANB LR3111               ; <A507>5st ｽｶﾗﾛﾎﾞｯﾄ 位置異常
OUT MR52204              ; 5stRB ｻｰﾎﾞON
TMR #1520 #10            ; 5stRB ｻｰﾎﾞONﾀｲﾑｵｰﾊﾞｰ
;
LD MR52015               ; 5stRB 主電源投入準備完了
MPS
AND MR52002              ; 5stRB ｻｰﾎﾞON
TMR @0 #2                ; 5stRB ｻｰﾎﾞON確認
MRD
AND R50808               ; 5stRB 自動ﾓｰﾄﾞ
TMR @1 #2                ; 5stRB 自動ﾓｰﾄﾞ確認
MRD
LD R50812                ; 2stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
OR @T2                   ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
ANL
TMR @2 #5                ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
MPP
LD R50811                ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
AND @T2                  ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
ANL
TMR @3 #20               ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
;自動ﾓｰﾄﾞ切替
LD MR52015               ; 5stRB 主電源投入準備完了
AND @T0                  ; 5stRB ｻｰﾎﾞON確認
ANB R50808               ; 5stRB 自動ﾓｰﾄﾞ
OUT MR52205              ; 5stRB 自動ﾓｰﾄﾞ切替指令
;ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ→ｽﾀｰﾄ
LD @T1                   ; 5stRB 自動ﾓｰﾄﾞ確認
AND @T0                  ; 5stRB ｻｰﾎﾞON確認
ANB @T2                  ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
OUT MR52206              ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ指令
LDP @T2                  ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
OR MR52207               ; 5stRB ｽﾀｰﾄ指令
AND @T2                  ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ状態
AND @T0                  ; 5stRB ｻｰﾎﾞON確認
OUT MR52207              ; 5stRB ｽﾀｰﾄ指令
;<h1/>手動
LD MR2000                ; 手動ﾓｰﾄﾞ 操作可能
MC
;ｽｶﾗRB 横移動
LD CR2002                ; 常時ON
ANP B0500                ; TP5stRB 原位置 移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
OUT MR21300              ; 5stRB 原位置 移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
LD CR2002                ; 常時ON
MPS
LDP B0501                ; TP5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21301              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MRD
LDP B0502                ; TP5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21302              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MRD
LDP B0503                ; TP5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21303              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MPP
LDP B0504                ; TP5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21304              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
LD CR2002                ; 常時ON
MPS
LDP B0506                ; TP5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21306              ; 5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MPP
LDP B0507                ; TP5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21307              ; 5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
LD CR2002                ; 常時ON
MPS
LDP B0508                ; TP5stRB 送りCVﾋﾉｳﾗ組付移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21308              ; 5stRB 送りCVﾋﾉｳﾗ組付移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MPP
LDP B0509                ; TP5stRB 送りCVｼﾞｼﾝ組付移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21309              ; 5stRB 送りCVｼﾞｼﾝ組付移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
;ｽｶﾗRB 上下移動
LD CR2002                ; 常時ON
MPS
LDP B050E                ; TP5stRB 下降 移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21314              ; 5stRB 下降 移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MPP
LDP B050F                ; TP5stRB 上昇 移動PB
AND= EM1208 #0           ; 5stRB 手動操作 2重起動IL
ANL
OUT MR21315              ; 5stRB 上昇 移動_Man
@MOV #1 EM1208           ; 5stRB 手動操作 2重起動IL
MCR
LD= B0500 #0             ; TP5stRB 原位置 移動PB
ANB MR52900              ; 5stRB 移動中
MOV #0 EM1208            ; 5stRB 手動操作 2重起動IL
;<h1/>動作IL
LDB LR407                ; <A071>5st ｽｶﾗﾛﾎﾞｯﾄｺﾝﾄﾛｰﾗ異常
ANB LR3110               ; <A506>5st ｽｶﾗﾛﾎﾞｯﾄ補正値 範囲外
AND MR52014              ; 5st ｱｰﾑ手系正常
AND MR52915              ; 5stRB 停止中
OUT MR52115              ; 5stRB 動作共通IL
LD MR52115               ; 5stRB 動作共通IL
MPS
AND MR52815              ; 5stRB 上昇端
OUT MR52300              ; 5stRB 横移動可
MRD
LD MR52815               ; 5stRB 上昇端
OR R50912                ; 5stRB 領域判定5[ｶﾒﾗ領域]
ANL
OUT MR52315              ; 5stRB ｶﾒﾗ位置移動可
MRD
LD MR52701               ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 上昇端
AND T360                 ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)ｽﾗｲﾀﾞ 出端
ANL
OUT MR52301              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降可
MRD
LD MR52702               ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 上昇端
AND T370                 ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)ｽﾗｲﾀﾞ 出端
AND T361                 ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)ｽﾗｲﾀﾞ 戻端
ANL
OUT MR52302              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降可
MRD
LD MR52703               ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 上昇端
AND T380                 ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)ｽﾗｲﾀﾞ 出端
ANL
OUT MR52303              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降可
MRD
LD MR52704               ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 上昇端
AND T390                 ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)ｽﾗｲﾀﾞ 出端
AND T381                 ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)ｽﾗｲﾀﾞ 戻端
ANL
OUT MR52304              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降可
MRD
LD MR52708               ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
AND T401                 ; 5st 送りCVｽﾄｯﾊﾟ 上昇端
LD T412                  ; 5st 送りCVｽﾄｯﾊﾟ 有確認
AND MR21002              ; 5st 送りCV L/R位置決め 出端
OR T413                  ; 5st 送りCVｽﾄｯﾊﾟ 無確認
ANL
AND T407                 ; 5st 送りCVﾌﾝｼﾝ位置決め 戻端
ANL
OUT MR52308              ; 5stRB ﾋﾉｳﾗｷﾞﾔCV組付下降可
MRD
LD MR52709               ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
AND T401                 ; 5st 送りCVｽﾄｯﾊﾟ 上昇端
LD T412                  ; 5st 送りCVｽﾄｯﾊﾟ 有確認
AND MR21002              ; 5st 送りCV L/R位置決め 出端
OR T413                  ; 5st 送りCVｽﾄｯﾊﾟ 無確認
ANL
AND T407                 ; 5st 送りCVﾌﾝｼﾝ位置決め 戻端
ANL
OUT MR52309              ; 5stRB ｼﾞｼﾝｷﾞﾔCV組付下降可
MPP
OUT MR52215              ; 5stRB 上昇 動作IL
LDB MR52301              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降可
ANB MR52302              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降可
ANB MR52303              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降可
ANB MR52304              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降可
ANB MR52308              ; 5stRB ﾋﾉｳﾗｷﾞﾔCV組付下降可
ANB MR52309              ; 5stRB ｼﾞｼﾝｷﾞﾔCV組付下降可
INV
OUT MR52214              ; 5stRB 下降 動作IL
;<h1/>移動補助
;原位置
LDP MR21300              ; 5stRB 原位置 移動_Man
AND MR52300              ; 5stRB 横移動可
ORP R74301               ; 5stRB 原位置 移動_1ｻｲｸﾙ
ORP MR24413              ; 5stRB ﾋﾉｳﾗｷ組付後 待機位置_AUT
ORP MR24513              ; 5stRB ｼﾞｼﾝｷﾞﾔ組付後 待機位置_AUT
ORP MR25111              ; 5stRB 待機位置へ移動_AUT
OUT MR52500              ; 5stRB 原位置 移動 補助
@MOV #1 DM5200           ; 5stRB 移動No
;ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R74511               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(上)下降_1ｻｲｸﾙ
AND MR52301              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降可
OUT MR52401              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降 補助
@MOV #50 DM5200          ; 5stRB 移動No
LDP MR21301              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動_Man
ORP R74507               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(上)上昇_1ｻｲｸﾙ
AND MR52300              ; 5stRB 横移動可
LDP MR21315              ; 5stRB 上昇 移動_Man
AND MR52601              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 下降端
ORL
ORP R74611               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(上)上昇_1ｻｲｸﾙ
OUT MR52501              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動 補助
@MOV #51 DM5200          ; 5stRB 移動No
;ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R74512               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(下)下降_1ｻｲｸﾙ
AND MR52302              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降可
OUT MR52402              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降 補助
@MOV #60 DM5200          ; 5stRB 移動No
LDP MR21302              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動_Man
ORP R74508               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(下)上昇_1ｻｲｸﾙ
AND MR52300              ; 5stRB 横移動可
LDP MR21315              ; 5stRB 上昇 移動_Man
AND MR52602              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 下降端
ORL
ORP R74612               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(下)上昇_1ｻｲｸﾙ
OUT MR52502              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動 補助
@MOV #61 DM5200          ; 5stRB 移動No
;ｼﾞｼﾝﾄﾚｲ(上)
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R74513               ; 5stRB ｼﾞｼﾝｷﾞﾔ(上)下降_1ｻｲｸﾙ
AND MR52303              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降可
OUT MR52403              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降 補助
@MOV #70 DM5200          ; 5stRB 移動No
LDP MR21303              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動_Man
ORP R74509               ; 5stRB ｼﾞｼﾝｷﾞﾔ(上)上昇_1ｻｲｸﾙ
AND MR52300              ; 5stRB 横移動可
LDP MR21315              ; 5stRB 上昇 移動_Man
AND MR52603              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 下降端
ORL
ORP R74613               ; 5stRB ｼﾞｼﾝｷﾞﾔ(上)上昇_1ｻｲｸﾙ
OUT MR52503              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動 補助
@MOV #71 DM5200          ; 5stRB 移動No
;ｼﾞｼﾝﾄﾚｲ(下)
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R74514               ; 5stRB ｼﾞｼﾝｷﾞﾔ(下)下降_1ｻｲｸﾙ
AND MR52304              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降可
OUT MR52404              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降 補助
@MOV #80 DM5200          ; 5stRB 移動No
LDP MR21304              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動_Man
ORP R74510               ; 5stRB ｼﾞｼﾝｷﾞﾔ(下)上昇_1ｻｲｸﾙ
AND MR52300              ; 5stRB 横移動可
LDP MR21315              ; 5stRB 上昇 移動_Man
AND MR52604              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 下降端
ORL
ORP R74614               ; 5stRB ｼﾞｼﾝｷﾞﾔ(下)上昇_1ｻｲｸﾙ
OUT MR52504              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動 補助
@MOV #81 DM5200          ; 5stRB 移動No
;ﾋﾉｳﾗｷﾞﾔ ｶﾒﾗ
LDP MR21306              ; 5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動_Man
ORP R74911               ; 5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ位置 移動_1ｻｲｸﾙ
ORP R75011               ; 5stRB ﾋﾉｳﾗ補正後ｶﾒﾗ移動_1ｻｲｸﾙ
LDP MR57609              ; ｶﾒﾗ2ｷｬﾘﾌﾞ継続→ｽｶﾗRB移動_画像
AND MR52706              ; 5stRB ﾋﾉｳﾗｷﾞｱｶﾒﾗ 上昇端
ORL
AND MR52315              ; 5stRB ｶﾒﾗ位置移動可
OUT MR52506              ; 5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動 補助
@MOV #15 DM5200          ; 5stRB 移動No
;ｼﾞｼﾝｷﾞﾔ ｶﾒﾗ
LDP MR21307              ; 5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動_Man
ORP R74912               ; 5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ位置 移動_1ｻｲｸﾙ
ORP R75012               ; 5stRB ｼﾞｼﾝ補正後ｶﾒﾗ移動_1ｻｲｸﾙ
LDP MR57609              ; ｶﾒﾗ2ｷｬﾘﾌﾞ継続→ｽｶﾗRB移動_画像
AND MR52707              ; 5stRB ｼﾞｼﾝｷﾞｱｶﾒﾗ 上昇端
ORL
AND MR52315              ; 5stRB ｶﾒﾗ位置移動可
OUT MR52507              ; 5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動 補助
@MOV #25 DM5200          ; 5stRB 移動No
;送りCV　ﾋﾉｳﾗｷﾞﾔ組付
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R75400               ; 5stRB ﾋﾉｳﾗｷﾞﾔ組込 下降_1ｻｲｸﾙ
AND MR52308              ; 5stRB ﾋﾉｳﾗｷﾞﾔCV組付下降可
OUT MR52408              ; 5stRB 送りCVﾋﾉｳﾗ組付下降 補助
@MOV #10 DM5200          ; 5stRB 移動No
LDP MR21308              ; 5stRB 送りCVﾋﾉｳﾗ組付移動_Man
ORP R75308               ; 5stRB ﾋﾉｳﾗ組込 移動(OFS無)_1ｻｲｸﾙ
ORP R75311               ; 5stRB ﾋﾉｳﾗ組込 上昇(OFS有)_1ｻｲｸﾙ
AND MR52300              ; 5stRB 横移動可
LDP MR21315              ; 5stRB 上昇 移動_Man
ORP R75408               ; 5stRB ﾋﾉｳﾗｷﾞﾔ組込 上昇_1ｻｲｸﾙ
AND MR52608              ; 5stRB 送りCVﾋﾉｳﾗ組込 下降端
ORL
OUT MR52508              ; 5stRB 送りCVﾋﾉｳﾗ組付移動 補助
@MOV #11 DM5200          ; 5stRB 移動No
;送りCV　ｼﾞｼﾝｷﾞﾔ組付
LDP MR21314              ; 5stRB 下降 移動_Man
ORP R75401               ; 5stRB ｼﾞｼﾝｷﾞﾔ組込 下降_1ｻｲｸﾙ
AND MR52309              ; 5stRB ｼﾞｼﾝｷﾞﾔCV組付下降可
OUT MR52409              ; 5stRB 送りCVｼﾞｼﾝ組付下降 補助
@MOV #20 DM5200          ; 5stRB 移動No
LDP MR21309              ; 5stRB 送りCVｼﾞｼﾝ組付移動_Man
AND MR52300              ; 5stRB 横移動可
LDP R75312               ; 5stRB ｼﾞｼﾝ組込 移動(OFS無)_1ｻｲｸﾙ
ORP R75315               ; 5stRB ｼﾞｼﾝ組込 上昇(OFS有)_1ｻｲｸﾙ
LD MR52300               ; 5stRB 横移動可
OR R60906                ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
ANL
ORL
LDP MR21315              ; 5stRB 上昇 移動_Man
ORP R75409               ; 5stRB ｼﾞｼﾝｷﾞﾔ組込 上昇_1ｻｲｸﾙ
AND MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
ORL
OUT MR52509              ; 5stRB 送りCVｼﾞｼﾝ組付移動 補助
@MOV #21 DM5200          ; 5stRB 移動No
;上昇
LDP MR21315              ; 5stRB 上昇 移動_Man
ANB MR52810              ; 5stRB 下降位置停止まとめ
ORP R75306               ; 5stRB ｷﾞﾔ組込 上昇_1ｻｲｸﾙ
ORP R75005               ; 5stRB ｶﾒﾗ位置上昇_1ｻｲｸﾙ
AND MR52215              ; 5stRB 上昇 動作IL
OUT MR52515              ; 5stRB 上昇 補助
@MOV #2 DM5200           ; 5stRB 移動No
;移動まとめ
LDB MR52401              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降 補助
ANB MR52402              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降 補助
ANB MR52403              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降 補助
ANB MR52404              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降 補助
ANB MR52408              ; 5stRB 送りCVﾋﾉｳﾗ組付下降 補助
ANB MR52409              ; 5stRB 送りCVｼﾞｼﾝ組付下降 補助
ANB MR52500              ; 5stRB 原位置 移動 補助
ANB MR52501              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動 補助
ANB MR52502              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動 補助
ANB MR52503              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動 補助
ANB MR52504              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動 補助
ANB MR52506              ; 5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動 補助
ANB MR52507              ; 5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動 補助
ANB MR52508              ; 5stRB 送りCVﾋﾉｳﾗ組付移動 補助
ANB MR52509              ; 5stRB 送りCVｼﾞｼﾝ組付移動 補助
ANB MR52515              ; 5stRB 上昇 補助
INV
ANB LR407                ; <A071>5st ｽｶﾗﾛﾎﾞｯﾄｺﾝﾄﾛｰﾗ異常
ANB LR3110               ; <A506>5st ｽｶﾗﾛﾎﾞｯﾄ補正値 範囲外
OUT MR52901              ; 5stRB 移動開始まとめ
;<h1/>品種設定 補正値
;CV組込
LD CR2002                ; 常時ON
MPS
FMOV #0 DM5230 #8        ; 5stRB品種 X補正値(CV)
MPP
LD<= #1 DM1281           ; 5stｽﾄｯﾊﾟﾊﾟﾚｯﾄNo
AND<= DM1281 #20         ; 5stｽﾄｯﾊﾟﾊﾟﾚｯﾄNo
ANL
CAL- DM1281 #1 Z4        ; 5stｽﾄｯﾊﾟﾊﾟﾚｯﾄNo
CAL* Z4 #5 Z4
MPS
LDA.S ZF600:Z4           ; 設定 治具1組込X値
CON
EXT.S
CON
STA.L DM5230             ; 5stRB品種 X補正値(CV)
MRD
LDA.S ZF601:Z4           ; 設定 治具1組込Y値
CON
EXT.S
CON
STA.L DM5232             ; 5stRB品種 Y補正値(CV)
MRD
LDA.S ZF602:Z4           ; 設定 治具1組込θ値
CON
EXT.S
CON
STA.L DM5234             ; 5stRB品種 θ補正値(CV)
MPP
LDA.S ZF603:Z4           ; 設定 治具1組込Z値
CON
EXT.S
CON
STA.L DM5236             ; 5stRB品種 Z補正値(CV)
;<h1/>補正値
LD CR2002                ; 常時ON
FMOV #0 DM5220 #8        ; 5stRB X方向補正値
MPS
LD MR1200                ; 手動ﾓｰﾄﾞ
ANB MR57601              ; 画像処理  ｶﾒﾗ2ｷｬﾘﾌﾞﾚｰｼｮﾝ
ANL
MPS
LD B051F                 ; TP 5stRB 補正有無[ON:有]
ANB MR20501              ; 5st ｽｶﾗRB 1ｻｲｸﾙ起動中
ANL
OUT MR52911              ; 5stRB 補正値TP入力値
MPP
LDB B051F                ; TP 5stRB 補正有無[ON:有]
AND MR52901              ; 5stRB 移動開始まとめ
ANL
@MOV #0 EM1201           ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
MRD
AND MR52911              ; 5stRB 補正値TP入力値
MOV.L W1200 DM5220       ; TP5stRB X方向補正値  5stRB X方向補正値
MOV.L W1202 DM5222       ; TP5stRB Y方向補正値  5stRB Y方向補正値
MOV.L W1204 DM5224       ; TP5stRB θ方向補正値  5stRB θ方向補正値
MOV.L W1206 DM5226       ; TP5stRB Z方向補正値  5stRB Z方向補正値
MRD
ANB MR52911              ; 5stRB 補正値TP入力値
MPS
LD= EM1201 #1            ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
OR= EM1201 #2            ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
ANL
CAL+.L W0820 DM5220 DM5220;ｶﾒﾗ2 ﾋﾉｳﾗｷﾞﾔX方向ｽﾞﾚ量 積算値  5stRB X方向補正値  5stRB X方向補正値
CAL+.L W0822 DM5222 DM5222;ｶﾒﾗ2 ﾋﾉｳﾗｷﾞﾔY方向ｽﾞﾚ量 積算値  5stRB Y方向補正値  5stRB Y方向補正値
CAL+.L W0824 DM5224 DM5224;ｶﾒﾗ2 ﾋﾉｳﾗｷﾞﾔθ方向ｽﾞﾚ量 積算値  5stRB θ方向補正値  5stRB θ方向補正値
CAL+.L +0 DM5226 DM5226  ; 5stRB Z方向補正値  5stRB Z方向補正値
MRD
LD= EM1201 #11           ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
OR= EM1201 #12           ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
ANL
CAL+.L W0830 DM5220 DM5220;ｶﾒﾗ2 ｼﾞｼﾝｷﾞﾔX方向ｽﾞﾚ量 積算値  5stRB X方向補正値  5stRB X方向補正値
CAL+.L W0832 DM5222 DM5222;ｶﾒﾗ2 ｼﾞｼﾝｷﾞﾔY方向ｽﾞﾚ量 積算値  5stRB Y方向補正値  5stRB Y方向補正値
CAL+.L W0834 DM5224 DM5224;ｶﾒﾗ2 ｼﾞｼﾝｷﾞﾔθ方向ｽﾞﾚ量 積算値  5stRB θ方向補正値  5stRB θ方向補正値
CAL+.L +0 DM5226 DM5226  ; 5stRB Z方向補正値  5stRB Z方向補正値
MPP
LD= EM1201 #2            ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
OR= EM1201 #12           ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
ANB LR10005              ; 5stｱﾗｲﾒﾝﾄ_検証中
ANL
CAL+.L DM5230 DM5220 DM5220;5stRB品種 X補正値(CV)  5stRB X方向補正値  5stRB X方向補正値
CAL+.L DM5232 DM5222 DM5222;5stRB品種 Y補正値(CV)  5stRB Y方向補正値  5stRB Y方向補正値
CAL+.L DM5234 DM5224 DM5224;5stRB品種 θ補正値(CV)  5stRB θ方向補正値  5stRB θ方向補正値
CAL+.L DM5236 DM5226 DM5226;5stRB品種 Z補正値(CV)  5stRB Z方向補正値  5stRB Z方向補正値
MRD
MOV.L DM5220 DM13020     ; 5stRB X方向補正値  5stRB 補正量X
MRD
MOV.L DM5222 DM13022     ; 5stRB Y方向補正値  5stRB 補正量Y
MRD
MOV.L DM5224 DM13024     ; 5stRB θ方向補正値  5stRB 補正量θ
MPP
MOV.L DM5226 DM13026     ; 5stRB Z方向補正値  5stRB 補正量Z
LD MR52911               ; 5stRB 補正値TP入力値
OR<> EM1201 #0           ; 5stRB 補正[0無/1ｶﾒﾗ/2CV]
OUT MR52111              ; 5stRB 補正値有
LD= DM13020 #0           ; 5stRB 補正量X
AND= DM13022 #0          ; 5stRB 補正量Y
AND= DM13024 #0          ; 5stRB 補正量θ
AND= DM13026 #0          ; 5stRB 補正量Z
OUT MR52112              ; 5stRB 補正値ｾﾞﾛ
LD<=.L DM7210 DM13020    ; 5stRB X軸補正量 下限値  5stRB 補正量X
AND<=.L DM13020 DM7212   ; 5stRB 補正量X  5stRB X軸補正量 上限値
AND<=.L DM7214 DM13022   ; 5stRB Y軸補正量 下限値  5stRB 補正量Y
AND<=.L DM13022 DM7216   ; 5stRB 補正量Y  5stRB Y軸補正量 上限値
AND<=.L DM7218 DM13024   ; 5stRB θ軸補正量 下限値  5stRB 補正量θ
AND<=.L DM13024 DM7220   ; 5stRB 補正量θ  5stRB θ軸補正量 上限値
LD MR57601               ; 画像処理  ｶﾒﾗ2ｷｬﾘﾌﾞﾚｰｼｮﾝ
AND MR1200               ; 手動ﾓｰﾄﾞ
ORL
AND<=.L -1000 DM13026    ; 5stRB 補正量Z
AND<=.L DM13026 +1000    ; 5stRB 補正量Z
LDB MR52111              ; 5stRB 補正値有
AND MR52112              ; 5stRB 補正値ｾﾞﾛ
ORL
OUT MR52110              ; 5stRB 補正値範囲内
;<h1/>ﾊﾟﾚｯﾄ番号
LDB MR52401              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)下降 補助
ANB MR52402              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)下降 補助
ANB MR52501              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動 補助
ANB MR52502              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動 補助
INV
AND MR52901              ; 5stRB 移動開始まとめ
OUT MR52905              ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ移動まとめ
LDB MR52403              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)下降 補助
ANB MR52404              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)下降 補助
ANB MR52503              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動 補助
ANB MR52504              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動 補助
INV
AND MR52901              ; 5stRB 移動開始まとめ
OUT MR52906              ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ移動まとめ
LDB MR4000               ; 自動運転
MPS
LDA W1210                ; TP5st ﾋﾉｳﾗﾄﾚｲ縦列No(手動)
CON
SUB #1
CON
MUL DM5211               ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ横数
CON
ADD W1211                ; TP5st ﾋﾉｳﾗﾄﾚｲ横列No(手動)
CON
STA DM5205               ; 5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助
MPP
LDA W1214                ; TP5st ｼﾞｼﾝﾄﾚｲ縦列No(手動)
CON
SUB #1
CON
MUL DM5215               ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ横数
CON
ADD W1215                ; TP5st ｼﾞｼﾝﾄﾚｲ横列No(手動)
CON
STA DM5206               ; 5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助
LD CR2002                ; 常時ON
MPS
LDP MR24211              ; 5stRB ﾋﾉｳﾗｷﾞﾔ(上)取出1ｻｲｸﾙ_AUT
LD MR24211               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(上)取出1ｻｲｸﾙ_AUT
ANP LR25004              ; 5stRBﾄﾚｲ 次取出選択_Mem
ORL
ANL
MOV DM1254 DM5205        ; 5st ﾋﾉｳﾗｷﾞﾔ(上)ﾄﾚｲ取出番号  5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助
MPP
LDP MR24212              ; 5stRB ﾋﾉｳﾗｷﾞﾔ(下)取出1ｻｲｸﾙ_AUT
LD MR24212               ; 5stRB ﾋﾉｳﾗｷﾞﾔ(下)取出1ｻｲｸﾙ_AUT
ANP LR25004              ; 5stRBﾄﾚｲ 次取出選択_Mem
ORL
ANL
MOV DM1255 DM5205        ; 5st ﾋﾉｳﾗｷﾞﾔ(下)ﾄﾚｲ取出番号  5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助
LD CR2002                ; 常時ON
MPS
LDP MR24311              ; 5stRB ｼﾞｼﾝｷﾞﾔ(上)取出1ｻｲｸﾙ_AUT
LD MR24311               ; 5stRB ｼﾞｼﾝｷﾞﾔ(上)取出1ｻｲｸﾙ_AUT
ANP LR25004              ; 5stRBﾄﾚｲ 次取出選択_Mem
ORL
ANL
MOV DM1256 DM5206        ; 5st ｼﾞｼﾝｷﾞﾔ(上)ﾄﾚｲ取出番号  5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助
MPP
LDP MR24312              ; 5stRB ｼﾞｼﾝｷﾞﾔ(下)取出1ｻｲｸﾙ_AUT
LD MR24312               ; 5stRB ｼﾞｼﾝｷﾞﾔ(下)取出1ｻｲｸﾙ_AUT
ANP LR25004              ; 5stRBﾄﾚｲ 次取出選択_Mem
ORL
ANL
MOV DM1257 DM5206        ; 5st ｼﾞｼﾝｷﾞﾔ(下)ﾄﾚｲ取出番号  5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助
LD CR2002                ; 常時ON
MPS
LDB MR4000               ; 自動運転
AND<= #1 DM5205          ; 5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助
AND<= DM5205 DM5212      ; 5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助  5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ収納数
LD MR4000                ; 自動運転
LD<= #1 DM1254           ; 5st ﾋﾉｳﾗｷﾞﾔ(上)ﾄﾚｲ取出番号
AND<= DM1254 DM5212      ; 5st ﾋﾉｳﾗｷﾞﾔ(上)ﾄﾚｲ取出番号  5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ収納数
LD= DM1276 #1            ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ取出[0無/1上/2下
OR= DM1276 #11           ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ取出[0無/1上/2下
ANL
LD<= #1 DM1255           ; 5st ﾋﾉｳﾗｷﾞﾔ(下)ﾄﾚｲ取出番号
AND<= DM1255 DM5212      ; 5st ﾋﾉｳﾗｷﾞﾔ(下)ﾄﾚｲ取出番号  5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ収納数
LD= DM1276 #2            ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ取出[0無/1上/2下
OR= DM1276 #12           ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ取出[0無/1上/2下
ANL
ORL
ANL
ORL
ANL
OUT MR52210              ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲNo 範囲内
MRD
LDB MR4000               ; 自動運転
AND<= #1 DM5206          ; 5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助
AND<= DM5206 DM5216      ; 5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助  5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ収納数
LD MR4000                ; 自動運転
LD<= #1 DM1256           ; 5st ｼﾞｼﾝｷﾞﾔ(上)ﾄﾚｲ取出番号
AND<= DM1256 DM5216      ; 5st ｼﾞｼﾝｷﾞﾔ(上)ﾄﾚｲ取出番号  5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ収納数
LD= DM1277 #1            ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ取出[0無/1上/2下
OR= DM1277 #11           ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ取出[0無/1上/2下
ANL
LD<= #1 DM1257           ; 5st ｼﾞｼﾝｷﾞﾔ(下)ﾄﾚｲ取出番号
AND<= DM1257 DM5216      ; 5st ｼﾞｼﾝｷﾞﾔ(下)ﾄﾚｲ取出番号  5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ収納数
LD= DM1277 #2            ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ取出[0無/1上/2下
OR= DM1277 #12           ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ取出[0無/1上/2下
ANL
ORL
ANL
ORL
ANL
OUT MR52211              ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲNo 範囲内
MRD
LD MR52210               ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲNo 範囲内
AND MR52905              ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ移動まとめ
ANL
MOV DM5205 DM13028       ; 5stRB ﾋﾉｳﾗｷﾞﾔ ﾄﾚｲNo補助  5stRB ﾄﾚｲNo
MPP
LD MR52211               ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲNo 範囲内
AND MR52906              ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ移動まとめ
ANL
MOV DM5206 DM13028       ; 5stRB ｼﾞｼﾝｷﾞﾔ ﾄﾚｲNo補助  5stRB ﾄﾚｲNo
;ﾄﾚｲ数設定
LD CR2002                ; 常時ON
MOV #10 DM5210           ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ縦数
MOV #4 DM5211            ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ横数
MOV #40 DM5212           ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ収納数
LD CR2002                ; 常時ON
MOV #10 DM5214           ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ縦数
MOV #4 DM5215            ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ横数
MOV #40 DM5216           ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ収納数
LD MR52905               ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ移動まとめ
AND MR52210              ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲNo 範囲内
LD MR52906               ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ移動まとめ
AND MR52211              ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲNo 範囲内
ORL
LDB MR52905              ; 5st ﾋﾉｳﾗｷﾞﾔﾄﾚｲ移動まとめ
ANB MR52906              ; 5st ｼﾞｼﾝｷﾞﾔﾄﾚｲ移動まとめ
ORL
AND MR52110              ; 5stRB 補正値範囲内
OUT MR52908              ; 5stRB 移動可
;<h1/>異常
LD R50908                ; 5stRB 領域判定1[原位置]
LD MR10902               ; 5stRB 停止位置正常取得
AND MR10912              ; 5stRB 停止位置範囲内
ORL
OUT MR52008              ; 5stRB 位置正常
LD MR52008               ; 5stRB 位置正常
AND MR907                ; 異常ﾘｾｯﾄ
OUT MR52009              ; 5stRB 位置異常ﾘｾｯﾄ
LDB MR52008              ; 5stRB 位置正常
ANP @T3                  ; 5stRB ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ実行中
LD LR3111                ; <A507>5st ｽｶﾗﾛﾎﾞｯﾄ 位置異常
ANB MR52009              ; 5stRB 位置異常ﾘｾｯﾄ
ORL
ANB LR10511              ; 位置異常無効
OUT LR3111               ; <A507>5st ｽｶﾗﾛﾎﾞｯﾄ 位置異常
;補正値範囲外
LDP MR52901              ; 5stRB 移動開始まとめ
ANB MR52110              ; 5stRB 補正値範囲内
LD LR3110                ; <A506>5st ｽｶﾗﾛﾎﾞｯﾄ補正値 範囲外
ANB MR907                ; 異常ﾘｾｯﾄ
ORL
OUT LR3110               ; <A506>5st ｽｶﾗﾛﾎﾞｯﾄ補正値 範囲外
;<h1/>軸移動
LDP MR52901              ; 5stRB 移動開始まとめ
AND MR52908              ; 5stRB 移動可
OR MR52900               ; 5stRB 移動中
AND MR52203              ; 5stRB 準備完了
ANB MR52909              ; 5stRB 移動完了
ANB T1522                ; 5stRB 動作ﾀｲﾑｵｰﾊﾞｰ
OUT MR52900              ; 5stRB 移動中
@MOV #0 DM5201           ; 5stRB 移動完了No
MPS
LD<> DM5200 #0           ; 5stRB 移動No
AND= DM5200 DM12018      ; 5stRB 移動No  5stRB ﾌﾟﾛｸﾞﾗﾑNo ACK
ANL
TMH @10 #2               ; 5stRB 移動DLY
MRD
AND @T10                 ; 5stRB 移動DLY
OUT MR52902              ; 5stRB ﾌﾟﾛｸﾞﾗﾑ ｽﾄﾛｰﾌﾞON
MPP
ANB LR10015              ; 動作ﾀｲﾑｵｰﾊﾞ無効
TMR #1522 DM5290         ; 5stRB 動作ﾀｲﾑｵｰﾊﾞｰ  5stRB 動作ﾀｲﾑｱｳﾄ時間
LD R50901                ; 5stRB 移動完了
OR MR52903               ; 5stRB 移動完了確認
AND MR52900              ; 5stRB 移動中
OUT MR52903              ; 5stRB 移動完了確認
@MOV DM5200 DM5201       ; 5stRB 移動No  5stRB 移動完了No
OUT MR52909              ; 5stRB 移動完了
;停止処理
LDF MR52002              ; 5stRB ｻｰﾎﾞON
@MOV #0 DM5201           ; 5stRB 移動完了No
;動作可能
LDB LR407                ; <A071>5st ｽｶﾗﾛﾎﾞｯﾄｺﾝﾄﾛｰﾗ異常
ANB LR3110               ; <A506>5st ｽｶﾗﾛﾎﾞｯﾄ補正値 範囲外
ANB MR52900              ; 5stRB 移動中
ANB MR52909              ; 5stRB 移動完了
ANB R50900               ; 2stRB ﾌﾟﾛｸﾞﾗﾑ受取完了
ANB R50901               ; 5stRB 移動完了
OUT MR52914              ; 5stRB 動作可能
MPS
OUT MR52915              ; 5stRB 停止中
MPP
LD MR52915               ; 5stRB 停止中
AND= DM5200 DM5201       ; 5stRB 移動No  5stRB 移動完了No
ANL
OUT MR52913              ; 5stRB ﾎﾟｲﾝﾄ一致
;<h1/>位置決め完了
LD MR52913               ; 5stRB ﾎﾟｲﾝﾄ一致
MPS
LD= DM5201 #1            ; 5stRB 移動完了No
AND R50909               ; 5stRB 領域判定2[上昇端]
ANL
OUT MR52700              ; 5stRB 原位置
MPP
AND= DM5201 #2           ; 5stRB 移動完了No
OUT MR52710              ; 5stRB 上昇位置
LD MR52913               ; 5stRB ﾎﾟｲﾝﾄ一致
MPS
AND= DM5201 #50          ; 5stRB 移動完了No
OUT MR52601              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 下降端
MRD
LD= DM5201 #51           ; 5stRB 移動完了No
AND R50909               ; 5stRB 領域判定2[上昇端]
ANL
OUT MR52701              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 上昇端
MRD
AND= DM5201 #60          ; 5stRB 移動完了No
OUT MR52602              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 下降端
MPP
LD= DM5201 #61           ; 5stRB 移動完了No
AND R50909               ; 5stRB 領域判定2[上昇端]
ANL
OUT MR52702              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 上昇端
LD MR52913               ; 5stRB ﾎﾟｲﾝﾄ一致
MPS
AND= DM5201 #70          ; 5stRB 移動完了No
OUT MR52603              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 下降端
MRD
LD= DM5201 #71           ; 5stRB 移動完了No
AND R50909               ; 5stRB 領域判定2[上昇端]
ANL
OUT MR52703              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 上昇端
MRD
AND= DM5201 #80          ; 5stRB 移動完了No
OUT MR52604              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 下降端
MPP
LD= DM5201 #81           ; 5stRB 移動完了No
AND R50909               ; 5stRB 領域判定2[上昇端]
ANL
OUT MR52704              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 上昇端
LD MR52913               ; 5stRB ﾎﾟｲﾝﾄ一致
MPS
AND= DM5201 #15          ; 5stRB 移動完了No
OUT MR52706              ; 5stRB ﾋﾉｳﾗｷﾞｱｶﾒﾗ 上昇端
MPP
AND= DM5201 #25          ; 5stRB 移動完了No
OUT MR52707              ; 5stRB ｼﾞｼﾝｷﾞｱｶﾒﾗ 上昇端
LD MR52913               ; 5stRB ﾎﾟｲﾝﾄ一致
MPS
AND= DM5201 #10          ; 5stRB 移動完了No
OUT MR52608              ; 5stRB 送りCVﾋﾉｳﾗ組込 下降端
MRD
LD= DM5201 #11           ; 5stRB 移動完了No
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R60906                ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
ANL
ANL
OUT MR52708              ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
MRD
AND= DM5201 #20          ; 5stRB 移動完了No
OUT MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
MPP
LD= DM5201 #21           ; 5stRB 移動完了No
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R60906                ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
ANL
ANL
OUT MR52709              ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
;下降中
LD MR52900               ; 5stRB 移動中
MPS
LD= DM5200 #50           ; 5stRB 移動No
OR= DM5200 #60           ; 5stRB 移動No
ANL
OUT MR50200              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ 下降中
MRD
LD= DM5200 #70           ; 5stRB 移動No
OR= DM5200 #80           ; 5stRB 移動No
ANL
OUT MR50201              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ 下降中
MPP
LD= DM5200 #10           ; 5stRB 移動No
OR= DM5200 #20           ; 5stRB 移動No
ANL
OUT MR50203              ; 5stRB 送りCV 下降中
;干渉外
LD MR52201               ; 5stRB ﾘﾓｰﾄREADY
MPS
LD R50909                ; 5stRB 領域判定2[上昇端]
LD MR52710               ; 5stRB 上昇位置
AND R60906               ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
ORL
ANB MR50200              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ 下降中
ANB MR50201              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ 下降中
ANB MR50203              ; 5stRB 送りCV 下降中
ANL
OUT MR52815              ; 5stRB 上昇端
MRD
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R50908                ; 5stRB 領域判定1[原位置]
OR R50911                ; 5stRB 領域判定4[ｼﾞｼﾝｷﾞﾔﾄﾚｲ領域]
OR R50912                ; 5stRB 領域判定5[ｶﾒﾗ領域]
OR R50914                ; 5stRB 領域判定7[送りCV]
ANB MR50200              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ 下降中
ANL
OUT MR52800              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ 干渉外
MRD
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R50908                ; 5stRB 領域判定1[原位置]
OR R50910                ; 5stRB 領域判定3[ﾋﾉｳﾗｷﾞﾔﾄﾚｲ領域]
OR R50912                ; 5stRB 領域判定5[ｶﾒﾗ領域]
OR R50914                ; 5stRB 領域判定7[送りCV]
ANB MR50201              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ 下降中
ANL
OUT MR52801              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ 干渉外
MRD
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R50908                ; 5stRB 領域判定1[原位置]
OR R50910                ; 5stRB 領域判定3[ﾋﾉｳﾗｷﾞﾔﾄﾚｲ領域]
OR R50911                ; 5stRB 領域判定4[ｼﾞｼﾝｷﾞﾔﾄﾚｲ領域]
OR R50914                ; 5stRB 領域判定7[送りCV]
ANL
OUT MR52802              ; 5stRB ｶﾒﾗ 干渉外
MPP
LD R50909                ; 5stRB 領域判定2[上昇端]
OR R50908                ; 5stRB 領域判定1[原位置]
OR R50910                ; 5stRB 領域判定3[ﾋﾉｳﾗｷﾞﾔﾄﾚｲ領域]
OR R50911                ; 5stRB 領域判定4[ｼﾞｼﾝｷﾞﾔﾄﾚｲ領域]
OR R50912                ; 5stRB 領域判定5[ｶﾒﾗ領域]
ANB MR50203              ; 5stRB 送りCV 下降中
ANL
OUT MR52803              ; 5stRB 送りCV 干渉外
LDB MR52601              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 下降端
ANB MR52602              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 下降端
ANB MR52603              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 下降端
ANB MR52604              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 下降端
ANB MR52608              ; 5stRB 送りCVﾋﾉｳﾗ組込 下降端
ANB MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
INV
AND MR52915              ; 5stRB 停止中
OUT MR52810              ; 5stRB 下降位置停止まとめ
LDB MR52700              ; 5stRB 原位置
ANB MR52701              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 上昇端
ANB MR52702              ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 上昇端
ANB MR52703              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 上昇端
ANB MR52704              ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 上昇端
ANB MR52708              ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
ANB MR52709              ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
INV
AND MR52915              ; 5stRB 停止中
OUT MR52811              ; 5stRB 上昇位置停止まとめ
;<h1/>出力
LD MR52200               ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理
OR MR52001               ; 5stRB CPU OK状態
OUT R60800               ; 5stRB 非常停止入力(ON =NORMAL)
LD MR52204               ; 5stRB ｻｰﾎﾞON
OUT R60801               ; 5stRB ｻｰﾎﾞON
LD MR52200               ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理
OR MR52001               ; 5stRB CPU OK状態
LDB B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
AND @T5                  ; 5stRB ｲﾝﾀｰﾛｯｸOFF
LD B10A1                 ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
AND @T4                  ; 5stRB ｲﾝﾀｰﾛｯｸOFF
ORL
ANL
OUT R60809               ; 5stRB ｲﾝﾀｰﾛｯｸ入力
LD MR52207               ; 5stRB ｽﾀｰﾄ指令
OUT R60810               ; 5stRB ｽﾀｰﾄ
LD MR52205               ; 5stRB 自動ﾓｰﾄﾞ切替指令
OUT R60811               ; 5stRB 自動ﾓｰﾄﾞ切替
LD MR52206               ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ指令
OUT R60813               ; 5stRB ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ
LD MR52200               ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理
OUT R61508               ; 5stRB ｲﾆｼｬﾙﾃﾞｰﾀ処理完了ﾌﾗｸﾞ
;
LD MR52902               ; 5stRB ﾌﾟﾛｸﾞﾗﾑ ｽﾄﾛｰﾌﾞON
OUT R60900               ; 5stRB ﾌﾟﾛｸﾞﾗﾑｽﾄﾛｰﾌﾞ
LDP MR52608              ; 5stRB 送りCVﾋﾉｳﾗ組込 下降端
ORP MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
OR R60904                ; 5stRB 減速上昇
ANB R50909               ; 5stRB 領域判定2[上昇端]
ANB MR52815              ; 5stRB 上昇端
ANB MR52708              ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
ANB MR52709              ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
OUT R60904               ; 5stRB 減速上昇
LDP MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
OR MR52910               ; 5stRB 送りCVｼﾞｼﾝ組込 ﾀｲﾏstart
ANB R50909               ; 5stRB 領域判定2[上昇端]
OUT MR52910              ; 5stRB 送りCVｼﾞｼﾝ組込 ﾀｲﾏstart
TMR #1350 #20            ; 上昇待機時間
LD T1350                 ; 上昇待機時間
OUT R60905               ; 5stRB 上昇待機
LDP MR52608              ; 5stRB 送りCVﾋﾉｳﾗ組込 下降端
OR R60906                ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
AND R2408                ; 5stRB ｼﾞｼﾝｷﾞﾔ吸着ON 
ANB MR52609              ; 5stRB 送りCVｼﾞｼﾝ組込 下降端
ANB R50909               ; 5stRB 領域判定2[上昇端]
OUT R60906               ; 5stRB ｼﾞｼﾝｷﾞﾔﾜｰｸ有
;機種切替
LD MR1200                ; 手動ﾓｰﾄﾞ
AND= DM1500 #1           ; 機種選択TP
LDB MR1200               ; 手動ﾓｰﾄﾞ
AND MR2501               ; 機種1 軸内導光無し
ORL
MOV #0 R61000            ; 5stRB_ﾜｰｸ機種No.
OUT MR52812              ; ﾜｰｸ機種(軸内動向無し)RB5選択中
LD MR1200                ; 手動ﾓｰﾄﾞ
LD= DM1500 #2            ; 機種選択TP
OR= DM1500 #4            ; 機種選択TP
ANL
LDB MR1200               ; 手動ﾓｰﾄﾞ
AND MR2502               ; 機種2 軸内導光有共通
ORL
MOV #1 R61000            ; 5stRB_ﾜｰｸ機種No.
LD MR1200                ; 手動ﾓｰﾄﾞ
AND= DM1500 #2           ; 機種選択TP
LDB MR1200               ; 手動ﾓｰﾄﾞ
AND= DM104 #2            ; CPU_B_____導光有無
ORL
OUT MR52813              ; ﾜｰｸ機種(軸内動向有旧)RB5選択中
LD MR1200                ; 手動ﾓｰﾄﾞ
AND= DM1500 #4           ; 機種選択TP
LDB MR1200               ; 手動ﾓｰﾄﾞ
AND= DM104 #3            ; CPU_B_____導光有無
ORL
OUT MR52809              ; ﾜｰｸ機種(軸内動向有新)RB5選択中
;ﾌﾟﾛｸﾞﾗﾑNo
LD CR2002                ; 常時ON
MPS
AND MR52900              ; 5stRB 移動中
MOV DM5200 DM13018       ; 5stRB 移動No  5stRB 移動No
MRD
ANB MR52900              ; 5stRB 移動中
MOV #0 DM13018           ; 5stRB 移動No
MRD
MOV #0 DM13019
MRD
MOV.D #1 DM13030         ; 5stRB 動作速度
MRD
LDB R74813               ; 5stRB ｱﾗｲﾒﾝﾄ補正中
ANB MR57601              ; 画像処理  ｶﾒﾗ2ｷｬﾘﾌﾞﾚｰｼｮﾝ
ANL
MPS
LD<=.D #1 DM522          ; TP5stRB 移動速度[ﾜｰｸ無][%]
AND<=.D DM522 #100       ; TP5stRB 移動速度[ﾜｰｸ無][%]
AND MR21500              ; 5stRB 全ﾊﾝﾄﾞﾜｰｸ無_Mem
ANL
MOV.D DM522 DM13030      ; TP5stRB 移動速度[ﾜｰｸ無][%]  5stRB 動作速度
MPP
LD<=.D #1 DM502          ; TP5stRB 移動速度[ﾜｰｸ有][%]
AND<=.D DM502 #100       ; TP5stRB 移動速度[ﾜｰｸ有][%]
ANB MR21500              ; 5stRB 全ﾊﾝﾄﾞﾜｰｸ無_Mem
ANL
MOV.D DM502 DM13030      ; TP5stRB 移動速度[ﾜｰｸ有][%]  5stRB 動作速度
MRD
AND R74813               ; 5stRB ｱﾗｲﾒﾝﾄ補正中
MOV.D #100 DM13030       ; 5stRB 動作速度
MRD
AND MR57601              ; 画像処理  ｶﾒﾗ2ｷｬﾘﾌﾞﾚｰｼｮﾝ
MOV.D #5 DM13030         ; 5stRB 動作速度
MPP
LD MR3000                ; 原点復帰
AND<.D DM538 DM13030     ; TP原点復帰速度  5stRB 動作速度
ANL
MOV.D DM538 DM13030      ; TP原点復帰速度  5stRB 動作速度
;動作ﾀｲﾑｱｳﾄ時間
LD CR2002                ; 常時ON
MPS
AND>.D #10 DM502         ; TP5stRB 移動速度[ﾜｰｸ有][%]
MOV #2000 DM5290         ; 5stRB 動作ﾀｲﾑｱｳﾄ時間
MPP
AND<=.D #10 DM502        ; TP5stRB 移動速度[ﾜｰｸ有][%]
MOV #200 DM5290          ; 5stRB 動作ﾀｲﾑｱｳﾄ時間
;TP出力
LD MR52700               ; 5stRB 原位置
LD= DM5200 #1            ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1500                ; TP5stRB 原位置 移動PL
LD MR52701               ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上) 上昇端
LD= DM5200 #51           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1501                ; TP5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(上)移動PL
LD MR52702               ; 5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下) 上昇端
LD= DM5200 #61           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1502                ; TP5stRB ﾋﾉｳﾗｷﾞﾔﾄﾚｲ(下)移動PL
LD MR52703               ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上) 上昇端
LD= DM5200 #71           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1503                ; TP5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(上)移動PL
LD MR52704               ; 5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下) 上昇端
LD= DM5200 #81           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1504                ; TP5stRB ｼﾞｼﾝｷﾞﾔﾄﾚｲ(下)移動PL
LD MR52706               ; 5stRB ﾋﾉｳﾗｷﾞｱｶﾒﾗ 上昇端
LD= DM5200 #15           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1506                ; TP5stRB ﾋﾉｳﾗｷﾞﾔｶﾒﾗ 移動PL
LD MR52707               ; 5stRB ｼﾞｼﾝｷﾞｱｶﾒﾗ 上昇端
LD= DM5200 #25           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1507                ; TP5stRB ｼﾞｼﾝｷﾞﾔｶﾒﾗ 移動PL
LD MR52708               ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
LD= DM5200 #11           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1508                ; TP5stRB 送りCVﾋﾉｳﾗ組付移動PL
LD MR52709               ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
LD= DM5200 #21           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1509                ; TP5stRB 送りCVｼﾞｼﾝ組付移動PL
LD MR52810               ; 5stRB 下降位置停止まとめ
LD MR52814               ; 5stRB 下降移動まとめ
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B150E                ; TP5stRB 下降 移動PL
LD<> DM5200 #10          ; 5stRB 移動No
AND<> DM5200 #20         ; 5stRB 移動No
AND<> DM5200 #50         ; 5stRB 移動No
AND<> DM5200 #60         ; 5stRB 移動No
AND<> DM5200 #70         ; 5stRB 移動No
AND<> DM5200 #80         ; 5stRB 移動No
INV
OUT MR52814              ; 5stRB 下降移動まとめ
LD MR52815               ; 5stRB 上昇端
ANB MR52900              ; 5stRB 移動中
LD<> DM5200 #2           ; 5stRB 移動No
AND MR52900              ; 5stRB 移動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B150F                ; TP5stRB 上昇 移動PL
;ｽｶﾗRB現在値
LD CR2002                ; 常時ON
MOV.L DM12020 W1250      ; 5stRB 現在値X  TP5stRB 現在値X
MOV.L DM12022 W1252      ; 5stRB 現在値Y  TP5stRB 現在値Y
MOV.L DM12024 W1254      ; 5stRB 現在値θ(ﾘﾓｰﾄZ)  TP5stRB 現在値θ
MOV.L DM12026 W1256      ; 5stRB 現在値Z(ﾘﾓｰﾄθ)  TP5stRB 現在値Z
;ﾛﾎﾞｯﾄ位置取込
LD MR52112               ; 5stRB 補正値ｾﾞﾛ
MPS
ANP MR52706              ; 5stRB ﾋﾉｳﾗｷﾞｱｶﾒﾗ 上昇端
BMOV DM12020 EM10200 #8  ; 5stRB 現在値X  5stRB ﾋﾉｳﾗｷﾞﾔ ｶﾒﾗ位置X
MPP
ANP MR52707              ; 5stRB ｼﾞｼﾝｷﾞｱｶﾒﾗ 上昇端
BMOV DM12020 EM10210 #8  ; 5stRB 現在値X  5stRB ﾌﾝｼﾝｷﾞﾔ ｶﾒﾗ位置X
LD MR52112               ; 5stRB 補正値ｾﾞﾛ
MPS
ANP MR52708              ; 5stRB 送りCVﾋﾉｳﾗ組込 上昇端
BMOV DM12020 EM10220 #8  ; 5stRB 現在値X  5stRB ﾋﾉｳﾗｷﾞﾔ CV組込位置X
MPP
ANP MR52709              ; 5stRB 送りCVｼﾞｼﾝ組込 上昇端
BMOV DM12020 EM10230 #8  ; 5stRB 現在値X  5stRB ﾌﾝｼﾝｷﾞﾔ CV組込位置X
;ﾛﾎﾞｯﾄ位置表示
LD CR2002                ; 常時ON
MPS
AND= W120A #0            ; TP5stRB 補正選択
BMOV EM10200 W1258 #8    ; 5stRB ﾋﾉｳﾗｷﾞﾔ ｶﾒﾗ位置X  TP5stRB 停止位置X
MRD
AND= W120A #1            ; TP5stRB 補正選択
BMOV EM10210 W1258 #8    ; 5stRB ﾌﾝｼﾝｷﾞﾔ ｶﾒﾗ位置X  TP5stRB 停止位置X
MRD
AND= W120A #2            ; TP5stRB 補正選択
BMOV EM10220 W1258 #8    ; 5stRB ﾋﾉｳﾗｷﾞﾔ CV組込位置X  TP5stRB 停止位置X
MPP
AND= W120A #3            ; TP5stRB 補正選択
BMOV EM10230 W1258 #8    ; 5stRB ﾌﾝｼﾝｷﾞﾔ CV組込位置X  TP5stRB 停止位置X
;積算ｽﾞﾚ量
LD CR2002                ; 常時ON
MPS
LD= W120A #0             ; TP5stRB 補正選択
OR= W120A #2             ; TP5stRB 補正選択
ANL
BMOV W0820 W1260 #6      ; ｶﾒﾗ2 ﾋﾉｳﾗｷﾞﾔX方向ｽﾞﾚ量 積算値  TPｶﾒﾗ2 積算ｽﾞﾚ量X
MPP
LD= W120A #1             ; TP5stRB 補正選択
OR= W120A #3             ; TP5stRB 補正選択
ANL
BMOV W0830 W1260 #6      ; ｶﾒﾗ2 ｼﾞｼﾝｷﾞﾔX方向ｽﾞﾚ量 積算値  TPｶﾒﾗ2 積算ｽﾞﾚ量X
;手動補正値
LDP B0D5                 ; TP5stRB補正値ｸﾘｱPB
FMOV #0 W1200 #8         ; TP5stRB X方向補正値
LDP B0D6                 ; TP5stRB補正値加算PB
CAL+.L DM820 W1200 W1200 ; ｶﾒﾗ2 X方向ｽﾞﾚ量  TP5stRB X方向補正値  TP5stRB X方向補正値
CAL+.L DM822 W1202 W1202 ; ｶﾒﾗ2 Y方向ｽﾞﾚ量  TP5stRB Y方向補正値  TP5stRB Y方向補正値
CAL+.L DM824 W1204 W1204 ; ｶﾒﾗ2 θ方向ｽﾞﾚ量  TP5stRB θ方向補正値  TP5stRB θ方向補正値
LDP R74809               ; 5st ｱﾗｲﾒﾝﾄ 1ｻｲｸﾙ正常完了
MOV.L W1260 W1200        ; TPｶﾒﾗ2 積算ｽﾞﾚ量X  TP5stRB X方向補正値
MOV.L W1262 W1202        ; TPｶﾒﾗ2 積算ｽﾞﾚ量Y  TP5stRB Y方向補正値
MOV.L W1264 W1204        ; TPｶﾒﾗ2 積算ｽﾞﾚ量θ  TP5stRB θ方向補正値
;<h1/>停止位置
LDF MR1101               ; 運転準備 完了
OR MR52004               ; 5stRB ｻｰﾎﾞOFF検出
ANB MR52006              ; 5stRB ｻｰﾎﾞOFF停止位置取得完了
ANB T1526                ; 5stRB 停止位置取得ﾀｲﾑｵｰﾊﾞｰ
OUT MR52004              ; 5stRB ｻｰﾎﾞOFF検出
TMR #1525 #30            ; 5stRB ｻｰﾎﾞOFF検出DLY
TMR #1526 #50            ; 5stRB 停止位置取得ﾀｲﾑｵｰﾊﾞｰ
LDP T1525                ; 5stRB ｻｰﾎﾞOFF検出DLY
OR MR52005               ; 5stRB ｻｰﾎﾞOFF停止位置取得
AND MR52004              ; 5stRB ｻｰﾎﾞOFF検出
MPS
OUT MR52005              ; 5stRB ｻｰﾎﾞOFF停止位置取得
MPP
ANP MR52005              ; 5stRB ｻｰﾎﾞOFF停止位置取得
MOV $8000 DM13016        ; 5stRB ﾘﾓｰﾄｺﾏﾝﾄﾞ
LD= DM12016 $0200        ; 5stRB ﾘﾓｰﾄｺﾏﾝﾄﾞ ｽﾃｰﾀｽ
AND MR52005              ; 5stRB ｻｰﾎﾞOFF停止位置取得
MPS
OUT MR52006              ; 5stRB ｻｰﾎﾞOFF停止位置取得完了
MPP
ANP MR52006              ; 5stRB ｻｰﾎﾞOFF停止位置取得完了
CAL*.L DM12020 +10 EM10290;5stRB 現在値X  5stRB 停止位置X
CAL*.L DM12022 +10 EM10292;5stRB 現在値Y  5stRB 停止位置Y
CAL*.L DM12026 +10 EM10294;5stRB 現在値Z(ﾘﾓｰﾄθ)  5stRB 停止位置θ
CAL*.L DM12024 +10 EM10296;5stRB 現在値θ(ﾘﾓｰﾄZ)  5stRB 停止位置Z
LDB MR52004              ; 5stRB ｻｰﾎﾞOFF検出
MOV $0000 DM13016        ; 5stRB ﾘﾓｰﾄｺﾏﾝﾄﾞ
LD CR2002                ; 常時ON
MPS
LDP MR52004              ; 5stRB ｻｰﾎﾞOFF検出
ORP T19                  ; 運転準備 完了DLY
ORP B10A1                ; TP5stRB ﾃｨｰﾁﾝｸﾞPL
ANL
RES MR10902              ; 5stRB 停止位置正常取得
MPP
ANP MR52006              ; 5stRB ｻｰﾎﾞOFF停止位置取得完了
SET MR10902              ; 5stRB 停止位置正常取得
LD CR2002                ; 常時ON
CAL-.L EM10290 +5000 EM11200;5stRB 停止位置X  5stRB 停止位置X Min
CAL+.L EM10290 +5000 EM11202;5stRB 停止位置X  5stRB 停止位置X Max
CAL-.L EM10292 +5000 EM11204;5stRB 停止位置Y  5stRB 停止位置Y Min
CAL+.L EM10292 +5000 EM11206;5stRB 停止位置Y  5stRB 停止位置Y Max
CAL-.L EM10294 +5000 EM11208;5stRB 停止位置θ  5stRB 停止位置θ Min
CAL+.L EM10294 +5000 EM11210;5stRB 停止位置θ  5stRB 停止位置θ Max
LD<=.L EM11200 DM12020   ; 5stRB 停止位置X Min  5stRB 現在値X
AND<=.L DM12020 EM11202  ; 5stRB 現在値X  5stRB 停止位置X Max
AND<=.L EM11204 DM12022  ; 5stRB 停止位置Y Min  5stRB 現在値Y
AND<=.L DM12022 EM11206  ; 5stRB 現在値Y  5stRB 停止位置Y Max
AND<=.L EM11208 DM12024  ; 5stRB 停止位置θ Min  5stRB 現在値θ(ﾘﾓｰﾄZ)
AND<=.L DM12024 EM11210  ; 5stRB 現在値θ(ﾘﾓｰﾄZ)  5stRB 停止位置θ Max
OUT MR10912              ; 5stRB 停止位置範囲内
END
ENDH
