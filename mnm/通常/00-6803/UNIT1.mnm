DEVICE:128
;MODULE:UNIT1
;MODULE_TYPE:0
;□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD CR2003                ; 常時OFF
TMR K101 K3              ; 入力受け1
LD CR2003                ; 常時OFF
TMR K102 K3              ; 入力受け2
LD CR2003                ; 常時OFF
TMR K103 K3              ; 入力受け3
LD CR2003                ; 常時OFF
TMR K104 K3              ; 入力受け4
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ圧入ﾛﾎﾞｯﾄ現在値
LD W00.10                ; #01_圧入RB_HEND
AND W00.11               ; #01_圧入RB_PEND
MPS
AND W00.0                ; #01_圧入RB_完了POS_1
OUT D1028.0              ; ﾌﾟﾗｸﾞ圧入 RB完了POS1
MRD
AND W00.1                ; #01_圧入RB_完了POS_2
OUT D1028.1              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS2
MRD
AND W00.2                ; #01_圧入RB_完了POS_4
OUT D1028.2              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS4
MRD
AND W00.3                ; #01_圧入RB_完了POS_8
OUT D1028.3              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS8
MRD
AND W00.4                ; #01_圧入RB_完了POS_16
OUT D1028.4              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS16
MRD
AND W00.5                ; #01_圧入RB_完了POS_32
OUT D1028.5              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS32
MPP
INV
MOV K0 D1028             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
;<h1/>▽_装置状態・動作条件
LD= D1028 K1             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
AND W00.11               ; #01_圧入RB_PEND
OUT M2384                ; ﾕﾆｯﾄ1原点
LDB B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
ANB B021                 ; ﾜｰｸ浮き検出異常
ANB B022                 ; ﾕﾆｯﾄ1_異常3
ANB B023                 ; ﾕﾆｯﾄ1_異常4
ANB B024                 ; ﾕﾆｯﾄ1_異常5
ANB B025                 ; ﾕﾆｯﾄ1_異常6
INV
OUT M2385                ; ﾕﾆｯﾄ1全異常
LD M2384                 ; ﾕﾆｯﾄ1原点
ANB M1600                ; S00 ﾕﾆｯﾄ1ｽﾃｯﾌﾟ開始
LD X06                   ; ﾜｰｸ在席確認(ON=有)
OR B1004                 ; 冶具動作中(TP)
ANL
AND X0A                  ; ﾜｰｸ浮き確認(手前)(OFF=浮き)
AND X0B                  ; ﾜｰｸ浮き確認(奥)(OFF=浮き)
OUT M2386                ; ﾕﾆｯﾄ1起動条件
LDB M64                  ; 運転準備
OR M65                   ; 即停止異常(緊急停止)
OR M2385                 ; ﾕﾆｯﾄ1全異常
OR T15                   ; 強制停止
OUT M2387                ; ﾕﾆｯﾄ1起動OFF
LD M98                   ; 原点復帰開始
ANB M2388                ; ﾕﾆｯﾄ1原点復帰中
OR M2388                 ; ﾕﾆｯﾄ1原点復帰中
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M2079                ; STEP79_ﾕﾆｯﾄ1原点復帰完了確認
OUT M2388                ; ﾕﾆｯﾄ1原点復帰中
LD M80                   ; 自動起動条件
ANP X03                  ; ｽﾀｰﾄSW
AND M2386                ; ﾕﾆｯﾄ1起動条件
OR M2389                 ; ﾕﾆｯﾄ1起動中
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M1679                ; ﾕﾆｯﾄ1動作完了
OUT M2389                ; ﾕﾆｯﾄ1起動中
LD M32                   ; 自動ﾓｰﾄﾞ
AND X03                  ; ｽﾀｰﾄSW
ANB M2386                ; ﾕﾆｯﾄ1起動条件
ANB M999                 ; 暫定修正　小池
OUT M2390                ; ｽﾀｰﾄ条件不足ﾌﾞｻﾞｰ
LD M80                   ; 自動起動条件
AND M2386                ; ﾕﾆｯﾄ1起動条件
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M1679                ; ﾕﾆｯﾄ1動作完了
ANB M2389                ; ﾕﾆｯﾄ1起動中
ANB M999                 ; 暫定修正　小池
OUT M2391                ; ｽﾀｰﾄ要求
LDB M1681                ; 圧入NGﾌﾞｻﾞｰ
LDB M2390                ; ｽﾀｰﾄ条件不足ﾌﾞｻﾞｰ
OR M999                  ; 暫定修正　小池
ANL
INV
OUT M2392                ; ﾕﾆｯﾄ1警報補助
;□ステップ起動用フラグ
LD M32                   ; 自動ﾓｰﾄﾞ
LD M33                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M2368                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
;<h1/>▽_自動
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
FMOV K0 M1600 K5         ; S00 ﾕﾆｯﾄ1ｽﾃｯﾌﾟ開始
LDB M2389                ; ﾕﾆｯﾄ1起動中
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M2389                 ; ﾕﾆｯﾄ1起動中
MOV D1000 Z1             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
OUT M1600:Z1             ; S00 ﾕﾆｯﾄ1ｽﾃｯﾌﾟ開始
;自動運転ステップ開始
;<h1/>____▼_S00_ﾌﾟﾗｸﾞ圧入
;	ﾌﾟﾗｸﾞ圧入下降
LD M1600                 ; S00 ﾕﾆｯﾄ1ｽﾃｯﾌﾟ開始
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D100                ; ﾜｰｸ投入数
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K2 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1601                 ; S01 圧入開始位置下降指令
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1602                 ; S02 開始位置移動確認
AND W00.11               ; #01_圧入RB_PEND
MPS
AND= K2 D1028            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@CAL+ D2020 K10 D1024    ; 動作設定No (自動ﾓｰﾄﾞで確定)  ﾌﾟﾗｸﾞ圧入  RB_POS確定
MPP
AND<> K2 D1028           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@MOV K1 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1603                 ; S03 圧入下降指令
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;	ﾌﾟﾗｸﾞ圧入判定
LD M1604                 ; S04 起動確認
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
AND T50                  ; RB1_MOVE_OFFﾀｲﾏ
MPS
AND<> D1028 K2           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
MPS
AND W00.11               ; #01_圧入RB_PEND
@MOV K20 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND= D1028 K2            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
MPS
AND W00.11               ; #01_圧入RB_PEND
@MOV K3 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LDB W00.6                ; #01_圧入RB_MOVE
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
TMR K50 K10              ; RB1_MOVE_OFFﾀｲﾏ
;<h1/>____▼_S05_ﾌﾟﾗｸﾞ圧入NG処理
;	NGｵﾍﾟｺｰﾙ、PB確認
LD M2389                 ; ﾕﾆｯﾄ1起動中
MPS
LDP M1605                ; S05 圧入失敗
OR M1680                 ; 圧入NGﾗﾝﾌﾟ表示
ANB M1635                ; S35 NGｽｲｯﾁ確認(終了)
ANL
@INC D104                ; NGﾜｰｸ数
CON
OUT M1680                ; 圧入NGﾗﾝﾌﾟ表示
MRD
LDP M1605                ; S05 圧入失敗
OR M1681                 ; 圧入NGﾌﾞｻﾞｰ
ANB M1635                ; S35 NGｽｲｯﾁ確認(終了)
ANB T120                 ; NGﾘｾｯﾄ復帰処理開始
ANL
OUT M1681                ; 圧入NGﾌﾞｻﾞｰ
MPP
LD M1680                 ; 圧入NGﾗﾝﾌﾟ表示
LD CR2005                ; 100msｸﾛｯｸﾊﾟﾙｽ
ORB M1605                ; S05 圧入失敗
ANL
ANB M999                 ; 暫定修正　小池
ANL
OUT M1685                ; 圧入NGﾗﾝﾌﾟON
LD M1605                 ; S05 圧入失敗
MPS
AND X04                  ; NGﾘｾｯﾄSW
MPS
ANB X05                  ; NGﾜｰｸ確認(ON=有)
TMR K120 K10             ; NGﾘｾｯﾄ復帰処理開始
MPP
AND X05                  ; NGﾜｰｸ確認(ON=有)
OUT M1636                ; 圧入NGBOX排出要求
MPP
LD T120                  ; NGﾘｾｯﾄ復帰処理開始
LDB M998                 ; ﾘｾｯﾄsw長押し無効要望20180108
AND X04                  ; NGﾘｾｯﾄSW
ORL
ANL
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1606                 ; S06 圧入上昇指令
AND W00.6                ; #01_圧入RB_MOVE
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1607                 ; S07 起動確認
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.11               ; #01_圧入RB_PEND
MPS
AND= D1028 K1            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND<> D1028 K1           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@MOV K6 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1608                 ; S08 位置決完了確認
@MOV K32 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S20_ﾌﾟﾗｸﾞ圧入OK処理
LD M1620                 ; S20 
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1621                 ; S21 
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1622                 ; S22 
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;	圧入上昇
LDB Y54                  ; 真空ﾎﾟﾝﾌﾟON
TMR K151 K5              ; 真空ﾎﾟﾝﾌﾟoffｳｪｲﾄ
LD M1623                 ; S23 圧入上昇
AND T151                 ; 真空ﾎﾟﾝﾌﾟoffｳｪｲﾄ
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D102                ; OKﾜｰｸ数
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1624                 ; S24 圧入上昇指令
ANB W00.11               ; #01_圧入RB_PEND
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1625                 ; S25 起動確認
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
AND W00.11               ; #01_圧入RB_PEND
MPS
AND= D1028 K1            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND<> D1028 K1           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@MOV K24 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S26_OKﾜｰｸ取出確認(終了処理)
LD M1626                 ; S26 判定OK
OR M1684                 ; 判定OKﾗﾝﾌﾟ表示
ANB M1679                ; ﾕﾆｯﾄ1動作完了
AND M2389                ; ﾕﾆｯﾄ1起動中
OUT M1684                ; 判定OKﾗﾝﾌﾟ表示
LD M1626                 ; S26 判定OK
LDB T150                 ; ﾗｲﾄｶｰﾃﾝOK
ORB X06                  ; ﾜｰｸ在席確認(ON=有)
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1627                 ; S27 OK取出開始確認(遮光)
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
ANB X06                  ; ﾜｰｸ在席確認(ON=有)
@MOV K79 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S32_NGﾜｰｸ取出確認(終了処理)
LD M1632                 ; S32 NG取出開始待ち
LDB T150                 ; ﾗｲﾄｶｰﾃﾝOK
ORB X06                  ; ﾜｰｸ在席確認(ON=有)
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1633                 ; S33 NG取出開始確認(遮光)
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
ANB X06                  ; ﾜｰｸ在席確認(ON=有)
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1634                 ; S34 NG取出完了確認(受光)
LDB B1004                ; 冶具動作中(TP)
AND X05                  ; NGﾜｰｸ確認(ON=有)
OR B1004                 ; 冶具動作中(TP)
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1635                 ; S35 NGｽｲｯﾁ確認(終了)
ANB M1680                ; 圧入NGﾗﾝﾌﾟ表示
@MOV K79 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1632                 ; S32 NG取出開始待ち
OR M1633                 ; S33 NG取出開始確認(遮光)
OR M1634                 ; S34 NG取出完了確認(受光)
LD M1605                 ; S05 圧入失敗
AND X05                  ; NGﾜｰｸ確認(ON=有)
ORL
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
LD X05                   ; NGﾜｰｸ確認(ON=有)
ANB M1605                ; S05 圧入失敗
ORL
OUT Y53                  ; ﾜｰｸ確認ﾗﾝﾌﾟ
;<h1/>▽_原点復帰
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
FMOV K0 M2000 K5         ; STEP0_ﾕﾆｯﾄ1原点復帰開始
LDB M2388                ; ﾕﾆｯﾄ1原点復帰中
MOV K0 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2388                 ; ﾕﾆｯﾄ1原点復帰中
MOV D1010 Z1             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
OUT M2000:Z1             ; STEP0_ﾕﾆｯﾄ1原点復帰開始
;原点復帰ステップ開始
LD M2000                 ; STEP0_ﾕﾆｯﾄ1原点復帰開始
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2001                 ; STEP1_ｼｬｯﾀｰ開閉 RB_原点復帰指令
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2002                 ; STEP2_起動確認
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2003                 ; STEP3_位置決完了確認
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
@MOV K0 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2004                 ; STEP4_ﾌﾟﾗｸﾞ圧入 RB_原点復帰指令
LD M2336                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
OR M2340                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
ANL
ANB W00.11               ; #01_圧入RB_PEND
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2005                 ; STEP5_起動確認
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.11               ; #01_圧入RB_PEND
MPS
AND= D1028 K0            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
AND<> D1028 K0           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
MOV K4 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2006                 ; STEP6_上昇位置移動
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2007                 ; STEP7_上昇位置移動指令
ANB W00.11               ; #01_圧入RB_PEND
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2008                 ; STEP8 上昇位置移動確認
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.11               ; #01_圧入RB_PEND
MPS
AND= D1028 K1            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
AND<> D1028 K1           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
MOV K6 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2009                 ; STEP9 原点確認
AND M2384                ; ﾕﾆｯﾄ1原点
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M2010                 ; STEP10 原点復帰終了
ANB M98                  ; 原点復帰開始
@MOV K79 D1010           ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;<h1/>▽_手動
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
ANB M2389                ; ﾕﾆｯﾄ1起動中
ANB M2388                ; ﾕﾆｯﾄ1原点復帰中
MC
;圧入ﾛﾎﾞｯﾄ下降
LD W00.10                ; #01_圧入RB_HEND
AND<> D1028 K2           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
OUT M2080                ; ﾍﾞﾝﾄ圧入   RB_下降可
LDP B1021                ; 圧入ﾛﾎﾞｯﾄ下降SW
AND M2080                ; ﾍﾞﾝﾄ圧入   RB_下降可
OR M2081                 ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2084                ; 位置決完了確認
MPS
OUT M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
MPP
ANB M2083                ; 起動確認
OUT M2082                ; ﾍﾞﾝﾄ圧入   RB_下降指令
@MOV K2 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2082                 ; ﾍﾞﾝﾄ圧入   RB_下降指令
AND W010.13              ; #01_圧入RB_CSTR
AND W00.6                ; #01_圧入RB_MOVE
OR M2083                 ; 起動確認
AND M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
OUT M2083                ; 起動確認
LD M2083                 ; 起動確認
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.11               ; #01_圧入RB_PEND
OR M2084                 ; 位置決完了確認
AND M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
OUT M2084                ; 位置決完了確認
LD M2081                 ; ﾍﾞﾝﾄ圧入   RB_下降
OR= D1028 K2             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
OUT B1521                ; 圧入ﾛﾎﾞｯﾄ下降PL
;圧入ﾛﾎﾞｯﾄ圧入下降
LD= D1028 K2             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
AND W00.10               ; #01_圧入RB_HEND
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
OUT M2085                ; ﾍﾞﾝﾄ圧入   RB_圧入可
LDP B1022                ; 圧入ﾛﾎﾞｯﾄ圧入下降SW
AND M2085                ; ﾍﾞﾝﾄ圧入   RB_圧入可
OR M2086                 ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2089                ; 位置決完了確認
MPS
OUT M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
MPP
ANB M2088                ; 起動確認
OUT M2087                ; ﾍﾞﾝﾄ圧入   RB_圧入指令
@CAL+ D2020 K10 D1040    ; 動作設定No (自動ﾓｰﾄﾞで確定)  手動押し込み位置指定
@MOV D1040 D1024         ; 手動押し込み位置指定  ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2087                 ; ﾍﾞﾝﾄ圧入   RB_圧入指令
AND W00.6                ; #01_圧入RB_MOVE
OR M2088                 ; 起動確認
AND M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
OUT M2088                ; 起動確認
LD M2088                 ; 起動確認
ANB W00.6                ; #01_圧入RB_MOVE
OR M2089                 ; 位置決完了確認
AND M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
OUT M2089                ; 位置決完了確認
LD M2086                 ; ﾍﾞﾝﾄ圧入   RB_圧入
LD>= D1028 K11           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
AND<= D1028 K17          ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
ORL
OUT B1522                ; 圧入ﾛﾎﾞｯﾄ圧入下降PL
;圧入ﾛﾎﾞｯﾄ上昇
LD W00.10                ; #01_圧入RB_HEND
AND<> D1028 K1           ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
OUT M2090                ; ﾍﾞﾝﾄ圧入   RB_上昇可
LDP B1020                ; 圧入ﾛﾎﾞｯﾄ上昇SW
AND M2090                ; ﾍﾞﾝﾄ圧入   RB_上昇可
OR M2091                 ; ﾍﾞﾝﾄ圧入   RB_上昇
ANB M2094                ; 位置決完了確認
MPS
OUT M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
MPP
ANB M2093                ; 起動確認
OUT M2092                ; ﾍﾞﾝﾄ圧入   RB_上昇指令
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2092                 ; ﾍﾞﾝﾄ圧入   RB_上昇指令
AND W00.6                ; #01_圧入RB_MOVE
OR M2093                 ; 起動確認
AND M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
OUT M2093                ; 起動確認
LD M2093                 ; 起動確認
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.11               ; #01_圧入RB_PEND
OR M2094                 ; 位置決完了確認
AND M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
OUT M2094                ; 位置決完了確認
LD M2091                 ; ﾍﾞﾝﾄ圧入   RB_上昇
OR= D1028 K1             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
OUT B1520                ; 圧入ﾛﾎﾞｯﾄ上昇PL
LDB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
OUT M2130                ; 手動操作中
MCR
;<h1/>□_出力回路
;■真空ポンプ出力
;	RB上昇時はポンプOFF
LDB M1605                ; S05 圧入失敗
ANB M1623                ; S23 圧入上昇
ANB M2092                ; ﾍﾞﾝﾄ圧入   RB_上昇指令
ANB M2003                ; STEP3_位置決完了確認
INV
LD M2100                 ; 真空ﾎﾟﾝﾌﾟOFF指令
ANB M1608                ; S08 位置決完了確認
ANB M1626                ; S26 判定OK
ANB M2094                ; 位置決完了確認
ANB M2009                ; STEP9 原点確認
ORL
OUT M2100                ; 真空ﾎﾟﾝﾌﾟOFF指令
LDB M2100                ; 真空ﾎﾟﾝﾌﾟOFF指令
AND M64                  ; 運転準備
ANB B1004                ; 冶具動作中(TP)
ANB M1623                ; S23 圧入上昇
ANB M1624                ; S24 圧入上昇指令
ANB M1625                ; S25 起動確認
OUT Y54                  ; 真空ﾎﾟﾝﾌﾟON
;■プラグ圧入ロボット初期原点動作
LDB M2004                ; STEP4_ﾌﾟﾗｸﾞ圧入 RB_原点復帰指令
INV
MPS
LD W00.10                ; #01_圧入RB_HEND
OR M2336                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANB M2337                ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
ANL
OUT M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
MPP
LDB W00.10               ; #01_圧入RB_HEND
OR M2337                 ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
ANB M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANL
OUT M2337                ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
LD M2337                 ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
MPS
ANB M2339                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
OUT M2338                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
MRD
LD M2338                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
AND W00.6                ; #01_圧入RB_MOVE
OR M2339                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
ANL
OUT M2339                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
MPP
LD M2339                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
ANB W00.6                ; #01_圧入RB_MOVE
AND W00.10               ; #01_圧入RB_HEND
AND W00.11               ; #01_圧入RB_PEND
OR M2340                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
ANL
OUT M2340                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
;<h1/>____▼_ﾛﾎﾞｯﾄ動作出力
LDB M1601                ; S01 圧入開始位置下降指令
ANB M1603                ; S03 圧入下降指令
ANB M1606                ; S06 圧入上昇指令
ANB M1624                ; S24 圧入上昇指令
INV
LDB M2007                ; STEP7_上昇位置移動指令
ANB M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANB M2340                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
INV
ORL
LDB M2082                ; ﾍﾞﾝﾄ圧入   RB_下降指令
ANB M2087                ; ﾍﾞﾝﾄ圧入   RB_圧入指令
ANB M2092                ; ﾍﾞﾝﾄ圧入   RB_上昇指令
INV
ORL
@MOV D1024 D1026         ; ﾌﾟﾗｸﾞ圧入  RB_POS確定  ﾌﾟﾗｸﾞ圧入 RB_指令POS
MPS
AND T150                 ; ﾗｲﾄｶｰﾃﾝOK
TMR K190 K2              ; ﾌﾟﾗｸﾞ圧入 RB_POS確定ﾃﾞｨﾚｲ
MPP
AND T190                 ; ﾌﾟﾗｸﾞ圧入 RB_POS確定ﾃﾞｨﾚｲ
OUT W010.13              ; #01_圧入RB_CSTR
LD CR2002                ; 常時ON
MPS
AND D1026.0              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS1
OUT W010.0               ; #01_圧入RB_指令POS_1
MRD
AND D1026.1              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS2
OUT W010.1               ; #01_圧入RB_指令POS_2
MRD
AND D1026.2              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS4
OUT W010.2               ; #01_圧入RB_指令POS_4
MRD
AND D1026.3              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS8
OUT W010.3               ; #01_圧入RB_指令POS_8
MRD
AND D1026.4              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS16
OUT W010.4               ; #01_圧入RB_指令POS_16
MPP
AND D1026.5              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS32
OUT W010.5               ; #01_圧入RB_指令POS_32
LD T150                  ; ﾗｲﾄｶｰﾃﾝOK
OUT W010.12              ; #01_圧入RB_*STP
LD M64                   ; 運転準備
OUT W010.15              ; #01_圧入RB_SON
LD M16                   ; ﾘｾｯﾄPB
ANB W00.14               ; #01_圧入RB_*ALM
OUT W010.14              ; #01_圧入RB_RES
LD M2338                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
OUT W010.11              ; #01_圧入RB_HOME
;<h1/>□_ﾊﾟﾈﾙ表示
LD M1684                 ; 判定OKﾗﾝﾌﾟ表示
LD M2391                 ; ｽﾀｰﾄ要求
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB M999                 ; 暫定修正　小池
ORL
LD W00.7                 ; #01_圧入RB_ZONE1
AND W00.12               ; #01_圧入RB_SV
INV
AND M64                  ; 運転準備
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y50                  ; ｽﾀｰﾄ/OKﾗﾝﾌﾟ
;NG表示
LDB M1680                ; 圧入NGﾗﾝﾌﾟ表示
INV
OUT M1690                ; NG_表示
;画面色
LD M1690                 ; NG_表示
AND= D10 K10             ; 表示中ﾍﾟｰｼﾞNO.
@MOV K1 D54              ; 画面色
INV
@MOV K0 D54              ; 画面色
;<h1/>□_ﾕｰｻﾞｰｱﾗｰﾑ表示
LD B020                  ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
@SET CR3310              ; ﾕｰｻﾞｱﾗｰﾑ10
LDB B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
@RES CR3310              ; ﾕｰｻﾞｱﾗｰﾑ10
LD B021                  ; ﾜｰｸ浮き検出異常
@SET CR3311              ; ﾕｰｻﾞｱﾗｰﾑ11
LDB B021                 ; ﾜｰｸ浮き検出異常
@RES CR3311              ; ﾕｰｻﾞｱﾗｰﾑ11
;<h1/>□_異常検出
;圧入ﾛﾎﾞｯﾄ異常
LDB W00.14               ; #01_圧入RB_*ALM
MEP
AND M64                  ; 運転準備
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
ORL
OUT B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
;ﾜｰｸ浮き検出異常
LD M80                   ; 自動起動条件
ANP X03                  ; ｽﾀｰﾄSW
LD X06                   ; ﾜｰｸ在席確認(ON=有)
OR B1004                 ; 冶具動作中(TP)
ANL
LDB X0A                  ; ﾜｰｸ浮き確認(手前)(OFF=浮き)
ORB X0B                  ; ﾜｰｸ浮き確認(奥)(OFF=浮き)
ANL
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B021                 ; ﾜｰｸ浮き検出異常
ORL
OUT B021                 ; ﾜｰｸ浮き検出異常
;異常3
LD CR2003                ; 常時OFF
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B022                 ; ﾕﾆｯﾄ1_異常3
ORL
OUT B022                 ; ﾕﾆｯﾄ1_異常3
;異常4
LD CR2003                ; 常時OFF
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B023                 ; ﾕﾆｯﾄ1_異常4
ORL
OUT B023                 ; ﾕﾆｯﾄ1_異常4
;異常5
LD CR2003                ; 常時OFF
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B024                 ; ﾕﾆｯﾄ1_異常5
ORL
OUT B024                 ; ﾕﾆｯﾄ1_異常5
;異常6
LD CR2003                ; 常時OFF
LDB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B025                 ; ﾕﾆｯﾄ1_異常6
ORL
OUT B025                 ; ﾕﾆｯﾄ1_異常6
;異常ﾌﾞｻﾞｰまとめ
LD<> B020 @D12           ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常  異常BzOFF記憶
AND<> B020 K0            ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
OUT M2393                ; ﾕﾆｯﾄ1異常BZ補助
LDB M122                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
ANB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
INV
MOV B020 @D12            ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常  異常BzOFF記憶
LD= B020 K0              ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
MOV K0 @D12              ; 異常BzOFF記憶
END
ENDH
