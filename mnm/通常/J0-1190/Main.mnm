DEVICE:128
;MODULE:Main
;MODULE_TYPE:0
;<h1/>ｽｲｯﾁ類入力
LD X08                   ; 原点復帰PB
TMR K16 K0               ; 原点復帰PB入力
LD X09                   ; 日当たりｶｳﾝﾀﾘｾｯﾄPB
TMR K17 K5               ; 日当たりｶｳﾝﾀﾘｾｯPB入力
LD X0A                   ; 時間当たりｶｳﾝﾀﾘｾｯﾄPB
TMR K18 K5               ; 時間当たりｶｳﾝﾀﾘｾｯﾄPB入力
LD X0C                   ; ﾘｾｯﾄKSW
TMR K21 K0               ; ﾘｾｯﾄKSW入力
LDP T21                  ; ﾘｾｯﾄKSW入力
ANB M1216                ; Unit1_自動動作中
AND M104                 ; 手動ﾓｰﾄﾞ選択
OUT M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
;<h1/>作動確認機出力
LD X118                  ; 作動確認機出力 車種信号1
TMH K22 K2               ; 車種信号1
LD X119                  ; 作動確認機出力 車種信号2
TMH K23 K2               ; 車種信号2
LD X11A                  ; 作動確認機出力 車種信号4
TMH K24 K2               ; 車種信号4
LD X11B                  ; 作動確認機出力 車種信号6
TMH K25 K2               ; 車種信号6
;<h1/>元圧確認
LD X110                  ; 元圧確認
TMR K26 K2               ; 元圧正常
LDB X110                 ; 元圧確認
TMR K27 K2               ; 元圧低下
;ﾜｰｸ有無
LD X111                  ; ﾜｰｸ確認
TMR K28 K3               ; ﾜｰｸあり
LDB X111                 ; ﾜｰｸ確認
TMR K29 K3               ; ﾜｰｸなし
;<h1/>PLC異常ﾘｾｯﾄ
LD CR3905                ; 重度ｴﾗｰ発生中
OR CR3906                ; 軽度ｴﾗｰ発生中
AND M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OUT CR3909               ; 現在のｴﾗｰｸﾘｱ
;<h1/>異常/警報
;即停止異常
LDB X00                  ; 非常停止正常
LD B00                   ; 異常_非常停止
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B00                  ; 異常_非常停止
LD T27                   ; 元圧低下
LD B01                   ; 異常_ｴｱ-元圧低下
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B01                  ; 異常_ｴｱ-元圧低下
LD CR3905                ; 重度ｴﾗｰ発生中
LD B02                   ; 異常_PLC重度ｴﾗｰ
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B02                  ; 異常_PLC重度ｴﾗｰ
;<h1/>├工程飛びエラー
LD T28                   ; ﾜｰｸあり
OR M1180                 ; 工程飛びｴﾗｰ_ﾜｰｸあり確認
ANB B03                  ; 異常_工程飛びｴﾗｰ
ANB M1183                ; 工程飛びｴﾗｰ_検査済ﾜｰｸ取外し確認
OUT M1180                ; 工程飛びｴﾗｰ_ﾜｰｸあり確認
LD M1270                 ; Unit1_ｸﾘｱﾗﾝｽ検査OK
AND M1272                ; Unit1_幅計測OK
AND M1240                ; Unit1_自動_検査ﾌﾟﾛｾｽ完了
OR M1271                 ; Unit1_ｸﾘｱﾗﾝｽ検査NG
OR M1273                 ; Unit1_幅計測NG
OUT M1181                ; 工程飛びｴﾗｰ_検査実行確認
LDP M1181                ; 工程飛びｴﾗｰ_検査実行確認
OR M1182                 ; 工程飛びｴﾗｰ_検査実行確認完了
AND M1180                ; 工程飛びｴﾗｰ_ﾜｰｸあり確認
OUT M1182                ; 工程飛びｴﾗｰ_検査実行確認完了
LD M1182                 ; 工程飛びｴﾗｰ_検査実行確認完了
AND T29                  ; ﾜｰｸなし
ANB B03                  ; 異常_工程飛びｴﾗｰ
OUT M1183                ; 工程飛びｴﾗｰ_検査済ﾜｰｸ取外し確認
LD M1180                 ; 工程飛びｴﾗｰ_ﾜｰｸあり確認
ANB M1182                ; 工程飛びｴﾗｰ_検査実行確認完了
AND T29                  ; ﾜｰｸなし
LD B03                   ; 異常_工程飛びｴﾗｰ
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B03                  ; 異常_工程飛びｴﾗｰ
;<h1/>├検査NG
LD M1271                 ; Unit1_ｸﾘｱﾗﾝｽ検査NG
LD B04                   ; 異常_ｸﾘｱﾗﾝｽ検査NG
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04                  ; 異常_ｸﾘｱﾗﾝｽ検査NG
LD M1273                 ; Unit1_幅計測NG
LD B05                   ; 異常_幅計測NG
ANB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05                  ; 異常_幅計測NG
;<h1/>├異常まとめ
LDB B00                  ; 異常_非常停止
ANB B01                  ; 異常_ｴｱ-元圧低下
ANB B02                  ; 異常_PLC重度ｴﾗｰ
ANB B03                  ; 異常_工程飛びｴﾗｰ
ANB B04                  ; 異常_ｸﾘｱﾗﾝｽ検査NG
ANB B05                  ; 異常_幅計測NG
ANB M1202                ; Unit1 即停止異常
INV
OUT M105                 ; 異常発生
;<h1/>└異常ランプ
LD M105                  ; 異常発生
OUT B1056                ; 異常LP
;<h1/>運転準備
LDB B00                  ; 異常_非常停止
OUT M50                  ; 運転準備可
;<h1/>ｶｳﾝﾄﾘｾｯﾄ
LD T17                   ; 日当たりｶｳﾝﾀﾘｾｯPB入力
MOV K0 D2520             ; 日当たりｶｳﾝﾄ
LD T18                   ; 時間当たりｶｳﾝﾀﾘｾｯﾄPB入力
MOV K0 D2522             ; 時間当たりｶｳﾝﾄ
;<h1/>原点復帰
LDB M100                 ; 自動運転中
AND M104                 ; 手動ﾓｰﾄﾞ選択
OUT M1130                ; 原点復帰IL_手動ﾓｰﾄﾞ
LDB M102                 ; 原点復帰完了
OUT M1131                ; 原点復帰IL_原点復帰未完了
LDB M105                 ; 異常発生
OUT M1132                ; 原点復帰IL_異常なし
LD X01                   ; 運転準備ON
OUT M1133                ; 原点復帰IL_運転準備
LD M1206                 ; Unit1 停止中
OUT M1134                ; 原点復帰IL_停止中
LDB T21                  ; ﾘｾｯﾄKSW入力
OUT M1135                ; 原点復帰IL_ﾘｾｯﾄ入力
LD M1130                 ; 原点復帰IL_手動ﾓｰﾄﾞ
AND M1131                ; 原点復帰IL_原点復帰未完了
AND M1132                ; 原点復帰IL_異常なし
AND M1133                ; 原点復帰IL_運転準備
AND M1134                ; 原点復帰IL_停止中
AND M1135                ; 原点復帰IL_ﾘｾｯﾄ入力
OUT M51                  ; 原点復帰可能条件
LD M101                  ; 原点復帰中
OUT M1137                ; 原点復帰IL_原点復帰中止
LD M102                  ; 原点復帰完了
OR M105                  ; 異常発生
ORB X01                  ; 運転準備ON
OR M110                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND M1137                ; 原点復帰IL_原点復帰中止
OUT M1138                ; 原点復帰_原点復帰中止
LDP T16                  ; 原点復帰PB入力
AND M51                  ; 原点復帰可能条件
OR M101                  ; 原点復帰中
ANB M1138                ; 原点復帰_原点復帰中止
OUT M101                 ; 原点復帰中
LD M1210                 ; Unit1 原点位置
OUT M102                 ; 原点復帰完了
;<h1/>自動/手動切替
;自動ﾓｰﾄﾞ
LD X0B                   ; ﾓｰﾄﾞ切替SSW 自動ﾓｰﾄﾞON
OUT M103                 ; 自動ﾓｰﾄﾞ選択
;手動ﾓｰﾄﾞ
LDB M103                 ; 自動ﾓｰﾄﾞ選択
OUT M104                 ; 手動ﾓｰﾄﾞ選択
;<h1/>自動
LD M1210                 ; Unit1 原点位置
OUT M1150                ; 自動IL_原点復帰完了
LDB M105                 ; 異常発生
OUT M1151                ; 自動IL_異常
LD X01                   ; 運転準備ON
OUT M1152                ; 自動IL_運転準備ON
LD T28                   ; ﾜｰｸあり
OUT M1153                ; 自動IL_ﾜｰｸあり
LD X112                  ; ｸﾞﾘｽへら検出
OUT M1154                ; 自動IL_ｸﾞﾘｽへらｾﾝｻOK
LDB M100                 ; 自動運転中
OUT M1155                ; 自動IL_前回自動運転完了
LD M103                  ; 自動ﾓｰﾄﾞ選択
OUT M1156                ; 自動IL_自動ﾓｰﾄﾞ選択
LD M1206                 ; Unit1 停止中
OUT M1157                ; 自動IL_装置停止中
LD= W08 K1               ; TP_切替ページNo.
OUT M1158                ; 自動IL_ﾒｲﾝ画面表示中
LD M1150                 ; 自動IL_原点復帰完了
AND M1151                ; 自動IL_異常
AND M1152                ; 自動IL_運転準備ON
AND M1153                ; 自動IL_ﾜｰｸあり
AND M1154                ; 自動IL_ｸﾞﾘｽへらｾﾝｻOK
AND M1155                ; 自動IL_前回自動運転完了
AND M1156                ; 自動IL_自動ﾓｰﾄﾞ選択
AND M1157                ; 自動IL_装置停止中
AND M1158                ; 自動IL_ﾒｲﾝ画面表示中
OUT M52                  ; 自動移行条件
;自動起動
LD X0D                   ; 起動PB1
MPS
ANB T11                  ; 左手側起動ﾎﾞﾀﾝONﾀｲﾏ
OUT M1170                ; 左手側起動ﾎﾞﾀﾝON
MPP
TMR K11 K5               ; 左手側起動ﾎﾞﾀﾝONﾀｲﾏ
LD X0E                   ; 起動PB2
MPS
ANB T12                  ; 右手側起動ﾎﾞﾀﾝONﾀｲﾏ
OUT M1171                ; 右手側起動ﾎﾞﾀﾝON
MPP
TMR K12 K5               ; 右手側起動ﾎﾞﾀﾝONﾀｲﾏ
LD M1170                 ; 左手側起動ﾎﾞﾀﾝON
AND M1171                ; 右手側起動ﾎﾞﾀﾝON
OUT M1172                ; 自動起動受付
LD M100                  ; 自動運転中
OUT M1160                ; 自動運転終了可
LD M105                  ; 異常発生
ORB X01                  ; 運転準備ON
LDB M1216                ; Unit1_自動動作中
AND M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OR M1299                 ; Unit1_検査完了
AND M1160                ; 自動運転終了可
OUT M1161                ; 自動運転終了
LD M1172                 ; 自動起動受付
AND M52                  ; 自動移行条件
OR M100                  ; 自動運転中
ANB M1161                ; 自動運転終了
OUT M100                 ; 自動運転中
;<h1/>出力
LD M50                   ; 運転準備可
OUT Y50                  ; PLC運転準備可
LD M101                  ; 原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M102                  ; 原点復帰完了
OUT Y58                  ; 原点復帰LP
END
ENDH
