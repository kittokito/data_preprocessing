DEVICE:53
;MODULE:Return_CV
;MODULE_TYPE:0
;□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X1386                 ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
MPS
TMR K901 K5              ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
MPP
AND T901                 ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
OUT M5401                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
LD X1387                 ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
MPS
TMR K902 K3              ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
MPP
AND T902                 ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
OUT M5402                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降端
LD X1388                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
MPS
TMR K903 K3              ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
MPP
AND T903                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
OUT M5403                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
LD X1389                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
MPS
TMR K904 K3              ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
MPP
AND T904                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
OUT M5404                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
LDB X1384                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
MPS
TMR K905 K2              ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
MPP
AND T905                 ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
OUT M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
LDB X1385                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ ﾗｯｸ到着確認
MPS
TMR K906 K2              ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
MPP
AND T906                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
OUT M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
LDB X1383                ; ﾘﾀｰﾝCVﾗｯｸ通過確認
MPS
TMR K907 K3              ; ﾘﾀｰﾝCVﾗｯｸ通過確認
MPP
AND T907                 ; ﾘﾀｰﾝCVﾗｯｸ通過確認
OUT M5407                ; ﾘﾀｰﾝCVﾗｯｸ通過確認
LD CR2003                ; 常時OFF
MPS
TMR K908 K3
MPP
AND T908
OUT M5408
LD CR2003                ; 常時OFF
MPS
TMR K909 K3
MPP
AND T909
OUT M5409
;□
;□□ 装置状態・動作条件 □□
;□
LD X1389                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
OUT M5480                ; ﾕﾆｯﾄ9原点
;□ 非常停止
LDB B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常補助
ANB B0122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常
ANB B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
ANB B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
ANB B0125                ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
INV
OUT M5481                ; ﾕﾆｯﾄ9全異常
;□ 起動条件
LD M5480                 ; ﾕﾆｯﾄ9原点
ANB M5481                ; ﾕﾆｯﾄ9全異常
ANB M5485                ; ﾕﾆｯﾄ9起動中
OUT M5482                ; ﾕﾆｯﾄ9起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
AND M5484                ; ﾕﾆｯﾄ9原点復帰中
ORL
OR M5486                 ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M5483                ; ﾕﾆｯﾄ9起動OFF
;□ ｻｲｸﾙ停止異常
LDB B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
ANB B0125                ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
ANB B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
INV
OUT M5486                ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M5487                ; ﾕﾆｯﾄ9払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M5488                ; ﾕﾆｯﾄ9警報補助
;□ 機器準備確認
LD CR2002                ; 常時ON
OUT M5490                ; ﾕﾆｯﾄ9機器準備完了
;□ ｴﾗｰ発生　非常停止扱い
LDB B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
INV
AND CR2003               ; 常時OFF
OUT M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
;□ 払出しﾜｰｸ無し
LD M36                   ; 払出ﾓｰﾄﾞ
ANB Y2422                ; ﾘﾀｰﾝCV移動ON
TMR K914 K10             ; UNIT9払出処理ﾜｰｸ無
LD T914                  ; UNIT9払出処理ﾜｰｸ無
OUT M5493                ; ﾕﾆｯﾄ9ﾜｰｸ無し
LD M1493                 ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
AND M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
AND M2993                ; ﾕﾆｯﾄ4ﾜｰｸ無し
AND M3493                ; ﾕﾆｯﾄ5ﾜｰｸ無し
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
OUT M5494                ; 前工程ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
;□ 原点復帰
LDP M82                  ; 原点復帰開始
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
OR M5484                 ; ﾕﾆｯﾄ9原点復帰中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
ANB M5481                ; ﾕﾆｯﾄ9全異常
ANB M5249                ; ｻｲｸﾙ終了
MPS
OUT M5484                ; ﾕﾆｯﾄ9原点復帰中
MPP
AND M401                 ; 扉閉ﾃﾞｨﾚｰON
TMR K900 K300            ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ
;□ 起動中
LD M73                   ; 主幹起動指令
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OR M4485                 ; ﾕﾆｯﾄ7起動中
AND M5482                ; ﾕﾆｯﾄ9起動条件
OR M5485                 ; ﾕﾆｯﾄ9起動中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
ANB M5249                ; ｻｲｸﾙ終了
OUT M5485                ; ﾕﾆｯﾄ9起動中
;□□□□□□□□□□□□□□□
;□　　 　UNIT9自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>ﾕﾆｯﾄ9 自動運転回路
LD= D5000 D5005          ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9動作中判定
ANB Y2422                ; ﾘﾀｰﾝCV移動ON
ORB M5485                ; ﾕﾆｯﾄ9起動中
TMR K910 K20             ; ﾘﾀｰﾝCV動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D5000 D5005          ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9動作中判定
LD T910                  ; ﾘﾀｰﾝCV動作無し判定ﾀｲﾏ
OUT M5492                ; ﾕﾆｯﾄ9動作無し確認
;□
;□　ﾕﾆｯﾄ9 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M5000 K18
LDB M5484                ; ﾕﾆｯﾄ9原点復帰中
ANB M5485                ; ﾕﾆｯﾄ9起動中
MOV K0 D5000             ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5484                 ; ﾕﾆｯﾄ9原点復帰中
OR M5485                 ; ﾕﾆｯﾄ9起動中
MOV D5000 Z1             ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
SET M5000:Z1
LD M5000
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5001
ANB M130                 ; 一時停止
MPS
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
MPP
LD M82                   ; 原点復帰開始
AND M5484                ; ﾕﾆｯﾄ9原点復帰中
AND M84                  ; 原点復帰起動確認
ANL
MOV K250 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5002                 ; ﾘﾀｰﾝCVﾌﾟﾛｸﾞﾗﾑ待機中
ANB M130                 ; 一時停止
MPS
LD M3350                 ; ﾘﾀｰﾝCVへﾗｯｸ供給要求
ANB M4370                ; 排出ﾘﾌﾀ ﾘﾀｰﾝCVへ排出中
AND M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
AND M3470                ; 基板収納RB8待機位置
ANL
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
MRD
LDB M4370                ; 排出ﾘﾌﾀ ﾘﾀｰﾝCVへ排出中
AND M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
ANB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
ANL
MOV K20 D5000            ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
MPP
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M4485                ; ﾕﾆｯﾄ7起動中
ANB M4370                ; 排出ﾘﾌﾀ ﾘﾀｰﾝCVへ排出中
ANB M3350                ; ﾘﾀｰﾝCVへﾗｯｸ供給要求
ANL
TMR K980 K5              ; ﾕﾆｯﾄ9終了確認
CON
AND T980                 ; ﾕﾆｯﾄ9終了確認
MOV K249 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5003
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5004                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇
ANB M130                 ; 一時停止
AND M5401                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5005                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降
MPS
@MOV K1 D5130            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
MPP
LDB M130                 ; 一時停止
AND M5403                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
AND M3402                ; 基板収納ﾗｯｸ位置決め下降端
ANL
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5006                 ; ﾘﾀｰﾝCV回転/ﾗｯｸ収納CV正転
ANB M130                 ; 一時停止
AND Y2422                ; ﾘﾀｰﾝCV移動ON
AND Y2425                ; 収納ﾘﾌﾀCV正転ON
MPS
OUT M5350                ; 収納ﾘﾌﾀｰへ排出中
MPP
LD M3403                 ; 収納ﾘﾌﾀｰﾗｯｸ到着確認
ANB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
ANL
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5007                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
ANB M130                 ; 一時停止
AND M5404                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
ANB M3350                ; ﾘﾀｰﾝCVへﾗｯｸ供給要求
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5008                 ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶ﾘｾｯﾄ
@MOV K0 D5130            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
@MOV K249 D5000          ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5020
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5021                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
ANB M130                 ; 一時停止
AND M5404                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5022                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
ANB M130                 ; 一時停止
AND M5402                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降端
MPS
MOV K1 D5140             ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
MPP
AND= D5140 K1            ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5023                 ; ﾘﾀｰﾝCV回転
ANB M130                 ; 一時停止
AND Y2422                ; ﾘﾀｰﾝCV移動ON
AND M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
ANB M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5024
MPS
MOV K0 D5140             ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
MPP
AND= D5140 K0            ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
@MOV K249 D5000          ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
;<h1/>ﾕﾆｯﾄ9原点復帰
LD M5250
ANB Y2422                ; ﾘﾀｰﾝCV移動ON
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5251                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ/ｴｽｹｰﾊﾟ上昇
AND M5404                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
AND M5401                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5252                 ; ﾘﾀｰﾝCV回転
AND Y2422                ; ﾘﾀｰﾝCV移動ON
TMR K970 K50
CON
AND T970
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5253
MPS
ANB M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
MPP
AND M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
MOV K255 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5254                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
AND M5402                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降端
MOV K255 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5255
INC D5000                ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
LD M5256
MOV K249 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K249 D5000           ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M1062                 ; ﾕﾆｯﾄ1-1ｻｲｸﾙ停止処理
OR CR2003                ; 常時OFF
AND CR2003               ; 常時OFF
@MOV K249 D5000          ; ﾕﾆｯﾄ9自動運転ｽﾃｯﾌﾟ
;<h1/>UNIT9手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT9手動回路  　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M5485                ; ﾕﾆｯﾄ9起動中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
MC
LD CR2002                ; 常時ON
OUT M5301                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇可
LDP B1101                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SW
AND M5301                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇可
OR M5302                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇
ANPB B1100               ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SW
OUT M5302                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇
LD M5405                 ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
ANB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
LDB M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
AND M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
ORL
LDB M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
ANB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
ORL
OUT M5303                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降可
LDP B1100                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SW
AND M5303                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降可
OR M5304                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
ANPB B1101               ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SW
OUT M5304                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
LD= D3024 K1             ; 基板収納RB8_完了POS
ORB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
OUT M5305                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降可
LDP B1102                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SW
AND M5305                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降可
OR M5306                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降
ANPB B1103               ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SW
OUT M5306                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降
LD CR2002                ; 常時ON
OUT M5307                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇可
LDP B1103                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SW
AND M5307                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇可
OR M5308                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
ANPB B1102               ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SW
OUT M5308                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
LD CR2002                ; 常時ON
OUT M5309                ; ﾘﾀｰﾝCVﾓｰﾀON可
LDP B1104                ; ﾘﾀｰﾝCVﾓｰﾀON_SW
AND M5309                ; ﾘﾀｰﾝCVﾓｰﾀON可
OR M5310                 ; ﾘﾀｰﾝCVﾓｰﾀON
ANB M5312                ; ﾘﾀｰﾝCVﾓｰﾀOFF
LD= D3024 K1             ; 基板収納RB8_完了POS
OR X1385                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ ﾗｯｸ到着確認
ANL
OUT M5310                ; ﾘﾀｰﾝCVﾓｰﾀON
LD CR2002                ; 常時ON
OUT M5311                ; ﾘﾀｰﾝCVﾓｰﾀOFF可
LD B1105                 ; ﾘﾀｰﾝCVﾓｰﾀOFF_SW
AND M5311                ; ﾘﾀｰﾝCVﾓｰﾀOFF可
OUT M5312                ; ﾘﾀｰﾝCVﾓｰﾀOFF
MCR
;<h1/>出力(シリンダ等)
LDB M5302                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇
ANB M5251                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ/ｴｽｹｰﾊﾟ上昇
ANB M5004                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇
ANB M4063                ; ﾘﾀｰﾝCV排出開始/ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
ANB M5531                ; ﾘﾀｰﾝCV排出開始/ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
INV
LD Y2544                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SOL
ANB Y2545                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SOL
ORL
OUT Y2544                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SOL
LDB M5304                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
ANB M5254                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
ANB M5022                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降
INV
LD Y2545                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SOL
ANB Y2544                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SOL
ORL
OUT Y2545                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SOL
LDB M5306                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降
ANB M5005                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
LD Y2546                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SOL
ANB Y2547                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SOL
ORL
OUT Y2546                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SOL
LDB M5308                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
ANB M5251                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ/ｴｽｹｰﾊﾟ上昇
ANB M5021                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
ANB M5007                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇
INV
LD Y2547                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SOL
ANB Y2546                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SOL
ORL
OUT Y2547                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SOL
LDB M5310                ; ﾘﾀｰﾝCVﾓｰﾀON
ANB M5252                ; ﾘﾀｰﾝCV回転
LDB M5023                ; ﾘﾀｰﾝCV回転
ANB M5006                ; ﾘﾀｰﾝCV回転/ﾗｯｸ収納CV正転
OR M130                  ; 一時停止
ANL
ANB M4067                ; ﾘﾀｰﾝCV排出動作中
ANB M5535                ; ﾘﾀｰﾝCV排出動作中
INV
OUT Y2422                ; ﾘﾀｰﾝCV移動ON
;□□　　パネル表示　　□□
LD X1386                 ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
LD Y2544                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SOL
ANB X1386                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1601                ; UNIT5-1-2 TP-PL
LD X1387                 ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
LD Y2545                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SOL
ANB X1387                ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1600                ; UNIT5-1-1 TP-PL
LD X1388                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
LD Y2546                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SOL
ANB X1388                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1602                ; UNIT5-1-3 TP-PL
LD X1389                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
LD Y2547                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SOL
ANB X1389                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1603                ; UNIT5-1-4 TP-PL
LD Y2422                 ; ﾘﾀｰﾝCV移動ON
OUT B1604                ; UNIT5-1-5 TP-PL
LDB Y2422                ; ﾘﾀｰﾝCV移動ON
OUT B1605                ; UNIT5-1-6 TP-PL
;□□　　異常検出　　□□
LD M50                   ; 運転準備
AND T900                 ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄBZ停止
AND B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
@DW K288 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2120                 ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄBZ停止
AND B0120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
OUT B2120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄBZ停止
LD Y2544                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇SOL
ANB X1386                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ上昇端
LD Y2545                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ下降SOL
ANB X1387                ; ﾘﾀｰﾝCVｴｽｹ-ﾌﾟ下降端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常BZ停止
AND B0121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常補助
ORL
MPS
TMR K951 K50             ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS確認
MPP
AND T951                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS確認
OUT B0121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常補助
@DW K289 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2121                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常BZ停止
AND B0121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常補助
OUT B2121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常BZ停止
LD Y2546                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降SOL
ANB X1388                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ下降端
LD Y2547                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇SOL
ANB X1389                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常BZ停止
AND B0122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常
ORL
MPS
TMR K952 K50             ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS確認
MPP
AND T952                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS確認
OUT B0122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常
@DW K290 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2122                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常BZ停止
AND B0122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常
OUT B2122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常BZ停止
LD M50                   ; 運転準備
ANB X1302                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常BZ停止
AND B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
ORL
MPS
TMH K953 K10             ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常確認
MPP
AND T953                 ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常確認
OUT B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
@DW K291 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2123                 ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常BZ停止
AND B0123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
OUT B2123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常BZ停止
LD M16                   ; ﾘｾｯﾄPB
ANB X1302                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
OUT Y2423                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰﾘｾｯﾄ
LD M17                   ; 原点復帰PB
ANB M72                  ; 自動運転開始
AND= D5130 K1            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB B119F                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了確認SW
ORB B2124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了BZ停止
AND B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
ORL
OUT B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
@DW K292 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B119F                 ; ﾘﾀｰﾝCVﾗｯｸ排出未完了確認SW
OR B2124                 ; ﾘﾀｰﾝCVﾗｯｸ排出未完了BZ停止
AND B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
OUT B2124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了BZ停止
LDP B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
OR M5385                 ; ﾘﾀｰﾝCVﾗｯｸ排出未了ﾒｯｾｰｼﾞON
ANB B119F                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了確認SW
AND B0124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
OUT M5385                ; ﾘﾀｰﾝCVﾗｯｸ排出未了ﾒｯｾｰｼﾞON
LD B119F                 ; ﾘﾀｰﾝCVﾗｯｸ排出未完了確認SW
@MOV K0 D5130            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
LD M5350                 ; 収納ﾘﾌﾀｰへ排出中
ANB M3403                ; 収納ﾘﾌﾀｰﾗｯｸ到着確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2125                ; ﾘﾀｰﾝCVﾗｯｸ排出異常BZ停止
AND B0125                ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
ORL
MPS
TMR K954 K200
MPP
AND T954
OUT B0125                ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
@DW K293 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2125                 ; ﾘﾀｰﾝCVﾗｯｸ排出異常BZ停止
AND B0125                ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
OUT B2125                ; ﾘﾀｰﾝCVﾗｯｸ排出異常BZ停止
LD M17                   ; 原点復帰PB
ANB M72                  ; 自動運転開始
AND= D5140 K1            ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB B1199                ; ﾘﾀｰﾝCVﾗｯｸ位置異常確認SW
ORB B2126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常BZ停止
AND B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
ORL
OUT B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
@DW K294 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B1199                 ; ﾘﾀｰﾝCVﾗｯｸ位置異常確認SW
OR B2126                 ; ﾘﾀｰﾝCVﾗｯｸ位置異常BZ停止
AND B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
OUT B2126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常BZ停止
LDP B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
OR M5386                 ; ﾘﾀｰﾝCVﾗｯｸ位置異常ﾒｯｾｰｼﾞON
ANB B1199                ; ﾘﾀｰﾝCVﾗｯｸ位置異常確認SW
AND B0126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常
OUT M5386                ; ﾘﾀｰﾝCVﾗｯｸ位置異常ﾒｯｾｰｼﾞON
LD B1199                 ; ﾘﾀｰﾝCVﾗｯｸ位置異常確認SW
@MOV K0 D5140            ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
LD B0120                 ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄ異常
ANB B2120                ; ﾘﾀｰﾝCV原点復帰ﾀｲﾑｱｳﾄBZ停止
LD B0121                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常補助
ANB B2121                ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上下LS異常BZ停止
ORL
LD B0122                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常
ANB B2122                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上下LS異常BZ停止
ORL
LD B0123                 ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常
ANB B2123                ; ﾘﾀｰﾝCVﾄﾞﾗｲﾊﾞｰ異常BZ停止
ORL
LD B0124                 ; ﾘﾀｰﾝCVﾗｯｸ排出未完了
ANB B2124                ; ﾘﾀｰﾝCVﾗｯｸ排出未完了BZ停止
ORL
LD B0125                 ; ﾘﾀｰﾝCVﾗｯｸ排出ﾀｲﾑｵｰﾊﾞｰ異常
ANB B2125                ; ﾘﾀｰﾝCVﾗｯｸ排出異常BZ停止
ORL
LD B0126                 ; ﾘﾀｰﾝCVﾗｯｸ位置異常
ANB B2126                ; ﾘﾀｰﾝCVﾗｯｸ位置異常BZ停止
ORL
OUT M5489                ; ﾕﾆｯﾄ9異常BZ補助
END
ENDH
