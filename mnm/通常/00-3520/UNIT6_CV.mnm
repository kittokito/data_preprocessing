DEVICE:53
;MODULE:UNIT6_CV
;MODULE_TYPE:0
;<h1/>□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LDB X1204                ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
TMR K601 K300            ; OKｺﾝﾍﾞﾔ満杯時間
LDB X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
TMR K602 K300            ; NGｺﾝﾍﾞﾔ満杯時間
LD X1202                 ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認
TMR K603 K3              ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認OFFﾀｲﾏ
LD X1203                 ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認
TMR K604 K3              ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認OFFﾀｲﾏ
;<h1/>■ロボット等機器入力受け
;<h1/>□□ 装置状態・動作条件 □□
;ﾕﾆｯﾄまとめ
LD M3570                 ; ﾕﾆｯﾄ6-1原点
AND M3650                ; ﾕﾆｯﾄ6-2原点
AND M3730                ; ﾕﾆｯﾄ6-3原点
OUT M3980                ; ﾕﾆｯﾄ6原点
;□ 非常停止
LDB M3571                ; ﾕﾆｯﾄ6-1全異常
ANB M3651                ; ﾕﾆｯﾄ6-2異常
ANB M3731                ; ﾕﾆｯﾄ6-3全異常
INV
OUT M3981                ; ﾕﾆｯﾄ6即停止異常
;□ 起動条件
LD CR2003                ; 常時OFF
OUT M3982                ; ﾕﾆｯﾄ6起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
ORL
OR T15                   ; 強制停止
OUT M3983                ; ﾕﾆｯﾄ6起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ORP B1139                ; UNIT6-2-10 TP-PB
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
OR M3984                 ; ﾕﾆｯﾄ6原点復帰中
ANB M3983                ; ﾕﾆｯﾄ6起動OFF
ANB M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
ANB M3797                ; STEPEND
MPS
OUT M3984                ; ﾕﾆｯﾄ6原点復帰中
MPP
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1639                ; UNIT6-2-10 TP-PL
;□ 起動中
LDB M3574                ; ﾕﾆｯﾄ6-1起動中
ANB M3654                ; ﾕﾆｯﾄ6-2起動中
ANB M3734                ; ﾕﾆｯﾄ6-3起動中
INV
MPS
OUT M3985                ; ﾕﾆｯﾄ6起動中
MPP
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1638                ; UNIT6-2-9 TP-PL
;□ ｻｲｸﾙ停止異常
LDB M3575                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ停止異常
ANB M3655                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ停止異常
ANB M3735                ; ﾕﾆｯﾄ6-3ｻｲｸﾙ停止異常
INV
OUT M3986                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3987                ; ﾕﾆｯﾄ6払出停止異常
;□ 警報補助
LDB B0C0                 ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
ANB B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
INV
OUT M3988                ; ﾕﾆｯﾄ6警報補助
;□ 機器準備確認
LDB CR2003               ; 常時OFF
OUT M3990                ; ﾕﾆｯﾄ6機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB M3576                ; ﾕﾆｯﾄ6-1ｴﾗｰ発生
ANB M3656                ; ﾕﾆｯﾄ6-2ｴﾗｰ発生
ANB M3736                ; ﾕﾆｯﾄ6-3ｴﾗｰ発生
INV
OUT M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD M3577                 ; ﾕﾆｯﾄ6-1動作無し
AND M3657                ; ﾕﾆｯﾄ6-2動作無し
AND M3737                ; ﾕﾆｯﾄ6-3動作無し
OUT M3992                ; ﾕﾆｯﾄ6動作無し確認
;□ 払出しﾜｰｸ無し
LD X1200                 ; OKｺﾝﾍﾞｱﾜｰｸ受取部ﾜｰｸ確認(1無_0有)
AND X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
AND X1202                ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認
AND X1203                ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認
AND X1204                ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
AND X1208                ; NGｺﾝﾍﾞｱﾜｰｸ受取部ﾜｰｸ確認
AND X1209                ; NGｺﾝﾍﾞｱﾌﾟｯｼｬｰ部ﾜｰｸ確認
AND X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
ANB M1574                ; ﾕﾆｯﾄ2-1起動中
AND M36                  ; 払出ﾓｰﾄﾞ
TMR K614 K50             ; ｺﾝﾍﾞﾔ部ﾜｰｸ無し
LD T614                  ; ｺﾝﾍﾞﾔ部ﾜｰｸ無し
OUT M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M3979                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ動作
;<h1/>□　　 　UNIT1自動回路  　　□
;<h1/>　■　OKｺﾝﾍﾞﾔ（ﾕﾆｯﾄ1-1)状態・動作条件(補助)
LDB Y55                  ; OK品ｼﾌﾄｺﾝﾍﾞｱ電源
AND X1211                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
AND X1212                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
AND X1214                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
OUT M3570                ; ﾕﾆｯﾄ6-1原点
LDB CR2003               ; 常時OFF
INV
OUT M3571                ; ﾕﾆｯﾄ6-1全異常
LD M3570                 ; ﾕﾆｯﾄ6-1原点
ANB M3574                ; ﾕﾆｯﾄ6-1起動中
ANB M130                 ; 一時停止
OUT M3572                ; ﾕﾆｯﾄ6-1起動条件
LDB M50                  ; 運転準備
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3573                ; ﾕﾆｯﾄ6-1起動OFF
LD M73                   ; 各工程起動指令
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANP B1138                ; UNIT6-2-9 TP-PB
ORL
AND M3572                ; ﾕﾆｯﾄ6-1起動条件
OR M3574                 ; ﾕﾆｯﾄ6-1起動中
ANB M3573                ; ﾕﾆｯﾄ6-1起動OFF
ANB M3563                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ終了
OUT M3574                ; ﾕﾆｯﾄ6-1起動中
;ﾕﾆｯﾄ1-1 ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3575                ; ﾕﾆｯﾄ6-1ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-1 ｴﾗｰ発生まとめ
LDB B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
ANB B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
ANB B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
ANB B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
INV
OUT M3576                ; ﾕﾆｯﾄ6-1ｴﾗｰ発生
;ﾕﾆｯﾄ1-1 停止判定
LD= D3500 D3505          ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-1動作中判定
ORB M3574                ; ﾕﾆｯﾄ6-1起動中
TMR K610 K20             ; ﾕﾆｯﾄ6動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D3500 D3505          ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-1動作中判定
LD T610                  ; ﾕﾆｯﾄ6動作無し判定ﾀｲﾏ
OUT M3577                ; ﾕﾆｯﾄ6-1動作無し
;□
;□　ﾕﾆｯﾄ1-1 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3500 K4         ; U6-1_S0_
LDB M3574                ; ﾕﾆｯﾄ6-1起動中
MOV K0 D3500             ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
RES M3911                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
CON
RES M3912                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾌﾟ起動
LD M3574                 ; ﾕﾆｯﾄ6-1起動中
MOV D3500 Z1             ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
OUT M3500:Z1             ; U6-1_S0_
;条件分岐
LD M3500                 ; U6-1_S0_
AND X1200                ; OKｺﾝﾍﾞｱﾜｰｸ受取部ﾜｰｸ確認(1無_0有)
MPS
AND X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
ANB X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
MOV K10 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;<h1/>　　■ﾜｰｸ搭載待ち
LD M3501                 ; U6-1_S1_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
ANB M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
MPS
AND M2310                ; OKｺﾝﾍﾞﾔ給材開始
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M3900                ; OKｺﾝﾍﾞﾔ部_ﾜｰｸ供給要求
MPP
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
MOV K50 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3502                 ; U6-1_S2_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB M2184                ; U2-S684_
ANB X1200                ; OKｺﾝﾍﾞｱﾜｰｸ受取部ﾜｰｸ確認(1無_0有)
AND= D3701 K1            ; OKｺﾝﾍﾞﾔ_ﾜｰｸ有無記憶
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3503                 ; U6-1_S3_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND M1840                ; Z_搬送高さ
AND X1204                ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3504                 ; U6-1_S4_ﾓｰﾀｰ回転指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND Y312                 ; OK品ｼﾌﾄｺﾝﾍﾞｱｺﾝﾍﾞｱ 回転
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3505                 ; U6-1_S5_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ前進
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1210                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進端
ANB B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3506                 ; U6-1_S6_ﾌﾟｯｼｬｰ押し出しﾜｰｸ確認
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
AND T620                 ; ﾜｰｸﾃﾞｨﾚｲ
MPS
ANB X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@MOV K9 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
TMR K620 K10             ; ﾜｰｸﾃﾞｨﾚｲ
;　　ﾌﾟｯｼｬｰﾜｰｸ無し異常
LD M3507                 ; U6-1_S7_ﾌﾟｯｼｬｰ部ﾜｰｸ無し異常
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3508                 ; U6-1_S8_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
MPS
ANB X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@MOV K5 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@MOV K63 D3500           ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3509                 ; U6-1_S9_ﾌﾟｯｼｬｰ後退指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1211                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
@MOV K63 D3500           ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;　　ﾌﾟｯｼｬｰ押し出し部ﾜｰｸ排出処理
LD M3510                 ; U6-1_S10_OKｺﾝﾍﾞﾔ回転
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND Y312                 ; OK品ｼﾌﾄｺﾝﾍﾞｱｺﾝﾍﾞｱ 回転
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3511                 ; U6-1_S11_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
AND T605                 ; ﾜｰｸ確認時間
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPS
ANB X1202                ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認
@SET M3911               ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
MPP
ANB X1203                ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認
@SET M3912               ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾌﾟ起動
MPP
TMR K605 K10             ; ﾜｰｸ確認時間
;　　□ﾌﾟｯｼｬｰ押し出し部→ｴｽｹｰﾊﾟｰ部へﾜｰｸ移動確認
LD M3512                 ; U6-1_S12_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1201                ; OKｺﾝﾍﾞｱﾌﾟｯｼｬｰ押出部ﾜｰｸ確認(1無)
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3513                 ; U6-1_S13_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB X1202                ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3514                 ; U6-1_S14_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB M3911                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
ANB M3912                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾌﾟ起動
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3515                 ; U6-1_S15_
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MOV K63 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;　　払出し停止用動作
LD M3550                 ; U6-1_S50_ﾓｰﾀｰ回転指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
ANB X1204                ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
@INC D3500               ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
MPP
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
MOV K55 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3551                 ; U6-1_S51_ﾓｰﾀｰ停止ﾜｰｸ除去待ち
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1204                ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
MOV K50 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;　　ﾃﾞｰﾀ消去
LD M3555                 ; U6-1_S55_
MOV K63 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
;<h1/>　　■ｴｽｹｰﾊﾟｰ部ｻｲｸﾙ
LD CR2002                ; 常時ON
FMOV K0 M3660 K1         ; ｴｽｹｰﾊﾟstep0
LDB M3911                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
MOV K0 D3502             ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3911                 ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
MOV D3502 Z1             ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
OUT M3660:Z1             ; ｴｽｹｰﾊﾟstep0
;　　ｻｲｸﾙ開始
LD M3660                 ; ｴｽｹｰﾊﾟstep0
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
INC D3502                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3661                 ; ｴｽｹｰﾊﾟstep1
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB X1202                ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認
INC D3502                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3662                 ; ｴｽｹｰﾊﾟstep2_ｴｽｹｰﾊﾟｰ下降指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1215                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降端
ANB B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
INC D3502                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3663                 ; ｴｽｹｰﾊﾟstep3
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND T603                 ; OKｺﾝﾍﾞｱｴｽｹｰﾊﾟｰ部ﾜｰｸ確認OFFﾀｲﾏ
INC D3502                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3664                 ; ｴｽｹｰﾊﾟstep4_ｴｽｹｰﾊﾟｰ上昇指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1214                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
ANB B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
INC D3502                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ
LD M3665                 ; ｴｽｹｰﾊﾟstep5
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
RES M3911                ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
;<h1/>　　■ｽﾄｯﾊﾟｰ部ｻｲｸﾙ
LD CR2002                ; 常時ON
FMOV K0 M3680 K1         ; ｽﾄｯﾊﾟｰstep0
LDB M3912                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾌﾟ起動
MOV K0 D3503             ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3911                 ; ｴｽｹｰﾊﾟｰ部ｽﾃｯﾌﾟ起動
MOV D3503 Z1             ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
OUT M3680:Z1             ; ｽﾄｯﾊﾟｰstep0
;　　ｻｲｸﾙ開始
LD M3680                 ; ｽﾄｯﾊﾟｰstep0
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
INC D3503                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3681                 ; ｽﾄｯﾊﾟｰstep1
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB X1203                ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認
INC D3503                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3682                 ; ｽﾄｯﾊﾟｰstep2_ｽﾄｯﾊﾟｰ下降指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1213                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降端
ANB B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
INC D3503                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3683                 ; ｽﾄｯﾊﾟｰstep3
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND T604                 ; OKｺﾝﾍﾞｱｽｯﾄﾊﾟｰ部ﾜｰｸ確認OFFﾀｲﾏ
INC D3503                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3684                 ; ｽﾄｯﾊﾟｰstep4_ｽﾄｯﾊﾟｰ上昇指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1212                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
ANB B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
INC D3503                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾊﾟー
LD M3685                 ; ｽﾄｯﾊﾟｰstep5
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
RES M3912                ; ｽﾄｯﾊﾟｰ部ｽﾃｯﾌﾟ起動
;<h1/>　■　NGｺﾝﾍﾞﾔ（ﾕﾆｯﾄ1-2状態・動作条件(補助)
LDB Y56                  ; NG品ｼﾌﾄｺﾝﾍﾞｱ電源
AND X1217                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
OUT M3650                ; ﾕﾆｯﾄ6-2原点
LDB CR2003               ; 常時OFF
INV
OUT M3651                ; ﾕﾆｯﾄ6-2異常
LD M3650                 ; ﾕﾆｯﾄ6-2原点
ANB M3654                ; ﾕﾆｯﾄ6-2起動中
ANB M130                 ; 一時停止
OUT M3652                ; ﾕﾆｯﾄ6-2起動条件
LDB M50                  ; 運転準備
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3653                ; ﾕﾆｯﾄ6-2起動OFF
LD M73                   ; 各工程起動指令
AND M3652                ; ﾕﾆｯﾄ6-2起動条件
OR M3654                 ; ﾕﾆｯﾄ6-2起動中
ANB M3653                ; ﾕﾆｯﾄ6-2起動OFF
ANB M3643                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ終了
OUT M3654                ; ﾕﾆｯﾄ6-2起動中
;ﾕﾆｯﾄ1-2ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3655                ; ﾕﾆｯﾄ6-2ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-2ｴﾗｰ発生まとめ
LDB B0D2                 ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
ANB B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
INV
OUT M3656                ; ﾕﾆｯﾄ6-2ｴﾗｰ発生
;ﾕﾆｯﾄ1-2停止判定
LD= D3501 D3506          ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-2動作中判定
ORB M3654                ; ﾕﾆｯﾄ6-2起動中
TMR K611 K20
LD CR2002                ; 常時ON
MOV D3501 D3506          ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6-2動作中判定
LD T611
LD CR2003                ; 常時OFF
AND M31                  ; 手動ﾓｰﾄﾞ
ORL
OUT M3657                ; ﾕﾆｯﾄ6-2動作無し
;□
;□　ﾕﾆｯﾄ1-2ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3580 K4         ; U6-2_S0
LDB M3654                ; ﾕﾆｯﾄ6-2起動中
MOV K0 D3501             ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3654                 ; ﾕﾆｯﾄ6-2起動中
MOV D3501 Z1             ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
OUT M3580:Z1             ; U6-2_S0
;<h1/>　　■ﾜｰｸ搭載待ち
LD M3580                 ; U6-2_S0
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
ANB M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
MPS
AND M2311                ; NGｺﾝﾍﾞﾔ給材開始
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
OUT M3901                ; NGｺﾝﾍﾞﾔ部_ﾜｰｸ供給要求
MPP
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
MOV K20 D3500            ; ﾕﾆｯﾄ6-1自動運転ｽﾃｯﾌﾟ
LD M3581                 ; U6-2_S1
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB M2184                ; U2-S684_
ANB X1208                ; NGｺﾝﾍﾞｱﾜｰｸ受取部ﾜｰｸ確認
AND= D3801 K1            ; NGｺﾝﾍﾞﾔ_ﾜｰｸ有無記憶
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3582                 ; U6-2_S2_満杯検出
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND M1840                ; Z_搬送高さ
AND X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3583                 ; U6-2_S3_NGｺﾝﾍﾞﾔ回転指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND Y313                 ; NG品ｼﾌﾄｺﾝﾍﾞｱｺﾝﾍﾞｱ 回転
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3584                 ; U6-2_S4_ﾌﾟｯｼｬｰ前進指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1216                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進端
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3585                 ; U6-2_S5_ﾌﾟｯｼｬｰ後退指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1217                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3586                 ; U6-2_S6
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
AND T621                 ; ﾜｰｸﾃﾞｨﾚｲ
MPS
AND X1209                ; NGｺﾝﾍﾞｱﾌﾟｯｼｬｰ部ﾜｰｸ確認
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
ANB X1209                ; NGｺﾝﾍﾞｱﾌﾟｯｼｬｰ部ﾜｰｸ確認
@MOV K10 D3501           ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
TMR K621 K10             ; ﾜｰｸﾃﾞｨﾚｲ
;　　NGﾌﾟｯｼｬｰ搬送異常
LD M3587                 ; U6-2_S7_NGｺﾝﾍﾞﾔ部ﾌﾟｯｼｬｰﾜｰｸ搬送異
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3588                 ; U6-2_S8
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
MPS
AND X1209                ; NGｺﾝﾍﾞｱﾌﾟｯｼｬｰ部ﾜｰｸ確認
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
ANB X1209                ; NGｺﾝﾍﾞｱﾌﾟｯｼｬｰ部ﾜｰｸ確認
@MOV K6 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3589                 ; U6-2_S9
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@MOV K63 D3501           ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;　　ｺﾝﾍﾞﾔﾓｰﾀｰ回転
LD M3590                 ; U6-2_S10_NGｺﾝﾍﾞﾔ起動指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ANB X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3591                 ; U6-2_S11
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3592                 ; U6-2_S12
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3593                 ; U6-2_S13
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3594                 ; U6-2_S14
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3595                 ; U6-2_S15
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MOV K63 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;　　払出し停止用動作
LD M3600                 ; U6-2_S20_NGｺﾝﾍﾞﾔ起動指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
MPS
ANB X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
@INC D3501               ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
MPP
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
MOV K25 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
LD M3601                 ; U6-2_S21
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X120A                ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
ANB B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
MOV K20 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;　　ﾃﾞｰﾀ消去
LD M3605                 ; U6-2_S25
MOV K63 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D3501            ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M3642                 ; U6-2_S62
OR CR2003                ; 常時OFF
@MOV K63 D3501           ; ﾕﾆｯﾄ6-2自動運転ｽﾃｯﾌﾟ
;<h1/>□　　 UNIT1原点復帰回路　　□
LD CR2002                ; 常時ON
FMOV K0 M3750 K3         ; STEP0
LDB M3984                ; ﾕﾆｯﾄ6原点復帰中
MOV K0 D3510             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3984                 ; ﾕﾆｯﾄ6原点復帰中
MOV D3510 Z1             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
OUT M3750:Z1             ; STEP0
;原点復帰ｻｲｸﾙ監視
LD M3984                 ; ﾕﾆｯﾄ6原点復帰中
TMR K600 K200            ; ﾗｯｸ排出CV原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M3750                 ; STEP0
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3751                 ; STEP1
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND M1840                ; Z_搬送高さ
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3752                 ; STEP2_ﾌﾟｯｼｬｰ後退指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1211                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
AND X1217                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3753                 ; STEP3_ｽﾄｯﾊﾟｰ上昇指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND X1212                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
AND X1214                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3754                 ; STEP4
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3755                 ; STEP5
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3756                 ; STEP6
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3757                 ; STEP7
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3758                 ; STEP8
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3759                 ; STEP9
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3760                 ; STEP10
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3761                 ; STEP11
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3762                 ; STEP12
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3763                 ; STEP13
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3764                 ; STEP14
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3765                 ; STEP15
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3766                 ; STEP16
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3767                 ; STEP17
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3768                 ; STEP18
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3769                 ; STEP19
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
@INC D3510               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M3770                 ; STEP20
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
AND M3980                ; ﾕﾆｯﾄ6原点
@MOV K46 D3510           ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M3796                 ; STEP46
ANB M82                  ; 原点復帰開始
@MOV K47 D3510           ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;<h1/>□　　 　UNIT1手動回路  　　□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
MC
;OKｺﾝﾍﾞﾔ起動
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3800                ; OKｺﾝﾍﾞﾔ起動OK
LDP B1120                ; OKｺﾝﾍﾞﾔ起動ONSW
AND M3800                ; OKｺﾝﾍﾞﾔ起動OK
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
ANB M3803                ; OKｺﾝﾍﾞﾔ起動OFF指令
ORL
OUT M3801                ; OKｺﾝﾍﾞﾔ起動指令
LD CR2002                ; 常時ON
OUT M3802                ; OKｺﾝﾍﾞﾔ起動OFF_OK
LDP B1121                ; OKｺﾝﾍﾞﾔ起動OFFSW
AND M3802                ; OKｺﾝﾍﾞﾔ起動OFF_OK
LD M3803                 ; OKｺﾝﾍﾞﾔ起動OFF指令
ANB M3801                ; OKｺﾝﾍﾞﾔ起動指令
ORL
OUT M3803                ; OKｺﾝﾍﾞﾔ起動OFF指令
;NGｺﾝﾍﾞﾔ起動
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3804                ; NGｺﾝﾍﾞﾔ起動OK
LDP B1122                ; NGｺﾝﾍﾞﾔ起動ONSW
ORP X30B                 ; NGｺﾝﾍﾞﾔ部ﾜｰｸ取出ｽｲｯﾁ
AND M3804                ; NGｺﾝﾍﾞﾔ起動OK
LD M3805                 ; NGｺﾝﾍﾞﾔ起動指令
ANB M3807                ; NGｺﾝﾍﾞﾔ起動OFF指令
ORL
OUT M3805                ; NGｺﾝﾍﾞﾔ起動指令
LD CR2002                ; 常時ON
OUT M3806                ; NGｺﾝﾍﾞﾔ起動OK
LDP B1123                ; NGｺﾝﾍﾞﾔ起動OFFSW
ORF X30B                 ; NGｺﾝﾍﾞﾔ部ﾜｰｸ取出ｽｲｯﾁ
AND M3806                ; NGｺﾝﾍﾞﾔ起動OK
LD M3807                 ; NGｺﾝﾍﾞﾔ起動OFF指令
ANB M3805                ; NGｺﾝﾍﾞﾔ起動指令
ORL
OUT M3807                ; NGｺﾝﾍﾞﾔ起動OFF指令
;OKｺﾝﾍﾞﾔｽﾄｯﾊﾟｰ上下
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3808                ; ｽﾄｯﾊﾟｰ上昇OK
LDP B1124                ; OKｽﾄｯﾊﾟｰ上昇SW
AND M3808                ; ｽﾄｯﾊﾟｰ上昇OK
LD M3809                 ; ｽﾄｯﾊﾟｰ上昇指令
ANB M3811                ; ｽﾄｯﾊﾟｰ下降指令
ORL
OUT M3809                ; ｽﾄｯﾊﾟｰ上昇指令
LD CR2002                ; 常時ON
OUT M3810                ; ｽﾄｯﾊﾟｰ下降OK
LDP B1125                ; OKｽﾄｯﾊﾟｰ下降SW
AND M3810                ; ｽﾄｯﾊﾟｰ下降OK
LD M3811                 ; ｽﾄｯﾊﾟｰ下降指令
ANB M3809                ; ｽﾄｯﾊﾟｰ上昇指令
ORL
OUT M3811                ; ｽﾄｯﾊﾟｰ下降指令
;OKｴｽｹｰﾊﾟｰ上下
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3812                ; ｴｽｹｰﾊﾟｰ上昇OK
LDP B1126                ; OKｴｽｹｰﾊﾟｰ上昇SW
AND M3812                ; ｴｽｹｰﾊﾟｰ上昇OK
LD M3813                 ; ｴｽｹｰﾊﾟｰ上昇指令
ANB M3815                ; ｴｽｹｰﾊﾟｰ下降指令
ORL
OUT M3813                ; ｴｽｹｰﾊﾟｰ上昇指令
LD CR2002                ; 常時ON
OUT M3814                ; ｴｽｹｰﾊﾟｰ下降OK
LDP B1127                ; OKｴｽｹｰﾊﾟｰ下降SW
AND M3814                ; ｴｽｹｰﾊﾟｰ下降OK
LD M3815                 ; ｴｽｹｰﾊﾟｰ下降指令
ANB M3813                ; ｴｽｹｰﾊﾟｰ上昇指令
ORL
OUT M3815                ; ｴｽｹｰﾊﾟｰ下降指令
;OKﾌﾟｯｼｬｰ前後
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3816                ; OKﾌﾟｯｼｬｰ前進OK
LDP B1128                ; OKﾌﾟｯｼｬｰ前進SW
AND M3816                ; OKﾌﾟｯｼｬｰ前進OK
LD M3817                 ; OKﾌﾟｯｼｬｰ前進指令
ANB M3819                ; OKﾌﾟｯｼｬｰ後退指令
ORL
OUT M3817                ; OKﾌﾟｯｼｬｰ前進指令
LD CR2002                ; 常時ON
OUT M3818                ; OKﾌﾟｯｼｬｰ後退OK
LDP B1129                ; OKﾌﾟｯｼｬｰ後退SW
AND M3818                ; OKﾌﾟｯｼｬｰ後退OK
LD M3819                 ; OKﾌﾟｯｼｬｰ後退指令
ANB M3817                ; OKﾌﾟｯｼｬｰ前進指令
ORL
OUT M3819                ; OKﾌﾟｯｼｬｰ後退指令
;NGﾌﾟｯｼｬｰ上下
LD CR2002                ; 常時ON
AND M129                 ; 安全入力OK
AND L999
OUT M3820                ; NGﾌﾟｯｼｬｰ前進OK
LDP B112A                ; NGｽﾄｯﾊﾟｰ上昇SW
AND M3820                ; NGﾌﾟｯｼｬｰ前進OK
LD M3821                 ; NGﾌﾟｯｼｬｰ前進指令
ANB M3823                ; NGﾌﾟｯｼｬｰ後退指令
ORL
OUT M3821                ; NGﾌﾟｯｼｬｰ前進指令
LD CR2002                ; 常時ON
OUT M3822                ; NGﾌﾟｯｼｬｰ後退NG
LDP B112B                ; NGｽﾄｯﾊﾟｰ下降SW
AND M3822                ; NGﾌﾟｯｼｬｰ後退NG
LD M3823                 ; NGﾌﾟｯｼｬｰ後退指令
ANB M3821                ; NGﾌﾟｯｼｬｰ前進指令
ORL
OUT M3823                ; NGﾌﾟｯｼｬｰ後退指令
;　　OKｺﾝﾍﾞﾔﾜｰｸ取り出し動作
LDP X30A                 ; OKｺﾝﾍﾞﾔ部ﾜｰｸ取出ｽｲｯﾁ
OR M3830                 ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
ANB B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
ANB B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
ANB M3835                ; ｽﾄｯﾊﾟｰ上昇確認&ｻｲｸﾙ停止
OUT M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
LD M3830                 ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
AND M3810                ; ｽﾄｯﾊﾟｰ下降OK
AND M3814                ; ｴｽｹｰﾊﾟｰ下降OK
OR M3831                 ; ｽﾄｯﾊﾟｰ下降指令
AND M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
OUT M3831                ; ｽﾄｯﾊﾟｰ下降指令
LD M3831                 ; ｽﾄｯﾊﾟｰ下降指令
AND X1213                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降端
AND X1215                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降端
AND M3800                ; OKｺﾝﾍﾞﾔ起動OK
OR M3832                 ; OKｺﾝﾍﾞﾔ回転指令
AND M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
ANB M3834                ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
OUT M3832                ; OKｺﾝﾍﾞﾔ回転指令
LD M3832                 ; OKｺﾝﾍﾞﾔ回転指令
ANB X30A                 ; OKｺﾝﾍﾞﾔ部ﾜｰｸ取出ｽｲｯﾁ
OR M3833                 ; ｽｲｯﾁ離し確認
AND M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
OUT M3833                ; ｽｲｯﾁ離し確認
LD M3833                 ; ｽｲｯﾁ離し確認
AND M3808                ; ｽﾄｯﾊﾟｰ上昇OK
AND M3812                ; ｴｽｹｰﾊﾟｰ上昇OK
OR M3834                 ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
AND M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
OUT M3834                ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
LD M3834                 ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
AND X1212                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
AND X1214                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
OR M3835                 ; ｽﾄｯﾊﾟｰ上昇確認&ｻｲｸﾙ停止
AND M3830                ; OKｺﾝﾍﾞﾔ起動指令(ﾜｰｸ取り出し用)
OUT M3835                ; ｽﾄｯﾊﾟｰ上昇確認&ｻｲｸﾙ停止
MCR
;<h1/>□□　　出力回路　　□□
;ソレノイドバルブ出力
;ﾓｰﾀｰ回転指令
LDB M3504                ; U6-1_S4_ﾓｰﾀｰ回転指令
ANB M3505                ; U6-1_S5_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ前進
ANB M3510                ; U6-1_S10_OKｺﾝﾍﾞﾔ回転
INV
LD M3909                 ; OKｺﾝﾍﾞﾔ起動指令
ANB M3507                ; U6-1_S7_ﾌﾟｯｼｬｰ部ﾜｰｸ無し異常
ORL
AND M3574                ; ﾕﾆｯﾄ6-1起動中
OUT M3909                ; OKｺﾝﾍﾞﾔ起動指令
LDB M3583                ; U6-2_S3_NGｺﾝﾍﾞﾔ回転指令
INV
OR M3910                 ; NGｺﾝﾍﾞﾔ起動指令
AND M3654                ; ﾕﾆｯﾄ6-2起動中
OUT M3910                ; NGｺﾝﾍﾞﾔ起動指令
LD M50                   ; 運転準備
OUT Y55                  ; OK品ｼﾌﾄｺﾝﾍﾞｱ電源
OUT Y56                  ; NG品ｼﾌﾄｺﾝﾍﾞｱ電源
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
LD M3909                 ; OKｺﾝﾍﾞﾔ起動指令
OR M3832                 ; OKｺﾝﾍﾞﾔ回転指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ORL
AND M50                  ; 運転準備
OUT Y312                 ; OK品ｼﾌﾄｺﾝﾍﾞｱｺﾝﾍﾞｱ 回転
LD M3803                 ; OKｺﾝﾍﾞﾔ起動OFF指令
LD M3910                 ; NGｺﾝﾍﾞﾔ起動指令
AND T201                 ; 搬送部安全OK
ANB M130                 ; 一時停止
ORL
AND M50                  ; 運転準備
OUT Y313                 ; NG品ｼﾌﾄｺﾝﾍﾞｱｺﾝﾍﾞｱ 回転
;OKｽﾄｯﾊﾟｰ上下
LD M3809                 ; ｽﾄｯﾊﾟｰ上昇指令
AND M3808                ; ｽﾄｯﾊﾟｰ上昇OK
OR M3684                 ; ｽﾄｯﾊﾟｰstep4_ｽﾄｯﾊﾟｰ上昇指令
OR M3753                 ; STEP3_ｽﾄｯﾊﾟｰ上昇指令
OR M3834                 ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
LD Y2366                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇
ANB Y2367                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降
ORL
AND M50                  ; 運転準備
OUT Y2366                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇
LD M3811                 ; ｽﾄｯﾊﾟｰ下降指令
AND M3810                ; ｽﾄｯﾊﾟｰ下降OK
OR M3682                 ; ｽﾄｯﾊﾟｰstep2_ｽﾄｯﾊﾟｰ下降指令
OR M3831                 ; ｽﾄｯﾊﾟｰ下降指令
LD Y2367                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降
ANB Y2366                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇
ORL
AND M50                  ; 運転準備
OUT Y2367                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降
;OKｴｽｹｰﾊﾟｰ上下
LD M3813                 ; ｴｽｹｰﾊﾟｰ上昇指令
AND M3812                ; ｴｽｹｰﾊﾟｰ上昇OK
OR M3664                 ; ｴｽｹｰﾊﾟstep4_ｴｽｹｰﾊﾟｰ上昇指令
OR M3753                 ; STEP3_ｽﾄｯﾊﾟｰ上昇指令
OR M3834                 ; ｽﾄｯﾊﾟｰ上昇指令&ﾓｰﾀｰ停止指令
LD Y2368                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇
ANB Y2369                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降
ORL
AND M50                  ; 運転準備
OUT Y2368                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇
LD M3815                 ; ｴｽｹｰﾊﾟｰ下降指令
AND M3814                ; ｴｽｹｰﾊﾟｰ下降OK
OR M3662                 ; ｴｽｹｰﾊﾟstep2_ｴｽｹｰﾊﾟｰ下降指令
OR M3831                 ; ｽﾄｯﾊﾟｰ下降指令
LD Y2369                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降
ANB Y2368                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇
ORL
AND M50                  ; 運転準備
OUT Y2369                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降
;OKﾌﾟｯｼｬｰ前後
LD M3817                 ; OKﾌﾟｯｼｬｰ前進指令
AND M3816                ; OKﾌﾟｯｼｬｰ前進OK
OR M3505                 ; U6-1_S5_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ前進
LD Y2364                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進
ANB Y2365                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退
ORL
AND M50                  ; 運転準備
OUT Y2364                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進
LD M3819                 ; OKﾌﾟｯｼｬｰ後退指令
AND M3818                ; OKﾌﾟｯｼｬｰ後退OK
OR M3752                 ; STEP2_ﾌﾟｯｼｬｰ後退指令
OR M3509                 ; U6-1_S9_ﾌﾟｯｼｬｰ後退指令
LD Y2365                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退
ANB Y2364                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進
ORL
AND M50                  ; 運転準備
OUT Y2365                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退
;NGﾌﾟｯｼｬｰ前後
LD M3821                 ; NGﾌﾟｯｼｬｰ前進指令
AND M3820                ; NGﾌﾟｯｼｬｰ前進OK
OR M3584                 ; U6-2_S4_ﾌﾟｯｼｬｰ前進指令
LD Y236A                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進
ANB Y236B                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退
ORL
AND M50                  ; 運転準備
OUT Y236A                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進
LD M3823                 ; NGﾌﾟｯｼｬｰ後退指令
AND M3820                ; NGﾌﾟｯｼｬｰ前進OK
OR M3584                 ; U6-2_S4_ﾌﾟｯｼｬｰ前進指令
OR M3752                 ; STEP2_ﾌﾟｯｼｬｰ後退指令
LD Y236B                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退
ANB Y236A                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進
ORL
AND M50                  ; 運転準備
OUT Y236B                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退
;■その他制御出力
LD X1204                 ; OKｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT Y31A                 ; ﾜｰｸ排出部ﾜｰｸ取出可ﾗﾝﾌﾟ
OUT Y31B                 ; ﾜｰｸ取出部ﾌﾞｻﾞｰ
LD X120A                 ; NGｺﾝﾍﾞｱ取出し部ﾜｰｸ確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT Y32A                 ; NGｺﾝﾍﾞﾔ部ﾜｰｸ取出可ﾗﾝﾌﾟ
OUT Y32B                 ; NGｺﾝﾍﾞﾔ部ﾌﾞｻﾞｰ
;□□　　パネル表示　　□□
LD M3801                 ; OKｺﾝﾍﾞﾔ起動指令
OUT B1620                ; OKｺﾝﾍﾞﾔ起動ONSWPL
LDB M3801                ; OKｺﾝﾍﾞﾔ起動指令
OUT B1621                ; OKｺﾝﾍﾞﾔ起動OFFSWPL
LD M3805                 ; NGｺﾝﾍﾞﾔ起動指令
OUT B1622                ; NGｺﾝﾍﾞﾔ起動ONSWPL
LDB M3805                ; NGｺﾝﾍﾞﾔ起動指令
OUT B1623                ; NGｺﾝﾍﾞﾔ起動OFFSWPL
;OKｽﾄｯﾊﾟｰ上下
LD X1212                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
LD Y2366                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1624                ; OKｽﾄｯﾊﾟｰ上昇SWPL
LD X1213                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降端
LD Y2367                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1625                ; OKｽﾄｯﾊﾟｰ下降SWPL
;OKｴｽｹｰﾊﾟｰ上下
LD X1214                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
LD Y2368                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1626                ; OKｴｽｹｰﾊﾟｰ上昇SWPL
LD X1215                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降端
LD Y2369                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1627                ; OKｴｽｹｰﾊﾟｰ下降SWPL
;OKﾌﾟｯｼｬｰ前後
LD X1210                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進端
LD Y2364                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1628                ; OKﾌﾟｯｼｬｰ前進SWPL
LD X1211                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
LD Y2365                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1629                ; OKﾌﾟｯｼｬｰ後退SWPL
;NGﾌﾟｯｼｬｰ前後
LD X1216                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進端
LD Y236A                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B162A                ; NGｽﾄｯﾊﾟｰ上昇SWPL
LD X1217                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
LD Y236B                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B162B                ; NGｽﾄｯﾊﾟｰ下降SWPL
;<h1/>□□　　異常検出　　□□
LD T601                  ; OKｺﾝﾍﾞﾔ満杯時間
AND M50                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B05C0                ; 192)_U6E1Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C0                 ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
ORL
OUT B0C0                 ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
LD B0C0                  ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20C0
MOV K193 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C0                 ; 192)_U6E1Bz停止
AND B0C0                 ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
OUT B05C0                ; 192)_U6E1Bz停止
LD T602                  ; NGｺﾝﾍﾞﾔ満杯時間
AND M50                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B05C1                ; 193)_U6E2Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
ORL
OUT B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
LD B0C1                  ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20C1
MOV K194 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C1                 ; 193)_U6E2Bz停止
AND B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
OUT B05C1                ; 193)_U6E2Bz停止
LD M3507                 ; U6-1_S7_ﾌﾟｯｼｬｰ部ﾜｰｸ無し異常
LD CR2003                ; 常時OFF
ANB B05C2                ; 194)_U6E3Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
ORL
OUT B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
LD B0C2                  ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20C2
MOV K195 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C2                 ; 194)_U6E3Bz停止
AND B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
OUT B05C2                ; 194)_U6E3Bz停止
LD M3587                 ; U6-2_S7_NGｺﾝﾍﾞﾔ部ﾌﾟｯｼｬｰﾜｰｸ搬送異
LD CR2003                ; 常時OFF
ANB B05C3                ; 195)_U6E4Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
ORL
OUT B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
LD B0C3                  ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20C3
MOV K196 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C3                 ; 195)_U6E4Bz停止
AND B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
OUT B05C3                ; 195)_U6E4Bz停止
;
LD Y2366                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇
ANB X1212                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
LD Y2367                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降
ANB X1213                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降端
ORL
LD X1212                 ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ上昇端
AND X1213                ; OKｺﾝﾍﾞｱﾜｰｸｽﾄｯﾊﾟ下降端
ORL
LD CR2003                ; 常時OFF
ANB B05D0                ; 208)_U6E17Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
ORL
MPS
AND T630                 ; CLﾀｲﾏ
OUT B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
MPP
TMR K630 K30             ; CLﾀｲﾏ
LD B0D0                  ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20D0
MOV K208 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05D0                 ; 208)_U6E17Bz停止
AND B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
OUT B05D0                ; 208)_U6E17Bz停止
;
LD Y2368                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇
ANB X1214                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
LD Y2369                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降
ANB X1215                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降端
ORL
LD X1214                 ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ上昇端
AND X1215                ; OKｺﾝﾍﾞｱﾜｰｸｴｽｹｰﾊﾟｰ下降端
ORL
LD CR2003                ; 常時OFF
ANB B05D1                ; 209)_U6E18Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
ORL
MPS
AND T631                 ; CLﾀｲﾏ
OUT B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
MPP
TMR K631 K30             ; CLﾀｲﾏ
LD B0D1                  ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20D1
MOV K209 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05D1                 ; 209)_U6E18Bz停止
AND B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
OUT B05D1                ; 209)_U6E18Bz停止
;
LD Y236A                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進
ANB X1216                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進端
LD Y236B                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退
ANB X1217                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
ORL
LD X1216                 ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ前進端
AND X1217                ; NGｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬ後退端
ORL
LD CR2003                ; 常時OFF
ANB B05D2                ; 210)_U6E19Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0D2                 ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
ORL
MPS
AND T632                 ; CLSﾀｲﾏ
OUT B0D2                 ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
MPP
TMR K632 K30             ; CLSﾀｲﾏ
LD B0D2                  ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20D2
MOV K210 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05D2                 ; 210)_U6E19Bz停止
AND B0D2                 ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
OUT B05D2                ; 210)_U6E19Bz停止
;
LD Y2364                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進
ANB X1210                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進端
LD Y2365                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退
ANB X1211                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
ORL
LD X1210                 ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後前進端
AND X1211                ; OKｺﾝﾍﾞｱﾜｰｸﾌﾟｯｼｬ前後後退端
ORL
LD CR2003                ; 常時OFF
ANB B05D3                ; 211)_U6E20Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
ORL
MPS
AND T633                 ; 炉排出ﾗｯｸ到着確認
OUT B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
MPP
TMR K633 K30             ; 炉排出ﾗｯｸ到着確認
LD B0D3                  ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
AND= D200 K0             ; ｱﾗｰﾑｺｰﾄﾞ番号
SET B20D3
MOV K211 D200            ; ｱﾗｰﾑｺｰﾄﾞ番号
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05D3                 ; 211)_U6E20Bz停止
AND B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
OUT B05D3                ; 211)_U6E20Bz停止
;異常ﾌﾞｻﾞｰまとめ
LDB B0C0                 ; 192)_U6E1_OKｺﾝﾍﾞﾔ満杯警報
OR B05C0                 ; 192)_U6E1Bz停止
LDB B0C1                 ; 193)_U6E2_NGｺﾝﾍﾞﾔ満杯警報
OR B05C1                 ; 193)_U6E2Bz停止
ANL
LDB B0C2                 ; 194)_U6E3_OKｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
OR B05C2                 ; 194)_U6E3Bz停止
ANL
LDB B0C3                 ; 195)_U6E4_NGｺﾝﾍﾞﾔﾌﾟｯｼｬｰ搬送異常
OR B05C3                 ; 195)_U6E4Bz停止
ANL
LDB B0C4                 ; 196)_U6E5
OR B05C4                 ; 196)_U6E5Bz停止
ANL
LDB B0C5                 ; 197)_U6E6
OR B05C5                 ; 197)_U6E6Bz停止
ANL
LDB B0C6                 ; 198)_U6E7
OR B05C6                 ; 198)_U6E7Bz停止
ANL
LDB B0C7                 ; 199)_U6E8
OR B05C7                 ; 199)_U6E8Bz停止
ANL
LDB B0C8                 ; 200)_U6E9
OR B05C8                 ; 200)_U6E9Bz停止
ANL
LDB B0C9                 ; 201)_U6E10
OR B05C9                 ; 201)_U6E10Bz停止
ANL
LDB B0CA                 ; 202)_U6E11
OR B05CA                 ; 202)_U6E11Bz停止
ANL
LDB B0CB                 ; 203)_U6E12
OR B05CB                 ; 203)_U6E12Bz停止
ANL
LDB B0CC                 ; 204)_U6E13
OR B05CC                 ; 204)_U6E13Bz停止
ANL
LDB B0CD                 ; 205)_U6E14
OR B05CD                 ; 205)_U6E14Bz停止
ANL
LDB B0CE                 ; 206)_U6E15
OR B05CE                 ; 206)_U6E15Bz停止
ANL
LDB B0CF                 ; 207)_U6E16
OR B05CF                 ; 207)_U6E16Bz停止
ANL
LDB B0D0                 ; 208)_U6E17_OKｺﾝﾍﾞﾔｽﾄｯﾊﾟCLS異常
OR B05D0                 ; 208)_U6E17Bz停止
ANL
LDB B0D1                 ; 209)_U6E18_OKｴｽｹｰﾊﾟｰ上下異常
OR B05D1                 ; 209)_U6E18Bz停止
ANL
LDB B0D2                 ; 210)_U6E19_NGﾌﾟｯｼｬｰ前後異常
OR B05D2                 ; 210)_U6E19Bz停止
ANL
LDB B0D3                 ; 211)_U6E20_OKｺﾝﾍﾞﾔﾜｰｸﾌﾟｯｼｬcl異常
OR B05D3                 ; 211)_U6E20Bz停止
ANL
LDB B0D4                 ; 212)_U6E21_OKｺﾝｽｯﾄﾊﾟCL異常
OR B05D4                 ; 212)_U6E21Bz停止
ANL
LDB B0D5                 ; 213)_U6E22_OKｺﾝｴｽｹｰﾊﾟｰcls異常
OR B05D5                 ; 213)_U6E22Bz停止
ANL
LDB B0D6                 ; 214)_U6E23
OR B05D6                 ; 214)_U6E23Bz停止
ANL
LDB B0D7                 ; 215)_U6E24
OR B05D7                 ; 215)_U6E24Bz停止
ANL
LDB B0D8                 ; 216)_U6E25
OR B05D8                 ; 216)_U6E25Bz停止
ANL
LDB B0D9                 ; 217)_U6E26
OR B05D9                 ; 217)_U6E26Bz停止
ANL
LDB B0DA                 ; 218)_U6E27
OR B05DA                 ; 218)_U6E27Bz停止
ANL
LDB B0DB                 ; 219)_U6E28
OR B05DB                 ; 219)_U6E28Bz停止
ANL
LDB B0DC                 ; 220)_U6E29
OR B05DC                 ; 220)_U6E29Bz停止
ANL
LDB B0DD                 ; 221)_U6E30
OR B05DD                 ; 221)_U6E30Bz停止
ANL
LDB B0DE                 ; 222)_U6E31
OR B05DE                 ; 222)_U6E31Bz停止
ANL
LDB B0DF                 ; 223)_U6E32
OR B05DF                 ; 223)_U6E32Bz停止
ANL
INV
OUT M3989                ; ﾕﾆｯﾄ6異常BZ補助
END
ENDH
