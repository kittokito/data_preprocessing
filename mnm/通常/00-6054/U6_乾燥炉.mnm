DEVICE:53
;MODULE:U6_乾燥炉
;MODULE_TYPE:0
;□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X136A                 ; 乾燥炉投入側ｼｬｯﾀｰ開端1
AND X136C                ; 乾燥炉投入側ｼｬｯﾀｰ開端2
MPS
TMH K601 K10
MPP
AND T601
OUT M3901                ; 乾燥炉投入側ｼｬｯﾀｰ開端
LD X136B                 ; 乾燥炉投入側ｼｬｯﾀｰ閉端1
AND X136D                ; 乾燥炉投入側ｼｬｯﾀｰ閉端2
MPS
TMH K602 K5
MPP
AND T602
OUT M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
LD X1370                 ; 乾燥炉出口側ｼｬｯﾀｰ開端1
AND X1372                ; 乾燥炉出口側ｼｬｯﾀｰ開端2
MPS
TMH K603 K10
MPP
AND T603
OUT M3903                ; 乾燥炉出口側ｼｬｯﾀｰ開端
LD X1371                 ; 乾燥炉出口側ｼｬｯﾀｰ閉端1
AND X1373                ; 乾燥炉出口側ｼｬｯﾀｰ閉端2
MPS
TMH K604 K5
MPP
AND T604
OUT M3904                ; 乾燥炉出口側ｼｬｯﾀｰ閉端
LD X1328                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
MPS
TMR K605 K3
MPP
AND T605
OUT M3905                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
LD X1329                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
MPS
TMR K606 K3
MPP
AND T606
OUT M3906                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
LD X132A                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
MPS
TMR K607 K3
MPP
AND T607
OUT M3907                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
LD X132B                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
MPS
TMR K608 K3
MPP
AND T608
OUT M3908                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
LDB X1368                ; 乾燥炉ｽﾄｯﾊﾟｰ1ﾗｯｸ確認(ON=無)
MPS
TMR K609 K3
MPP
AND T609
OUT M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
LDB X1369                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ確認(ON=無)
MPS
TMR K610 K3
MPP
AND T610
OUT M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
LDB X1366                ; 炉投入側ｼｬｯﾀｰﾗｯｸ通過確認
MPS
TMR K611 K2
MPP
AND T611
OUT M3911                ; 炉投入側ｼｬｯﾀｰﾗｯｸ通過中
LDB X1367                ; 炉排出側ｼｬｯﾀｰﾗｯｸ通過確認(OFF通)
MPS
TMR K612 K2
MPP
AND T612
OUT M3912                ; 炉排出側ｼｬｯﾀｰﾗｯｸ通過中確認
;<h1/>□ 装置状態・動作条件
LD X1329                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
AND X132B                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
LD M3902                 ; 乾燥炉投入側ｼｬｯﾀｰ閉端
OR M72                   ; 自動運転開始
OR M3370                 ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
ANL
AND M3904                ; 乾燥炉出口側ｼｬｯﾀｰ閉端
OUT M3980                ; ﾕﾆｯﾄ6原点
;□ エラー
LDB B0C1                 ; 乾燥炉投入側ｼｬｯﾀｰ右開閉LS異常
ANB B0C2                 ; 乾燥炉投入側ｼｬｯﾀｰ左開閉LS異常
ANB B0C3                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上下LS異常
ANB B0C4                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上下LS異常
ANB B0C6                 ; 乾燥炉出口側ｼｬｯﾀｰ右開閉LS異常
ANB B0C7                 ; 乾燥炉出口側ｼｬｯﾀｰ左開閉LS異常
ANB B0C8                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
ANB B0C9                 ; 炉排出ﾗｯｸ排出未完了
INV
OUT M3981                ; ﾕﾆｯﾄ6ｴﾗｰ
LD CR2002                ; 常時ON
ANB B0140                ; 乾燥_上流1段目FAN10回転低下異常
ANB B0141                ; 乾燥_上流2段目FAN11回転低下異常
ANB B0142                ; 乾燥_上流3段目FAN12回転低下異常
ANB B0143                ; 乾燥_上流4段目FAN13回転低下異常
ANB B0144                ; 乾燥_上流5段目FAN14回転低下異常
ANB B0145                ; 乾燥_上流6段目FAN15回転低下異常
ANB B0148                ; 乾燥_下流1段目FAN20回転低下異常
ANB B0149                ; 乾燥_下流2段目FAN21回転低下異常
ANB B014A                ; 乾燥_下流3段目FAN22回転低下異常
ANB B014B                ; 乾燥_下流4段目FAN23回転低下異常
ANB B014C                ; 乾燥_下流5段目FAN24回転低下異常
INV
OUT M3998                ; Oven Fan回転異常一括
;□ 起動条件
LD M3980                 ; ﾕﾆｯﾄ6原点
ANB M3981                ; ﾕﾆｯﾄ6ｴﾗｰ
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M3993                ; 乾燥炉払出停止
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M3982                ; ﾕﾆｯﾄ6起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
ORL
OR M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3983                ; ﾕﾆｯﾄ6起動OFF
;□ ｻｲｸﾙ停止異常
LDB B0C0                 ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0C5                 ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
ANB B0CA                 ; 乾燥炉内ﾗｯｸ移動異常
ANB B0CB                 ; 乾燥炉内ﾗｯｸ位置異常
INV
OUT M3986                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
LD M3986                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
ANB M3985                ; ﾕﾆｯﾄ6起動中
OR M3996                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常記憶
AND M41                  ; 全ﾕﾆｯﾄ起動中
OUT M3996                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常記憶
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M3987                ; ﾕﾆｯﾄ6払出停止異常
;□ 警報補助
LDB B0140                ; 乾燥_上流1段目FAN10回転低下異常
ANB B0141                ; 乾燥_上流2段目FAN11回転低下異常
ANB B0142                ; 乾燥_上流3段目FAN12回転低下異常
ANB B0143                ; 乾燥_上流4段目FAN13回転低下異常
ANB B0144                ; 乾燥_上流5段目FAN14回転低下異常
ANB B0145                ; 乾燥_上流6段目FAN15回転低下異常
ANB B0148                ; 乾燥_下流1段目FAN20回転低下異常
ANB B0149                ; 乾燥_下流2段目FAN21回転低下異常
ANB B014A                ; 乾燥_下流3段目FAN22回転低下異常
ANB B014B                ; 乾燥_下流4段目FAN23回転低下異常
ANB B014C                ; 乾燥_下流5段目FAN24回転低下異常
ANB B014D                ; 乾燥_下流6段目FAN25回転低下異常
INV
OUT M3988                ; ﾕﾆｯﾄ6警報補助
;□ 機器準備確認
LD CR2002                ; 常時ON
OUT M3990                ; ﾕﾆｯﾄ6機器準備完了
;□ ｴﾗｰ発生　非常停止扱い
LD CR2003                ; 常時OFF
OUT M3991                ; ﾕﾆｯﾄ6即停止異常発生
;<h1/>____▼_払出しﾜｰｸ無し
LD CR2002                ; 常時ON
MPS
WSUM D3601 K20           ; 炉入口側_ﾗｯｸ1段目ﾜｰｸ有記憶
CON
STA D3520                ; 炉入口側_ﾗｯｸ内ﾜｰｸ有判別
CON
WSUM D3701 K20           ; 炉出口側_ﾗｯｸ1段目ﾜｰｸ有記憶
CON
STA D3522                ; 炉出口側_ﾗｯｸ内ﾜｰｸ有判別
MRD
AND<> D3520 K0           ; 炉入口側_ﾗｯｸ内ﾜｰｸ有判別
OUT M3970                ; 炉入口側_ﾗｯｸ内ﾜｰｸ有
MPP
AND<> D3522 K0           ; 炉出口側_ﾗｯｸ内ﾜｰｸ有判別
OUT M3971                ; 炉出口側_ﾗｯｸ内ﾜｰｸ有
LD M36                   ; 払出停止指令(乾燥炉内のﾜｰｸ以外)
LDB M190                 ; 全ﾜｰｸ払出停止指令
OR M192                  ; ﾛｯﾄ切替用ｻｲｸﾙ停止指令
LD M190                  ; 全ﾜｰｸ払出停止指令
ANB M3970                ; 炉入口側_ﾗｯｸ内ﾜｰｸ有
ANB M3971                ; 炉出口側_ﾗｯｸ内ﾜｰｸ有
ORL
ANL
TMR K614 K10             ; UNIT6払出処理ﾜｰｸ無
LD M3700                 ; S200_ｻｲｸﾙ停止
AND M3493                ; 収納ﾘﾌﾀｰ払出停止完了
AND T614                 ; UNIT6払出処理ﾜｰｸ無
OR M3993                 ; 乾燥炉払出停止
AND M36                  ; 払出停止指令(乾燥炉内のﾜｰｸ以外)
OR M3996                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常記憶
LD M3496                 ; ﾕﾆｯﾄ5ｻｲｸﾙ停止異常記憶
ANB M3985                ; ﾕﾆｯﾄ6起動中
ORL
OUT M3993                ; 乾燥炉払出停止
;□ ｽﾃｯﾌﾟ起動
;□ 原点復帰
LDP M82                  ; 原点復帰開始
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
OR M3984                 ; ﾕﾆｯﾄ6原点復帰中
ANB M3983                ; ﾕﾆｯﾄ6起動OFF
ANB M3981                ; ﾕﾆｯﾄ6ｴﾗｰ
ANB M3749                ; S249_ｻｲｸﾙ終了
MPS
OUT M3984                ; ﾕﾆｯﾄ6原点復帰中
MPP
AND M401                 ; 扉閉ﾃﾞｨﾚｰON
TMR K600 K100            ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ
;□ 起動中
LD M73                   ; 主幹起動指令
LD M3485                 ; ﾕﾆｯﾄ5起動中
AND M3370                ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
ORL
AND M3982                ; ﾕﾆｯﾄ6起動条件
OR M3985                 ; ﾕﾆｯﾄ6起動中
ANB M3983                ; ﾕﾆｯﾄ6起動OFF
ANB M3749                ; S249_ｻｲｸﾙ終了
OUT M3985                ; ﾕﾆｯﾄ6起動中
;□□□□□□□□□□□□□□□
;□　　 　UNIT5自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>□_自動運転回路
LD= D3500 D3505          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6動作中判定
ANB Y2420                ; 乾燥炉CV移動ON
ORB M3985                ; ﾕﾆｯﾄ6起動中
TMR K620 K20             ; 乾燥炉動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D3500 D3505          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ6動作中判定
LD T620                  ; 乾燥炉動作無し判定ﾀｲﾏ
OUT M3992                ; ﾕﾆｯﾄ6動作無し確認
LD M1993                 ; ﾜｰｸ投入部払出完了
AND M2493                ; 基板反転&搬送払出完了
AND M2993                ; 塗布RB部払出終了完了
AND M3493                ; 収納ﾘﾌﾀｰ払出停止完了
OUT M3994                ; 前工程ﾜｰｸ無し
;□
;□　ﾕﾆｯﾄ5 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M3500 K18        ; S000_
LDB M3984                ; ﾕﾆｯﾄ6原点復帰中
ANB M3985                ; ﾕﾆｯﾄ6起動中
MOV K0 D3500             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3984                 ; ﾕﾆｯﾄ6原点復帰中
OR M3985                 ; ﾕﾆｯﾄ6起動中
MPS
MOV D3500 Z1             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
ANB M140                 ; 一時停止中_ﾄﾞｱﾛｯｸ開
SET M3500:Z1             ; S000_
LD M3500                 ; S000_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3501                 ; S001_
ANB M130                 ; 一時停止
MPS
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
LD M82                   ; 原点復帰開始
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
AND M84                  ; 原点復帰起動確認
ANL
MOV K250 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S002_動作条件分岐
;<h1/>____▼_乾燥炉内ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
AND<= D3580 D3750        ; 乾燥炉１ﾗｯｸ滞在時間  乾燥炉１ﾗｯｸ滞在時間設定値
MPS
@INC D3580               ; 乾燥炉１ﾗｯｸ滞在時間
MPP
AND<> D3740 K0           ; 乾燥炉１ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
@DEC D3740               ; 乾燥炉１ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
ANB M3                   ; OFFLINE_MODE
OUT W0103.11             ; 乾燥ｴﾘｱ#1 ﾗｯｸ有
MOV D3621 W0123          ; 炉入口側_排出ﾗｯｸ ID記憶  乾燥ｴﾘｱ#1 ﾗｯｸID
LDB M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
MOV K0 W0123             ; 乾燥ｴﾘｱ#1 ﾗｯｸID
MOV K0 D3580             ; 乾燥炉１ﾗｯｸ滞在時間
MOV D3750 D3740          ; 乾燥炉１ﾗｯｸ滞在時間設定値  乾燥炉１ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
LD CR2002                ; 常時ON
MOV D3600 W013E          ; 炉入口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.  乾燥ｴﾘｱ#1ﾗｯｸｲﾝﾃﾞｯｸｽNo.
;	乾燥炉２
LD M3910                 ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
AND<= D3590 D3760        ; 乾燥炉２ﾗｯｸ滞在時間  乾燥炉２ﾗｯｸ滞在時間設定値
MPS
@INC D3590               ; 乾燥炉２ﾗｯｸ滞在時間
MPP
AND<> D3742 K0           ; 乾燥炉２ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
@DEC D3742               ; 乾燥炉２ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
LD M3910                 ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
ANB M3                   ; OFFLINE_MODE
OUT W0103.12             ; 乾燥ｴﾘｱ#2 ﾗｯｸ有無
MOV D3721 W0124          ; 炉出口側_排出ﾗｯｸ ID記憶  乾燥ｴﾘｱ#2 ﾗｯｸID
LDB M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
MOV K0 W0124             ; 乾燥ｴﾘｱ#2 ﾗｯｸID
LD CR2002                ; 常時ON
MOV D3700 W013F          ; 炉出口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.  乾燥ｴﾘｱ#2ﾗｯｸｲﾝﾃﾞｯｸｽNo.
LDB M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
MOV K0 D3590             ; 乾燥炉２ﾗｯｸ滞在時間
MOV D3760 D3742          ; 乾燥炉２ﾗｯｸ滞在時間設定値  乾燥炉２ﾗｯｸ滞在時間ｶｳﾝﾄﾀﾞｳﾝ
LD CR2002                ; 常時ON
CAL- D3750 K3 D3752      ; 乾燥炉１ﾗｯｸ滞在時間設定値  乾燥炉１ﾗｯｸ滞在時間設定-3秒
CAL- D3760 K3 D3762      ; 乾燥炉２ﾗｯｸ滞在時間設定値  乾燥炉２ﾗｯｸ滞在時間設定-3秒
;S003・・・乾燥炉内のﾗｯｸを1→2へ移動
;S020・・・収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ移動
;S040・・・乾燥炉→排出ﾘﾌﾀｰへﾗｯｸ移動
LD M3502                 ; S002_
ANB M130                 ; 一時停止
MPS
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
ANB M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
AND>= D3580 D3750        ; 乾燥炉１ﾗｯｸ滞在時間  乾燥炉１ﾗｯｸ滞在時間設定値
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
LD M3370                 ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
ANB M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
ANL
MOV K20 D3500            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
LD M4350                 ; 排出ﾘﾌﾀｰ → 乾燥炉へﾗｯｸ排出可
AND M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
AND>= D3590 D3762        ; 乾燥炉２ﾗｯｸ滞在時間  乾燥炉２ﾗｯｸ滞在時間設定-3秒
ANL
MOV K40 D3500            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
LD M3493                 ; 収納ﾘﾌﾀｰ払出停止完了
AND T614                 ; UNIT6払出処理ﾜｰｸ無
OR M4496                 ; ﾕﾆｯﾄ7ｻｲｸﾙ停止異常記憶
ANL
MOV K200 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S003_乾燥炉内ﾗｯｸ移動1→2へ
LD M3503                 ; S003_乾燥炉投入側ｼｬｯﾀｰ開
LD M3370                 ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
AND Y2520                ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
ORB M3370                ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3504                 ; S004_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3505                 ; S005_
ANB M130                 ; 一時停止
AND M3908                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3506                 ; S006_乾燥炉ｽﾄｯﾊﾟｰ1下降
ANB M130                 ; 一時停止
AND M3905                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3507                 ; S007_乾燥炉内ﾗｯｸ移動中ON
AND M3870                ; 乾燥炉内ﾗｯｸ移動中
AND Y2420                ; 乾燥炉CV移動ON
AND= D3640 K1            ; 乾燥炉内ﾗｯｸ移動中
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3507                 ; S007_乾燥炉内ﾗｯｸ移動中ON
OR M3870                 ; 乾燥炉内ﾗｯｸ移動中
ANB M3512                ; S012_乾燥炉内ﾗｯｸ移動中OFF
AND M3985                ; ﾕﾆｯﾄ6起動中
OUT M3870                ; 乾燥炉内ﾗｯｸ移動中
MOV K1 D3640             ; 乾燥炉内ﾗｯｸ移動中
LD M3508                 ; S008_乾燥炉内ﾃﾞｰﾀｼﾌﾄ
BMOV D3600 D3700 K30     ; 炉入口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.  炉出口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.
@FMOV K0 D3600 K30       ; 炉入口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3509                 ; S009_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3510                 ; S010_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3511                 ; S011_乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ到着確認
ANB M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
AND M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3512                 ; S012_乾燥炉内ﾗｯｸ移動中OFF
MPS
MOV K0 D3640             ; 乾燥炉内ﾗｯｸ移動中
MPP
LDB M3870                ; 乾燥炉内ﾗｯｸ移動中
ANB Y2420                ; 乾燥炉CV移動ON
AND= D3640 K0            ; 乾燥炉内ﾗｯｸ移動中
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3513                 ; S013_乾燥炉ｽﾄｯﾊﾟｰ1上昇
ANB M130                 ; 一時停止
AND M3906                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3514                 ; S014_
MOV K249 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S020_収納ﾘﾌﾀ‐から乾燥炉へﾗｯｸ受取
LD M3520                 ; S020_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3521                 ; S021_乾燥炉ｽﾄｯﾊﾟｰ1上昇
ANB M130                 ; 一時停止
AND M3906                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3522                 ; S022_乾燥炉投入側ｼｬｯﾀｰ開
ANB M130                 ; 一時停止
AND M3901                ; 乾燥炉投入側ｼｬｯﾀｰ開端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3523                 ; S023_収納ﾗｯｸへﾗｯｸ供給可ON
ANB M130                 ; 一時停止
MPS
ANB M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
OUT M3850                ; 収納ﾗｯｸへﾗｯｸ供給可
MPP
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
AND X1366                ; 炉投入側ｼｬｯﾀｰﾗｯｸ通過確認
ANB M3371                ; 乾燥炉へﾗｯｸ排出中
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3524                 ; S024_乾燥炉投入側ｼｬｯﾀｰ閉
ANB M130                 ; 一時停止
AND M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3525                 ; S025_
@MOV K249 D3500          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S040_乾燥炉から排出ﾘﾌﾀｰへﾗｯｸ排出
LD M3540                 ; S040_排出ﾘﾌﾀｰへﾗｯｸ排出要求ON
ANB M130                 ; 一時停止
OR M3860                 ; 乾燥炉→排出ﾘﾌﾀｰへﾗｯｸ排出開始
ANB M3551                ; S051_排出ﾘﾌﾀｰへﾗｯｸ排出要求OFF
AND M3985                ; ﾕﾆｯﾄ6起動中
OUT M3860                ; 乾燥炉→排出ﾘﾌﾀｰへﾗｯｸ排出開始
LD M3540                 ; S040_排出ﾘﾌﾀｰへﾗｯｸ排出要求ON
AND M3860                ; 乾燥炉→排出ﾘﾌﾀｰへﾗｯｸ排出開始
AND M4010                ; S010_排出ﾘﾌﾀｰへﾗｯｸ排出要求OFF確
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3541                 ; S041_乾燥炉出口側ｼｬｯﾀｰ開
AND Y2522                ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
AND>= D3590 D3760        ; 乾燥炉２ﾗｯｸ滞在時間  乾燥炉２ﾗｯｸ滞在時間設定値
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3542                 ; S042_
ANB M130                 ; 一時停止
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3543                 ; S043_乾燥炉出口側ｼｬｯﾀｰ開
ANB M130                 ; 一時停止
AND M3903                ; 乾燥炉出口側ｼｬｯﾀｰ開端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3544                 ; S044_ｽﾄｯﾊﾟｰ2下降/Eject位置決下降
ANB M130                 ; 一時停止
AND M3907                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
AND M4402                ; 排出ﾗｯｸ位置決め下降端
AND M4406                ; 排出ﾗｯｸﾗｯｸ取出し側ｽﾄｯﾊﾟ上昇端
AND M4404                ; 排出ﾗｯｸ投入側ｽﾄｯﾊﾟ下降端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3545                 ; S045_乾燥炉2ﾗｯｸ排出
MPS
@MOV K1 D3630            ; 乾燥炉２ﾗｯｸ排出中記憶
MPP
LD M3871                 ; 乾燥炉2ﾗｯｸ排出中
AND Y2429                ; 排出ﾘﾌﾀCV逆転ON
AND Y2420                ; 乾燥炉CV移動ON
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3545                 ; S045_乾燥炉2ﾗｯｸ排出
OR M3871                 ; 乾燥炉2ﾗｯｸ排出中
ANB M3550                ; S050_乾燥炉2ﾗｯｸ排出中OFF
AND M3985                ; ﾕﾆｯﾄ6起動中
OUT M3871                ; 乾燥炉2ﾗｯｸ排出中
LD M3546                 ; S046_乾燥炉2から排出ﾘﾌﾀへﾃﾞｰﾀｼﾌﾄ
@BMOV D3700 D4100 K30    ; 炉出口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.  排出ﾘﾌﾀ_ﾗｯｸｲﾝﾃﾞｯｸｽNo.
@FMOV K0 D3700 K30       ; 炉出口側_ﾗｯｸｲﾝﾃﾞｯｸｽNo.
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3547                 ; S047_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3548                 ; S048_炉排出側ﾗｯｸ到着確認
AND M124                 ; ｴﾘｱｾﾝｻ正常
ANB M130                 ; 一時停止
ANB B0C8                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
AND M4407                ; 排出ﾘﾌﾀｰﾗｯｸ到着確認
AND X1367                ; 炉排出側ｼｬｯﾀｰﾗｯｸ通過確認(OFF通)
AND X1375                ; 排出ﾘﾌﾀｰ基板飛び出し確認2
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3549                 ; S049_乾燥炉へﾗｯｸ排出可OFF確認
ANB M4350                ; 排出ﾘﾌﾀｰ → 乾燥炉へﾗｯｸ排出可
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3550                 ; S050_乾燥炉2ﾗｯｸ排出中OFF
MPS
@MOV K0 D3630            ; 乾燥炉２ﾗｯｸ排出中記憶
MPP
LDB M3871                ; 乾燥炉2ﾗｯｸ排出中
AND= D3630 K0            ; 乾燥炉２ﾗｯｸ排出中記憶
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3551                 ; S051_排出ﾘﾌﾀｰへﾗｯｸ排出要求OFF
ANB M3860                ; 乾燥炉→排出ﾘﾌﾀｰへﾗｯｸ排出開始
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3552                 ; S052_乾燥炉ｽﾄｯﾊﾟｰ2上昇
AND M3908                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3553                 ; S053_乾燥炉出口側ｼｬｯﾀｰ閉
ANB M3903                ; 乾燥炉出口側ｼｬｯﾀｰ開端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3554                 ; S054_
MOV K249 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>____▼_S200_ｻｲｸﾙ停止
LD M3700                 ; S200_ｻｲｸﾙ停止
LD M3993                 ; 乾燥炉払出停止
ORB M36                  ; 払出停止指令(乾燥炉内のﾜｰｸ以外)
ANL
MOV K249 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>□_原点復帰
LD M3750                 ; S250_CV停止.乾燥炉ｽﾄｯﾊﾟｰ1､2上昇
ANB Y2420                ; 乾燥炉CV移動ON
AND M3906                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
AND M3908                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3751                 ; S251_ｼｬｯﾀｰ確認
MPS
ANB M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
AND M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
MOV K253 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3752                 ; S252_乾燥炉CV ON
AND Y2420                ; 乾燥炉CV移動ON
TMR K625 K50
CON
AND T625
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3753                 ; S253_炉投入側ｼｬｯﾀｰ確認
LD X1366                 ; 炉投入側ｼｬｯﾀｰﾗｯｸ通過確認
OR M3902                 ; 乾燥炉投入側ｼｬｯﾀｰ閉端
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3754                 ; S254_乾燥炉投入側ｼｬｯﾀｰ閉
AND M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3755                 ; S255_炉排出側ｼｬｯﾀｰ確認
LD X1367                 ; 炉排出側ｼｬｯﾀｰﾗｯｸ通過確認(OFF通)
OR M3904                 ; 乾燥炉出口側ｼｬｯﾀｰ閉端
ANL
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3756                 ; S256_乾燥炉出口側ｼｬｯﾀｰ閉
AND M3904                ; 乾燥炉出口側ｼｬｯﾀｰ閉端
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3757                 ; S257_
INC D3500                ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M3758                 ; S258_
MOV K249 D3500           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>□_手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT6手動回路  　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
MC
LD CR2002                ; 常時ON
OUT M3801                ; 乾燥炉投入側ｼｬｯﾀｰ開可
LDP B1120                ; 乾燥炉投入側ｼｬｯﾀｰ開SW
ORP B111A                ; 乾燥炉投入側ｼｬｯﾀｰ開SW
AND M3801                ; 乾燥炉投入側ｼｬｯﾀｰ開可
OR M3802                 ; 乾燥炉投入側ｼｬｯﾀｰ開
ANPB B1121               ; 乾燥炉投入側ｼｬｯﾀｰ閉SW
ANPB B111B               ; 乾燥炉投入側ｼｬｯﾀｰ閉SW
OUT M3802                ; 乾燥炉投入側ｼｬｯﾀｰ開
LD CR2002                ; 常時ON
OUT M3803                ; 乾燥炉投入側ｼｬｯﾀｰ閉可
LDP B1121                ; 乾燥炉投入側ｼｬｯﾀｰ閉SW
ORP B111B                ; 乾燥炉投入側ｼｬｯﾀｰ閉SW
AND M3803                ; 乾燥炉投入側ｼｬｯﾀｰ閉可
OR M3804                 ; 乾燥炉投入側ｼｬｯﾀｰ閉
ANPB B1120               ; 乾燥炉投入側ｼｬｯﾀｰ開SW
ANPB B111A               ; 乾燥炉投入側ｼｬｯﾀｰ開SW
OUT M3804                ; 乾燥炉投入側ｼｬｯﾀｰ閉
LDB Y2420                ; 乾燥炉CV移動ON
OUT M3805                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降可
LDP B1122                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SW
AND M3805                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降可
OR M3806                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降
ANPB B1123               ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SW
OUT M3806                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降
LDB Y2420                ; 乾燥炉CV移動ON
OUT M3807                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇可
LDP B1123                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SW
AND M3807                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇可
OR M3808                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇
ANPB B1122               ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SW
OUT M3808                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇
LDB Y2420                ; 乾燥炉CV移動ON
AND M4461                ; 基板排出RB9ﾗｯｸ収納位置
OUT M3809                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降可
LDP B1124                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SW
AND M3809                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降可
OR M3810                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降
ANPB B1125               ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SW
OUT M3810                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降
LDB Y2420                ; 乾燥炉CV移動ON
OUT M3811                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇可
LDP B1125                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SW
AND M3811                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇可
OR M3812                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇
ANPB B1124               ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SW
OUT M3812                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇
LD M3908                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
LD M3907                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
AND M4461                ; 基板排出RB9ﾗｯｸ収納位置
ORL
OUT M3813                ; 乾燥炉CV　ON可
LDP B1126                ; 乾燥炉CV　ON_SW
ORP B111C                ; 乾燥炉CV　ON_SW
AND M3813                ; 乾燥炉CV　ON可
OR M3814                 ; 乾燥炉CV　ON
ANB B1127                ; 乾燥炉CV　OFF_SW
ANB B111D                ; 乾燥炉CV　OFF_SW
OUT M3814                ; 乾燥炉CV　ON
LD CR2002                ; 常時ON
OUT M3815                ; 乾燥炉出口側ｼｬｯﾀｰ開可
LDP B1128                ; 乾燥炉出口側ｼｬｯﾀｰ開SW
AND M3815                ; 乾燥炉出口側ｼｬｯﾀｰ開可
OR M3816                 ; 乾燥炉出口側ｼｬｯﾀｰ開
ANPB B1129               ; 乾燥炉出口側ｼｬｯﾀｰ閉SW
OUT M3816                ; 乾燥炉出口側ｼｬｯﾀｰ開
LD CR2002                ; 常時ON
OUT M3817                ; 乾燥炉出口側ｼｬｯﾀｰ閉可
LDP B1129                ; 乾燥炉出口側ｼｬｯﾀｰ閉SW
AND M3817                ; 乾燥炉出口側ｼｬｯﾀｰ閉可
OR M3818                 ; 乾燥炉出口側ｼｬｯﾀｰ閉
ANPB B1128               ; 乾燥炉出口側ｼｬｯﾀｰ開SW
OUT M3818                ; 乾燥炉出口側ｼｬｯﾀｰ閉
MCR
LDB M3902                ; 乾燥炉投入側ｼｬｯﾀｰ閉端
ORB M3904                ; 乾燥炉出口側ｼｬｯﾀｰ閉端
ANB X02                  ; 安全扉正常
OUT M3825                ; 乾燥炉ｼｬｯﾀｰ開　FAN　OFF
LD CR2002                ; 常時ON
ANB M72                  ; 自動運転開始
OUT M3819                ; 乾燥炉FANﾓｰﾀ起動ON可
LDP B117E                ; 乾燥炉FANﾓｰﾀ起動ON SW
AND M3819                ; 乾燥炉FANﾓｰﾀ起動ON可
OR M72                   ; 自動運転開始
OR M3820                 ; 乾燥炉FANﾓｰﾀ起動ON
ANB M3822                ; 乾燥炉FANﾓｰﾀ起動OFF
ANPB M3825               ; 乾燥炉ｼｬｯﾀｰ開　FAN　OFF
OUT M3820                ; 乾燥炉FANﾓｰﾀ起動ON
LD CR2002                ; 常時ON
ANB M72                  ; 自動運転開始
OUT M3821                ; 乾燥炉FANﾓｰﾀ起動OFF可
LD CR2002                ; 常時ON
WSUM D3601 K20           ; 炉入口側_ﾗｯｸ1段目ﾜｰｸ有記憶
CON
STA D3650                ; ﾗｯｸ内1ﾜｰｸ記憶_有(0以外有)
CON
WSUM D3701 K20           ; 炉出口側_ﾗｯｸ1段目ﾜｰｸ有記憶
CON
STA D3652                ; ﾗｯｸ内2ﾜｰｸ記憶_有(0以外有)
LDP B117F                ; 乾燥炉FANﾓｰﾀ起動OFF SW
AND M3821                ; 乾燥炉FANﾓｰﾀ起動OFF可
LDF M72                  ; 自動運転開始
AND=.D D3650 K0          ; ﾗｯｸ内1ﾜｰｸ記憶_有(0以外有)
AND=.D D3652 K0          ; ﾗｯｸ内2ﾜｰｸ記憶_有(0以外有)
ORL
OUT M3822                ; 乾燥炉FANﾓｰﾀ起動OFF
;<h1/>出力(シリンダ等)
LDB M3814                ; 乾燥炉CV　ON
ANB M3752                ; S252_乾燥炉CV ON
ANB M3371                ; 乾燥炉へﾗｯｸ排出中
ANB M3870                ; 乾燥炉内ﾗｯｸ移動中
INV
LD M3871                 ; 乾燥炉2ﾗｯｸ排出中
AND M124                 ; ｴﾘｱｾﾝｻ正常
ORL
ANB B0A5                 ; 炉投入ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
ANB B0C8                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
OUT Y2420                ; 乾燥炉CV移動ON
LDB M3802                ; 乾燥炉投入側ｼｬｯﾀｰ開
ANB M3522                ; S022_乾燥炉投入側ｼｬｯﾀｰ開
ANB CR2003               ; 常時OFF
INV
LD M3503                 ; S003_乾燥炉投入側ｼｬｯﾀｰ開
AND M3370                ; 収納ﾘﾌﾀｰ→乾燥炉へﾗｯｸ排出要求
ORL
LD Y2520                 ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
ANB Y2521                ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
ORL
OUT Y2520                ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
LDB M3804                ; 乾燥炉投入側ｼｬｯﾀｰ閉
ANB M3754                ; S254_乾燥炉投入側ｼｬｯﾀｰ閉
ANB M3524                ; S024_乾燥炉投入側ｼｬｯﾀｰ閉
ANB CR2003               ; 常時OFF
INV
LD Y2521                 ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
ANB Y2520                ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
ORL
OUT Y2521                ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
LDB M3816                ; 乾燥炉出口側ｼｬｯﾀｰ開
ANB M3541                ; S041_乾燥炉出口側ｼｬｯﾀｰ開
ANB M3543                ; S043_乾燥炉出口側ｼｬｯﾀｰ開
ANB CR2003               ; 常時OFF
INV
LD Y2522                 ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
ANB Y2523                ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
ORL
OUT Y2522                ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
LDB M3818                ; 乾燥炉出口側ｼｬｯﾀｰ閉
ANB M3756                ; S256_乾燥炉出口側ｼｬｯﾀｰ閉
ANB M3553                ; S053_乾燥炉出口側ｼｬｯﾀｰ閉
ANB CR2003               ; 常時OFF
INV
LD Y2523                 ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
ANB Y2522                ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
ORL
OUT Y2523                ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
LDB M3806                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降
ANB M3506                ; S006_乾燥炉ｽﾄｯﾊﾟｰ1下降
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
LD Y2540                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SOL
ANB Y2541                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SOL
ORL
OUT Y2540                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SOL
LDB M3808                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇
ANB M3750                ; S250_CV停止.乾燥炉ｽﾄｯﾊﾟｰ1､2上昇
ANB M3521                ; S021_乾燥炉ｽﾄｯﾊﾟｰ1上昇
ANB M3513                ; S013_乾燥炉ｽﾄｯﾊﾟｰ1上昇
INV
LD Y2541                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SOL
ANB Y2540                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SOL
ORL
OUT Y2541                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SOL
LDB M3810                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降
ANB M3544                ; S044_ｽﾄｯﾊﾟｰ2下降/Eject位置決下降
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
LD Y2542                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SOL
ANB Y2543                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SOL
ORL
OUT Y2542                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SOL
LDB M3812                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇
ANB M3750                ; S250_CV停止.乾燥炉ｽﾄｯﾊﾟｰ1､2上昇
ANB M3552                ; S052_乾燥炉ｽﾄｯﾊﾟｰ2上昇
ANB CR2003               ; 常時OFF
INV
LD Y2543                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SOL
ANB Y2542                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SOL
ORL
OUT Y2543                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SOL
LD M3820                 ; 乾燥炉FANﾓｰﾀ起動ON
OUT Y2435                ; 乾燥炉 Fan電源ON1
OUT Y2436                ; 乾燥炉 Fan電源ON2
;□□　　パネル表示　　□□
LD X136A                 ; 乾燥炉投入側ｼｬｯﾀｰ開端1
AND X136C                ; 乾燥炉投入側ｼｬｯﾀｰ開端2
LD Y2520                 ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
LDB X136A                ; 乾燥炉投入側ｼｬｯﾀｰ開端1
ORB X136D                ; 乾燥炉投入側ｼｬｯﾀｰ閉端2
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1620                ; UNIT6-1-1 TP-PL
OUT B161A                ; UNIT5-2-11 TP-PL
LD X136B                 ; 乾燥炉投入側ｼｬｯﾀｰ閉端1
AND X136D                ; 乾燥炉投入側ｼｬｯﾀｰ閉端2
LD Y2521                 ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
LDB X136B                ; 乾燥炉投入側ｼｬｯﾀｰ閉端1
ORB X136D                ; 乾燥炉投入側ｼｬｯﾀｰ閉端2
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1621                ; UNIT6-1-2 TP-PL
OUT B161B                ; UNIT5-2-12 TP-PL
LD X1328                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
LD Y2540                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SOL
ANB X1328                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1622                ; UNIT6-1-3 TP-PL
LD X1329                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
LD Y2541                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SOL
ANB X1329                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1623                ; UNIT6-1-4 TP-PL
LD X132A                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
LD Y2542                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SOL
ANB X132A                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1624                ; UNIT6-1-5 TP-PL
LD X132B                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
LD Y2543                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SOL
ANB X132B                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1625                ; UNIT6-1-6 TP-PL
LD Y2420                 ; 乾燥炉CV移動ON
OUT B1626                ; UNIT6-1-7 TP-PL
LDB Y2420                ; 乾燥炉CV移動ON
OUT B1627                ; UNIT6-1-8 TP-PL
LD X1370                 ; 乾燥炉出口側ｼｬｯﾀｰ開端1
AND X1372                ; 乾燥炉出口側ｼｬｯﾀｰ開端2
LD Y2522                 ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
LDB X1370                ; 乾燥炉出口側ｼｬｯﾀｰ開端1
ORB X1372                ; 乾燥炉出口側ｼｬｯﾀｰ開端2
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1628                ; UNIT6-1-9 TP-PL
LD X1371                 ; 乾燥炉出口側ｼｬｯﾀｰ閉端1
AND X1373                ; 乾燥炉出口側ｼｬｯﾀｰ閉端2
LD Y2523                 ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
LDB X1371                ; 乾燥炉出口側ｼｬｯﾀｰ閉端1
ORB X1373                ; 乾燥炉出口側ｼｬｯﾀｰ閉端2
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1629                ; UNIT6-1-10 TP-PL
LD Y2435                 ; 乾燥炉 Fan電源ON1
OUT B167E
LDB Y2435                ; 乾燥炉 Fan電源ON1
OUT B167F
;<h1/>異常検出
LD T0                    ; 電源投入ONﾃﾞｨﾚｲ
MC
LD M50                   ; 運転準備
AND T600                 ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C0                 ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B0C0                 ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常
LD Y2520                 ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
ANB X136A                ; 乾燥炉投入側ｼｬｯﾀｰ開端1
LD Y2521                 ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
ANB X136B                ; 乾燥炉投入側ｼｬｯﾀｰ閉端1
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C1                 ; 乾燥炉投入側ｼｬｯﾀｰ右開閉LS異常
ORL
MPS
TMR K651 K50             ; 乾燥炉投入側ｼｬｯﾀｰ右開閉LS確認
MPP
AND T651                 ; 乾燥炉投入側ｼｬｯﾀｰ右開閉LS確認
OUT B0C1                 ; 乾燥炉投入側ｼｬｯﾀｰ右開閉LS異常
LD Y2520                 ; 乾燥炉投入側ｼｬｯﾀｰ開SOL
ANB X136C                ; 乾燥炉投入側ｼｬｯﾀｰ開端2
LD Y2521                 ; 乾燥炉投入側ｼｬｯﾀｰ閉SOL
ANB X136D                ; 乾燥炉投入側ｼｬｯﾀｰ閉端2
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C2                 ; 乾燥炉投入側ｼｬｯﾀｰ左開閉LS異常
ORL
MPS
TMR K652 K50             ; 乾燥炉投入側ｼｬｯﾀｰ左開閉LS確認
MPP
AND T652                 ; 乾燥炉投入側ｼｬｯﾀｰ左開閉LS確認
OUT B0C2                 ; 乾燥炉投入側ｼｬｯﾀｰ左開閉LS異常
LD Y2540                 ; 乾燥炉ｽﾄｯﾊﾟｰ1下降SOL
ANB X1328                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
LD Y2541                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇SOL
ANB X1329                ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C3                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上下LS異常
ORL
MPS
TMR K653 K50             ; 乾燥炉ｽﾄｯﾊﾟｰ1上下LS確認
MPP
AND T653                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上下LS確認
OUT B0C3                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上下LS異常
LD Y2542                 ; 乾燥炉ｽﾄｯﾊﾟｰ2下降SOL
ANB X132A                ; 乾燥炉ｽﾄｯﾊﾟｰ2下降端
LD Y2543                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇SOL
ANB X132B                ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C4                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上下LS異常
ORL
MPS
TMR K654 K50             ; 乾燥炉ｽﾄｯﾊﾟｰ2上下LS確認
MPP
AND T654                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上下LS確認
OUT B0C4                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上下LS異常
LD M50                   ; 運転準備
ANB X1301                ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C5                 ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
ORL
MPS
TMH K655 K10             ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
MPP
AND T655                 ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
OUT B0C5                 ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
LD M16                   ; ﾘｾｯﾄPB
ANB X1301                ; 乾燥炉CVﾓｰﾀｰｲﾄﾞﾗｲﾊﾞｰ異常
OUT Y2421                ; 乾燥炉CVｲﾝﾊﾞｰﾀｱﾗｰﾑﾘｾｯﾄ
LD Y2522                 ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
ANB X1370                ; 乾燥炉出口側ｼｬｯﾀｰ開端1
LD Y2523                 ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
ANB X1371                ; 乾燥炉出口側ｼｬｯﾀｰ閉端1
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C6                 ; 乾燥炉出口側ｼｬｯﾀｰ右開閉LS異常
ORL
MPS
TMR K656 K50             ; 乾燥炉出口側ｼｬｯﾀｰ右開閉LS確認
MPP
AND T656                 ; 乾燥炉出口側ｼｬｯﾀｰ右開閉LS確認
OUT B0C6                 ; 乾燥炉出口側ｼｬｯﾀｰ右開閉LS異常
LD Y2522                 ; 乾燥炉出口側ｼｬｯﾀｰ開SOL
ANB X1372                ; 乾燥炉出口側ｼｬｯﾀｰ開端2
LD Y2523                 ; 乾燥炉出口側ｼｬｯﾀｰ閉SOL
ANB X1373                ; 乾燥炉出口側ｼｬｯﾀｰ閉端2
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C7                 ; 乾燥炉出口側ｼｬｯﾀｰ左開閉LS異常
ORL
MPS
TMR K657 K50             ; 乾燥炉出口側ｼｬｯﾀｰ左開閉LS確認
MPP
AND T657                 ; 乾燥炉出口側ｼｬｯﾀｰ左開閉LS確認
OUT B0C7                 ; 乾燥炉出口側ｼｬｯﾀｰ左開閉LS異常
LD M3871                 ; 乾燥炉2ﾗｯｸ排出中
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LD M3903                 ; 乾燥炉出口側ｼｬｯﾀｰ開端
AND M3912                ; 炉排出側ｼｬｯﾀｰﾗｯｸ通過中確認
ORL
AND B0C8                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
ORL
MPS
TMR K658 K400            ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ
MPP
AND T658                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ
OUT B0C8                 ; 炉排出ﾗｯｸ通過ﾀｲﾑｱｳﾄ異常
LD M17                   ; 原点復帰PB
ANB M72                  ; 自動運転開始
AND= D3630 K1            ; 乾燥炉２ﾗｯｸ排出中記憶
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB B119C                ; 炉排出ﾗｯｸ排出未完了確認SW
AND B0C9                 ; 炉排出ﾗｯｸ排出未完了
ORL
OUT B0C9                 ; 炉排出ﾗｯｸ排出未完了
LDP B0C9                 ; 炉排出ﾗｯｸ排出未完了
OR M3898                 ; 炉排出ﾗｯｸ排出未了ﾒｯｾｰｼﾞON
ANB B119C                ; 炉排出ﾗｯｸ排出未完了確認SW
OUT M3898                ; 炉排出ﾗｯｸ排出未了ﾒｯｾｰｼﾞON
LD B119C                 ; 炉排出ﾗｯｸ排出未完了確認SW
@MOV K0 D3630            ; 乾燥炉２ﾗｯｸ排出中記憶
LD M3870                 ; 乾燥炉内ﾗｯｸ移動中
ANB M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ有確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0CA                 ; 乾燥炉内ﾗｯｸ移動異常
ORL
MPS
TMR K659 K200            ; 乾燥炉内ﾗｯｸ移動異常確認
MPP
AND T659                 ; 乾燥炉内ﾗｯｸ移動異常確認
OUT B0CA                 ; 乾燥炉内ﾗｯｸ移動異常
LD M17                   ; 原点復帰PB
ANB M72                  ; 自動運転開始
LD= D3640 K1             ; 乾燥炉内ﾗｯｸ移動中
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1(投入側)ﾗｯｸ有確認
AND M3905                ; 乾燥炉ｽﾄｯﾊﾟｰ1下降端
ORL
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB B1198                ; 乾燥炉内ﾗｯｸ位置異常確認SW
AND B0CB                 ; 乾燥炉内ﾗｯｸ位置異常
ORL
OUT B0CB                 ; 乾燥炉内ﾗｯｸ位置異常
LDP B0CB                 ; 乾燥炉内ﾗｯｸ位置異常
OR M3899                 ; 乾燥炉内ﾗｯｸ位置異常ﾒｯｾｰｼﾞON
ANB B1198                ; 乾燥炉内ﾗｯｸ位置異常確認SW
AND B0CB                 ; 乾燥炉内ﾗｯｸ位置異常
OUT M3899                ; 乾燥炉内ﾗｯｸ位置異常ﾒｯｾｰｼﾞON
LD B1198                 ; 乾燥炉内ﾗｯｸ位置異常確認SW
@MOV K0 D3640            ; 乾燥炉内ﾗｯｸ移動中
LD Y2435                 ; 乾燥炉 Fan電源ON1
TMR K670 K600            ; Oven Fan監視ﾃﾞｨﾚｰ時間
LD Y2435                 ; 乾燥炉 Fan電源ON1
AND T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
MPS
ANB T672                 ; 1時間ｸﾛｯｸ　ﾘｾｯﾄ
TMR K671 K36000          ; 1時間ｸﾛｯｸ
MPP
AND T671                 ; 1時間ｸﾛｯｸ
TMR K672 K3              ; 1時間ｸﾛｯｸ　ﾘｾｯﾄ
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1400                ; 乾燥炉_上流1段目FAN10回転監視
LDF X1400                ; 乾燥炉_上流1段目FAN10回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0140                ; 乾燥_上流1段目FAN10回転低下異常
ORL
OUT B0140                ; 乾燥_上流1段目FAN10回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1401                ; 乾燥炉_上流2段目FAN11回転監視
LDF X1401                ; 乾燥炉_上流2段目FAN11回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0141                ; 乾燥_上流2段目FAN11回転低下異常
ORL
OUT B0141                ; 乾燥_上流2段目FAN11回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1402                ; 乾燥炉_上流3段目FAN12回転監視
LDF X1402                ; 乾燥炉_上流3段目FAN12回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0142                ; 乾燥_上流3段目FAN12回転低下異常
ORL
OUT B0142                ; 乾燥_上流3段目FAN12回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1403                ; 乾燥炉_上流4段目FAN13回転監視
LDF X1403                ; 乾燥炉_上流4段目FAN13回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0143                ; 乾燥_上流4段目FAN13回転低下異常
ORL
OUT B0143                ; 乾燥_上流4段目FAN13回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1404                ; 乾燥炉_上流5段目FAN14回転監視
LDF X1404                ; 乾燥炉_上流5段目FAN14回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0144                ; 乾燥_上流5段目FAN14回転低下異常
ORL
OUT B0144                ; 乾燥_上流5段目FAN14回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1405                ; 乾燥炉_上流6段目FAN15回転監視
LDF X1405                ; 乾燥炉_上流6段目FAN15回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0145                ; 乾燥_上流6段目FAN15回転低下異常
ORL
OUT B0145                ; 乾燥_上流6段目FAN15回転低下異常
LD CR2003                ; 常時OFF
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0146
ORL
OUT B0146
LD CR2003                ; 常時OFF
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0147
ORL
OUT B0147
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1408                ; 乾燥炉_下流1段目FAN20回転監視
LDF X1408                ; 乾燥炉_下流1段目FAN20回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0148                ; 乾燥_下流1段目FAN20回転低下異常
ORL
OUT B0148                ; 乾燥_下流1段目FAN20回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X1409                ; 乾燥炉_下流2段目FAN21回転監視
LDF X1409                ; 乾燥炉_下流2段目FAN21回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0149                ; 乾燥_下流2段目FAN21回転低下異常
ORL
OUT B0149                ; 乾燥_下流2段目FAN21回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X140A                ; 乾燥炉_下流3段目FAN22回転監視
LDF X140A                ; 乾燥炉_下流3段目FAN22回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014A                ; 乾燥_下流3段目FAN22回転低下異常
ORL
OUT B014A                ; 乾燥_下流3段目FAN22回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X140B                ; 乾燥炉_下流4段目FAN23回転監視
LDF X140B                ; 乾燥炉_下流4段目FAN23回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014B                ; 乾燥_下流4段目FAN23回転低下異常
ORL
OUT B014B                ; 乾燥_下流4段目FAN23回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X140C                ; 乾燥炉_下流5段目FAN24回転監視
LDF X140C                ; 乾燥炉_下流5段目FAN24回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014C                ; 乾燥_下流5段目FAN24回転低下異常
ORL
OUT B014C                ; 乾燥_下流5段目FAN24回転低下異常
LDP T670                 ; Oven Fan監視ﾃﾞｨﾚｰ時間
ORP T671                 ; 1時間ｸﾛｯｸ
ANB X140D                ; 乾燥炉_下流6段目FAN25回転監視
LDF X140D                ; 乾燥炉_下流6段目FAN25回転監視
AND M3820                ; 乾燥炉FANﾓｰﾀ起動ON
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014D                ; 乾燥_下流6段目FAN25回転低下異常
ORL
OUT B014D                ; 乾燥_下流6段目FAN25回転低下異常
LD CR2003                ; 常時OFF
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014E
ORL
OUT B014E
LD CR2003                ; 常時OFF
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B014F
ORL
OUT B014F
;ﾌﾞｻﾞｰ処理
LD<>.D B0C0 D3510        ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常  ﾌﾞｻﾞｰ停止
AND<>.D B0C0 K0          ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常
LD<> B0140 D3512         ; 乾燥_上流1段目FAN10回転低下異常  ﾌﾞｻﾞｰ停止
AND<> B0140 K0           ; 乾燥_上流1段目FAN10回転低下異常
ORL
OUT M3989                ; ﾕﾆｯﾄ6異常BZ補助
LDB M110                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
INV
MOV.D B0C0 D3510         ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常  ﾌﾞｻﾞｰ停止
MOV.D B0140 D3512        ; 乾燥_上流1段目FAN10回転低下異常  ﾌﾞｻﾞｰ停止
LD=.D B0C0 K0            ; 乾燥炉原点復帰ﾀｲﾑｱｳﾄ異常
@MOV.D K0 D3510          ; ﾌﾞｻﾞｰ停止
LD= B0140 K0             ; 乾燥_上流1段目FAN10回転低下異常
@MOV K0 D3512            ; ﾌﾞｻﾞｰ停止
MCR
END
ENDH
