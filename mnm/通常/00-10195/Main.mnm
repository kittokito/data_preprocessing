DEVICE:128
;MODULE:Main
;MODULE_TYPE:0
;******************************
;*                            *
;*  Main                      *
;*                            *
;******************************
;
;-----  基礎要素  -----
LD CR2007                ; 運転開始時1ｽｷｬﾝOFF
TMR K0 K50               ; 電源ON_5秒
LD T0                    ; 電源ON_5秒
ANB M1014                ; 異常で運転準備OFF必要
ANB B070                 ; 運転準備切ｷｰ
OUT M0                   ; 運転準備可
LD X00                   ; 運転準備MgSW
OUT M1                   ; 運転準備
TMH K1 K100              ; 運転準備安定
LDB T3                   ; 0.3秒ｸﾛｯｸ(ﾀﾞﾐｰ)
MPS
TMH K2 K15               ; 0.3秒ｸﾛｯｸ
MPP
AND T2                   ; 0.3秒ｸﾛｯｸ
TMH K3 K15               ; 0.3秒ｸﾛｯｸ(ﾀﾞﾐｰ)
LDB T5                   ; 2秒ｸﾛｯｸ(ﾀﾞﾐｰ)
MPS
TMH K4 K100              ; 2秒ｸﾛｯｸ
MPP
AND T4                   ; 2秒ｸﾛｯｸ
TMH K5 K100              ; 2秒ｸﾛｯｸ(ﾀﾞﾐｰ)
LDB Y154                 ; ｼｰﾙ機:ｼｰﾙｽﾀｰﾄﾘﾚｰ
TMR K6 K5                ; ﾋｰﾄｼｰﾙOFF安定
LDF X207                 ; 運転準備PB
LD M2                    ; 運転準備PB_OFF有り
ANB T7                   ; 運転準備ON許容
ORL
OR M3                    ; 運転準備起動
ANB T1                   ; 運転準備安定
OUT M2                   ; 運転準備PB_OFF有り
MPS
TMH K7 K30               ; 運転準備ON許容
MPP
AND X0C                  ; ｾｰﾌﾃｨ運転準備可
OUT M3                   ; 運転準備起動
LDP X208                 ; 予熱ﾋｰﾀPB
AND M10                  ; 予熱ﾋｰﾀ入
OUT M80                  ; 予熱中止
LDP X208                 ; 予熱ﾋｰﾀPB
ANB M10                  ; 予熱ﾋｰﾀ入
ANB M50                  ; 自動運転
LD M10                   ; 予熱ﾋｰﾀ入
LDPB X208                ; 予熱ﾋｰﾀPB
OR M50                   ; 自動運転
ANL
ORL
AND T1                   ; 運転準備安定
ANB M80                  ; 予熱中止
OUT M10                  ; 予熱ﾋｰﾀ入
LD Y153                  ; 予熱ﾋｰﾀ通電可
TMR K8 K50               ; 予熱ﾋｰﾀ通電後5sec
;-----  運転モード、起動  -----
LD X202                  ; 起動PB
TMR K10 K50              ; 起動PB長押し
OUT M20                  ; 起動PB
MEP
OUT M21                  ; 起動PB_PLS
LD X201                  ; 運転ﾓｰﾄﾞCOS自動
TMR K11 K3               ; ﾓｰﾄﾞ自動安定
INV
TMR K12 K3               ; ﾓｰﾄﾞ手動安定
LDB X01                  ; 正面扉ﾄﾞｱｽｲｯﾁOFF
ANB X02                  ; 後面扉ﾄﾞｱｽｲｯﾁOFF
OUT M30                  ; 扉閉じOK
LD M1104                 ; ｴｱ元圧PS安定
OUT M31                  ; ｴｱ元圧OK
LD M1101                 ; 下ﾋｰﾀ下端安定
AND M1103                ; 上ﾋｰﾀ上端安定
AND M1106                ; 予熱ﾋｰﾀ開き端安定
OUT M32                  ; 装置原位置
LD= D3028 K0             ; 実行中品種設定5L
OUT M132                 ; 予熱無仕様
LD M10                   ; 予熱ﾋｰﾀ入
AND M1107                ; 予熱ﾋｰﾀ上:温度安定
AND M1108                ; 予熱ﾋｰﾀ下:温度安定
OR M132                  ; 予熱無仕様
OUT M33                  ; 予熱ﾋｰﾀOK
LD T1                    ; 運転準備安定
AND T11                  ; ﾓｰﾄﾞ自動安定
AND M31                  ; ｴｱ元圧OK
AND M32                  ; 装置原位置
AND M33                  ; 予熱ﾋｰﾀOK
ANB M1010                ; 異常有り
ANB M1012                ; 警報有り
OUT M40                  ; 自動準備完了
LD M1010                 ; 異常有り
ORB M1                   ; 運転準備
ORB X201                 ; 運転ﾓｰﾄﾞCOS自動
OR T120                  ; 強制ﾘｾｯﾄ
OUT M42                  ; 自動停止条件(即停止)
LD X202                  ; 起動PB
OR T20                   ; 起動BZ_ON時間UP
AND M40                  ; 自動準備完了
AND M1201                ; 起動BZ有り
ANB T21                  ; 起動BZ_OFF時間UP
ANB T120                 ; 強制ﾘｾｯﾄ
OUT M46                  ; 自動起動BZ_ON中
MPS
TMR K20 D2604            ; 起動BZ_ON時間UP  装置設3:起動BZ_ON_T
MPP
AND T20                  ; 起動BZ_ON時間UP
TMR K21 D2606            ; 起動BZ_OFF時間UP  装置設4:起動BZ_OFF_T
LD M46                   ; 自動起動BZ_ON中
AND T20                  ; 起動BZ_ON時間UP
ORB M1201                ; 起動BZ有り
AND M21                  ; 起動PB_PLS
AND M40                  ; 自動準備完了
OR M50                   ; 自動運転
ANB M42                  ; 自動停止条件(即停止)
ANB M55                  ; 自動運転動作終了
OUT M50                  ; 自動運転
LD M140                  ; 自動運転完了統括
AND M32                  ; 装置原位置
LD M150                  ; ｽﾃｯﾌﾟ停止ﾀｲﾐﾝｸﾞ
AND M101                 ; 停止PB押下げ有り
ORL
OUT M55                  ; 自動運転動作終了
LD T1                    ; 運転準備安定
AND T12                  ; ﾓｰﾄﾞ手動安定
AND M31                  ; ｴｱ元圧OK
ANB M50                  ; 自動運転
OUT M70                  ; 手動運転
LD B072                  ; 原点復帰ｷｰ
LD M72                   ; 原点復帰動作
ANB M32                  ; 装置原位置
ORL
AND M70                  ; 手動運転
ANB M1010                ; 異常有り
ANB M1012                ; 警報有り
OUT M72                  ; 原点復帰動作
LD M70                   ; 手動運転
ANB M72                  ; 原点復帰動作
OUT M74                  ; 手動操作可
END
ENDH
