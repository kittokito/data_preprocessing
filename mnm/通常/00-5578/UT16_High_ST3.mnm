DEVICE:53
;MODULE:UT16_High_ST3
;MODULE_TYPE:0
;SCRIPT_TYPE:
;<h1/>□_入力受け
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X1303                 ; 高温槽全扉正常(まとめ)
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
TMR K1601 K5             ; HiST3扉閉
;ｺﾝﾀｸﾄﾍｯﾄﾞID
LD X1384                 ; HiST3_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_1
OUT D8606.0
LD X1385                 ; HiST3_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_2
OUT D8606.1
LD X1386                 ; HiST3_部_ｺﾝﾀｸﾄﾍｯﾄﾞID_4
OUT D8606.2
;ﾊﾟﾚｯﾄID
LD X1387                 ; HiST3_部_ﾊﾟﾚｯﾄID_1
OUT D8607.0
LD X1388                 ; HiST3_部_ﾊﾟﾚｯﾄID_2
OUT D8607.1
LD X1389                 ; HiST3_部_ﾊﾟﾚｯﾄID_4
OUT D8607.2
;ＩＤ保持
LD M71                   ; 自動運転開始BZ
AND L1600                ; Hi検査ST3_有効状態
LD M9912                 ; HiST3_ID保持ﾄﾘｶﾞ
ANB M9910                ; HiST3_無効中
ORL
AND M71                  ; 自動運転開始BZ
OUT M9912                ; HiST3_ID保持ﾄﾘｶﾞ
;____ｺﾝﾀｸﾄﾍｯﾄﾞＩＤ保持
;※PC送信用は生ﾃﾞｰﾀに変更
LD L1600                 ; Hi検査ST3_有効状態
MPS
TMR K1606 K5             ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
MPP
AND T1606                ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
@MOV D8606 D8600         ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
LD CR2002                ; 常時ON
MOV D8606 W013A          ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy  ﾃｽﾄﾍｯﾄﾞID#3
LDB L1600                ; Hi検査ST3_有効状態
MOV K0 D8600             ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
;____ﾊﾟﾚｯﾄＩＤ保持
LD L1600                 ; Hi検査ST3_有効状態
LD M9621                 ; RB14_ﾜｰｸ受取り位置(ID確認位置)
OR T1607                 ; HiST3_ﾊﾟﾚｯﾄID_更新_DLY
ANL
TMR K1607 K5             ; HiST3_ﾊﾟﾚｯﾄID_更新_DLY
@MOV D8607 D8601         ; HiST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態  HiST3_ﾊﾟﾚｯﾄID
LD CR2002                ; 常時ON
MOV D8601 W0134          ; HiST3_ﾊﾟﾚｯﾄID  ﾊﾟﾚｯﾄID#3
LDB L1600                ; Hi検査ST3_有効状態
MOV K0 D8601             ; HiST3_ﾊﾟﾚｯﾄID
;<h1/>__ST_有効/無効選択
;段取り状態中ﾌﾗｸﾞの追加
;自動中は段取り状態中に、ST上のﾜｰｸ無、RBﾊﾝﾄﾞﾜｰｸ無し、供給部ﾜｰｸ無しにて無効状態OKとなる
LDPB X138F               ; ST3電特検査_有効/無効SW
ANPB B126A               ; HiST3検査_有効/無効_Tpb
INV
ANB L1600                ; Hi検査ST3_有効状態
OR M9904                 ; HiST3検査_有効確認要求
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
ANB M9905                ; HiST3検査_有効確認OK
OUT M9904                ; HiST3検査_有効確認要求
LD M9904                 ; HiST3検査_有効確認要求
AND W0107.10             ; Hi_ST3ﾁｪｯｶ-確認要求
LD W07.10                ; Hi_ST3ﾁｪｯｶ-確認完了
OR M3                    ; OFFLINE_MODE
ANL
OUT M9905                ; HiST3検査_有効確認OK
LD M9905                 ; HiST3検査_有効確認OK
ANB W05.3                ; ｽﾃｰｼﾞ#3 無効切替要求
LDB X138F                ; ST3電特検査_有効/無効SW
ANB B126A                ; HiST3検査_有効/無効_Tpb
ANB W05.3                ; ｽﾃｰｼﾞ#3 無効切替要求
INV
AND L1600                ; Hi検査ST3_有効状態
ORL
OUT M9906                ; HiST3検査_有効/無効要求
LD M71                   ; 自動運転開始BZ
LDB M7981                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M7988                ; ﾊﾝﾄﾞ1_Hi検査ST4再検品
OR M7983                 ; ﾊﾝﾄﾞ1_OK品
ANL
LDB M7991                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M7998                ; ﾊﾝﾄﾞ2_Hi検査ST4再検品
OR M7993                 ; ﾊﾝﾄﾞ2_OK品
ANL
AND= D8701 K0            ; HiST3 ﾜｰｸ有無記憶
LDB M9985                ; U16_起動中
LD M9501                 ; U16-1_S01
ANB M7902                ; 【搬送】搬送→Hi_ST3搬送開始
ORL
ANL
ANB M8034                ; ﾊﾝﾄﾞ1_HiST3給材中ﾌﾗｸﾞ
ANB M8035                ; ﾊﾝﾄﾞ2_HiST3給材中ﾌﾗｸﾞ
ANB M7981                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M7991                ; ﾊﾝﾄﾞ2_Hi検査未了品
OUT M9907                ; HiST3_自動中無効条件OK
LDB M71                  ; 自動運転開始BZ
OR M9907                 ; HiST3_自動中無効条件OK
OUT M9908                ; HiST3_無効OK条件
LD M9909                 ; HiST3_無効予約(自動運転)
ANB M9910                ; HiST3_無効中
ANP M9906                ; HiST3検査_有効/無効要求
OUT M9911                ; HiST3_予約ｷｬﾝｾﾙ
LDP M9906                ; HiST3検査_有効/無効要求
AND L1600                ; Hi検査ST3_有効状態
AND M71                  ; 自動運転開始BZ
ANB M9909                ; HiST3_無効予約(自動運転)
LD M9909                 ; HiST3_無効予約(自動運転)
ANB M9910                ; HiST3_無効中
ANB M9911                ; HiST3_予約ｷｬﾝｾﾙ
ORL
OUT M9909                ; HiST3_無効予約(自動運転)
LD M9909                 ; HiST3_無効予約(自動運転)
AND M9908                ; HiST3_無効OK条件
LDP M9906                ; HiST3検査_有効/無効要求
ANB M41                  ; ﾕﾆｯﾄ_起動中
ORL
LDB B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
ANB B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
INV
MEP
ORL
LD M9910                 ; HiST3_無効中
ANB L1600                ; Hi検査ST3_有効状態
ORL
OUT M9910                ; HiST3_無効中
;<h1/>__ST_有効処理
LDP M9906                ; HiST3検査_有効/無効要求
LD<> D8606 K0            ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND<> D8607 K0           ; HiST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8606 K0            ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
AND= D8607 K0            ; HiST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
ORL
ANL
ANB L1600                ; Hi検査ST3_有効状態
LD L1600                 ; Hi検査ST3_有効状態
ANB M9910                ; HiST3_無効中
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
ORL
OUT L1600                ; Hi検査ST3_有効状態
;JIG_ID一致確認
LD L1600                 ; Hi検査ST3_有効状態
MPS
LD= D8600 D7211          ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  ﾊﾝﾄﾞ1 高温検査ﾃｽﾄﾍｯﾄﾞID
AND= D8601 D7210         ; HiST3_ﾊﾟﾚｯﾄID  ﾊﾝﾄﾞ1 高温検査ﾊﾟﾚｯﾄID
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8600 K0            ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
AND= D8601 K0            ; HiST3_ﾊﾟﾚｯﾄID
ORL
ANL
OUT M9901                ; HiST3-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
MPP
LD= D8600 D7311          ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  ﾊﾝﾄﾞ2 高温検査ﾃｽﾄﾍｯﾄﾞID
AND= D8601 D7310         ; HiST3_ﾊﾟﾚｯﾄID  ﾊﾝﾄﾞ2 高温検査ﾊﾟﾚｯﾄID
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D8600 K0            ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID
AND= D8601 K0            ; HiST3_ﾊﾟﾚｯﾄID
ORL
ANL
OUT M9902                ; HiST3-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
;<h1/>__ﾛﾎﾞｯﾄ等機器入力受け
;<h1/>□_装置状態・動作条件
;ﾕﾆｯﾄまとめ
LD M9570                 ; U16-1原点
AND M9730                ; U16-3原点
OUT M9980                ; U16_原点
;□ 非常停止
LDB M9571                ; U16-1全異常
ANB M9731                ; U16-3全異常
INV
OUT M9981                ; U16_即停止異常
;□ 起動条件
LD CR2003                ; 常時OFF
OUT M9982                ; U16_起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M9984                ; U16_原点復帰中
ORL
OR M9986                 ; U16_CYCLE停止異常
OR T15                   ; 強制停止
OUT M9983                ; U16_起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M9984                ; U16_原点復帰中
OR M9984                 ; U16_原点復帰中
ANB M9983                ; U16_起動OFF
ANB M9991                ; U16_ERR発生
ANB M9797                ; OriStepEND
OUT M9984                ; U16_原点復帰中
;□ 起動中
LDB M9574                ; U16-1起動中
ANB M9734                ; U16-3(検査SUB)起動中
INV
OUT M9985                ; U16_起動中
;□ ｻｲｸﾙ停止異常
LDB M9575                ; U16-1ｻｲｸﾙ停止異常
ANB M9735                ; U16-3ｻｲｸﾙ停止異常
INV
OUT M9986                ; U16_CYCLE停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9987                ; U16_払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M9988                ; U16_警報補助
;□ 機器準備確認
LD X384C                 ; H3st(RB14)ｻｰﾎﾞ状態_SV
ANB X3849                ; H3st(RB14)運転ﾓｰﾄﾞ_RMDS
AND X384D                ; H3st(RB14)非常停止_*EMGS
AND X384E                ; H3st(RB14)ｱﾗｰﾑ_*ALM
OUT M9990                ; U16_機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB M9576                ; U16-1ｴﾗｰ発生
ANB M9736                ; U16-3ｴﾗｰ発生
INV
OUT M9991                ; U16_ERR発生
;□ 動作中ﾕﾆｯﾄ無し
LD M9577                 ; U16-1動作無し
AND M9737                ; U16-3動作無し
OUT M9992                ; U16_動作無し確認
;□ 払出しﾜｰｸ無し
LDB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
ANB M7981                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M7987                ; ﾊﾝﾄﾞ1_Hi検査ST3再検品
ANB M7991                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M7997                ; ﾊﾝﾄﾞ2_Hi検査ST3再検品
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
TMR K1614 K10
LD T1614
OUT M9993                ; U16_WORK無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
AND T1601                ; HiST3扉閉
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M57                  ; 全ﾕﾆｯﾄ払出完了
ANB M59                  ; 払出停止異常まとめ
ANB M9991                ; U16_ERR発生
LDB M140                 ; 全一時停止
ANB M180                 ; 高温検査一時停止
ORB M9517                ; U16-1_S17(検査SUB)起動確認
ANL
OUT M9979                ; U16_STEP動作
;<h1/>□_自動回路
;<h1/>◇_状態・動作条件(補助)
LD X1381                 ; HiST3_部_ﾜｰｸ押_上昇端
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
ANB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
AND= D8701 K0            ; HiST3 ﾜｰｸ有無記憶
OUT M9570                ; U16-1原点
LDB B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
ANB B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
ANB B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
ANB B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
ANB B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
ANB B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
INV
OUT M9571                ; U16-1全異常
LD M9570                 ; U16-1原点
ANB M9574                ; U16-1起動中
AND M9979                ; U16_STEP動作
LD M9901                 ; HiST3-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
OR M9902                 ; HiST3-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
ANL
ANB M9908                ; HiST3_無効OK条件
OUT M9572                ; U16-1起動条件
LDB M50                  ; 運転準備
ORB L1600                ; Hi検査ST3_有効状態
OR M9986                 ; U16_CYCLE停止異常
OR T15                   ; 強制停止
OUT M9573                ; U16-1起動OFF
LD M73                   ; 各工程起動指令
AND M9572                ; U16-1起動条件
ANB B101D                ; 途中投入選択TP(1:単独_0:連動)
OR M9574                 ; U16-1起動中
ANB M9573                ; U16-1起動OFF
ANB M9563                ; U16-1 StepEnd
OUT M9574                ; U16-1起動中
;ﾕﾆｯﾄ1-1 ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9575                ; U16-1ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-1 ｴﾗｰ発生まとめ
LDB B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
ANB B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
ANB B021F                ; B21F_544)HiST3ﾜｰｸ検査中
INV
OUT M9576                ; U16-1ｴﾗｰ発生
;ﾕﾆｯﾄ1-1 停止判定
LD= D8500 D8505          ; HiST3-1自動運転ｽﾃｯﾌﾟ  HiST3-1動作中判定
ORB M9574                ; U16-1起動中
TMR K1610 K20            ; 停止完了判定ﾀｲﾏｰ
LD CR2002                ; 常時ON
MOV D8500 D8505          ; HiST3-1自動運転ｽﾃｯﾌﾟ  HiST3-1動作中判定
LD T1610                 ; 停止完了判定ﾀｲﾏｰ
OUT M9577                ; U16-1動作無し
;□
;□　ﾕﾆｯﾄ1-1 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M9500 K4         ; U16-1_S00
LDB M9574                ; U16-1起動中
MOV K0 D8500             ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9574                 ; U16-1起動中
MOV D8500 Z1             ; HiST3-1自動運転ｽﾃｯﾌﾟ
SET M9500:Z1             ; U16-1_S00
;ﾜｰｸ有無判定等
LD M9500                 ; U16-1_S00
AND M9979                ; U16_STEP動作
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S01_給材要求
LD M9501                 ; U16-1_S01
MPS
ANB M9993                ; U16_WORK無し
MPS
AND M7902                ; 【搬送】搬送→Hi_ST3搬送開始
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
LD M9901                 ; HiST3-搬送ﾊﾝﾄﾞ1ID一致ﾌﾗｸﾞ
OR M9902                 ; HiST3-搬送ﾊﾝﾄﾞ2ID一致ﾌﾗｸﾞ
ANB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
ANL
OUT M9920                ; HiST3→搬送ﾜｰｸ供給要求
MPP
LD M9993                 ; U16_WORK無し
AND M36                  ; 払出処理
ANL
MOV K63 D8500            ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9502                 ; U16-1_S02
LD M7907                 ; 【搬送】搬送→Hi_ST3搬送完了
OR M9922                 ; HiST3→搬送ﾜｰｸ受取り完了
ANL
MPS
LDB M7902                ; 【搬送】搬送→Hi_ST3搬送開始
ANB M7907                ; 【搬送】搬送→Hi_ST3搬送完了
ANL
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M9922                ; HiST3→搬送ﾜｰｸ受取り完了
LD M9503                 ; U16-1_S03
AND M9979                ; U16_STEP動作
AND= D8701 K1            ; HiST3 ﾜｰｸ有無記憶
MPS
LD X1382                 ; HiST3_部_ﾜｰｸ確認(ON=有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
MOV K60 D8500            ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9560                 ; U16-1_S60 HiST2_ﾜｰｸ受取異常
AND B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
MOV K63 D8500            ; HiST3-1自動運転ｽﾃｯﾌﾟ
;　　ｸﾗﾝﾌﾟ指令
LD M9504                 ; U16-1_S04_ｸﾗﾝﾌﾟ指令
AND M9979                ; U16_STEP動作
AND X1380                ; HiST3_部_ﾜｰｸ押_下降端
ANB B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;　　Z軸上昇確認
LD M9505                 ; U16-1_S05_開始確認待ち
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
LD L1352                 ; Hi搬送高さ移動完了(HIGH)
OR L1351                 ; Hi搬送高さ移動完了(MIDLE)
ANL
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
@INC D8520               ; HiST3投入回数
LD M9506                 ; U16-1_S06
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9507                 ; U16-1_S07
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9508                 ; U16-1_S08
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;　　■電特検査前処理
LD M9509                 ; U16-1_S09
AND M9979                ; U16_STEP動作
AND M9602                ; RB14_位置決め条件
MPS
@MOV D8550 D8540         ; ｺﾈｸﾀ挿入開始位置指令Pos  RB14_指令Pos
MPP
AND= D8550 D8540         ; ｺﾈｸﾀ挿入開始位置指令Pos  RB14_指令Pos
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9510                 ; U16-1_S10_ﾌﾟﾛｰﾌﾞｱﾌﾟﾛｰﾁ移動指令
AND M9604                ; RB14_位置決め起動指令
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9511                 ; U16-1_S11_到着確認
ANB M9604                ; RB14_位置決め起動指令
AND M9622                ; RB14_ｺﾈｸﾀ挿入開始位置
AND M9979                ; U16_STEP動作
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9512                 ; U16-1_S12_起動条件確認
AND M9979                ; U16_STEP動作
AND M9602                ; RB14_位置決め条件
MPS
@MOV D8551 D8540         ; ｺﾈｸﾀ挿入押込み量※1指令Pos  RB14_指令Pos
MPP
AND= D8551 D8540         ; ｺﾈｸﾀ挿入押込み量※1指令Pos  RB14_指令Pos
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9513                 ; U16-1_S13_ｺﾈｸﾀ押込位置移動指指令
AND M9604                ; RB14_位置決め起動指令
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9514                 ; U16-1_S14_到着確認
ANB M9604                ; RB14_位置決め起動指令
AND M9623                ; RB14_ｺﾈｸﾀ挿入押込み完了位置
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S15_電特検査処理開始
LD M9515                 ; U16-1_S15_検査SUB動作無確認
AND M9979                ; U16_STEP動作
ANB M9734                ; U16-3(検査SUB)起動中
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9516                 ; U16-1_S16_検査SUB開始ﾌﾗｸﾞ
AND M9979                ; U16_STEP動作
AND M9734                ; U16-3(検査SUB)起動中
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9517                 ; U16-1_S17(検査SUB)起動確認
AND M9979                ; U16_STEP動作
ANB M9734                ; U16-3(検査SUB)起動中
ANB M140                 ; 全一時停止
ANB M180                 ; 高温検査一時停止
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9518                 ; U16-1_S18
AND M9979                ; U16_STEP動作
AND M9602                ; RB14_位置決め条件
MPS
@MOV D8552 D8540         ; ｺﾈｸﾀ抜き位置Pos  RB14_指令Pos
MPP
AND= D8552 D8540         ; ｺﾈｸﾀ抜き位置Pos  RB14_指令Pos
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9519                 ; U16-1_S19_ｺﾈｸﾀ抜き位置移動指令
AND M9604                ; RB14_位置決め起動指令
AND M9979                ; U16_STEP動作
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;　　ｽﾃｰｼﾞ後退
LD M9520                 ; U16-1_S20_到着確認
ANB M9604                ; RB14_位置決め起動指令
AND M9624                ; RB14_ｺﾈｸﾀ抜き位置
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9521                 ; U16-1_S21_起動条件確認
AND M9979                ; U16_STEP動作
AND M9602                ; RB14_位置決め条件
MPS
@MOV K1 D8540            ; RB14_指令Pos
MPP
AND= K1 D8540            ; RB14_指令Pos
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9522                 ; U16-1_S22_ﾜｰｸ受取位置移動指令
AND M9604                ; RB14_位置決め起動指令
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9523                 ; U16-1_S23_到着確認
ANB M9604                ; RB14_位置決め起動指令
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9524                 ; U16-1_S24
AND M9979                ; U16_STEP動作
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
;<h1/>__S25_ﾜｰｸ除材要求
LDB M7981                ; ﾊﾝﾄﾞ1_Hi検査未了品
ANB M7988                ; ﾊﾝﾄﾞ1_Hi検査ST4再検品
ANB M7991                ; ﾊﾝﾄﾞ2_Hi検査未了品
ANB M7998                ; ﾊﾝﾄﾞ2_Hi検査ST4再検品
ANB M8024                ; ﾊﾝﾄﾞ1HiST3除材中ﾌﾗｸﾞ
ANB M8025                ; ﾊﾝﾄﾞ2HiST3除材中ﾌﾗｸﾞ
ANB M8909                ; HiST1_無効予約(自動運転)
ANB M9409                ; HiST2_無効予約(自動運転)
ANB M9909                ; HiST3_無効予約(自動運転)
ANB M10409               ; HiST4_無効予約(自動運転)
ANB M10909               ; HiST5_無効予約(自動運転)
INV
OUT M9925                ; HiST3_ﾜｰｸ除去要求発行許可
;M9　M1304を上流仮置きよりﾜｰｸ除材要求なしに入れ替え
LD M9525                 ; U16-1_S25_ﾜｰｸ受取要求
MPS
AND M7912                ; 【搬送】HiST3除材開始
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
LDB M6057                ; U10_S57_受渡可(→検査RB)
OR M9925                 ; HiST3_ﾜｰｸ除去要求発行許可
ANL
OUT M9921                ; HiST3→搬送1除材要求
LD M9526                 ; U16-1_S26
LD M7917                 ; 【搬送】HiST3除材完了
OR M9923                 ; HiST3→搬送除材完了受取
ANL
MPS
LDB M7912                ; 【搬送】HiST3除材開始
ANB M7917                ; 【搬送】HiST3除材完了
ANL
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
OUT M9923                ; HiST3→搬送除材完了受取
LD M9527                 ; U16-1_S27
AND M9979                ; U16_STEP動作
AND= D8701 K0            ; HiST3 ﾜｰｸ有無記憶
LD L1352                 ; Hi搬送高さ移動完了(HIGH)
OR L1351                 ; Hi搬送高さ移動完了(MIDLE)
OR L1350                 ; ﾜｰｸ入替高さ移動完了(HiW)
ANL
MPS
ANB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
OUT M9930                ; HiST3_ﾜｰｸ受渡し異常
LD M9528                 ; U16-1_S28
AND M9979                ; U16_STEP動作
INC D8500                ; HiST3-1自動運転ｽﾃｯﾌﾟ
LD M9529                 ; U16-1_S29
AND M9979                ; U16_STEP動作
MOV K63 D8500            ; HiST3-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M9562                 ; U16-1_S62
OR CR2003                ; 常時OFF
@MOV K63 D8500           ; HiST3-1自動運転ｽﾃｯﾌﾟ
;<h1/>◇_電特検査SUB 状態・動作条件(補助)
LD CR2002                ; 常時ON
OUT M9730                ; U16-3原点
LDB CR2003               ; 常時OFF
INV
OUT M9731                ; U16-3全異常
LD M9730                 ; U16-3原点
ANB M9734                ; U16-3(検査SUB)起動中
AND M9979                ; U16_STEP動作
LD M300                  ; PC通信正常
OR M3                    ; OFFLINE_MODE
ANL
OUT M9732                ; U16-3起動条件
LDB M50                  ; 運転準備
OR M9986                 ; U16_CYCLE停止異常
OR T15                   ; 強制停止
OUT M9733                ; U16-3起動OFF
LD M9516                 ; U16-1_S16_検査SUB開始ﾌﾗｸﾞ
AND M9732                ; U16-3起動条件
OR M9734                 ; U16-3(検査SUB)起動中
ANB M9733                ; U16-3起動OFF
ANB M9723                ; U16-3 StepEnd
OUT M9734                ; U16-3(検査SUB)起動中
;tp表示
LD CR2002                ; 常時ON
DW K0 D8900              ; Hi検査_ｽﾃｰﾀｽ表示
LD M9734                 ; U16-3(検査SUB)起動中
DW K1 D8900              ; Hi検査_ｽﾃｰﾀｽ表示
LD= D8704 K1             ; HiST3 Hi検査結果(1=OK_2=NG)
DW K2 D8900              ; Hi検査_ｽﾃｰﾀｽ表示
LD= D8704 K2             ; HiST3 Hi検査結果(1=OK_2=NG)
DW K3 D8900              ; Hi検査_ｽﾃｰﾀｽ表示
LDB L1600                ; Hi検査ST3_有効状態
DW K4 D8900              ; Hi検査_ｽﾃｰﾀｽ表示
;ﾕﾆｯﾄ1-3ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M9735                ; U16-3ｻｲｸﾙ停止異常
;ﾕﾆｯﾄ1-3ｴﾗｰ発生まとめ
LDB CR2003               ; 常時OFF
INV
OUT M9736                ; U16-3ｴﾗｰ発生
;ﾕﾆｯﾄ1-3停止判定
LD= D8502 D8507          ; HiST3-3自動運転ｽﾃｯﾌﾟ  HiST3-3動作中判定
TMR K1612 K20
LD CR2002                ; 常時ON
MOV D8502 D8507          ; HiST3-3自動運転ｽﾃｯﾌﾟ  HiST3-3動作中判定
LD T1612
ORB M9734                ; U16-3(検査SUB)起動中
OUT M9737                ; U16-3動作無し
;□
;□　ﾕﾆｯﾄ1-3自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M9660 K4         ; U16-3_S0
LDB M9734                ; U16-3(検査SUB)起動中
MOV K0 D8502             ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9734                 ; U16-3(検査SUB)起動中
MOV D8502 Z1             ; HiST3-3自動運転ｽﾃｯﾌﾟ
SET M9660:Z1             ; U16-3_S0
;<h1/>__S0_ﾜｰｸ有無判定
LD M9660                 ; U16-3_S0
MPS
LD T1601                 ; HiST3扉閉
AND M9979                ; U16_STEP動作
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
OR M3                    ; OFFLINE_MODE
ANL
MPS
AND T1630                ; Hi検査時間(仮)
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
MPP
TMR K1630 K880           ; Hi検査時間(仮)
LD M9661                 ; U16-3_S1Hi検査ｽﾀｰﾄ要求
AND W0105.8              ; 電気特性検査開始要求#3
LD W05.8                 ; 電気特性検査ｽﾃｰｼﾞ状態#3
ANB W05.13               ; 電気特性検査結果#3
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9662                 ; U16-3_S2
LDB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#3
OR M3                    ; OFFLINE_MODE
ANL
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9663                 ; U16-3_S3
MPS
AND<> D8704 K0           ; HiST3 Hi検査結果(1=OK_2=NG)
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
MRD
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M3                   ; OFFLINE_MODE
ANL
MPS
AND W05.13               ; 電気特性検査結果#3
MOV K1 D8704             ; HiST3 Hi検査結果(1=OK_2=NG)
MPP
ANB W05.13               ; 電気特性検査結果#3
MOV K2 D8704             ; HiST3 Hi検査結果(1=OK_2=NG)
MPP
LD M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MPS
ANB B1038                ; OFF_LINE_検査結果(OFF=OK_ON=NG)
MOV K1 D8704             ; HiST3 Hi検査結果(1=OK_2=NG)
MPP
AND B1038                ; OFF_LINE_検査結果(OFF=OK_ON=NG)
MOV K2 D8704             ; HiST3 Hi検査結果(1=OK_2=NG)
LD M9664                 ; U16-3_S4
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9665                 ; U16-3_S5
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9666                 ; U16-3_S6
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9667                 ; U16-3_S7
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9668                 ; U16-3_S8
INC D8502                ; HiST3-3自動運転ｽﾃｯﾌﾟ
LD M9669                 ; U16-3_S9
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
MOV K63 D8502            ; HiST3-3自動運転ｽﾃｯﾌﾟ
;<h1/>□_原点復帰回路
LD CR2002                ; 常時ON
FMOV K0 M9750 K3         ; OriStep0
LDB M9984                ; U16_原点復帰中
MOV K0 D8510             ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9984                 ; U16_原点復帰中
MOV D8510 Z1             ; HiST3 原点復帰ｽﾃｯﾌﾟ
SET M9750:Z1             ; OriStep0
;原点復帰ｻｲｸﾙ監視
LD M9984                 ; U16_原点復帰中
TMR K1600 K200           ; UNIT17原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M9750                 ; OriStep0
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND L1352                ; Hi搬送高さ移動完了(HIGH)
DW K0 D8520              ; HiST3投入回数
CON
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9751                 ; OriStep1
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
ANB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#3
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9752                 ; OriStep2
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND X384A                ; H3st(RB14)原点確定_HEND
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9753                 ; OriStep3_
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND M9602                ; RB14_位置決め条件
MPS
@MOV K1 D8540            ; RB14_指令Pos
MPP
AND= K1 D8540            ; RB14_指令Pos
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9754                 ; OriStep4_ｽﾃｰｼﾞ戻り指令
AND M9604                ; RB14_位置決め起動指令
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9755                 ; OriStep5_
ANB M9604                ; RB14_位置決め起動指令
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9756                 ; OriStep6_
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND L1352                ; Hi搬送高さ移動完了(HIGH)
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
LD M9757                 ; OriStep7_ｸﾗﾝﾌﾟ上昇指令
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND X1381                ; HiST3_部_ﾜｰｸ押_上昇端
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ有無ﾁｪｯｸ
LD M9758                 ; OriStep8
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
MPS
ANB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
MOV K10 D8510            ; HiST3 原点復帰ｽﾃｯﾌﾟ
MPP
AND X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
INC D8510                ; HiST3 原点復帰ｽﾃｯﾌﾟ
;　　ﾜｰｸ除去要求
LD M9759                 ; OriStep9_ORGﾜｰｸ除去要求
AND T1601                ; HiST3扉閉
AND M9979                ; U16_STEP動作
AND B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
MOV K46 D8510            ; HiST3 原点復帰ｽﾃｯﾌﾟ
;　　
LD M9760                 ; OriStep10
MPS
AND M9980                ; U16_原点
MOV K46 D8510            ; HiST3 原点復帰ｽﾃｯﾌﾟ
MPP
FMOV K0 D8700 K20        ; HiST3 INDEX NO.
LD M9796                 ; OriStep46
ANB M82                  ; 原点復帰開始
MOV K47 D8510            ; HiST3 原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M9985                ; U16_起動中
ANB M9984                ; U16_原点復帰中
LDB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#3
OR M3                    ; OFFLINE_MODE
ANL
MC
LD M9602                 ; RB14_位置決め条件
LD L1350                 ; ﾜｰｸ入替高さ移動完了(HiW)
OR L1351                 ; Hi搬送高さ移動完了(MIDLE)
OR L1352                 ; Hi搬送高さ移動完了(HIGH)
ANL
ANB M9830                ; RB14_ﾜｰｸ受取り位置移動指令
ANB M9831                ; RB14_ｺﾈｸﾀ挿入開始位置移動指令
ANB M9832                ; RB14_ｺﾈｸﾀ挿入押込完了位置移動指
ANB M9833                ; RB14_ｺﾈｸﾀ抜き位置移動指令
OUT M9820                ; RB14_ﾜｰｸ受取り位置移動OK
OUT M9821                ; RB14_ｺﾈｸﾀ挿入開始位置移動OK
OUT M9822                ; RB14_ｺﾈｸﾀ挿入押込完了位置移動OK
OUT M9823                ; RB14_ｺﾈｸﾀ抜き位置移動OK
LDP B1260                ; RB14_ﾜｰｸ受取り位置TP-PB
AND M9820                ; RB14_ﾜｰｸ受取り位置移動OK
OR M9830                 ; RB14_ﾜｰｸ受取り位置移動指令
ANB M9607                ; RB14_位置決め完了
OUT M9830                ; RB14_ﾜｰｸ受取り位置移動指令
MOV K1 D8540             ; RB14_指令Pos
LDP B1261                ; RB14_ｺﾈｸﾀ挿入開始位置TP-PB
AND M9821                ; RB14_ｺﾈｸﾀ挿入開始位置移動OK
OR M9831                 ; RB14_ｺﾈｸﾀ挿入開始位置移動指令
ANB M9607                ; RB14_位置決め完了
OUT M9831                ; RB14_ｺﾈｸﾀ挿入開始位置移動指令
MOV D8550 D8540          ; ｺﾈｸﾀ挿入開始位置指令Pos  RB14_指令Pos
LDP B1262                ; RB14_ｺﾈｸﾀ挿入押込み完了位置TP-PB
AND M9822                ; RB14_ｺﾈｸﾀ挿入押込完了位置移動OK
OR M9832                 ; RB14_ｺﾈｸﾀ挿入押込完了位置移動指
ANB M9607                ; RB14_位置決め完了
OUT M9832                ; RB14_ｺﾈｸﾀ挿入押込完了位置移動指
MOV D8551 D8540          ; ｺﾈｸﾀ挿入押込み量※1指令Pos  RB14_指令Pos
LDP B1263                ; RB14_ｺﾈｸﾀ抜き位置TP-PB
AND M9823                ; RB14_ｺﾈｸﾀ抜き位置移動OK
OR M9833                 ; RB14_ｺﾈｸﾀ抜き位置移動指令
ANB M9607                ; RB14_位置決め完了
OUT M9833                ; RB14_ｺﾈｸﾀ抜き位置移動指令
MOV D8552 D8540          ; ｺﾈｸﾀ抜き位置Pos  RB14_指令Pos
;　　ｸﾗﾝﾌﾟ上下
LD CR2002                ; 常時ON
AND M139                 ; 安全入力OK
AND M7802                ; U13-3_RCX240起動条件
AND M7811                ; RCX240動作完了
LD L1352                 ; Hi搬送高さ移動完了(HIGH)
OR L1319                 ; ﾊﾝﾄﾞ1st3部ﾜｰｸ受渡し動作完了
OR L1321                 ; ﾊﾝﾄﾞ2st3部ﾜｰｸ受渡し動作完了
ANL
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
OUT M9804                ; HiST3ｸﾗﾝﾌﾟ開閉_閉_起動OK
LDP B1264                ; HiST3ｸﾗﾝﾌﾟ閉_TP-PB
AND M9804                ; HiST3ｸﾗﾝﾌﾟ開閉_閉_起動OK
LD M9805                 ; HiST3ｸﾗﾝﾌﾟ開閉_閉_指令
ANB M9807                ; HiST3ｸﾗﾝﾌﾟ開閉_開__指令
ORL
OUT M9805                ; HiST3ｸﾗﾝﾌﾟ開閉_閉_指令
LD CR2002                ; 常時ON
ANB W05.8                ; 電気特性検査ｽﾃｰｼﾞ状態#3
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
OUT M9806                ; HiST3ｸﾗﾝﾌﾟ開閉_開_起動OK
LDP B1265                ; HiST3ｸﾗﾝﾌﾟ開_TP-PB
AND M9806                ; HiST3ｸﾗﾝﾌﾟ開閉_開_起動OK
LD M9807                 ; HiST3ｸﾗﾝﾌﾟ開閉_開__指令
ANB M9805                ; HiST3ｸﾗﾝﾌﾟ開閉_閉_指令
ORL
OUT M9807                ; HiST3ｸﾗﾝﾌﾟ開閉_開__指令
MCR
LDP B1266                ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
ANB M9812                ; HiST3SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
LDPB B1266               ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
AND M9812                ; HiST3SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANB M6312                ; Hi_RBﾁｬｯｸ開可
ANB M9813                ; HiST3SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
AND M29                  ; 未使用
OUT M9812                ; HiST3SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
LDP B1267                ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
ANB M9813                ; HiST3SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
LDPB B1267               ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
AND M9813                ; HiST3SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
ANB M9812                ; HiST3SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
ANB M6312                ; Hi_RBﾁｬｯｸ開可
ORL
AND= D50 K201            ; TpM-K_表示中ページNo.
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
AND M50                  ; 運転準備
AND M29                  ; 未使用
OUT M9813                ; HiST3SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
;<h1/>▽_検査ｽﾃｰｼﾞRB移動ｻﾌﾞﾙｰﾁﾝ
;　　移動POS
LD X384A                 ; H3st(RB14)原点確定_HEND
AND X384B                ; H3st(RB14)位置決め完了_PEND
MPS
AND X3840                ; H3st(RB14)完了Pos_PM1
OUT D8544.0
MRD
AND X3841                ; H3st(RB14)完了Pos_PM2
OUT D8544.1
MRD
AND X3842                ; H3st(RB14)完了Pos_PM4
OUT D8544.2
MRD
AND X3843                ; H3st(RB14)完了Pos_PM8
OUT D8544.3
MRD
AND X3844                ; H3st(RB14)完了Pos_PM16
OUT D8544.4
MRD
AND X3845                ; H3st(RB14)完了Pos_PM32
OUT D8544.5
MRD
AND X3846                ; H3st(RB14)完了Pos_PM64
OUT D8544.6
MRD
AND X3847                ; H3st(RB14)完了Pos_PM128
OUT D8544.7
MPP
INV
MOV K0 D8544             ; RB14_完了Pos
;'RB14 POS MANUAL JIG SETTING
;IF D250 < K1 THEN
;   D250 = K1
;END IF
;D8550 = (D250 - K1)*3 + 51   'ｺﾈｸﾀ挿入開始
;D8551 = (D250 - K1)*3 + 52   'ｺﾈｸﾀ挿入開始
;D8552 = (D250 - K1)*3 + 53   'ｺﾈｸﾀ挿入開始
LD M31                   ; 手動ﾓｰﾄﾞ
NCJ K1000
;'RB14 POS MANUAL JIG SETTING
;
;IF D250 < K1 THEN
;
LD< D250 K1              ; 手動_JIG_NO.指定
NCJ K2000
;   D250 = K1
;
LD CR2002                ; 常時ON
MOV K1 D250              ; 手動_JIG_NO.指定
;END IF
;
LABEL K2000
;D8550 = (D250 - K1)*3 + 51   'ｺﾈｸﾀ挿入開始
;
LD CR2002                ; 常時ON
LDA D250                 ; 手動_JIG_NO.指定
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +51
CON
STA D8550                ; ｺﾈｸﾀ挿入開始位置指令Pos
;D8551 = (D250 - K1)*3 + 52   'ｺﾈｸﾀ挿入開始
;
LD CR2002                ; 常時ON
LDA D250                 ; 手動_JIG_NO.指定
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +52
CON
STA D8551                ; ｺﾈｸﾀ挿入押込み量※1指令Pos
;D8552 = (D250 - K1)*3 + 53   'ｺﾈｸﾀ挿入開始
LD CR2002                ; 常時ON
LDA D250                 ; 手動_JIG_NO.指定
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +53
CON
STA D8552                ; ｺﾈｸﾀ抜き位置Pos
LABEL K1000
;
;'RB14 POS AUTO JIG SETTING
;D8550 = (D8715 - K1)*3 + 51   'ｺﾈｸﾀ挿入開始
;D8551 = (D8715 - K1)*3 + 52   'ｺﾈｸﾀ挿入開始
;D8552 = (D8715 - K1)*3 + 53   'ｺﾈｸﾀ挿入開始
LD M30                   ; 自動ﾓｰﾄﾞ
NCJ K1001
;'RB14 POS AUTO JIG SETTING
;
;D8550 = (D8715 - K1)*3 + 51   'ｺﾈｸﾀ挿入開始
;
LD CR2002                ; 常時ON
LDA D8715                ; HiST3 高温検査ﾛﾎﾞｯﾄNo.
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +51
CON
STA D8550                ; ｺﾈｸﾀ挿入開始位置指令Pos
;D8551 = (D8715 - K1)*3 + 52   'ｺﾈｸﾀ挿入開始
;
LD CR2002                ; 常時ON
LDA D8715                ; HiST3 高温検査ﾛﾎﾞｯﾄNo.
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +52
CON
STA D8551                ; ｺﾈｸﾀ挿入押込み量※1指令Pos
;D8552 = (D8715 - K1)*3 + 53   'ｺﾈｸﾀ挿入開始
LD CR2002                ; 常時ON
LDA D8715                ; HiST3 高温検査ﾛﾎﾞｯﾄNo.
CON
EXT
CON
SUB.L +1
CON
MUL.L +3
CON
ADD.L +53
CON
STA D8552                ; ｺﾈｸﾀ抜き位置Pos
LABEL K1001
;
LD= D8544 K1             ; RB14_完了Pos
AND<> D8544 K0           ; RB14_完了Pos
OUT M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
LD= D8544 D8550          ; RB14_完了Pos  ｺﾈｸﾀ挿入開始位置指令Pos
AND<> D8544 K0           ; RB14_完了Pos
OUT M9622                ; RB14_ｺﾈｸﾀ挿入開始位置
LD= D8544 D8551          ; RB14_完了Pos  ｺﾈｸﾀ挿入押込み量※1指令Pos
AND<> D8544 K0           ; RB14_完了Pos
OUT M9623                ; RB14_ｺﾈｸﾀ挿入押込み完了位置
LD= D8544 D8552          ; RB14_完了Pos  ｺﾈｸﾀ抜き位置Pos
AND<> D8544 K0           ; RB14_完了Pos
OUT M9624                ; RB14_ｺﾈｸﾀ抜き位置
;	検査ｽﾗｲﾀﾞ位置決め処理
LD X384C                 ; H3st(RB14)ｻｰﾎﾞ状態_SV
ANB X3849                ; H3st(RB14)運転ﾓｰﾄﾞ_RMDS
LD X384A                 ; H3st(RB14)原点確定_HEND
OR CR2002                ; 常時ON
ANL
AND X384D                ; H3st(RB14)非常停止_*EMGS
AND X384E                ; H3st(RB14)ｱﾗｰﾑ_*ALM
OUT M9600                ; RB14_状態OK
LD M50                   ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
AND M9600                ; RB14_状態OK
ANB M9604                ; RB14_位置決め起動指令
OUT M9602                ; RB14_位置決め条件
LDB M9510                ; U16-1_S10_ﾌﾟﾛｰﾌﾞｱﾌﾟﾛｰﾁ移動指令
ANB M9513                ; U16-1_S13_ｺﾈｸﾀ押込位置移動指指令
ANB M9519                ; U16-1_S19_ｺﾈｸﾀ抜き位置移動指令
ANB M9522                ; U16-1_S22_ﾜｰｸ受取位置移動指令
ANB M9754                ; OriStep4_ｽﾃｰｼﾞ戻り指令
INV
LDB M9830                ; RB14_ﾜｰｸ受取り位置移動指令
ANB M9831                ; RB14_ｺﾈｸﾀ挿入開始位置移動指令
ANB M9832                ; RB14_ｺﾈｸﾀ挿入押込完了位置移動指
ANB M9833                ; RB14_ｺﾈｸﾀ抜き位置移動指令
INV
ORL
OUT M9603                ; RB14_位置決め起動指令(まとめ)
LD M9603                 ; RB14_位置決め起動指令(まとめ)
AND M9602                ; RB14_位置決め条件
OR M9604                 ; RB14_位置決め起動指令
AND M50                  ; 運転準備
ANB M9608                ; RB14_位置決め動作完了
AND X384D                ; H3st(RB14)非常停止_*EMGS
AND X384E                ; H3st(RB14)ｱﾗｰﾑ_*ALM
OUT M9604                ; RB14_位置決め起動指令
LD M9604                 ; RB14_位置決め起動指令
@MOV D8540 D8542         ; RB14_指令Pos  RB14_ｺﾝﾄﾛｰﾗｰ指令POS
TMH K1650 K5             ; RB14ﾃﾞｰﾀ転送ﾃﾞｨﾚｲ
LD T1650                 ; RB14ﾃﾞｰﾀ転送ﾃﾞｨﾚｲ
OR M9605                 ; RB14_位置決めｽﾀｰﾄ指令
AND M9604                ; RB14_位置決め起動指令
ANB T1651                ; RB14位置決め再ｽﾀｰﾄ指令
OUT M9605                ; RB14_位置決めｽﾀｰﾄ指令
LD M9605                 ; RB14_位置決めｽﾀｰﾄ指令
AND Y484D                ; H3st(RB14)ｽﾀｰﾄ_CSTR
ANB X384B                ; H3st(RB14)位置決め完了_PEND
OR M9606                 ; RB14_位置決め起動確認
AND M9605                ; RB14_位置決めｽﾀｰﾄ指令
OUT M9606                ; RB14_位置決め起動確認
LD M9606                 ; RB14_位置決め起動確認
ANB Y484D                ; H3st(RB14)ｽﾀｰﾄ_CSTR
AND X384B                ; H3st(RB14)位置決め完了_PEND
AND= D8542 D8544         ; RB14_ｺﾝﾄﾛｰﾗｰ指令POS  RB14_完了Pos
OR M9607                 ; RB14_位置決め完了
AND M9605                ; RB14_位置決めｽﾀｰﾄ指令
OUT M9607                ; RB14_位置決め完了
LD M9606                 ; RB14_位置決め起動確認
ANB Y484D                ; H3st(RB14)ｽﾀｰﾄ_CSTR
AND X384B                ; H3st(RB14)位置決め完了_PEND
AND<> D8542 D8544        ; RB14_ｺﾝﾄﾛｰﾗｰ指令POS  RB14_完了Pos
TMR K1651 K30            ; RB14位置決め再ｽﾀｰﾄ指令
LD M9607                 ; RB14_位置決め完了
ANPB M9607               ; RB14_位置決め完了
OUT M9608                ; RB14_位置決め動作完了
;<h1/>□_出力回路
;ソレノイドバルブ出力
;ｸﾗﾝﾌﾟ/ｱﾝｸﾗﾝﾌﾟ
LD M9805                 ; HiST3ｸﾗﾝﾌﾟ開閉_閉_指令
AND M9804                ; HiST3ｸﾗﾝﾌﾟ開閉_閉_起動OK
OR M7956                 ; HiST3 ｸﾗﾝﾌﾟ指令(Hi給材)
OR M9504                 ; U16-1_S04_ｸﾗﾝﾌﾟ指令
LD Y2424                 ; HiST3ﾜｰｸ押さえ下降
ANB Y2425                ; HiST3ﾜｰｸ押さえ上昇
ORL
AND M50                  ; 運転準備
OUT Y2424                ; HiST3ﾜｰｸ押さえ下降
LD M9807                 ; HiST3ｸﾗﾝﾌﾟ開閉_開__指令
AND M9806                ; HiST3ｸﾗﾝﾌﾟ開閉_開_起動OK
OR M7966                 ; HiST3_ｱﾝｸﾗﾝﾌﾟ指令(Hi除材)
OR M9757                 ; OriStep7_ｸﾗﾝﾌﾟ上昇指令
LD Y2425                 ; HiST3ﾜｰｸ押さえ上昇
ANB Y2424                ; HiST3ﾜｰｸ押さえ下降
ORL
AND M50                  ; 運転準備
OUT Y2425                ; HiST3ﾜｰｸ押さえ上昇
;<h1/>____▼_検査ｽﾗｲﾀﾞ出力
LD CR2002                ; 常時ON
MPS
AND D8542.0
OUT Y4840                ; H3st(RB14)指令pos_PC1
MRD
AND D8542.1
OUT Y4841                ; H3st(RB14)指令pos_PC2
MRD
AND D8542.2
OUT Y4842                ; H3st(RB14)指令pos_PC4
MRD
AND D8542.3
OUT Y4843                ; H3st(RB14)指令pos_PC8
MRD
AND D8542.4
OUT Y4844                ; H3st(RB14)指令pos_PC16
MRD
AND D8542.5
OUT Y4845                ; H3st(RB14)指令pos_PC32
MRD
AND D8542.6
OUT Y4846                ; H3st(RB14)指令pos_PC64
MPP
AND D8542.7
OUT Y4847                ; H3st(RB14)指令pos_PC128
LD B126F                 ; ﾏﾆｭｱﾙﾓｰﾄﾞ切替SW(1：ﾏﾆｭｱﾙ)
OUT Y484A                ; H3st(RB14)運転ﾓｰﾄﾞ_RMOD
LD<> D50 K208            ; TpM-K_表示中ページNo.
AND<> D70 K208           ; TpM-K_表示中ページNo.
AND<> D90 K208           ; TpM-K_表示中ページNo.
AND<> D110 K208          ; TpM-K_表示中ページNo.
RES B126F                ; ﾏﾆｭｱﾙﾓｰﾄﾞ切替SW(1：ﾏﾆｭｱﾙ)
LD M9752                 ; OriStep2
ANB X384A                ; H3st(RB14)原点確定_HEND
OUT Y484B                ; H3st(RB14)原点復帰_HOME
LD M128                  ; 高温検査安全扉正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OUT Y484C                ; H3st(RB14)一時停止_*STP
LD M9605                 ; RB14_位置決めｽﾀｰﾄ指令
ANB M9606                ; RB14_位置決め起動確認
OUT Y484D                ; H3st(RB14)ｽﾀｰﾄ_CSTR
LD M16                   ; ﾘｾｯﾄPB
ANB X384E                ; H3st(RB14)ｱﾗｰﾑ_*ALM
OUT Y484E                ; H3st(RB14)ﾘｾｯﾄ_RES
LD T10                   ; 運転準備ﾃﾞｨﾚｲ
OR M9623                 ; RB14_ｺﾈｸﾀ挿入押込み完了位置
LD W05.8                 ; 電気特性検査ｽﾃｰｼﾞ状態#3
ANB M3                   ; OFFLINE_MODE
ORL
OUT Y484F                ; H3st(RB14)ｻｰﾎﾞON_SON
;■その他制御出力
LD M9661                 ; U16-3_S1Hi検査ｽﾀｰﾄ要求
OUT W0105.8              ; 電気特性検査開始要求#3
LD L1600                 ; Hi検査ST3_有効状態
ANB M9909                ; HiST3_無効予約(自動運転)
OUT W0105.13             ; 電気特性検査ｽﾃｰｼﾞ有効/無効#3
LD M9904                 ; HiST3検査_有効確認要求
OUT W0107.10             ; Hi_ST3ﾁｪｯｶ-確認要求
LD L1600                 ; Hi検査ST3_有効状態
ANB M9909                ; HiST3_無効予約(自動運転)
LD M9909                 ; HiST3_無効予約(自動運転)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y2412                ; HiST3__有効/無効表示
OUT B176A                ; HiST3__有効/無効表示
;□□　　パネル表示　　□□
LD M9830                 ; RB14_ﾜｰｸ受取り位置移動指令
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M9621                 ; RB14_ﾜｰｸ受取り位置(ID確認位置)
OUT B1760                ; RB14_ﾜｰｸ受取り位置TP-PB
LD M9831                 ; RB14_ｺﾈｸﾀ挿入開始位置移動指令
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M9622                 ; RB14_ｺﾈｸﾀ挿入開始位置
OUT B1761                ; RB14_ｺﾈｸﾀ挿入開始位置TP-PB
LD M9832                 ; RB14_ｺﾈｸﾀ挿入押込完了位置移動指
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M9623                 ; RB14_ｺﾈｸﾀ挿入押込み完了位置
OUT B1762                ; RB14_ｺﾈｸﾀ挿入押込み完了位置TP-PB
LD M9833                 ; RB14_ｺﾈｸﾀ抜き位置移動指令
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M9624                 ; RB14_ｺﾈｸﾀ抜き位置
OUT B1763                ; RB14_ｺﾈｸﾀ抜き位置TP-PB
LD Y2424                 ; HiST3ﾜｰｸ押さえ下降
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1380                 ; HiST3_部_ﾜｰｸ押_下降端
OUT B1764                ; HiST3ｸﾗﾝﾌﾟ閉_TP-PL
LD Y2425                 ; HiST3ﾜｰｸ押さえ上昇
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR X1381                 ; HiST3_部_ﾜｰｸ押_上昇端
OUT B1765                ; HiST3ｸﾗﾝﾌﾟ開_TP-PL
LD M9812                 ; HiST3SwBoxMode_ﾊﾟﾚｯﾄ前後Flag
OUT B1766                ; SWBox_ﾊﾟﾚｯﾄ前後ﾓｰﾄﾞsw
LD M9813                 ; HiST3SwBoxMode_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄFlag
OUT B1767                ; SWBox_ｸﾗﾝﾌﾟ&ﾊﾟﾚｯﾄﾓｰﾄﾞsw
;<h1/>□_異常検出
;[][]    Error Detect    [][]
;B200--B20F  B210--B21F
LDB X384E                ; H3st(RB14)ｱﾗｰﾑ_*ALM
AND T12                  ; 運転準備ONﾃﾞｨﾚｲ(3S)
LD CR2003                ; 常時OFF
ANB B0700                ; B200_513)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
ORL
OUT B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
LDP B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0201 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0700                 ; B200_513)Bz停止
AND B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
OUT B0700                ; B200_513)Bz停止
;　　
LD Y2424                 ; HiST3ﾜｰｸ押さえ下降
ANB X1380                ; HiST3_部_ﾜｰｸ押_下降端
LD Y2425                 ; HiST3ﾜｰｸ押さえ上昇
ANB X1381                ; HiST3_部_ﾜｰｸ押_上昇端
ORL
LD X1380                 ; HiST3_部_ﾜｰｸ押_下降端
AND X1381                ; HiST3_部_ﾜｰｸ押_上昇端
ORL
LD CR2003                ; 常時OFF
ANB B0701                ; B201_514)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
ORL
MPS
AND T1691                ; CLSﾀｲﾑｱｳﾄ
OUT B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
MPP
TMR K1691 K100           ; CLSﾀｲﾑｱｳﾄ
LDP B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0202 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0701                 ; B201_514)Bz停止
AND B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
OUT B0701                ; B201_514)Bz停止
;
LD T1606                 ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_更新_DLY
AND L1600                ; Hi検査ST3_有効状態
AND<> D8600 D8606        ; HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID  HiST3_ｺﾝﾀｸﾄﾍｯﾄﾞID_Copy
LD CR2003                ; 常時OFF
ANB B0702                ; B202_515)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
ORL
OUT B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
LDP B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0203 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0702                 ; B202_515)Bz停止
AND B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
OUT B0702                ; B202_515)Bz停止
;
LD T1607                 ; HiST3_ﾊﾟﾚｯﾄID_更新_DLY
AND L1600                ; Hi検査ST3_有効状態
AND<> D8601 D8607        ; HiST3_ﾊﾟﾚｯﾄID  HiST3_ﾊﾟﾚｯﾄID_ｾﾝｻ状態
AND M9621                ; RB14_ﾜｰｸ受取り位置(ID確認位置)
LD CR2003                ; 常時OFF
ANB B0703                ; B203_516)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
ORL
OUT B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
LDP B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0204 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0703                 ; B203_516)Bz停止
AND B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
OUT B0703                ; B203_516)Bz停止
;
LD M9560                 ; U16-1_S60 HiST2_ﾜｰｸ受取異常
LD CR2003                ; 常時OFF
ANB B0704                ; B204_517)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
ORL
OUT B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
LDP B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0205 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0704                 ; B204_517)Bz停止
AND B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
OUT B0704                ; B204_517)Bz停止
;
LD M9930                 ; HiST3_ﾜｰｸ受渡し異常
LD CR2003                ; 常時OFF
ANB B0705                ; B205_518)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
ORL
OUT B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
LDP B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0206 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0705                 ; B205_518)Bz停止
AND B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
OUT B0705                ; B205_518)Bz停止
;
LD D8701.0
ANB X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
LDB D8701.0
AND X1382                ; HiST3_部_ﾜｰｸ確認(ON=有)
ORL
ANB M7902                ; 【搬送】搬送→Hi_ST3搬送開始
ANB M7907                ; 【搬送】搬送→Hi_ST3搬送完了
ANB M7912                ; 【搬送】HiST3除材開始
ANB M7917                ; 【搬送】HiST3除材完了
ANB M9553                ; U16-1_S53
ANB M9554                ; U16-1_S54
AND L1600                ; Hi検査ST3_有効状態
AND M71                  ; 自動運転開始BZ
LD CR2003                ; 常時OFF
ANB B0706                ; B206_519)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
ORL
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
LDP B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
AND< D900 K64            ; FIFO_DATA_VALUE
MOV H0207 D0             ; エラーコード
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0706                 ; B206_519)Bz停止
AND B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
OUT B0706                ; B206_519)Bz停止
;　　
LD M9759                 ; OriStep9_ORGﾜｰｸ除去要求
LD CR2003                ; 常時OFF
ANB B071E                ; B21E_543)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
ORL
OUT B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B071E                 ; B21E_543)Bz停止
AND B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
OUT B071E                ; B21E_543)Bz停止
;電特検査中
LD W05.8                 ; 電気特性検査ｽﾃｰｼﾞ状態#3
AND M31                  ; 手動ﾓｰﾄﾞ
AND M9751                ; OriStep1
LD CR2003                ; 常時OFF
ANB B071F                ; B21F_544)Bz停止
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B021F                ; B21F_544)HiST3ﾜｰｸ検査中
ORL
OUT B021F                ; B21F_544)HiST3ﾜｰｸ検査中
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B071F                 ; B21F_544)Bz停止
AND B021F                ; B21F_544)HiST3ﾜｰｸ検査中
OUT B071F                ; B21F_544)Bz停止
;異常ﾌﾞｻﾞｰまとめ
LDB B0200                ; B200_513)HiST3ｽﾃｰｼﾞ前後RB異常
OR B0700                 ; B200_513)Bz停止
LDB B0201                ; B201_514)HiST3ｸﾗﾝﾌﾟ開閉CLS異常
OR B0701                 ; B201_514)Bz停止
ANL
LDB B0202                ; B202_515)HiST3_ﾃｽﾄﾍｯﾄﾞID_不一致
OR B0702                 ; B202_515)Bz停止
ANL
LDB B0203                ; B203_516)HiST3_ﾊﾟﾚｯﾄID_不一致
OR B0703                 ; B203_516)Bz停止
ANL
LDB B0204                ; B204_517)HiST3_ﾜｰｸ受取異常
OR B0704                 ; B204_517)Bz停止
ANL
LDB B0205                ; B205_518)HiST3_ﾜｰｸ受渡し異常
OR B0705                 ; B205_518)Bz停止
ANL
LDB B0206                ; B206_519)HiST3 ﾜｰｸ情報不一致
OR B0706                 ; B206_519)Bz停止
ANL
LDB B0207                ; B207_520)
OR B0707                 ; B207_520)Bz停止
ANL
LDB B0208                ; B208_521)
OR B0708                 ; B208_521)Bz停止
ANL
LDB B0209                ; B209_522)
OR B0709                 ; B209_522)Bz停止
ANL
LDB B020A                ; B20A_523)
OR B070A                 ; B20A_523)Bz停止
ANL
LDB B020B                ; B20B_524)
OR B070B                 ; B20B_524)Bz停止
ANL
LDB B020C                ; B20C_525)
OR B070C                 ; B20C_525)Bz停止
ANL
LDB B020D                ; B20D_526)
OR B070D                 ; B20D_526)Bz停止
ANL
LDB B020E                ; B20E_527)
OR B070E                 ; B20E_527)Bz停止
ANL
LDB B020F                ; B20F_528)
OR B070F                 ; B20F_528)Bz停止
ANL
LDB B0210                ; B210_529)
OR B0710                 ; B210_529)Bz停止
ANL
LDB B0211                ; B211_530)
OR B0711                 ; B211_530)Bz停止
ANL
LDB B0212                ; B212_531)
OR B0712                 ; B212_531)Bz停止
ANL
LDB B0213                ; B213_532)
OR B0713                 ; B213_532)Bz停止
ANL
LDB B0214                ; B214_533)
OR B0714                 ; B214_533)Bz停止
ANL
LDB B0215                ; B215_534)
OR B0715                 ; B215_534)Bz停止
ANL
LDB B0216                ; B216_535)
OR B0716                 ; B216_535)Bz停止
ANL
LDB B0217                ; B217_536)
OR B0717                 ; B217_536)Bz停止
ANL
LDB B0218                ; B218_537)
OR B0718                 ; B218_537)Bz停止
ANL
LDB B0219                ; B219_538)
OR B0719                 ; B219_538)Bz停止
ANL
LDB B021A                ; B21A_539)
OR B071A                 ; B21A_539)Bz停止
ANL
LDB B021B                ; B21B_540)
OR B071B                 ; B21B_540)Bz停止
ANL
LDB B021C                ; B21C_541)
OR B071C                 ; B21C_541)Bz停止
ANL
LDB B021D                ; B21D_542)
OR B071D                 ; B21D_542)Bz停止
ANL
LDB B021E                ; B21E_543)HiST3ﾜｰｸ除去要求
OR B071E                 ; B21E_543)Bz停止
ANL
LDB B021F                ; B21F_544)HiST3ﾜｰｸ検査中
OR B071F                 ; B21F_544)Bz停止
ANL
INV
OUT M9989                ; U16_異常BZ補助
END
ENDH
