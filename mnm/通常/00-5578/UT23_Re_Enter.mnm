DEVICE:53
;MODULE:UT23_Re_Enter
;MODULE_TYPE:0
;<h1/>2018/2/28 能拡追加
;再投入シャトル部新規追加
;このプログラムモジュール(UT23)を全て追加しています。
;<h1/>/ 入力受け
;□□    入力受け    □□
LD X1643                 ; 再投入_投入側ﾜｰｸ確認
TMH K2311 K10            ; 給材部ﾜｰｸ有確認(ON=有)
LDB X1643                ; 再投入_投入側ﾜｰｸ確認
TMR K2312 K3             ; 給材部ﾜｰｸ無確認(ON=無)
LD X1645                 ; 再投入_受渡側ﾜｰｸ浮確認1
TMH K2313 K10            ; 給材ﾜｰｸ浮き確認1(ON=OK)
LD X1646                 ; 再投入_受渡側ﾜｰｸ浮確認2
TMH K2314 K10            ; 給材ﾜｰｸ浮き確認2(ON=OK)
LD X1644                 ; 再投入_受渡側ﾜｰｸ確認
TMH K2315 K10            ; ﾜｰｸ受渡し部ﾜｰｸ有確認(ON=有)
LDB X1644                ; 再投入_受渡側ﾜｰｸ確認
TMH K2316 K10            ; ﾜｰｸ受渡し部ﾜｰｸ無確認(ON=無)
LD X164C                 ; 製品CN部温度確認（ON:NG)
TMH K2317 K10            ; 給材ﾜｰｸ温度確認(ON=NG)
LDB X1644                ; 再投入_受渡側ﾜｰｸ確認
TMR K2318 K3             ; 給材ｽﾗｲﾀﾞ上ﾜｰｸ無確認
LD X164E                 ; 再投入ﾗｲﾄｶｰﾃﾝ1_OUT1
AND X164F                ; 再投入ﾗｲﾄｶｰﾃﾝ1_OUT2
TMR K2326 K5             ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
LDB X164E                ; 再投入ﾗｲﾄｶｰﾃﾝ1_OUT1
ANB X164F                ; 再投入ﾗｲﾄｶｰﾃﾝ1_OUT2
TMR K2327 K5             ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
;□ ｾｯﾄ治具ID
LD CR2002                ; 常時ON
MPS
AND X1640                ; 再投入ﾊﾟﾚｯﾄ_ID-1
OUT D12052.0
MRD
AND X1641                ; 再投入ﾊﾟﾚｯﾄ_ID-2
OUT D12052.1
MPP
AND X1642                ; 再投入ﾊﾟﾚｯﾄ_ID-3
OUT D12052.2
LD<> D12050 D12052       ; 再投入ﾊﾟﾚｯﾄID  再投入ﾊﾟﾚｯﾄID_Copy
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MPS
TMR K2306 K5             ; 再投入ﾊﾟﾚｯﾄID_変更有り
MPP
AND T2306                ; 再投入ﾊﾟﾚｯﾄID_変更有り
MOV D12052 D12050        ; 再投入ﾊﾟﾚｯﾄID_Copy  再投入ﾊﾟﾚｯﾄID
;再投入ｼｬﾄﾙ現在値
LD X412A                 ; 再投入(RB4C)原点確定_HEND
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
AND X4120                ; 再投入(RB4C)完了Pos_PM1
OUT D12028.0             ; 再投入ｼｬﾄﾙ_RB_完了POS1
MRD
AND X4121                ; 再投入(RB4C)完了Pos_PM2
OUT D12028.1             ; 再投入ｼｬﾄﾙ_RB_完了POS2
MRD
AND X4122                ; 再投入(RB4C)完了Pos_PM4
OUT D12028.2             ; 再投入ｼｬﾄﾙ_RB_完了POS4
MRD
AND X4123                ; 再投入(RB4C)完了Pos_PM8
OUT D12028.3             ; 再投入ｼｬﾄﾙ_RB_完了POS8
MRD
AND X4124                ; 再投入(RB4C)完了Pos_PM16
OUT D12028.4             ; 再投入ｼｬﾄﾙ_RB_完了POS16
MRD
AND X4125                ; 再投入(RB4C)完了Pos_PM32
OUT D12028.5             ; 再投入ｼｬﾄﾙ_RB_完了POS32
MRD
AND X4126                ; 再投入(RB4C)完了Pos_PM64
OUT D12028.6             ; 再投入ｼｬﾄﾙ_RB_完了POS64
MRD
AND X4127                ; 再投入(RB4C)完了Pos_PM128
OUT D12028.7             ; 再投入ｼｬﾄﾙ_RB_完了POS128
MPP
INV
MOV K0 D12028            ; 再投入ｼｬﾄﾙ_RB_完了POS
LD= D12028 K1            ; 再投入ｼｬﾄﾙ_RB_完了POS
OUT M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
LD= D12028 K2            ; 再投入ｼｬﾄﾙ_RB_完了POS
OUT M13472               ; RB4C_ﾜｰｸ受渡し位置
;<h1/>/ 装置状態･条件
;ﾕﾆｯﾄ原点
LDB X1644                ; 再投入_受渡側ﾜｰｸ確認
ANB X1643                ; 再投入_投入側ﾜｰｸ確認
OR M73                   ; 各工程起動指令
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
OUT M13480               ; U23_原点
;非常停止
LDB B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
ANB B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
ANB B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
ANB B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
INV
OUT M13481               ; U23_即停止異常
;起動条件
LD M13480                ; U23_原点
ANB M25                  ; 払出停止ﾓｰﾄﾞ
AND<> D12050 K0          ; 再投入ﾊﾟﾚｯﾄID
AND= D12050 D12570       ; 再投入ﾊﾟﾚｯﾄID  Buffer_JIG_ID
OUT M13482               ; U23_起動条件
;起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M13484               ; U23_原点復帰中
ORL
OR M13486                ; U23_CYCLE停止異常
OR T15                   ; 強制停止
OUT M13483               ; U23_起動OFF
;原点復帰
LD M82                   ; 原点復帰開始
LDB M82                  ; 原点復帰開始
ANP B1349                ; UNIT23-1-10 TP-PB
ORL
ANB M13484               ; U23_原点復帰中
OR M13484                ; U23_原点復帰中
ANB M13483               ; U23_起動OFF
ANB M13491               ; U23_ERR発生
ANB M13299               ; ﾕﾆｯﾄ23原点復帰完了確認
MPS
OUT M13484               ; U23_原点復帰中
MPP
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1849                ; UNIT23-1-10 TP-PL
;起動中
LD M73                   ; 各工程起動指令
AND M13482               ; U23_起動条件
OR M13485                ; U23_起動中
ANB M13483               ; U23_起動OFF
ANB M13049               ; ﾕﾆｯﾄ23-1ｻｲｸﾙ終了
OUT M13485               ; U23_起動中
;ｻｲｸﾙ停止異常
LDB CR2003               ; 常時OFF
INV
OUT M13486               ; U23_CYCLE停止異常
;払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M13487               ; U23_払出停止異常
;警報補助
LDB B02E1                ; B2E1_738)再投入ID仮置台と不一致
OR B07E1
LDB B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
OR B07E4
ANL
LDB B02E6                ; B2E6_743)投入不可ﾌﾚｰﾑ取出要求
OR B07E6
ANL
LDB B02E7                ; B2E7_744)吸着ｴﾗｰﾌﾚｰﾑ取出要求
OR B07E7
ANL
LDB B02EA                ; B2EA_747)再投入部ﾜｰｸ温度NG
OR B07EA
ANL
INV
OUT M13488               ; U23_警報補助
;機器準備確認
LDB X4129                ; 再投入(RB4C)運転ﾓｰﾄﾞ_RMDS
AND X412C                ; 再投入(RB4C)ｻｰﾎﾞ状態_SV
AND X412D                ; 再投入(RB4C)非常停止_*EMGS
AND X412E                ; 再投入(RB4C)ｱﾗｰﾑ_*ALM
OUT M13490               ; U23_機器準備完了
;ｴﾗｰ発生  一時停止
LDB B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
INV
OUT M13491               ; U23_ERR発生
;動作中ﾕﾆｯﾄ無し
LD CR2002                ; 常時ON
ORB M13485               ; U23_起動中
ANB M13484               ; U23_原点復帰中
TMR K2301 K20            ; ﾕﾆｯﾄ23動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D12000 D12005        ; U23_自動運転ｽﾃｯﾌﾟ  U23_動作中判定
LD T2301                 ; ﾕﾆｯﾄ23動作無し判定ﾀｲﾏ
OUT M13492               ; U23_動作無し確認
;払出しﾜｰｸ無し
LDB X1643                ; 再投入_投入側ﾜｰｸ確認
ANB X1644                ; 再投入_受渡側ﾜｰｸ確認
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
TMR K2305 K10            ; UNIT23払出処理ﾜｰｸ無
LD T2305                 ; UNIT23払出処理ﾜｰｸ無
OUT M13493               ; U23_WORK無し
;ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
OR M31                   ; 手動ﾓｰﾄﾞ
AND M130                 ; 冷却槽安全扉正常
ANB M13491               ; U23_ERR発生
ANB M140                 ; 全一時停止
ANB M220                 ; 冷却搬送一時停止
ANB M220                 ; 冷却搬送一時停止
OUT M13479               ; U23_STEP動作
;<h1/>■自動
;/ ﾌﾚｰﾑ供給
LD CR2002                ; 常時ON
ZRES M13000 M13049       ; 再投入部ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ23-1ｻｲｸﾙ終了
LDB M13485               ; U23_起動中
MOV K0 D12000            ; U23_自動運転ｽﾃｯﾌﾟ
LD M13485                ; U23_起動中
MOV D12000 Z1            ; U23_自動運転ｽﾃｯﾌﾟ
OUT M13000:Z1            ; 再投入部ｽﾃｯﾌﾟ開始
LD M13000                ; 再投入部ｽﾃｯﾌﾟ開始
LD B3010                 ; Leak_Final_自動運転中
OR M1065                 ; 起動時確認完了ﾌﾗｸﾞ
ANL
@INC D12000              ; U23_自動運転ｽﾃｯﾌﾟ
;	JIG_IDﾁｪｯｸ
LD M13001                ; U23-S1 治具IDﾁｪｯｸ
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
MPS
LD<> D12050 K0           ; 再投入ﾊﾟﾚｯﾄID
AND= D12050 D12570       ; 再投入ﾊﾟﾚｯﾄID  Buffer_JIG_ID
ANB B02E1                ; B2E1_738)再投入ID仮置台と不一致
ANL
MOV K3 D12000            ; U23_自動運転ｽﾃｯﾌﾟ
MPP
AND<> D12050 D12570      ; 再投入ﾊﾟﾚｯﾄID  Buffer_JIG_ID
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13002                ; U23-S2 治具ID不一致
AND B02E1                ; B2E1_738)再投入ID仮置台と不一致
MOV K1 D12000            ; U23_自動運転ｽﾃｯﾌﾟ
LD M13043                ; ｼｬﾄﾙ供給位置移動
OR M13044                ; 位置決め完了OFF確認
AND T2327                ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
AND T2311                ; 給材部ﾜｰｸ有確認(ON=有)
OR M13060                ; ﾜｰｸ供給記憶
ANB M13004               ; U23-S4 再投入中
AND M73                  ; 各工程起動指令
OUT M13060               ; ﾜｰｸ供給記憶
;Work供給確認
;Work supply check
LD M13003                ; U23-S3 再投入待ち
MPS
LD T2327                 ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
OR M13060                ; ﾜｰｸ供給記憶
ANL
@INC D12000              ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LD T2326                 ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
ANB T2311                ; 給材部ﾜｰｸ有確認(ON=有)
AND M25                  ; 払出停止ﾓｰﾄﾞ
ANL
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
LD M13004                ; U23-S4 再投入中
ANB M13060               ; ﾜｰｸ供給記憶
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
MPS
LD T2311                 ; 給材部ﾜｰｸ有確認(ON=有)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MPS
TMH K2330 K5             ; ﾜｰｸ投入確認ﾃﾞｨﾚｲ
MPP
AND T2330                ; ﾜｰｸ投入確認ﾃﾞｨﾚｲ
@INC D12000              ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LD T2312                 ; 給材部ﾜｰｸ無確認(ON=無)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
MOV K0 D12000            ; U23_自動運転ｽﾃｯﾌﾟ
LD M13005                ; U23-S5
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13006                ; U23-S6
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
;ｽｷｬﾅ読取位置移動&ﾜｰｸ情報ｾｯﾄ
LD M13007                ; U23-S7
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K2 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K2           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13008                ; ｼｬﾄﾙ取出位置移動
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13009                ; 位置決め完了OFF確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13472               ; RB4C_ﾜｰｸ受渡し位置
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13472               ; RB4C_ﾜｰｸ受渡し位置
MOV K7 D12000            ; U23_自動運転ｽﾃｯﾌﾟ
;<h1/>/ S10_ﾜｰｸ浮きﾁｪｯｸ
LD M13010                ; ﾜｰｸ浮き確認
MPS
TMR K2331 K5             ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
MPP
LD T2331                 ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
ANL
MPS
LD T2313                 ; 給材ﾜｰｸ浮き確認1(ON=OK)
AND T2314                ; 給材ﾜｰｸ浮き確認2(ON=OK)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MOV K16 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LDB T2313                ; 給材ﾜｰｸ浮き確認1(ON=OK)
ORB T2314                ; 給材ﾜｰｸ浮き確認2(ON=OK)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
@MOV K11 D12000          ; U23_自動運転ｽﾃｯﾌﾟ
LD M13016                ; U23-S16
MPS
TMR K2332 K5             ; CN部温度確認ﾃﾞｨﾚｲ
MPP
LD T2332                 ; CN部温度確認ﾃﾞｨﾚｲ
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
ANL
MPS
LD B101C                 ; 製品CN部温度確認（1:使用)
ANB T2317                ; 給材ﾜｰｸ温度確認(ON=NG)
ORB B101C                ; 製品CN部温度確認（1:使用)
OR M35                   ; 空運転ﾓｰﾄﾞ
ANL
MOV K19 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LD T2317                 ; 給材ﾜｰｸ温度確認(ON=NG)
AND B101C                ; 製品CN部温度確認（1:使用)
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
@MOV K24 D12000          ; U23_自動運転ｽﾃｯﾌﾟ
;  CN_温度きNG
LD M13024                ; 製品CN部温度確認
AND M13479               ; U23_STEP動作
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13025                ; ｼｬﾄﾙ供給位置移動
AND M13479               ; U23_STEP動作
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13026                ; 位置決め完了OFF確認
AND M13479               ; U23_STEP動作
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K11 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
LD M13027                ; CN部温度NG
ANPB M13027              ; CN部温度NG
AND T2327                ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13028                ; U23-S28
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
ANB B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;  ﾜｰｸ浮きNG
LD M13011                ; U23-S10
AND M13479               ; U23_STEP動作
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13012                ; ｼｬﾄﾙ供給位置移動
AND M13479               ; U23_STEP動作
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13013                ; 位置決め完了OFF確認
AND M13479               ; U23_STEP動作
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K11 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
LD M13014                ; ﾜｰｸ浮きNG
ANPB M13014              ; ﾜｰｸ浮きNG
AND T2327                ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13015                ; U23-S15
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
ANB B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;<h1/>/ S19_治具ID照合_ﾌﾚｰﾑ取出可(→RB)
;JIG ID Verification
LD M13019                ; 再投入ｼｬﾄﾙ取出可(→Final_Sup_RB)
MPS
AND M13501               ; Buffer_JIG_ID不一致
MOV K20 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MRD
AND M13557               ; U24-S57 再投入取出NG取出処理
MOV K30 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MRD
AND M13563               ; U24_S63_取出完了(再投入へ）
MOV K40 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LDB M13985               ; U24_起動中
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANL
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;<h1/>/ S20_IDｴﾗｰ･ｼｬﾄﾙ戻
LD M13020                ; 治具ID照合ｴﾗｰ処理
AND M1479                ; U1_STEP動作
ANB M13501               ; Buffer_JIG_ID不一致
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13021                ; ｼｬﾄﾙ供給位置移動
AND M13479               ; U23_STEP動作
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13022                ; 位置決め完了OFF確認
AND M13479               ; U23_STEP動作
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K20 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
LD M13023                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
AND M13479               ; U23_STEP動作
ANB X1643                ; 再投入_投入側ﾜｰｸ確認
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X1651                ; 再投入部ﾘｾｯﾄPB
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;<h1/>/ S30_吸着ｴﾗｰ･ｼｬﾄﾙ戻
LD M13030                ; 吸着ｴﾗｰﾌﾚｰﾑ取出処理
AND M1479                ; U1_STEP動作
ANB M13557               ; U24-S57 再投入取出NG取出処理
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13031                ; ｼｬﾄﾙ供給位置移動
AND M13479               ; U23_STEP動作
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13032                ; 位置決め完了OFF確認
AND M13479               ; U23_STEP動作
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K30 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
LD M13033                ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
AND M13479               ; U23_STEP動作
ANB X1643                ; 再投入_投入側ﾜｰｸ確認
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X1651                ; 再投入部ﾘｾｯﾄPB
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ取出確認
LD M13039                ; U23-S40
MPS
AND M13563               ; U24_S63_取出完了(再投入へ）
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
MPP
LDB M13985               ; U24_起動中
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANL
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ取出完了確認
LD M13040                ; ﾌﾚｰﾑ取出完了確認
ANB M13563               ; U24_S63_取出完了(再投入へ）
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13041                ; 取出済ﾜｰｸ確認
AND T2316                ; ﾜｰｸ受渡し部ﾜｰｸ無確認(ON=無)
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ取出完了ｼｬﾄﾙ戻
LD M13042                ; ﾌﾚｰﾑ無確認完了
AND M13479               ; U23_STEP動作
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13043                ; ｼｬﾄﾙ供給位置移動
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
INC D12000               ; U23_自動運転ｽﾃｯﾌﾟ
LD M13044                ; 位置決め完了OFF確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND Y512C                ; 再投入(RB4C)一時停止_*STP
MPS
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K49 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
MPP
ANB M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MOV K42 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M13003               ; U23-S3 再投入待ち
MOV K48 D12000           ; U23_自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
;Cycle end
LD M13048                ; ﾕﾆｯﾄ23-1ｻｲｸﾙ停止処理
@MOV K49 D12000          ; U23_自動運転ｽﾃｯﾌﾟ
;<h1/>■原点復帰回路
;[][][][][][][][][][][][][][][]
;[][]    UNIT23 原点復帰   [][]
;[][][][][][][][][][][][][][][]
LD CR2002                ; 常時ON
ZRES M13250 M13299       ; ﾕﾆｯﾄ23原点復帰開始  ﾕﾆｯﾄ23原点復帰完了確認
LDB M13484               ; U23_原点復帰中
MOV K0 D12010            ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13484                ; U23_原点復帰中
MOV D12010 Z1            ; U23_原点復帰ｽﾃｯﾌﾟ
OUT M13250:Z1            ; ﾕﾆｯﾄ23原点復帰開始
;原点復帰ｻｲｸﾙ監視
;Origin cycle monitoring
LD M13484                ; U23_原点復帰中
AND M13480               ; U23_原点
TMR K2300 K300           ; ﾕﾆｯﾄ23原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
;Origin step start
LD M13250                ; ﾕﾆｯﾄ23原点復帰開始
AND M13479               ; U23_STEP動作
AND M13980               ; U24_原点
INC D12010               ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13251                ; U24原点確認
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
AND M13479               ; U23_STEP動作
AND M13970               ; 受渡Z(RB6C)上昇待機位置
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
AND= D12024 K1           ; 再投入ｼｬﾄﾙ_RB_POS_確定
INC D12010               ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13252                ; ｼｬﾄﾙ供給位置移動
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
@INC D12010              ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13253                ; 位置決め完了OFF確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
MPS
AND T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
@INC D12010              ; U23_原点復帰ｽﾃｯﾌﾟ
MPP
ANB T2326                ; 投入部ﾗｲﾄｶｰﾃﾝ_確認
MOV K1 D12010            ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13254                ; ﾜｰｸ有無確認
AND M13479               ; U23_STEP動作
MPS
AND X1643                ; 再投入_投入側ﾜｰｸ確認
@INC D12010              ; U23_原点復帰ｽﾃｯﾌﾟ
MPP
ANB X1643                ; 再投入_投入側ﾜｰｸ確認
MOV K6 D12010            ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13255                ; ﾜｰｸ有取出要求
AND M13479               ; U23_STEP動作
ANB X1643                ; 再投入_投入側ﾜｰｸ確認
AND B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
@MOV K49 D12010          ; U23_原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
;Origin end
LD M13256                ; ﾜｰｸ無確認
AND M13480               ; U23_原点
@MOV K48 D12010          ; U23_原点復帰ｽﾃｯﾌﾟ
LD M13298                ; ﾕﾆｯﾄ23原点復帰完了
ANB M82                  ; 原点復帰開始
@MOV K49 D12010          ; U23_原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;[][][][][][][][][][][][][][][]
;[]        UNIT23 手動       []
;[][][][][][][][][][][][][][][]
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M13485               ; U23_起動中
ANB M13484               ; U23_原点復帰中
ANB M13360               ; RB4C_MANUAL_ON
MC
;供給位置移動
LD M130                  ; 冷却槽安全扉正常
AND M131                 ; 再投入ｴﾘｱｾﾝｻ正常
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND M13980               ; U24_原点
ANB M13841
ANB M13301               ; 供給位置移動
ANB M13306               ; 受渡位置移動
ANB M13831               ; RB6C Z給徐材下降中
OUT M13300               ; 供給位置移動可
LD B1340                 ; UNIT23-1-1 TP-PB
AND M13300               ; 供給位置移動可
OR M13301                ; 供給位置移動
ANB M13304               ; 位置決め完了
OUT M13301               ; 供給位置移動
MPS
MOV K1 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
LD= D12024 K1            ; 再投入ｼｬﾄﾙ_RB_POS_確定
ANB M13303               ; 位置決め完了OFF確認
ANL
OUT M13302               ; 供給位置移動要求
LD M13301                ; 供給位置移動
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
OR M13303                ; 位置決め完了OFF確認
AND M13301               ; 供給位置移動
OUT M13303               ; 位置決め完了OFF確認
LD M13303                ; 位置決め完了OFF確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND M13471               ; RB4C_ﾜｰｸ投入位置(待機位置)
OR M13304                ; 位置決め完了
AND M13301               ; 供給位置移動
OUT M13304               ; 位置決め完了
;供給位置移動
LD M130                  ; 冷却槽安全扉正常
AND M131                 ; 再投入ｴﾘｱｾﾝｻ正常
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND M13980               ; U24_原点
ANB M13841
ANB M13301               ; 供給位置移動
ANB M13306               ; 受渡位置移動
OUT M13305               ; 受渡位置移動可
LD B1341                 ; UNIT23-1-2 TP-PB
AND M13305               ; 受渡位置移動可
OR M13306                ; 受渡位置移動
ANB M13309               ; 位置決め完了
OUT M13306               ; 受渡位置移動
MPS
MOV K2 D12024            ; 再投入ｼｬﾄﾙ_RB_POS_確定
MPP
LD= D12024 K2            ; 再投入ｼｬﾄﾙ_RB_POS_確定
ANB M13308               ; 位置決め完了OFF確認
ANL
OUT M13307               ; 受渡位置移動要求
LD M13307                ; 受渡位置移動要求
ANB X412B                ; 再投入(RB4C)位置決め完了_PEND
OR M13308                ; 位置決め完了OFF確認
AND M13306               ; 受渡位置移動
OUT M13308               ; 位置決め完了OFF確認
LD M13308                ; 位置決め完了OFF確認
AND X412B                ; 再投入(RB4C)位置決め完了_PEND
AND M13472               ; RB4C_ﾜｰｸ受渡し位置
OR M13309                ; 位置決め完了
AND M13306               ; 受渡位置移動
OUT M13309               ; 位置決め完了
MCR
;<h1/>■出力回路
;□□    出力回路    □□
LDB M13023               ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M13033               ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
INV
AND T1                   ; 0.3Secｸﾛｯｸ
LDB B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y2666                ; 再投入部ﾘｾｯﾄPB_PL
LD M13485                ; U23_起動中
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
OR M25                   ; 払出停止ﾓｰﾄﾞ
MPS
LDB Y2664                ; 再投入部投入OK_PL
ANB M13023               ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M13033               ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
LD M13023                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
OR M13033                ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
AND T1                   ; 0.3Secｸﾛｯｸ
ORL
ANL
OUT Y2665                ; 再投入部投入NG_PL
MPP
LD M13003                ; U23-S3 再投入待ち
OR M13004                ; U23-S4 再投入中
ANL
OUT Y2664                ; 再投入部投入OK_PL
LDB M13023               ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
ANB M13033               ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
INV
LDB M13488               ; U23_警報補助
ANB M13489               ; U23_異常BZ補助
ANB M13988               ; U24_警報補助
ANB M13989               ; U24_異常BZ補助
INV
ORL
OUT Y2667                ; 再投入部BZ
;ROBOT AUTO/MANU
LDP B135F                ; UNIT23-2-16 TP-PB
AND M13360               ; RB4C_MANUAL_ON
LD<> D50 K201            ; TpM-K_表示中ページNo.
MEP
ORL
LD<> D70 K201            ; TpM-K_表示中ページNo.
MEP
ORL
LD<> D90 K201            ; TpM-K_表示中ページNo.
MEP
ORL
LD<> D110 K201           ; TpM-K_表示中ページNo.
MEP
ORL
OUT M13361               ; RB4C_MANUAL_OFF
LDP B135F                ; UNIT23-2-16 TP-PB
ANB M13360               ; RB4C_MANUAL_ON
OR M13360                ; RB4C_MANUAL_ON
ANB M13361               ; RB4C_MANUAL_OFF
AND M31                  ; 手動ﾓｰﾄﾞ
OUT M13360               ; RB4C_MANUAL_ON
;ｼｬﾄﾙRB
LDB M13008               ; ｼｬﾄﾙ取出位置移動
ANB M13012               ; ｼｬﾄﾙ供給位置移動
ANB M13021               ; ｼｬﾄﾙ供給位置移動
ANB M13043               ; ｼｬﾄﾙ供給位置移動
ANB M13252               ; ｼｬﾄﾙ供給位置移動
INV
LDB M13302               ; 供給位置移動要求
ANB M13307               ; 受渡位置移動要求
ANB M13031               ; ｼｬﾄﾙ供給位置移動
ANB M13025               ; ｼｬﾄﾙ供給位置移動
INV
ORL
MOV D12024 D12026        ; 再投入ｼｬﾄﾙ_RB_POS_確定  再投入ｼｬﾄﾙ_RB_指令POS
MPS
TMH K2391 K5             ; 再投入ｼｬﾄﾙRB_POS確定ﾃﾞｨﾚｲ
MPP
AND T2391                ; 再投入ｼｬﾄﾙRB_POS確定ﾃﾞｨﾚｲ
OUT Y512D                ; 再投入(RB4C)ｽﾀｰﾄ_CSTR
LD CR2002                ; 常時ON
MPS
AND D12026.0
OUT Y5120                ; 再投入(RB4C)指令pos_PC1
MRD
AND D12026.1
OUT Y5121                ; 再投入(RB4C)指令pos_PC2
MRD
AND D12026.2
OUT Y5122                ; 再投入(RB4C)指令pos_PC4
MRD
AND D12026.3
OUT Y5123                ; 再投入(RB4C)指令pos_PC8
MRD
AND D12026.4
OUT Y5124                ; 再投入(RB4C)指令pos_PC16
MRD
AND D12026.5
OUT Y5125                ; 再投入(RB4C)指令pos_PC32
MRD
AND D12026.6
OUT Y5126                ; 再投入(RB4C)指令pos_PC64
MPP
AND D12026.7
OUT Y5127                ; 再投入(RB4C)指令pos_PC128
LD M13360                ; RB4C_MANUAL_ON
OUT Y512A                ; 再投入(RB4C)運転ﾓｰﾄﾞ_RMOD
LDB CR2003               ; 常時OFF
INV
OUT Y512B                ; 再投入(RB4C)原点復帰_HOME
LD M130                  ; 冷却槽安全扉正常
AND M131                 ; 再投入ｴﾘｱｾﾝｻ正常
LD M13970                ; 受渡Z(RB6C)上昇待機位置
LD< D12534 K30           ; 受渡Z軸_RB_POS_確定
AND>=.L +100 D20492      ; RB6C現在座標
AND X416A                ; 受渡Z(RB6C)原点確定_HEND
ORL
ANL
OR M13360                ; RB4C_MANUAL_ON
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OUT Y512C                ; 再投入(RB4C)一時停止_*STP
LD M16                   ; ﾘｾｯﾄPB
ANB X412E                ; 再投入(RB4C)ｱﾗｰﾑ_*ALM
OUT Y512E                ; 再投入(RB4C)ﾘｾｯﾄ_RES
LD M50                   ; 運転準備
OUT Y512F                ; 再投入(RB4C)ｻｰﾎﾞON_SON
;ﾛﾎﾞｯﾄｻｰﾎﾞ電源
LD M121                  ; 非常停止正常
OUT Y2660                ; 再投入搬送RB電源
;<h1/>■表示
;□□　　パネル表示　　□□
LD M13471                ; RB4C_ﾜｰｸ投入位置(待機位置)
LD M13301                ; 供給位置移動
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1840                ; UNIT23-1-1 TP-PL
LD M13472                ; RB4C_ﾜｰｸ受渡し位置
LD M13306                ; 受渡位置移動
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1841                ; UNIT23-1-2 TP-PL
;<h1/>■異常
LD X1651                 ; 再投入部ﾘｾｯﾄPB
DIFU M13499              ; 再投入部ﾘｾｯﾄPB PLS
;□□    異常検出    □□
;B2E0(K737)--B2EF(K752)  B2F0(K753)--B2FF(K768)
LDB X412E                ; 再投入(RB4C)ｱﾗｰﾑ_*ALM
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
ORL
OUT B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
LDP B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E1 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E0
AND B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
OUT B07E0
;
LD M13002                ; U23-S2 治具ID不一致
AND<> D12050 D12570      ; 再投入ﾊﾟﾚｯﾄID  Buffer_JIG_ID
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02E1                ; B2E1_738)再投入ID仮置台と不一致
ORL
OUT B02E1                ; B2E1_738)再投入ID仮置台と不一致
LDP B02E1                ; B2E1_738)再投入ID仮置台と不一致
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E2 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E1
AND B02E1                ; B2E1_738)再投入ID仮置台と不一致
OUT B07E1
;
LD M13038                ; U23-S38
AND T2316                ; ﾜｰｸ受渡し部ﾜｰｸ無確認(ON=無)
LD M13041                ; 取出済ﾜｰｸ確認
AND T2315                ; ﾜｰｸ受渡し部ﾜｰｸ有確認(ON=有)
ORL
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
ORL
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
LDP B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E3 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E2
AND B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
OUT B07E2
;
LDP M13255               ; ﾜｰｸ有取出要求
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR X1643                 ; 再投入_投入側ﾜｰｸ確認
AND B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
ORL
OUT B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
LDP B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E4 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E3
AND B02E3                ; B2E3_740)再投入部ﾜｰｸ有除去要求
OUT B07E3
;
LDP M13014               ; ﾜｰｸ浮きNG
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LDB T2327                ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
ORB M13014               ; ﾜｰｸ浮きNG
ANL
AND B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
ORL
OUT B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
LDP B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E5 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E4
AND B02E4                ; B2E4_741)再投入部ﾜｰｸ浮き異常
OUT B07E4
;
LDP M13027               ; CN部温度NG
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LDB T2327                ; 投入部ﾗｲﾄｶｰﾃﾝ_遮光_確認
ORB M13027               ; CN部温度NG
ANL
AND B02EA                ; B2EA_747)再投入部ﾜｰｸ温度NG
ORL
OUT B02EA                ; B2EA_747)再投入部ﾜｰｸ温度NG
LDP B02EA                ; B2EA_747)再投入部ﾜｰｸ温度NG
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02EB D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07EA
AND B02EA                ; B2EA_747)再投入部ﾜｰｸ温度NG
OUT B07EA
;
LDB X164E                ; 再投入ﾗｲﾄｶｰﾃﾝ1_OUT1
MPS
TMR K2351 K80            ; 再投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
MPP
AND T2351                ; 再投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
OUT B02E5                ; B2E5_742)再投入部ﾗｲﾄｶｰﾃﾝ遮光中
LDP B02E5                ; B2E5_742)再投入部ﾗｲﾄｶｰﾃﾝ遮光中
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E6 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E5
AND B02E5                ; B2E5_742)再投入部ﾗｲﾄｶｰﾃﾝ遮光中
OUT B07E5
;
LD M13023                ; 投入不可ﾌﾚｰﾑ取出要求(BZ､PL)
OUT B02E6                ; B2E6_743)投入不可ﾌﾚｰﾑ取出要求
LDP B02E6                ; B2E6_743)投入不可ﾌﾚｰﾑ取出要求
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E7 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E6
AND B02E6                ; B2E6_743)投入不可ﾌﾚｰﾑ取出要求
OUT B07E6
;
LD M13033                ; 吸着ｴﾗｰﾌﾚｰﾑ取出要求(BZ､PL)
OUT B02E7                ; B2E7_744)吸着ｴﾗｰﾌﾚｰﾑ取出要求
LDP B02E7                ; B2E7_744)吸着ｴﾗｰﾌﾚｰﾑ取出要求
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E8 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E7
AND B02E7                ; B2E7_744)吸着ｴﾗｰﾌﾚｰﾑ取出要求
OUT B07E7
;
LD M18                   ; 起動PB
AND M13480               ; U23_原点
AND= D12050 K0           ; 再投入ﾊﾟﾚｯﾄID
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
ORL
OUT B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
LDP B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02E9 D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E8
AND B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
OUT B07E8
;
LD M18                   ; 起動PB
AND M13480               ; U23_原点
AND<> D12050 K0          ; 再投入ﾊﾟﾚｯﾄID
AND<> D12050 D12570      ; 再投入ﾊﾟﾚｯﾄID  Buffer_JIG_ID
LDB M13499               ; 再投入部ﾘｾｯﾄPB PLS
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
ORL
OUT B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
LDP B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
AND< D900 K64            ; FIFO_DATA_VALUE
DW H02EA D0              ; エラーコード
CON
FIFOW D0 D900            ; エラーコード  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13499                ; 再投入部ﾘｾｯﾄPB PLS
OR B07E9
AND B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
OUT B07E9
;異常ﾌﾞｻﾞｰまとめ
LDB B02E0                ; B2E0_737)RBC4C ｺﾝﾄﾛｰﾗｰ異常
OR B07E0
LDB B02E2                ; B2E2_739)再投入部ﾜｰｸ受渡異常
OR B07E2
ANL
LDB B02E8                ; B2E8_745)途中投入ﾊﾟﾚｯﾄID無異常
OR B07E8
ANL
LDB B02E9                ; B2E9_746)途中投入_ﾊﾞｯﾌｧID不一致
OR B07E9
ANL
INV
OUT M13489               ; U23_異常BZ補助
END
ENDH
