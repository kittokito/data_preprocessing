DEVICE:53
;MODULE:UNIT6_ScrewRB
;MODULE_TYPE:0
;
;UNIT6 Screw RB
;
;■ロボット等機器入力受け
;ロボット、検査装置など状態、現在位置などをまとめる信号は一旦ここで受ける。
LD CR2002                ; 常時ON
PMOV X1050 K0 D6023 K0 K8; ﾈｼﾞ締め箇所 1  Screwe RB  ﾈｼﾞ締め箇所 No.
PMOV X1058 K0 D6024 K0 K8; ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ 1  Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ
;<h1/>装置状態・動作条件
LD X1066                 ; 原点ﾌﾗｸﾞ
AND X104F                ; ｴﾘｱ1(原点確認)
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
AND X1004                ; 外部ﾓｰﾄﾞ
AND X1009                ; ｻｰﾎﾞON中
ANB X100B                ; ﾃｨｰﾁﾝｸﾞ中
OR M27                   ; ねじ締めRB無視
OUT M6900                ; ネジ締めRB原点
LDB B0C0                 ; 原点復帰時ﾈｼﾞ排出異常
ANB B0C1                 ; 原点復帰ｴﾘｱ外
ANB B0C2                 ; ﾈｼﾞ締めRB応答待ち時間
ANB B0C3                 ; ﾕﾆｯﾄ6_異常4
ANB B0C4                 ; ﾕﾆｯﾄ6_異常5
ANB B0C5                 ; ﾕﾆｯﾄ6_異常6
INV
OUT M6901                ; ﾕﾆｯﾄ6全異常
LD M6900                 ; ネジ締めRB原点
ANB M6000                ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ開始
LDB X1001                ; ﾛﾎﾞｯﾄ運転中
AND X1008                ; ﾛﾎﾞｯﾄ電源入り完了
AND X100A                ; CAL 完了
AND X1003                ; 自動ﾓｰﾄﾞ
AND X1004                ; 外部ﾓｰﾄﾞ
ANB X100B                ; ﾃｨｰﾁﾝｸﾞ中
OR M27                   ; ねじ締めRB無視
ANL
OUT M6902                ; ﾕﾆｯﾄ6起動条件
LDB M64                  ; 運転準備
OR M65                   ; 即停止異常(緊急停止)
OR M6901                 ; ﾕﾆｯﾄ6全異常
OR T15                   ; 強制停止
ORF M6631                ; 1st ﾈｼﾞ締め実行
ORF M6632                ; 2st ﾈｼﾞ締め実行
OUT M6903                ; ﾕﾆｯﾄ6起動OFF
LD M98                   ; 原点復帰開始
ANB M6904                ; ﾕﾆｯﾄ6原点復帰中
OR M6904                 ; ﾕﾆｯﾄ6原点復帰中
ANB M6903                ; ﾕﾆｯﾄ6起動OFF
ANB M6579                ; ﾕﾆｯﾄ6原点復帰完了確認
OUT M6904                ; ﾕﾆｯﾄ6原点復帰中
LD M83                   ; 各工程起動指令
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
LD M6631                 ; 1st ﾈｼﾞ締め実行
OR M6632                 ; 2st ﾈｼﾞ締め実行
ANL
ORL
AND M6902                ; ﾕﾆｯﾄ6起動条件
OR M6905                 ; ﾕﾆｯﾄ6起動中
ANB M6903                ; ﾕﾆｯﾄ6起動OFF
ANB M6099                ; ﾕﾆｯﾄ6動作完了
OUT M6905                ; ﾕﾆｯﾄ6起動中
LD M6200                 ; ﾈｼﾞ残量低下OP_CALL
AND CR2003               ; 常時OFF
OUT M6908                ; ﾕﾆｯﾄ6警報補助
;Pack処理
LD>= D6000 K3            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
AND< D6000 K10           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD>= D6000 K13           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
AND< D6000 K20           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
ANB M6016                ; 排出1ﾈｼﾞ締め結果確認
ORL
OUT M6914                ; ﾈｼﾞ締めRB Y1側動作中
LD>= D6000 K103          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
AND< D6000 K110          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD>= D6000 K113          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
AND< D6000 K120          ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
ANB M6016                ; 排出1ﾈｼﾞ締め結果確認
ORL
OUT M6915                ; ﾈｼﾞ締めRB Y2側動作中
LDB X1001                ; ﾛﾎﾞｯﾄ運転中
ANB M6914                ; ﾈｼﾞ締めRB Y1側動作中
ANB M6915                ; ﾈｼﾞ締めRB Y2側動作中
INV
ANB X1049                ; ﾈｼﾞ締めRB上空待機中
OUT M6910                ; UNIT6 Pack
;□エラー発生　一時停止フラグ
;　全体とネジ締め排出エリアの一時停止フラグを分けてある
LD CR2002                ; 常時ON
MPS
PMOV.D M6920 K0 D6100 K0 K10;Err420:ねじ締めRB警告  UT6ｴﾗｰ判別
MPP
AND<>.D D6100 K0         ; UT6ｴﾗｰ判別
OUT M6912                ; UNIT6 ｴﾗｰ
LD CR2002                ; 常時ON
MPS
PMOV.D M6930 K0 D6102 K0 K10;Err430:ﾈｼﾞ供給引っ掛かり異常  UT6_排出停止 ｴﾗｰ判別
MPP
AND<>.D D6102 K0         ; UT6_排出停止 ｴﾗｰ判別
OUT M6911                ; UNIT6 ﾈｼﾞ供給ｴﾗｰ
LDB M6901                ; ﾕﾆｯﾄ6全異常
ANB M160                 ; ALL PAUSE
ANB M180                 ; UNIT6.7.8PAUSE
ANB M6908                ; ﾕﾆｯﾄ6警報補助
ANB M6911                ; UNIT6 ﾈｼﾞ供給ｴﾗｰ
ANB M6912                ; UNIT6 ｴﾗｰ
LDB M32                  ; 自動ﾓｰﾄﾞ
ANB M33                  ; 手動ﾓｰﾄﾞ
LD M32                   ; 自動ﾓｰﾄﾞ
OR M33                   ; 手動ﾓｰﾄﾞ
AND M6905                ; ﾕﾆｯﾄ6起動中
ORL
LD M33                   ; 手動ﾓｰﾄﾞ
AND M6904                ; ﾕﾆｯﾄ6原点復帰中
ORL
ANL
OUT M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
;RB interlock
LD T1215                 ; ﾈｼﾞ締めRB CPU正常
ANB X100D                ; ﾊﾞｯﾃﾘれ警告
ANB X1002                ; ﾛﾎﾞｯﾄ異常
AND X1009                ; ｻｰﾎﾞON中
AND X1008                ; ﾛﾎﾞｯﾄ電源入り完了
AND X100A                ; CAL 完了
AND X1003                ; 自動ﾓｰﾄﾞ
AND X1004                ; 外部ﾓｰﾄﾞ
ANB X100B                ; ﾃｨｰﾁﾝｸﾞ中
AND Y210                 ; ﾈｼﾞ締めRBｲﾈｰﾌﾞﾙ
AND M131                 ; 安全扉正常
AND M136                 ; 背面扉正常
OUT M6960                ; ねじ締めRB動作可能
;<h1/>■自動回路 
;
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M6000 M6099         ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ6動作完了
CON
ZRES M6400 M6499         ; Daily check
LDB M6905                ; ﾕﾆｯﾄ6起動中
MOV K0 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M6905                 ; ﾕﾆｯﾄ6起動中
MOV D6000 Z1             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
OUT M6000:Z1             ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ開始
;自動運転ステップ開始
;設定初期化
;Setting initialization
LD M6000                 ; ﾕﾆｯﾄ6ｽﾃｯﾌﾟ開始
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
LDB M27                  ; ねじ締めRB無視
LDB M39                  ; 日常点検ﾓｰﾄﾞ
OR M45                   ; ﾈｼﾞ締めRB日常点検
ANL
ANL
MPS
DW K0 D6020              ; Screwe RB  Prog. No.
CON
DW K0 D6021              ; Screwe RB  Type No.
CON
DW K0 D6022              ; Screwe RB  PLT No.
MPP
LD= D6020 K0             ; Screwe RB  Prog. No.
AND= D6021 K0            ; Screwe RB  Type No.
AND= D6022 K0            ; Screwe RB  PLT No.
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
LD M27                   ; ねじ締めRB無視
LD M39                   ; 日常点検ﾓｰﾄﾞ
ANB M45                  ; ﾈｼﾞ締めRB日常点検
ORL
AND M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANL
DW K98 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;処理判定
;Processing discrimination
LD M6001                 ; 機種_ﾊﾟﾚｯﾄNo初期化確認
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
ANB M45                  ; ﾈｼﾞ締めRB日常点検
MPS
LD M7850                 ; 排出1ﾈｼﾞ締め要求
OR M6631                 ; 1st ﾈｼﾞ締め実行
ANL
DW K10 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
LD M8850                 ; 排出2ﾈｼﾞ締め要求
OR M6632                 ; 2st ﾈｼﾞ締め実行
ANL
DW K50 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
LDB M7850                ; 排出1ﾈｼﾞ締め要求
ANB M8850                ; 排出2ﾈｼﾞ締め要求
AND M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M7398                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止
AND M8398                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止
ANL
DW K90 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
AND M45                  ; ﾈｼﾞ締めRB日常点検
DW K400 D6000            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>□　　１側ネジ締め
;Tighten a screw on ST1
;排出1側ﾈｼﾞ締めﾃﾞｰﾀ転送
;※排出1側の場合ﾊﾟﾚｯﾄNo.1
LD M6010                 ; 排出1ﾈｼﾞ締めﾃﾞｰﾀｾｯﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
DW K1 D6020              ; Screwe RB  Prog. No.
CON
DW K1 D6022              ; Screwe RB  PLT No.
CON
MOV E55 D6021            ; Type No.  Screwe RB  Type No.
MPP
LD= D6020 K1             ; Screwe RB  Prog. No.
AND<> D6021 K0           ; Screwe RB  Type No.
AND= D6022 K1            ; Screwe RB  PLT No.
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め起動
;Screw Start
LD M6011                 ; 排出1ﾈｼﾞ締め開始
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M6960                ; ねじ締めRB動作可能
FMOV K0 D6031 K5         ; ﾈｼﾞ締めPOS.1-1 ｴﾗｰｺｰﾄﾞ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M6012                 ; 排出1ﾈｼﾞ締め起動ﾃﾞｨﾚｲ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
TMH K621 K3              ; 起動ﾃﾞｨﾚｲ
CON
AND T621                 ; 起動ﾃﾞｨﾚｲ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ起動確認
;Robot Start Check
LD M6013                 ; 排出1ﾈｼﾞ締めPRG.ｽﾀｰﾄ
MPS
LD X1001                 ; ﾛﾎﾞｯﾄ運転中
AND X1005                ; ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾘｾｯﾄ
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6751                ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
LD M6014                 ; 排出1ﾈｼﾞ締め動作開始確認
AND X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
ANB X1060                ; 全ﾈｼﾞ締め完了
ANB X1063                ; ﾈｼﾞ取出不良
ANB X1065                ; ﾈｼﾞ締め不良
ANB X106A                ; 停止処理中
ANB X106C                ; ﾈｼﾞ排出ﾐｽ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め1ｻｲｸﾙ完了
;Screw 1Cycle End
LD M6015                 ; 排出1ﾈｼﾞ締め完了待ち
MPS
LD X106B                 ; ﾈｼﾞ締め動作停止ﾌﾗｸﾞ
ANB X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
AND X100C                ; 1ｻｲｸﾙ終了
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6760                ; 排出1ﾈｼﾞ締め完了待ち
;結果判断
;Result check
LD M6016                 ; 排出1ﾈｼﾞ締め結果確認
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
LD X1060                 ; 全ﾈｼﾞ締め完了
ANB X1063                ; ﾈｼﾞ取出不良
ANB X1065                ; ﾈｼﾞ締め不良
ANB X106A                ; 停止処理中
ANB X106C                ; ﾈｼﾞ排出ﾐｽ
ANL
DW K20 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
LD X1063                 ; ﾈｼﾞ取出不良
AND CR2003               ; 常時OFF
ANL
INC D6050                ; ﾈｼﾞ取出NGｶｳﾝﾀ
CON
DW K30 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
AND X1065                ; ﾈｼﾞ締め不良
INC D6051                ; 1側ﾈｼﾞ締めNGｶｳﾝﾀ
CON
DW K21 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
AND X106A                ; 停止処理中
DW K22 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め要求OFF確認
;Screw request is waiting to off
LD M6020                 ; 排出1ﾈｼﾞ締めOK完了
OR M6021                 ; 排出1ﾈｼﾞ締めNG完了
OR M6022                 ; 排出1ﾈｼﾞ締め停止処理完了
ANB M7850                ; 排出1ﾈｼﾞ締め要求
ANB M6631                ; 1st ﾈｼﾞ締め実行
DW K30 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>□□　２側ネジ締め
;Tighten a screw on ST2
;排出2側ﾈｼﾞ締めﾃﾞｰﾀ転送
;※排出1側の場合ﾊﾟﾚｯﾄNo.2
LD M6050                 ; 排出2ﾈｼﾞ締めﾃﾞｰﾀｾｯﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
DW K1 D6020              ; Screwe RB  Prog. No.
CON
DW K2 D6022              ; Screwe RB  PLT No.
CON
MOV E55 D6021            ; Type No.  Screwe RB  Type No.
MPP
LD= D6020 K1             ; Screwe RB  Prog. No.
AND<> D6021 K0           ; Screwe RB  Type No.
AND= D6022 K2            ; Screwe RB  PLT No.
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め起動
;Screw Start
LD M6051                 ; 排出2ﾈｼﾞ締め開始
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M6960                ; ねじ締めRB動作可能
FMOV K0 D6041 K5         ; ﾈｼﾞ締めPOS.2-1 ｴﾗｰｺｰﾄﾞ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M6052                 ; 排出2ﾈｼﾞ締め起動ﾃﾞｨﾚｲ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
TMH K622 K3              ; 起動ﾃﾞｨﾚｲ
CON
AND T622                 ; 起動ﾃﾞｨﾚｲ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ起動確認
;Robot Start Check
LD M6053                 ; 排出2ﾈｼﾞ締めPRG.ｽﾀｰﾄ
MPS
LD X1001                 ; ﾛﾎﾞｯﾄ運転中
AND X1005                ; ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄﾘｾｯﾄ
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6752                ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
LD M6054                 ; 排出2ﾈｼﾞ締め動作開始確認
AND X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
ANB X1060                ; 全ﾈｼﾞ締め完了
ANB X1063                ; ﾈｼﾞ取出不良
ANB X1065                ; ﾈｼﾞ締め不良
ANB X106A                ; 停止処理中
ANB X106C                ; ﾈｼﾞ排出ﾐｽ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め1ｻｲｸﾙ完了
;Screw 1Cycle End
LD M6055                 ; 排出2ﾈｼﾞ締め完了待ち
MPS
LD X106B                 ; ﾈｼﾞ締め動作停止ﾌﾗｸﾞ
ANB X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
AND X100C                ; 1ｻｲｸﾙ終了
ANL
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6761                ; 排出2ﾈｼﾞ締め完了待ち
;結果判断
;Result check
LD M6056                 ; 排出2ﾈｼﾞ締め結果確認
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
LD X1060                 ; 全ﾈｼﾞ締め完了
ANB X1063                ; ﾈｼﾞ取出不良
ANB X1065                ; ﾈｼﾞ締め不良
ANB X106A                ; 停止処理中
ANB X106C                ; ﾈｼﾞ排出ﾐｽ
ANL
DW K60 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
LD X1063                 ; ﾈｼﾞ取出不良
AND CR2003               ; 常時OFF
ANL
INC D6050                ; ﾈｼﾞ取出NGｶｳﾝﾀ
CON
DW K70 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MRD
AND X1065                ; ﾈｼﾞ締め不良
INC D6052                ; 2側ﾈｼﾞ締めNGｶｳﾝﾀ
CON
DW K61 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
AND X106A                ; 停止処理中
DW K62 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め要求OFF確認
;Screw request is waiting to off
LD M6060                 ; 排出2ﾈｼﾞ締めOK完了
OR M6061                 ; 排出2ﾈｼﾞ締めNG完了
OR M6062                 ; 排出2停止処理完了
ANB M8850                ; 排出2ﾈｼﾞ締め要求
ANB M6632                ; 2st ﾈｼﾞ締め実行
DW K70 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ﾈｼﾞ締め終了
;Screw complete
LD M6030                 ; 排出1ﾈｼﾞ締め終了
OR M6070                 ; 排出2ﾈｼﾞ締め終了
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
DW K90 D6000             ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;<h1/>☆　　ねじ締めトルク日常点検
;Daily check for Screw RB torque.
;▼↓↓↓　Daily check mode　↓↓↓▼
LD M6400                 ; Daily check
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M131                 ; 安全扉正常
AND M136                 ; 背面扉正常
AND M6960                ; ねじ締めRB動作可能
RES M42                  ; RB Torque Daily check OK
CON
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ねじ締めRB ﾄﾙｸ測定位置移動
;Screw RB move to Torque mesurement point
;Daily Check Data set
LD M6401                 ; ﾃﾞｰﾀｾｯﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
DW K10 D6020             ; Screwe RB  Prog. No.
CON
AND= D6020 K10           ; Screwe RB  Prog. No.
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;Screw RB Start
LD M6402                 ; Screw RB 起動ｳｪｲﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M6960                ; ねじ締めRB動作可能
TMH K623 K3              ; 起動ﾃﾞｨﾚｲ
CON
AND T623                 ; 起動ﾃﾞｨﾚｲ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;Robot Start Check
LD M6403                 ; ﾈｼﾞ締めRB PRG.ｽﾀｰﾄ
MPS
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6753                ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
;Screw RB 1Cycle End
LD M6404                 ; ﾈｼﾞ締めRB 動作開始確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
AND X100C                ; 1ｻｲｸﾙ終了
AND X104A                ; ﾈｼﾞ締めRB 日常点検ﾄﾙｸ測定位置
DW K410 D6000            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;装置一時停止　
;Machine PAUSE
LD M6410                 ; Machine   PAUSE
MPS
AND M160                 ; ALL PAUSE
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6770                ; DC_T PAUSE Pls.
;ﾄﾙｸ測定
;Mesurment of Torque
LD M160                  ; ALL PAUSE
ANB M131                 ; 安全扉正常
OR M6771                 ; ﾄﾙｸ測定動作確認
AND<= D6000 K414         ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
OUT M6771                ; ﾄﾙｸ測定動作確認
LD M6411                 ; 作業者 測定開始待ち
MPS
ANB M160                 ; ALL PAUSE
MPS
AND M6771                ; ﾄﾙｸ測定動作確認
DW K420 D6000            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
ANB M6771                ; ﾄﾙｸ測定動作確認
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6772                ; 作業者 ﾄﾙｸ測定依頼
;ﾄﾙｸ測定動作未確認
;Err421:No torque mesurement
LD M6412                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K421 D148             ; 警報No.
CON
SET M6921                ; Err421:ﾄﾙｸ測定未確認
CON
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M6413                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
LD M6414                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K410 D6000            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;作業者判断
;Operator Judgment
LD M6420                 ; 作業者判断
MPS
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
AND B1518                ; DC_T OK sw
SET M42                  ; RB Torque Daily check OK
CON
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
AND B1519                ; DC_T NG sw
RES M42                  ; RB Torque Daily check OK
CON
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6773                ; DC_作業者OK確認依頼画面表示
;ねじ締めRB 原位置移動
;Screw RB move to HOME point
;Daily Check Data set
LD M6421                 ; ﾃﾞｰﾀｾｯﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
DW K50 D6020             ; Screwe RB  Prog. No.
CON
AND= D6020 K50           ; Screwe RB  Prog. No.
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;Screw RB Start
LD M6422                 ; Screw RB 起動ｳｪｲﾄ
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M6960                ; ねじ締めRB動作可能
TMH K624 K3              ; 起動ﾃﾞｨﾚｲ
CON
AND T624                 ; 起動ﾃﾞｨﾚｲ
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;Robot Start Check
LD M6423                 ; ﾈｼﾞ締めRB PRG.ｽﾀｰﾄ
MPS
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D6000               ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6754                ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
;Screw RB 1Cycle End
LD M6424                 ; ﾈｼﾞ締めRB 動作開始確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
AND X100C                ; 1ｻｲｸﾙ終了
AND M6900                ; ネジ締めRB原点
DW K430 D6000            ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
;Cycle end process
LD M6430
MPS
AND M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
@MOV K98 D6000           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
OUT M6774                ; DC_T ｻｲｸﾙ停止
;▲↑↑↑　Daily check mode　↑↑↑▲
;サイクル停止条件
;Cycle end check
LD M6090                 ; 1ｻｲｸﾙ終了
MPS
LDB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ORB M7398                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止
ORB M8398                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止
AND M32                  ; 自動ﾓｰﾄﾞ
ANL
DW K0 D6000              ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M7398                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止
AND M8398                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止
ANL
@MOV K98 D6000           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;終了
;Unit AUTO cycle end
LD CR2003                ; 常時OFF
OR M6098                 ; ﾕﾆｯﾄ6ｻｲｸﾙ停止
MPS
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M72                  ; 全ﾕﾆｯﾄ停止確認
ANL
@MOV K99 D6000           ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
MPP
ANB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
DW K0 D6000              ; ﾕﾆｯﾄ6自動運転ｽﾃｯﾌﾟ
;■ﾈｼﾞ締め判定ﾌﾗｸﾞ
;排出1
LD M6020                 ; 排出1ﾈｼﾞ締めOK完了
OUT M6700                ; 排出1側ﾈｼﾞ締めOK
LD M6021                 ; 排出1ﾈｼﾞ締めNG完了
OR M6022                 ; 排出1ﾈｼﾞ締め停止処理完了
OUT M6701                ; 排出1側ﾈｼﾞ締めNG
;排出2
LD M6060                 ; 排出2ﾈｼﾞ締めOK完了
OUT M6710                ; 排出2側ﾈｼﾞ締めOK
LD M6061                 ; 排出2ﾈｼﾞ締めNG完了
OR M6062                 ; 排出2停止処理完了
OUT M6711                ; 排出2側ﾈｼﾞ締めNG
;ﾈｼﾞ締め停止、続行指令補助
;先行動作は今の所無いので実行と続行は常時可
LD CR2003                ; 常時OFF
AND X1069                ; 待機中ﾌﾗｸﾞ
ANB M6102                ; ﾈｼﾞ締め続行ﾌﾗｸﾞ
OR M6100                 ; 停止指令
AND M6905                ; ﾕﾆｯﾄ6起動中
ANB M6101                ; ﾈｼﾞ締め実行許可
OUT M6100                ; 停止指令
LD CR2002                ; 常時ON
LD M6760                 ; 排出1ﾈｼﾞ締め完了待ち
OR M6761                 ; 排出2ﾈｼﾞ締め完了待ち
ANL
ANB M6100                ; 停止指令
OR M6101                 ; ﾈｼﾞ締め実行許可
AND M6905                ; ﾕﾆｯﾄ6起動中
OUT M6101                ; ﾈｼﾞ締め実行許可
TMH K640 K5              ; ﾈｼﾞ締めｽﾀｰﾄ
LD M6101                 ; ﾈｼﾞ締め実行許可
OUT M6102                ; ﾈｼﾞ締め続行ﾌﾗｸﾞ
;ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ読込
LD M6905                 ; ﾕﾆｯﾄ6起動中
LDP X1060                ; 全ﾈｼﾞ締め完了
ORP X1063                ; ﾈｼﾞ取出不良
ORP X1065                ; ﾈｼﾞ締め不良
ANL
MPS
AND= D6023 K1            ; Screwe RB  ﾈｼﾞ締め箇所 No.
MOV D6024 D6031          ; Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ  ﾈｼﾞ締めPOS.1-1 ｴﾗｰｺｰﾄﾞ
MRD
AND= D6023 K2            ; Screwe RB  ﾈｼﾞ締め箇所 No.
MOV D6024 D6032          ; Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ  ﾈｼﾞ締めPOS.1-2 ｴﾗｰｺｰﾄﾞ
MRD
AND= D6023 K3            ; Screwe RB  ﾈｼﾞ締め箇所 No.
MOV D6024 D6033          ; Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ  ﾈｼﾞ締めPOS.1-3 ｴﾗｰｺｰﾄﾞ
MRD
AND= D6023 K4            ; Screwe RB  ﾈｼﾞ締め箇所 No.
MOV D6024 D6034          ; Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ  ﾈｼﾞ締めPOS.1-4 ｴﾗｰｺｰﾄﾞ
MPP
AND= D6023 K5            ; Screwe RB  ﾈｼﾞ締め箇所 No.
MOV D6024 D6035          ; Screwe RB  ﾈｼﾞ締めｴﾗｰｺｰﾄﾞ  ﾈｼﾞ締めPOS.1-5 ｴﾗｰｺｰﾄﾞ
;<h1/>///　ネジ供給自動運転
;Quicher Operation
;運転開始・ステッパ初期化・補助接点へ展開
LD M83                   ; 各工程起動指令
OR M6905                 ; ﾕﾆｯﾄ6起動中
ANB M6199                ; UT6SUB1動作終了
ANB M27                  ; ねじ締めRB無視
OUT M6150                ; ﾈｼﾞ供給起動
LD CR2002                ; 常時ON
ZRES M6160 M6199         ; ﾈｼﾞ供給開始  UT6SUB1動作終了
LDB M6150                ; ﾈｼﾞ供給起動
MOV K0 D6001             ; UT6SUB1   
LD M6150                 ; ﾈｼﾞ供給起動
MOV D6001 Z1             ; UT6SUB1   
OUT M6160:Z1             ; ﾈｼﾞ供給開始
;自動運転ステップ開始
LD M6160                 ; ﾈｼﾞ供給開始
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
ANB T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
@INC D6001               ; UT6SUB1   
MPP
AND T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
DW K10 D6001             ; UT6SUB1   
LD M6161                 ; ｴｽｹｰﾌﾟ戻り
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND T1602                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
@INC D6001               ; UT6SUB1   
;ﾈｼﾞ到着確認
;到着時はｴｽｹｰﾌﾟ動作・未到着時は引っ掛かりNG
LD M6162                 ; ｴｽｹｰﾌﾟ戻り確認
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
ANB T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
MPS
TMR K650 K100            ; ﾈｼﾞ締引っ掛かり
MPP
AND T650                 ; ﾈｼﾞ締引っ掛かり
@INC D6001               ; UT6SUB1   
MPP
LD T1600                 ; ﾈｼﾞ供給ﾈｼﾞ検出
AND M135                 ; ﾈｼﾞ供給ﾕﾆｯﾄ閉正常
ANL
DW K10 D6001             ; UT6SUB1   
;ﾈｼﾞ引っ掛りｴﾗｰ発生
LD M6163                 ; ﾈｼﾞ引っ掛り
ANB M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
DW K430 D148             ; 警報No.
CON
SET M6930                ; Err430:ﾈｼﾞ供給引っ掛かり異常
CON
@INC D6001               ; UT6SUB1   
LD M6164                 ; ﾈｼﾞ引っ掛りｴﾗｰ発生
AND M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
@INC D6001               ; UT6SUB1   
LD M6165                 ; ﾈｼﾞ引っ掛りｴﾗｰ停止中
ANB M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
DW K2 D6001              ; UT6SUB1   
;ｴｽｹｰﾌﾟ送り確認、ﾛﾎﾞｯﾄへ取出可ON
LD M6170                 ; ﾈｼﾞ到着ｴｽｹｰﾌﾟ送り
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND T1601                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側端
@INC D6001               ; UT6SUB1   
;ﾛﾎﾞｯﾄより取出完了確認
LD M6171                 ; ﾈｼﾞ取出可ON
MPS
AND X1062                ; ﾈｼﾞ取出完了
DW K15 D6001             ; UT6SUB1   
MPP
AND X1063                ; ﾈｼﾞ取出不良
@INC D6001               ; UT6SUB1   
;ERR434:RBねじ取出し不良
;ERR431:Screw RB Screw pick up error
LD M6172                 ; ねじ取出しｴﾗｰ
ANB X1063                ; ﾈｼﾞ取出不良
ANB M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
DW K434 D148             ; 警報No.
CON
SET M6934                ; Err434:RBﾈｼﾞ取出し不良
CON
@INC D6001               ; UT6SUB1   
LD M6173                 ; ﾈｼﾞ取出しｴﾗｰ
AND M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
@INC D6001               ; UT6SUB1   
LD M6174                 ; ﾈｼﾞ取出しｴﾗｰ停止中
AND T131                 ; UT6 7 8   ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K15 D6001             ; UT6SUB1   
;ねじ残ﾁｪｯｸ
LD M6175                 ; ﾈｼﾞ取出完了確認
ANB X1062                ; ﾈｼﾞ取出完了
MPS
ANB T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
DW K20 D6001             ; UT6SUB1   
MPP
AND T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
TMR K609 K10             ; ﾈｼﾞ残確認時間
CON
AND T609                 ; ﾈｼﾞ残確認時間
@INC D6001               ; UT6SUB1   
;ERR431:切り出しねじ残
;ERR431:Screw remain at cuttingout position on Quicher
LD M6176                 ; ﾈｼﾞ残ｴﾗｰ
ANB M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
DW K431 D148             ; 警報No.
CON
SET M6931                ; Err431:ｸｲｯﾁｬ切出位置ﾈｼﾞ残
CON
@INC D6001               ; UT6SUB1   
LD M6177                 ; ﾈｼﾞ残ｴﾗｰ
AND M75                  ; ﾕﾆｯﾄ6.7.8 Error PAUSE
@INC D6001               ; UT6SUB1   
LD M6178                 ; ﾈｼﾞ残ｴﾗｰ停止中
AND T131                 ; UT6 7 8   ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K15 D6001             ; UT6SUB1   
;ｴｽｹｰﾌﾟ戻
LD M6180                 ; ﾈｼﾞ取出完了OFF確認
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
@INC D6001               ; UT6SUB1   
LD M6181                 ; ｴｽｹｰﾌﾟ戻り
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND T1602                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
@INC D6001               ; UT6SUB1   
LD M6182                 ; ｴｽｹｰﾌﾟ戻り確認
DW K30 D6001             ; UT6SUB1   
;サイクル停止条件
;Cycle end check
LD M6190                 ; UT6SUB1 1ｻｲｸﾙ終了
MPS
LDB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ORB M7398                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止
ORB M8398                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止
ANL
DW K0 D6001              ; UT6SUB1   
MPP
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M7398                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止
AND M8398                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止
ANL
@MOV K38 D6001           ; UT6SUB1   
;終了
;Unit AUTO cycle end
LD M6198                 ; UT6SUB1ｻｲｸﾙ停止
MPS
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M72                  ; 全ﾕﾆｯﾄ停止確認
ANL
@MOV K39 D6001           ; UT6SUB1   
MPP
ANB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
DW K0 D6001              ; UT6SUB1   
;<h1/>■原点復帰回路
;
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M6500 M6599         ; ﾕﾆｯﾄ6原点復帰開始
LDB M6904                ; ﾕﾆｯﾄ6原点復帰中
MOV K0 D6010             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M6904                 ; ﾕﾆｯﾄ6原点復帰中
MOV D6010 Z1             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
OUT M6500:Z1             ; ﾕﾆｯﾄ6原点復帰開始
;原点復帰ステップ開始
;Origin start
LD M6500                 ; ﾕﾆｯﾄ6原点復帰開始
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
MPS
LDB M27                  ; ねじ締めRB無視
AND M6960                ; ねじ締めRB動作可能
ANL
@INC D6010               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
MPP
LD M27                   ; ねじ締めRB無視
ANB M6960                ; ねじ締めRB動作可能
ANL
DW K10 D6010             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M6501                 ; PRG. No. 書き込み
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
DW K50 D6020             ; Screwe RB  Prog. No.
TMR K620 K2              ; 運転開始ﾃﾞｨﾚｲ
CON
AND T620                 ; 運転開始ﾃﾞｨﾚｲ
@INC D6010               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
LD M6502                 ; HOME 開始
MPS
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D6010               ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
MPP
OUT M6750                ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
LD M6503                 ; 動作完了待ち
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
AND X100C                ; 1ｻｲｸﾙ終了
DW K10 D6010             ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M6510                 ; 原点復帰動作完了
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
AND M6900                ; ネジ締めRB原点
OUT M6578                ; ﾕﾆｯﾄ6原点復帰完了
LD M6578                 ; ﾕﾆｯﾄ6原点復帰完了
ANB M98                  ; 原点復帰開始
@MOV K79 D6010           ; ﾕﾆｯﾄ6原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路 
;
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
LDB M6905                ; ﾕﾆｯﾄ6起動中
OR M6631                 ; 1st ﾈｼﾞ締め実行
OR M6632                 ; 2st ﾈｼﾞ締め実行
ANL
ANB M97                  ; 原点復帰動作中
MC
;Quicher cylinder
LDB M6910                ; UNIT6 Pack
MPS
ANP B1159                ; ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側 PB
OUT M6601                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
MPP
LDP B115A                ; ｸｲｯﾁｬｴｽｹｰﾌﾟorg側 PB
ANB T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
ANL
OUT M6602                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟorg側
;Quicher Screw set 1 cycle
LD B115E                 ; ｸｲｯﾁｬ ﾈｼﾞｾｯﾄ1ｻｲｸﾙ PB
TMR K661 K20             ; 切り出し連続ON時間
LD T661                  ; 切り出し連続ON時間
OR M6614                 ; 連続切り出しモード
ANPB B115E               ; ｸｲｯﾁｬ ﾈｼﾞｾｯﾄ1ｻｲｸﾙ PB
OUT M6614                ; 連続切り出しモード
LD CR2002                ; 常時ON
LDP B115E                ; ｸｲｯﾁｬ ﾈｼﾞｾｯﾄ1ｻｲｸﾙ PB
OR M6614                 ; 連続切り出しモード
LD T1601                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側端
ANB T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
AND T660                 ; ねじ無し検出ｳｪｲﾄ
OR T1602                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
ANL
OR M6610                 ; ﾈｼﾞｾｯﾄ 1ｻｲｸﾙ
ANL
ANB M6613                ; 切り出し完了
OUT M6610                ; ﾈｼﾞｾｯﾄ 1ｻｲｸﾙ
MPS
LDP M6610                ; ﾈｼﾞｾｯﾄ 1ｻｲｸﾙ
ANB T1602                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
ANL
OUT M6611                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟorg側
MRD
LD T1602                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
AND T1600                ; ﾈｼﾞ供給ﾈｼﾞ検出
OR M6612                 ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
ANL
OUT M6612                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
MPP
LD M6612                 ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
AND T1601                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側端
ANL
OUT M6613                ; 切り出し完了
;Quicher power
LDP B1158                ; ｸｲｯﾁｬ電源ONPB
AND M33                  ; 手動ﾓｰﾄﾞ
AND M129                 ; 非常停止正常
AND M130                 ; 外部非常停止正常
ANB M6905                ; ﾕﾆｯﾄ6起動中
ANB M97                  ; 原点復帰動作中
ALT M6600                ; Quicher power ON
;ねじ締め1ｻｲｸﾙ
;Screw RB 1cycle
LDB M6020                ; 排出1ﾈｼﾞ締めOK完了
ANB M6021                ; 排出1ﾈｼﾞ締めNG完了
ANB M6030                ; 排出1ﾈｼﾞ締め終了
ANB M6060                ; 排出2ﾈｼﾞ締めOK完了
ANB M6061                ; 排出2ﾈｼﾞ締めNG完了
ANB M6070                ; 排出2ﾈｼﾞ締め終了
INV
OUT M6630                ; ﾈｼﾞ締めRB動作終了
LD CR2002                ; 常時ON
LDP B115B                ; ﾈｼﾞ締めRB 1st側 1ｻｲｸﾙ
AND T1700                ; ﾈｼﾞ締め治具1 ﾜｰｸ検出
LD<.L D128 +12000        ; RCA5_Position Data
LD=.L D128 E3108         ; RCA5_Position Data  除材1Y_TP_Pos5_Data
AND=.L D130 E3128        ; RCA6_Position Data  除材1Z_TP_Pos5_Data
ORL
ANL
AND= D93 E3423           ; Screw Jig1 ID No.  Screw Jig1 ID 
AND M6902                ; ﾕﾆｯﾄ6起動条件
OR M6631                 ; 1st ﾈｼﾞ締め実行
ANL
ANB M6630                ; ﾈｼﾞ締めRB動作終了
OUT M6631                ; 1st ﾈｼﾞ締め実行
LD CR2002                ; 常時ON
LDP B115C                ; ﾈｼﾞ締めRB 2st側 1ｻｲｸﾙ
AND T1708                ; ﾈｼﾞ締め治具2 ﾜｰｸ検出
LD<.L D134 +12000        ; RCA8_Position Data
LD=.L D134 E3148         ; RCA8_Position Data  除材2Y_TP_Pos5_Data
AND=.L D136 E3168        ; RCA9_Position Data  除材2Z_TP_Pos5_Data
ORL
ANL
AND= D94 E3424           ; Screw2 Jig ID  Screw Jig2 ID 
AND M6902                ; ﾕﾆｯﾄ6起動条件
OR M6632                 ; 2st ﾈｼﾞ締め実行
ANL
ANB M6630                ; ﾈｼﾞ締めRB動作終了
OUT M6632                ; 2st ﾈｼﾞ締め実行
;ﾈｼﾞ締めRB ｲﾈｰﾌﾞﾙ
;Screw RB Inable
LDP B115D                ; ﾈｼﾞ締めRB Inable OFF
ALT M6640                ; Manual Inable OFF
MCR
LDF M33                  ; 手動ﾓｰﾄﾞ
ORP M97                  ; 原点復帰動作中
RES M6600                ; Quicher power ON
RES M6640                ; Manual Inable OFF
;<h1/>出力回路
;
;ﾛﾎﾞｯﾄ
;Screw RB
LD CR2002                ; 常時ON
PMOV D6020 K0 Y1808 K0 K7; Screwe RB  Prog. No.  ﾌﾟﾛｸﾞﾗﾑ選択 0
PMOV D6021 K0 Y1818 K0 K8; Screwe RB  Type No.  機種 No. 1
PMOV D6022 K0 Y1820 K0 K8; Screwe RB  PLT No.  ﾊﾟﾚｯﾄ No. 1
LD T1001                 ; DOOR LOCK SIGNAL
LD X1001                 ; ﾛﾎﾞｯﾄ運転中
AND X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
ORL
OUT Y1800                ; ｽｷｯﾌﾟ停止 (全ﾀｽｸ)
LDB M6903                ; ﾕﾆｯﾄ6起動OFF
LD T1001                 ; DOOR LOCK SIGNAL
LD X1001                 ; ﾛﾎﾞｯﾄ運転中
AND X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
ORL
ANL
OUT Y1802                ; 瞬時停止 (全ﾀｽｸ)
LD M6750                 ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
OR M6751                 ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
OR M6752                 ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
OR M6753                 ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
OR M6754                 ; ﾈｼﾞ締めRB PRG. ｽﾀｰﾄ
OUT Y1805                ; ﾌﾟﾛｸﾞﾗﾑｽﾀｰﾄ
LD M6100                 ; 停止指令
OR T640                  ; ﾈｼﾞ締めｽﾀｰﾄ
OUT Y1831                ; ﾈｼﾞｼﾒ 　ｽﾀｰﾄ
LD M6171                 ; ﾈｼﾞ取出可ON
OUT Y1833                ; ｸｲｯﾁｬｰ　ﾈｼﾞ取出OK
LD M6102                 ; ﾈｼﾞ締め続行ﾌﾗｸﾞ
OUT Y1835                ; 続行 FLG
;ﾊﾞﾙﾌﾞ
;Quicher cylinder
LDP M6170                ; ﾈｼﾞ到着ｴｽｹｰﾌﾟ送り
ORP M6601                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
ORP M6612                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟ切出側
ANB M6801                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側補助
OUT M6800                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側補助
LDP M6161                ; ｴｽｹｰﾌﾟ戻り
ORP M6181                ; ｴｽｹｰﾌﾟ戻り
ORP M6602                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟorg側
ORP M6611                ; 手動：ｸｲｯﾁｬｴｽｹｰﾌﾟorg側
ANB M6800                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側補助
OUT M6801                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側補助
LD M6800                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側補助
OR Y2462                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側
ANB M6801                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側補助
OUT Y2462                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側
LD M6801                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側補助
OR Y2463                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側
ANB M6800                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側補助
OUT Y2463                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側
;Controlled AC100V
LD M32                   ; 自動ﾓｰﾄﾞ
AND M6905                ; ﾕﾆｯﾄ6起動中
OR M6600                 ; Quicher power ON
OUT Y246F                ; AC100電源ON
;<h1/>異常検出
;
LD CR2002                ; 常時ON
MPS
LD M6503                 ; 動作完了待ち
AND X106C                ; ﾈｼﾞ排出ﾐｽ
LDB B05C0                ; 　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C0                 ; 原点復帰時ﾈｼﾞ排出異常
ORL
ANL
OUT B0C0                 ; 原点復帰時ﾈｼﾞ排出異常
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C0                 ; 　BZ停止
AND B0C0                 ; 原点復帰時ﾈｼﾞ排出異常
ANL
OUT B05C0                ; 　BZ停止
LD CR2002                ; 常時ON
MPS
LD M6503                 ; 動作完了待ち
AND X1067                ; 作業範囲外ﾌﾗｸﾞ
LDB B05C1                ; 　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C1                 ; 原点復帰ｴﾘｱ外
ORL
ANL
OUT B0C1                 ; 原点復帰ｴﾘｱ外
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C1                 ; 　BZ停止
AND B0C1                 ; 原点復帰ｴﾘｱ外
ANL
OUT B05C1                ; 　BZ停止
LD M7850                 ; 排出1ﾈｼﾞ締め要求
OR M8850                 ; 排出2ﾈｼﾞ締め要求
ANB M6700                ; 排出1側ﾈｼﾞ締めOK
ANB M6701                ; 排出1側ﾈｼﾞ締めNG
ANB M6710                ; 排出2側ﾈｼﾞ締めOK
ANB M6711                ; 排出2側ﾈｼﾞ締めNG
AND M6913                ; UNIT6     ｴﾗｰ・PAUSE無し
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
ANB X1068                ; ﾈｼﾞ締め動作開始ﾌﾗｸﾞ
ANB M27                  ; ねじ締めRB無視
TMR K611 K300            ; ﾈｼﾞ締めRB応答待ち時間
LD CR2002                ; 常時ON
MPS
LD T611                  ; ﾈｼﾞ締めRB応答待ち時間
LDB B05C2                ; ﾈｼﾞ締めRB応答待ち時間　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C2                 ; ﾈｼﾞ締めRB応答待ち時間
ORL
ANL
OUT B0C2                 ; ﾈｼﾞ締めRB応答待ち時間
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C2                 ; ﾈｼﾞ締めRB応答待ち時間　BZ停止
AND B0C2                 ; ﾈｼﾞ締めRB応答待ち時間
ANL
OUT B05C2                ; ﾈｼﾞ締めRB応答待ち時間　BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05C3                ; 　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C3                 ; ﾕﾆｯﾄ6_異常4
ORL
ANL
OUT B0C3                 ; ﾕﾆｯﾄ6_異常4
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C3                 ; 　BZ停止
AND B0C3                 ; ﾕﾆｯﾄ6_異常4
ANL
OUT B05C3                ; 　BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05C4                ; 　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C4                 ; ﾕﾆｯﾄ6_異常5
ORL
ANL
OUT B0C4                 ; ﾕﾆｯﾄ6_異常5
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C4                 ; 　BZ停止
AND B0C4                 ; ﾕﾆｯﾄ6_異常5
ANL
OUT B05C4                ; 　BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05C5                ; 　BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C5                 ; ﾕﾆｯﾄ6_異常6
ORL
ANL
OUT B0C5                 ; ﾕﾆｯﾄ6_異常6
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05C5                 ; 　BZ停止
AND B0C5                 ; ﾕﾆｯﾄ6_異常6
ANL
OUT B05C5                ; 　BZ停止
;異常ﾌﾞｻﾞｰまとめ
LD B0C0                  ; 原点復帰時ﾈｼﾞ排出異常
ANB B05C0                ; 　BZ停止
LD B0C1                  ; 原点復帰ｴﾘｱ外
ANB B05C1                ; 　BZ停止
ORL
LD B0C2                  ; ﾈｼﾞ締めRB応答待ち時間
ANB B05C2                ; ﾈｼﾞ締めRB応答待ち時間　BZ停止
ORL
LD B0C3                  ; ﾕﾆｯﾄ6_異常4
ANB B05C3                ; 　BZ停止
ORL
LD B0C4                  ; ﾕﾆｯﾄ6_異常5
ANB B05C4                ; 　BZ停止
ORL
LD B0C5                  ; ﾕﾆｯﾄ6_異常6
ANB B05C5                ; 　BZ停止
ORL
OUT M6909                ; ﾕﾆｯﾄ6異常BZ補助
;ｸｲｯﾁｬ　ｼﾘﾝﾀﾞ動作監視
;Quicher cylinder check
LD Y2462                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側
ANB T1601                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟ切出側端
TMR K652 K50             ; ｸｲｯﾁｬ切り出し側監視時間
CON
AND T652                 ; ｸｲｯﾁｬ切り出し側監視時間
AND T131                 ; UT6 7 8   ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K432 D148             ; 警報No.
CON
SET M6932                ; Err432:ｸｲｯﾁｬｼﾘﾝﾀﾞｴﾗｰ(切出側)
LD Y2463                 ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側
ANB T1602                ; ﾈｼﾞ供給ｴｽｹｰﾌﾟORG側端
TMR K653 K50             ; ｸｲｯﾁｬ原点側監視時間
CON
AND T653                 ; ｸｲｯﾁｬ原点側監視時間
AND T131                 ; UT6 7 8   ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K433 D148             ; 警報No.
CON
SET M6933                ; Err433:ｸｲｯﾁｬｼﾘﾝﾀﾞｴﾗｰ(原点側)
;ねじ締めRB警告
;Screw RB warning
LDB M27                  ; ねじ締めRB無視
ANB M6640                ; Manual Inable OFF
AND X100E                ; ﾛﾎﾞｯﾄ警告
AND=.D D32044 K0         ; 他局通信状態(1-16)
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
AND<> D12008 K30         ; 切り替え画面No.
DW K420 D148             ; 警報No.
CON
SET M6920                ; Err420:ねじ締めRB警告
;ﾈｼﾞ残量低下監視
;起動時に残量低下がONして所定の時間経過で残量低下ｵﾍﾟｺｰﾙ
;ﾈｼﾞ供給後に供給完了PB_ONで解除
LDB M27                  ; ねじ締めRB無視
ANB M39                  ; 日常点検ﾓｰﾄﾞ
AND M83                  ; 各工程起動指令
MPS
AND T1608                ; ﾈｼﾞ供給ﾈｼﾞ残量確認
TMR K651 K100            ; ﾈｼﾞ供給残量確認
MPP
LD T651                  ; ﾈｼﾞ供給残量確認
OR M6200                 ; ﾈｼﾞ残量低下OP_CALL
ANL
MPS
ANB M6201                ; ﾈｼﾞ供給確認
OUT M6200                ; ﾈｼﾞ残量低下OP_CALL
MRD
LDP T1611                ; ﾈｼﾞ不足確認ｽｲｯﾁ
ANB T1608                ; ﾈｼﾞ供給ﾈｼﾞ残量確認
ANL
OUT M6201                ; ﾈｼﾞ供給確認
MRD
LDP T1611                ; ﾈｼﾞ不足確認ｽｲｯﾁ
ORP M15                  ; ﾌﾞｻﾞｰ停止PB
AND T1608                ; ﾈｼﾞ供給ﾈｼﾞ残量確認
ANL
OUT M6202                ; 作業者確認
MPP
ANB M6202                ; 作業者確認
TMR K654 K300            ; ﾈｼﾞ不足再警告ｳｪｲﾄ
LDP M6200                ; ﾈｼﾞ残量低下OP_CALL
ORP T654                 ; ﾈｼﾞ不足再警告ｳｪｲﾄ
OR M6203                 ; ねじ不足BZ on
ANPB T1611               ; ﾈｼﾞ不足確認ｽｲｯﾁ
ANPB M15                 ; ﾌﾞｻﾞｰ停止PB
OUT M6203                ; ねじ不足BZ on
END
ENDH
