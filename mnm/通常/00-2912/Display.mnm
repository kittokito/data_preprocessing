DEVICE:53
;MODULE:Display
;MODULE_TYPE:0
;ﾌﾘｯｶ
LD CR2002                ; 常時ON
FLIK K30 K30 @M0         ; 0.3sﾌﾘｯｶ
FLIK K5 K145 @M1         ; Door open 音程
FLIK K10 K10 @M2         ; 0.1sﾌﾘｯｶ
LD M68                   ; 異常ﾌﾞｻﾞｰ
LD M65                   ; 即停止異常(緊急停止)
OR M66                   ; 主幹異常(ｻｲｸﾙ停止)
ANL
LD M113                  ; 警報ﾌﾞｻﾞｰON
OR M114                  ; ｴﾗｰﾌﾞｻﾞｰON
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
LD M2961                 ; NG処理確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OR T901                  ; 自動起動BZ
LDB M131                 ; 安全扉正常
ORB M136                 ; 背面扉正常
AND @M1                  ; Door open 音程
ORL
LD T114                  ; 日常点検治具取外し催促
OR T115                  ; 日常点検治具取外し催促
OR T116                  ; 日常点検治具取外し催促
OR T314                  ; 日常点検治具取外し催促
OR T315                  ; 日常点検治具取外し催促
OR T316                  ; 日常点検治具取外し催促
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD T140                  ; UNIT1_SMA431一時停止超過
OR T240                  ; RCA1一時停止超過
OR T340                  ; UNIT3_SMA441一時停止超過
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
AND T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
ORL
OUT Y200                 ; BUZZER1(Front)
LDB M113                 ; 警報ﾌﾞｻﾞｰON
ANB M114                 ; ｴﾗｰﾌﾞｻﾞｰON
ANB T2000                ; 排出CV1取出催促
ANB T2001                ; 排出CV2取出催促
OUT M117                 ; 異常・催促なし
LD M68                   ; 異常ﾌﾞｻﾞｰ
LD M65                   ; 即停止異常(緊急停止)
OR M66                   ; 主幹異常(ｻｲｸﾙ停止)
ANL
LD M113                  ; 警報ﾌﾞｻﾞｰON
OR M114                  ; ｴﾗｰﾌﾞｻﾞｰON
OR M6203                 ; ねじ不足BZ on
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
OR T901                  ; 自動起動BZ
LD T2000                 ; 排出CV1取出催促
OR T2001                 ; 排出CV2取出催促
LD M6200                 ; ﾈｼﾞ残量低下OP_CALL
ANB M6203                ; ねじ不足BZ on
ORL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LDB M131                 ; 安全扉正常
ORB M136                 ; 背面扉正常
AND M117                 ; 異常・催促なし
AND @M1                  ; Door open 音程
ORL
OUT Y2465                ; BUZZER2 (Rear)
;<h1/>Switch Lamp
LDB M39                  ; 日常点検ﾓｰﾄﾞ
AND M1015                ; Case投入依頼
LDB M38                  ; 数合ﾓｰﾄﾞ
LD M38                   ; 数合ﾓｰﾄﾞ
LDB T1300                ; 投入1_ﾜｰｸ有
ORB M1950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
LD T1300                 ; 投入1_ﾜｰｸ有
AND M1950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
ANL
ORL
ANL
LD M44                   ; 接着剤塗布日常点検
AND M1404                ; 在荷確認
ANB B2055                ; DC_Y2 select
ORL
OUT Y23A                 ; 投入1_投入許可ﾗﾝﾌﾟ(G)
LD M1310                 ; 治具取り外し待ち
OR M1423                 ; 治具取り外し確認
OR M1012                 ; 日常点検？判断
OUT Y23B                 ; 投入1_投入許可ﾗﾝﾌﾟ(R)
LDB M38                  ; 数合ﾓｰﾄﾞ
LD M38                   ; 数合ﾓｰﾄﾞ
LDB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND>= D160 D162          ; Case投入数  PCB投入数
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND> D160 D162           ; Case投入数  PCB投入数
ORL
ANL
LDB T1400                ; 投入2_ﾜｰｸ有
ORB M2950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
LD T1400                 ; 投入2_ﾜｰｸ有
AND M2950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
ANL
ORL
AND M2012                ; Cover投入依頼
OUT Y23C                 ; 投入2_投入許可ﾗﾝﾌﾟ(G)
LD CR2003                ; 常時OFF
OUT Y23D                 ; 投入2_投入許可ﾗﾝﾌﾟ(R)
LDB M39                  ; 日常点検ﾓｰﾄﾞ
AND M3015                ; Case投入依頼
LDB M38                  ; 数合ﾓｰﾄﾞ
LD M38                   ; 数合ﾓｰﾄﾞ
LDB T1500                ; 投入3_ﾜｰｸ有無
ORB M3950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
LD T1500                 ; 投入3_ﾜｰｸ有無
AND M3950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
ANL
ORL
ANL
LD M44                   ; 接着剤塗布日常点検
AND M3404                ; 在荷確認
AND B2055                ; DC_Y2 select
ORL
OUT Y23E                 ; 投入3_投入許可ﾗﾝﾌﾟ(G)
LD M3310                 ; 治具取り外し待ち
OR M3423                 ; 日常点検治具Check
OR M3012                 ; 日常点検Case check
OUT Y23F                 ; 投入3_投入許可ﾗﾝﾌﾟ(R)
LD CR2002                ; 常時ON
MPS
LD<>.D D1100 K0          ; UT1ｴﾗｰ判別
AND @M0                  ; 0.3sﾌﾘｯｶ
ANL
OUT Y55                  ; 投入1_NG解除ｽｲｯﾁﾗﾝﾌﾟ
MRD
LD<>.D D2100 K0          ; UT2ｴﾗｰ判別
AND @M0                  ; 0.3sﾌﾘｯｶ
LD M2961                 ; NG処理確認
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT Y56                  ; 投入2_NG解除ｽｲｯﾁﾗﾝﾌﾟ
MPP
LD<>.D D3100 K0          ; UT3ｴﾗｰ判別
AND @M0                  ; 0.3sﾌﾘｯｶ
ANL
OUT Y57                  ; 投入3_NG解除ｽｲｯﾁﾗﾝﾌﾟ
LD T1610                 ; ﾈｼﾞ詰まり確認ｽｲｯﾁ
LD M75                   ; ﾕﾆｯﾄ6.7.8 Error PAUSE
AND<>.D D6102 K0         ; UT6_排出停止 ｴﾗｰ判別
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y2466                ; ﾈｼﾞ詰まりｽｲｯﾁﾗﾝﾌﾟ(R)
LD T1611                 ; ﾈｼﾞ不足確認ｽｲｯﾁ
LD M6200                 ; ﾈｼﾞ残量低下OP_CALL
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y2467                ; ﾈｼﾞ不足ｽｲｯﾁﾗﾝﾌﾟ(Y)
LD CR2003                ; 常時OFF
AND M7913                ; UNIT7     ｴﾗｰ・PAUSE無し
ANB T2000                ; 排出CV1取出催促
LD T2000                 ; 排出CV1取出催促
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y2468                ; 除材1_除材要求ﾗﾝﾌﾟ(G)
LD M7930                 ; Err450:除材1ﾈｼﾞ締め異常
AND @M0                  ; 0.3sﾌﾘｯｶ
OUT Y246B                ; 除材1_除材要求ﾗﾝﾌﾟ(R)
LD CR2003                ; 常時OFF
AND M8913                ; UNIT8     ｴﾗｰ・PAUSE無し
ANB T2001                ; 排出CV2取出催促
LD T2001                 ; 排出CV2取出催促
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y246C                ; 除材2_除材要求ﾗﾝﾌﾟ(G)
LD M8930                 ; Err470:除材2ﾈｼﾞ締め異常
AND @M0                  ; 0.3sﾌﾘｯｶ
OUT Y246E                ; 除材2_除材要求ﾗﾝﾌﾟ(R)
LD M7590                 ; 除材1ﾁｬｯｸ強制開P1移動
OUT Y246A                ; 除材1_ﾁｬｯｸ開ｽｲｯﾁﾗﾝﾌﾟ
LD M8590                 ; 除材2ﾁｬｯｸ強制開P1移動
OUT Y2464                ; 除材2_ﾁｬｯｸ開ｽｲｯﾁﾗﾝﾌﾟ
LD T1702                 ; 除材1_NG解除ｽｲｯﾁ
LD M75                   ; ﾕﾆｯﾄ6.7.8 Error PAUSE
AND<>.D D7102 K0         ; UT7_排出停止 ｴﾗｰ判別
AND M136                 ; 背面扉正常
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y2469                ; 除材1_NG解除ｽｲｯﾁﾗﾝﾌﾟ
LD T1710                 ; 除材2_NG解除ｽｲｯﾁ
LD M75                   ; ﾕﾆｯﾄ6.7.8 Error PAUSE
AND<>.D D8102 K0         ; UT8_排出停止 ｴﾗｰ判別
AND M136                 ; 背面扉正常
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y246D                ; 除材2_NG解除ｽｲｯﾁﾗﾝﾌﾟ
LD M5590                 ; 嵌合ﾁｬｯｸ 強制開移動
OUT Y201                 ; 嵌合ﾁｬｯｸ開ｽｲｯﾁﾗﾝﾌﾟ
;<h1/>Bond pump operation box
LD T1800                 ; 接着剤P_電源ｽｲｯﾁ
OUT Y2500                ; 接着剤P_電源ｽｲｯﾁlamp
LD Y2510                 ; P切替_自動三方ﾊﾞﾙﾌﾞA
AND M9703                ; P2運転
LD Y2511                 ; P切替_自動三方ﾊﾞﾙﾌﾞB
AND M9702                ; P1運転
ORL
ANB M81                  ; 自動運転開始BZ
OR Y2501                 ; 接着剤P_切替ｽｲｯﾁlamp
AND T1801                ; 接着剤P_切替ｽｲｯﾁ
OUT Y2501                ; 接着剤P_切替ｽｲｯﾁlamp
LD M9702                 ; P1運転
LD T1800                 ; 接着剤P_電源ｽｲｯﾁ
AND L33                  ; P1 捨打ち運転記憶
LDB L36                  ; P1 残量警告記憶
LD L36                   ; P1 残量警告記憶
AND @M2                  ; 0.1sﾌﾘｯｶ
ORL
ANL
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y2502                ; 接着剤P1_PUMP運転ｽｲｯﾁlamp
LDB M9702                ; P1運転
ANB L33                  ; P1 捨打ち運転記憶
OUT Y2503                ; 接着剤P1_PUMP停止ｽｲｯﾁlamp
LD Y2514                 ; 接着剤P1_ﾗﾑ駆動ｼﾘﾝﾀﾞ上昇
OUT Y2504                ; 接着剤P1_ﾗﾑ上昇ｽｲｯﾁlamp
LD Y2513                 ; 接着剤P1_ﾗﾑ駆動ｼﾘﾝﾀﾞ下降
OUT Y2505                ; 接着剤P1_ﾗﾑ下降ｽｲｯﾁlamp
LD T1806                 ; 接着剤P1_缶内ｴｱｽｲｯﾁ
OUT Y2506                ; 接着剤P1_缶内ｴｱｽｲｯﾁlamp
LD M9703                 ; P2運転
LD T1800                 ; 接着剤P_電源ｽｲｯﾁ
AND L38                  ; P2 捨打ち運転記憶
LDB L41                  ; P2 残量警告記憶
LD L41                   ; P2 残量警告記憶
AND @M2                  ; 0.1sﾌﾘｯｶ
ORL
ANL
AND @M0                  ; 0.3sﾌﾘｯｶ
ORL
OUT Y250A                ; 接着剤P2_PUMP運転ｽｲｯﾁlamp
LDB M9703                ; P2運転
ANB L38                  ; P2 捨打ち運転記憶
OUT Y250B                ; 接着剤P2_PUMP停止ｽｲｯﾁlamp
LD Y2518                 ; 接着剤P2_ﾗﾑ駆動ｼﾘﾝﾀﾞ上昇
OUT Y250C                ; 接着剤P2_ﾗﾑ上昇ｽｲｯﾁlamp
LD Y2517                 ; 接着剤P2_ﾗﾑ駆動ｼﾘﾝﾀﾞ下降
OUT Y250D                ; 接着剤P2_ﾗﾑ下降ｽｲｯﾁlamp
LD T1814                 ; 接着剤P2_缶内ｴｱｽｲｯﾁ
OUT Y250E                ; 接着剤P2_缶内ｴｱｽｲｯﾁlamp
;<h1/>Andon
LD M9702                 ; P1運転
AND Y2513                ; 接着剤P1_ﾗﾑ駆動ｼﾘﾝﾀﾞ下降
AND T1906                ; 接着剤P1_手動三方ﾊﾞﾙﾌﾞA
MPS
AND T1900                ; P切替_自動三方ﾊﾞﾙﾌﾞA
OUT Y251A                ; あんどん_P1_Operation
MPP
AND T1901                ; P切替_自動三方ﾊﾞﾙﾌﾞB
OUT Y251B                ; あんどん_P1_Ready
LD T1                    ; 電源投入0N後13秒ﾃﾞｨﾚｲ
ANB T1904                ; 接着剤P1_残量予告
OR M4840                 ; P1接着剤残量予告記憶
AND @M0                  ; 0.3sﾌﾘｯｶ
LD T1                    ; 電源投入0N後13秒ﾃﾞｨﾚｲ
ANB T1905                ; 接着剤P1_残量警告
ORL
OUT Y251C                ; あんどん_P1_Empty
LD M9703                 ; P2運転
AND Y2517                ; 接着剤P2_ﾗﾑ駆動ｼﾘﾝﾀﾞ下降
AND T1914                ; 接着剤P2_手動三方ﾊﾞﾙﾌﾞA
MPS
AND T1901                ; P切替_自動三方ﾊﾞﾙﾌﾞB
OUT Y251D                ; あんどん_P2_Operation
MPP
AND T1900                ; P切替_自動三方ﾊﾞﾙﾌﾞA
OUT Y251E                ; あんどん_P2_Ready
LD T1                    ; 電源投入0N後13秒ﾃﾞｨﾚｲ
ANB T1912                ; 接着剤P2_残量予告
OR M4841                 ; P2接着剤残量予告記憶
AND @M0                  ; 0.3sﾌﾘｯｶ
LD T1                    ; 電源投入0N後13秒ﾃﾞｨﾚｲ
ANB T1913                ; 接着剤P2_残量警告
ORL
OUT Y251F                ; あんどん_P2_Empty
END
ENDH
