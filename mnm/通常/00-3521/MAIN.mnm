DEVICE:53
;MODULE:MAIN
;MODULE_TYPE:0
;□□　　ダミー接点　　□□
LD CR2003                ; 常時OFF
OUT M0                   ; 常時OFF
LD CR3905                ; 重度ｴﾗｰ発生中
INV
OUT M1                   ; CPU正常
LD CR2008                ; 運転開始時1ｽｷｬﾝON
OUT M2                   ; 1ｽｷｬﾝON
LD CR2002                ; 常時ON
ANB T2                   ; 0.3Secｸﾛｯｸ
TMH K1 K30               ; 0.3Secｸﾛｯｸ
LD T1                    ; 0.3Secｸﾛｯｸ
TMH K2 K30               ; 0.3Secｸﾛｯｸ
LD CR2002                ; 常時ON
ANB T5                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
TMH K4 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
LD T4                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
TMH K5 K25               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
LD CR2002                ; 常時ON
ANB T7                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
TMH K6 K40               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
LD T6                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
TMH K7 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
LD CR2002                ; 常時ON
ANB T9                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
TMR K8 K10               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
LD T8                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
TMR K9 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
LD CR2002                ; 常時ON
ANB T37                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
TMR K36 K100             ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
LD T36                   ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
TMR K37 K5               ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
LD CR2002                ; 常時ON
ANB T39                  ; ﾜｰｸ投入可音程ﾀｲﾏ1
TMH K38 K30              ; ﾜｰｸ投入可音程ﾀｲﾏ
LD T38                   ; ﾜｰｸ投入可音程ﾀｲﾏ
TMH K39 K100             ; ﾜｰｸ投入可音程ﾀｲﾏ1
LD X04                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT1
TMR K60 K5               ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
;<h1/>□□　安全関連入力受け　□□
;安全系の入力は一旦ここで受ける。
LD X03                   ; 圧空元圧正常
OUT M120                 ; 圧空正常
LD X01                   ; 非常停止正常
OUT M121                 ; 非常停止正常
LD CR2002                ; 常時ON
OUT M122                 ; 外部非常停止正常
LD X02                   ; 安全扉正常
OUT M123                 ; 安全扉正常
LD X04                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT1
OUT M124                 ; ｴﾘｱｾﾝｻ正常
;<h1/>□□　スイッチ変換　□□
;操作系の入力は一旦ここで受ける。
LD B1016                 ; DRY_RUN_PB
OUT M7                   ; 空運転PB
LD B1001                 ; 自動/手動PB
OUT M10                  ; 自動/手動PB
;今回は運転準備PB無し装置電源入で常時ON
LD X00                   ; 装置電源入
OUT M12                  ; 運転準備入PB
LD CR2003                ; 常時OFF
OUT M13                  ; 運転準備切PB
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
OUT M15                  ; ﾌﾞｻﾞｰ停止PB
LD B1011                 ; ｴﾗｰﾘｾｯﾄPB
OUT M16                  ; ﾘｾｯﾄPB
LD B1000                 ; 原点復帰PB
OUT M17                  ; 原点復帰PB
LD B1002                 ; ｽﾀｰﾄPB
AND X02                  ; 安全扉正常
OUT M18                  ; 起動PB
LD B1003                 ; ｻｲｸﾙ停止PB
TMR K15 K50              ; 強制停止
OUT M19                  ; 停止PB
;用途に応じて使用する
LD CR2003                ; 常時OFF
MPS
TMR K16 K20              ; 終了PB_ONﾃﾞｨﾚｲ
MPP
AND T16                  ; 終了PB_ONﾃﾞｨﾚｲ
OUT M20                  ; 終了PB
LD CR2003                ; 常時OFF
MPS
TMR K17 K10              ; 払出PB_ONﾃﾞｨﾚｲ
MPP
AND T17                  ; 払出PB_ONﾃﾞｨﾚｲ
OUT M21                  ; 払出PB
;<h1/>□□　　各モード選択　　□□
;自動・手動モード
LDP M10                  ; 自動/手動PB
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M30                  ; 自動ﾓｰﾄﾞ
MEP
LD M30                   ; 自動ﾓｰﾄﾞ
ANPB M31                 ; 手動ﾓｰﾄﾞ
ORL
OUT M30                  ; 自動ﾓｰﾄﾞ
LDP M10                  ; 自動/手動PB
ANB M31                  ; 手動ﾓｰﾄﾞ
ANB M41                  ; 全ﾕﾆｯﾄ起動中
MEP
OR T49                   ; 自動終了時手動移行確認
OR CR2008                ; 運転開始時1ｽｷｬﾝON
LD M31                   ; 手動ﾓｰﾄﾞ
ANPB M30                 ; 自動ﾓｰﾄﾞ
ORL
ANB M71                  ; 自動運転開始BZ
OUT M31                  ; 手動ﾓｰﾄﾞ
LDB M40                  ; 全ﾕﾆｯﾄ原点
ANB M71                  ; 自動運転開始BZ
ORB M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M41                  ; 全ﾕﾆｯﾄ起動中
TMR K49 K10              ; 自動終了時手動移行確認
;この手動操作可条件はメインのみの条件
;ユニット毎に必要な条件は各ユニットへ追加する
LDB M72                  ; 自動運転開始
AND M120                 ; 圧空正常
AND M121                 ; 非常停止正常
AND M122                 ; 外部非常停止正常
LD M123                  ; 安全扉正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ANL
LD M124                  ; ｴﾘｱｾﾝｻ正常
OR CR2002                ; 常時ON
ANL
OUT M32                  ; ﾒｲﾝ手動操作可能条件
LD M40                   ; 全ﾕﾆｯﾄ原点
AND M2499                ; ｺｰﾃｨﾝｸﾞ部準備OK
OUT M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
;空運転､払出モードは必要に応じて使用
LD M7                    ; 空運転PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K34 K50              ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANP T34                  ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LD M35                   ; 空運転ﾓｰﾄﾞ
ANPB T34                 ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
ORL
OUT M35                  ; 空運転ﾓｰﾄﾞ
LDP M19                  ; 停止PB
OR M59                   ; 払出停止異常まとめ
OR M13151                ; PC→自動運転停止ﾌﾗｸﾞ
ANB M130                 ; 一時停止
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M36                   ; 払出ﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M41                  ; 全ﾕﾆｯﾄ起動中
OUT M36                  ; 払出ﾓｰﾄﾞ
;ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD B1015                 ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙPB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
ORL
TMR K35 K50              ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
LDP T35                  ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LDPB T35                 ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
AND M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M41                  ; 全ﾕﾆｯﾄ起動中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
OUT M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
;<h1/>□□　　装置状態集約　　□□
;■装置原点
LD M1480                 ; ﾕﾆｯﾄ1原点
AND M1980                ; ﾕﾆｯﾄ2原点
AND M2480                ; ﾕﾆｯﾄ3原点
AND M2980                ; ﾕﾆｯﾄ4原点
AND M3480                ; ﾕﾆｯﾄ5原点
AND M3980                ; ﾕﾆｯﾄ6原点
AND M4480                ; ﾕﾆｯﾄ7原点
AND M4980                ; ﾕﾆｯﾄ8原点
AND M5480                ; ﾕﾆｯﾄ9原点
AND M88                  ; 基板、ﾗｯｸ排出中記憶無し確認
AND M90                  ; 基板有無記憶　無し状態
OUT M40                  ; 全ﾕﾆｯﾄ原点
;■起動中
LDB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M2485                ; ﾕﾆｯﾄ3起動中
ANB M2985                ; ﾕﾆｯﾄ4起動中
ANB M3485                ; ﾕﾆｯﾄ5起動中
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M4485                ; ﾕﾆｯﾄ7起動中
ANB M4985                ; ﾕﾆｯﾄ8起動中
ANB M5485                ; ﾕﾆｯﾄ9起動中
INV
OUT M41                  ; 全ﾕﾆｯﾄ起動中
;■原点復帰中
LDB M1484                ; ﾕﾆｯﾄ1原点復帰中
ANB M1984                ; ﾕﾆｯﾄ2原点復帰中
ANB M2484                ; ﾕﾆｯﾄ3原点復帰中
ANB M2984                ; ﾕﾆｯﾄ4原点復帰中
ANB M3484                ; ﾕﾆｯﾄ5原点復帰中
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
ANB M4484                ; ﾕﾆｯﾄ7原点復帰中
ANB M4984                ; ﾕﾆｯﾄ8原点復帰中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
INV
OUT M42                  ; 全ﾕﾆｯﾄ原点復帰中
;■ﾛﾎﾞｯﾄ準備完了
LD M1490                 ; ﾕﾆｯﾄ1機器準備完了
AND M1990                ; ﾕﾆｯﾄ2機器準備完了
AND M2490                ; ﾕﾆｯﾄ3機器準備完了
AND M2990                ; ﾕﾆｯﾄ4機器準備完了
AND M3490                ; ﾕﾆｯﾄ5機器準備完了
AND M3990                ; ﾕﾆｯﾄ6機器準備完了
AND M4490                ; ﾕﾆｯﾄ7機器準備完了
AND M4990                ; ﾕﾆｯﾄ8機器準備完了
AND M5490                ; ﾕﾆｯﾄ9機器準備完了
OUT M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
;付帯装置起動条件
LD CR2002                ; 常時ON
AND CR2002               ; 常時ON
AND CR2002               ; 常時ON
AND CR2002               ; 常時ON
OUT M44                  ; 付帯装置準備OK
;<h1/>■運転準備
LD CR2007                ; 運転開始時1ｽｷｬﾝOFF
TMR K0 K20               ; 電源投入0Nﾃﾞｨﾚｲ
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
AND M12                  ; 運転準備入PB
OR M5                    ; 運転準備補助
LDB M13                  ; 運転準備切PB
OR M41                   ; 全ﾕﾆｯﾄ起動中
ANL
ANB M51                  ; 即停止異常(緊急停止)
OUT M5                   ; 運転準備補助
LD M5                    ; 運転準備補助
TMR K10 K50              ; 運転準備ﾃﾞｨﾚｲ
OUT M50                  ; 運転準備
LD M50                   ; 運転準備
@MOV K1 D58              ; ﾒｲﾝ画面ﾍﾟｰｼﾞ切替
LD M50                   ; 運転準備
AND X1317                ; ｻﾌﾞTP接続SW　ON
TMR K72 K30              ; ｻﾌﾞTP画面切替えﾃﾞｨﾚｰ
CON
AND T72                  ; ｻﾌﾞTP画面切替えﾃﾞｨﾚｰ
@MOV K1 D88
;<h1/>□□　　異常検出まとめ　　□□
;■即停止異常(全ﾕﾆｯﾄ即停止)
LDB B01                  ; 非常停止異常
ANB B02                  ; 圧空低下異常
ANB B03                  ; 安全扉開異常
ANB B04                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ANB B05                  ; 外部非常停止
ANB B06                  ; PLC異常
ANB B07                  ; ﾘﾝｸ異常
ANB M58                  ; 非常停止異常まとめ
ANB M61                  ; PC Error　ON　非常停止
ANB B010                 ; ｻﾌﾞTP非常停止SW　ON異常
ANB B011                 ; MAIN TP非常停止SW　ON異常
ANB B012                 ; ﾜｰｸ投入非常停止SW　ON異常
ANB B014                 ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常
ANB B015                 ; 乾燥炉非常停止SW　ON異常
ANB B016                 ; 排出部非常停止SW　ON異常
ANB B0A7                 ; RB8移動中収納ﾗｯｸ基板はみだし異常
ANB B0ED                 ; RB9移動中排出ﾗｯｸ基板はみだし異常
INV
OUT M51                  ; 即停止異常(緊急停止)
LD W00.1                 ; PC Error
ANB M2985                ; ﾕﾆｯﾄ4起動中
OUT M61                  ; PC Error　ON　非常停止
;■ﾕﾆｯﾄ異常検出(該当ﾕﾆｯﾄ以外ｻｲｸﾙ停止)
LDB M1486                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
ANB M1986                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ANB M2486                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
ANB M2986                ; ﾕﾆｯﾄ4ｻｲｸﾙ停止異常
ANB M3486                ; ﾕﾆｯﾄ5ｻｲｸﾙ停止異常
ANB M3986                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
ANB M4486                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止異常
ANB M4986                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止異常
ANB M5486                ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
INV
OUT M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
;■ﾕﾆｯﾄ毎各種警報検出(ﾜｰｸ不足･ｵﾍﾟｺｰﾙ等)
LDB M1488                ; ﾕﾆｯﾄ1警報補助
ANB M1988                ; ﾕﾆｯﾄ2警報補助
ANB M2488                ; ﾕﾆｯﾄ3警報補助
ANB M2988                ; ﾕﾆｯﾄ4警報補助
ANB M3488                ; ﾕﾆｯﾄ5警報補助
ANB M3988                ; ﾕﾆｯﾄ6警報補助
ANB M4488                ; ﾕﾆｯﾄ7警報補助
ANB M4988                ; ﾕﾆｯﾄ8警報補助
ANB M5488                ; ﾕﾆｯﾄ9警報補助
INV
OUT M53                  ; 警報(ﾜｰｸ不足等)
;□□　　自動運転停止指令(ｻｲｸﾙ停止)　　□□
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR T15                   ; 強制停止
OR M57                   ; 自動起動停止指令(払出停止)
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M41                  ; 全ﾕﾆｯﾄ起動中
OUT M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
;□□　　払出完了運転停止指令(ｻｲｸﾙ停止)　　□□
LD M36                   ; 払出ﾓｰﾄﾞ
AND M41                  ; 全ﾕﾆｯﾄ起動中
AND M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
AND M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
AND M2993                ; ﾕﾆｯﾄ4ﾜｰｸ無し
AND M3493                ; ﾕﾆｯﾄ5ﾜｰｸ無し
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
AND M4493                ; ﾕﾆｯﾄ7ﾜｰｸ無し
AND M4993                ; ﾕﾆｯﾄ8ﾜｰｸ無し
AND M5493                ; ﾕﾆｯﾄ9ﾜｰｸ無し
AND M1492                ; ﾕﾆｯﾄ1動作無し確認
AND M1992                ; ﾕﾆｯﾄ2動作無し確認
AND M2492                ; ﾕﾆｯﾄ3動作無し確認
AND M2992                ; ﾕﾆｯﾄ4動作無し確認
AND M3492                ; ﾕﾆｯﾄ5動作無し確認
AND M3992                ; ﾕﾆｯﾄ6動作無し確認
AND M4492                ; ﾕﾆｯﾄ7動作無し確認
AND M4992                ; ﾕﾆｯﾄ8動作無し
AND M5492                ; ﾕﾆｯﾄ9動作無し確認
OUT M57                  ; 自動起動停止指令(払出停止)
;<h1/>__□ 異常検出まとめ
;緊急停止異常まとめ
LDB M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M1991                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M2491                ; ﾕﾆｯﾄ3ｴﾗｰ発生
ANB M2991                ; ﾕﾆｯﾄ4ｴﾗｰ発生
ANB M3491                ; ﾕﾆｯﾄ5ｴﾗｰ発生
ANB M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
ANB M3491                ; ﾕﾆｯﾄ5ｴﾗｰ発生
ANB M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
ANB M4491                ; ﾕﾆｯﾄ7ｴﾗｰ発生
ANB M4991                ; ﾕﾆｯﾄ8ｴﾗｰ発生
ANB M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
INV
OUT M58                  ; 非常停止異常まとめ
;払出停止異常まとめ
LDB M1487                ; ﾕﾆｯﾄ1払出停止異常
ANB M1987                ; ﾕﾆｯﾄ2払出停止異常
ANB M2487                ; ﾕﾆｯﾄ3払出停止異常
ANB M2987                ; ﾕﾆｯﾄ4払出停止異常
ANB M3487                ; ﾕﾆｯﾄ5払出停止異常
ANB M3987                ; ﾕﾆｯﾄ6払出停止異常
ANB M4487                ; ﾕﾆｯﾄ7払出停止異常
ANB M4987                ; ﾕﾆｯﾄ8払出停止異常
ANB M5487                ; ﾕﾆｯﾄ9払出停止異常
INV
OUT M59                  ; 払出停止異常まとめ
;■各ﾕﾆｯﾄｴﾗｰ検出
LDB B08                  ; PLCﾊﾞｯﾃﾘ低下
ANB B0A                  ; 原点未完ﾓｰﾄﾞ切替不可異常
ANB B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
ANB B017                 ; 自動起動時PC応答無し異常
ANB M1481                ; ﾕﾆｯﾄ1全異常
ANB M1981                ; ﾕﾆｯﾄ2全異常
ANB M2481                ; ﾕﾆｯﾄ3全異常
ANB M2981                ; ﾕﾆｯﾄ4全異常
ANB M3481                ; ﾕﾆｯﾄ5全異常
ANB M3981                ; ﾕﾆｯﾄ6全異常
ANB M4481                ; ﾕﾆｯﾄ7全異常
ANB M4981                ; ﾕﾆｯﾄ8全異常
ANB M5481                ; ﾕﾆｯﾄ9全異常
INV
OUT M60                  ; ｴﾗｰ発生
;<h1/>__□一時停止処理
;アラーム発生時に使用
LD M72                   ; 自動運転開始
OR M41                   ; 全ﾕﾆｯﾄ起動中
ANB M149                 ; 一時停止処理終了
ANB M133                 ; 一時停止　ｷｬﾝｾﾙ
MC
LDP B1012                ; 一時停止PB
OR M4091                 ; 排出ﾘﾌﾀ　ﾗｯｸ交換一時停止
OR M130                  ; 一時停止
LDB M18                  ; 起動PB
OR M4380                 ; 排出ﾘﾌﾀ　ﾗｯｸ交換排出動作中
ANB M4093                ; 排出ﾘﾌﾀ　ﾗｯｸ交換完了一時停止解除
ORB X02                  ; 安全扉正常
ANL
OUT M130                 ; 一時停止
LD M1492                 ; ﾕﾆｯﾄ1動作無し確認
AND M1992                ; ﾕﾆｯﾄ2動作無し確認
AND M2492                ; ﾕﾆｯﾄ3動作無し確認
AND M2992                ; ﾕﾆｯﾄ4動作無し確認
AND M3492                ; ﾕﾆｯﾄ5動作無し確認
AND M3992                ; ﾕﾆｯﾄ6動作無し確認
AND M4492                ; ﾕﾆｯﾄ7動作無し確認
AND M4992                ; ﾕﾆｯﾄ8動作無し
AND M5492                ; ﾕﾆｯﾄ9動作無し確認
OUB M131                 ; PACK中
LD M130                  ; 一時停止
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OR M133                  ; 一時停止　ｷｬﾝｾﾙ
MPS
LDB M133                 ; 一時停止　ｷｬﾝｾﾙ
ANB M131                 ; PACK中
ANL
OUT M132                 ; 全ﾕﾆｯﾄ停止OK
MPP
LDB M132                 ; 全ﾕﾆｯﾄ停止OK
AND M131                 ; PACK中
ANP B1002                ; ｽﾀｰﾄPB
ANB M4380                ; 排出ﾘﾌﾀ　ﾗｯｸ交換排出動作中
ANL
OUT M133                 ; 一時停止　ｷｬﾝｾﾙ
LD M132                  ; 全ﾕﾆｯﾄ停止OK
OR M140                  ; ﾄﾞｱﾛｯｸ開
ANB M141                 ; ﾄﾞｱ閉確認
OUT M140                 ; ﾄﾞｱﾛｯｸ開
LD M140                  ; ﾄﾞｱﾛｯｸ開
AND X02                  ; 安全扉正常
LDP B1002                ; ｽﾀｰﾄPB
OR M4093                 ; 排出ﾘﾌﾀ　ﾗｯｸ交換完了一時停止解除
ANL
OR M141                  ; ﾄﾞｱ閉確認
OUT M141                 ; ﾄﾞｱ閉確認
LD M141                  ; ﾄﾞｱ閉確認
AND X02                  ; 安全扉正常
AND Y51                  ; 安全扉ﾛｯｸ
OR M148                  ; 状態復帰完了
OUT M148                 ; 状態復帰完了
LD M148                  ; 状態復帰完了
OR M149                  ; 一時停止処理終了
OUT M149                 ; 一時停止処理終了
MCR
;<h1/>□□　　自動起動回路　　□□
LDB M35                  ; 空運転ﾓｰﾄﾞ
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D100 K1             ; Product-No.
ORL
AND M30                  ; 自動ﾓｰﾄﾞ
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
AND M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M44                  ; 付帯装置準備OK
AND M50                  ; 運転準備
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB W00.8                ; PC自動制御中
AND= W010 K0             ; PC　停止ﾓｰﾄﾞ
AND M2499                ; ｺｰﾃｨﾝｸﾞ部準備OK
AND M89                  ; 搬送部基板無し確認
OUT M70                  ; 自動起動条件
LD M18                   ; 起動PB
AND M70                  ; 自動起動条件
ANB M35                  ; 空運転ﾓｰﾄﾞ
OR M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB M71                  ; 自動運転開始BZ
ANB M76                  ; PCより起動不可ON
ANB B017                 ; 自動起動時PC応答無し異常
ANB B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
OUT M75                  ; 自動運転開始PCﾁｪｯｸ開始
OUT W0100.6              ; PCへ起動確認要求
LD M75                   ; 自動運転開始PCﾁｪｯｸ開始
MPS
ANB W00.6                ; PCより起動可能
MPS
AND W00.5                ; PCより起動確認完了
OUT M76                  ; PCより起動不可ON
MPP
ANB W00.5                ; PCより起動確認完了
TMR K71 K100             ; 自動運転開始PCﾁｪｯｸﾀｲﾑｵｰﾊﾞｰ
MPP
LD W00.6                 ; PCより起動可能
AND W00.5                ; PCより起動確認完了
ANL
OUT M77                  ; PCより起動可能ON
LD M18                   ; 起動PB
AND M35                  ; 空運転ﾓｰﾄﾞ
LD M77                   ; PCより起動可能ON
ANB M35                  ; 空運転ﾓｰﾄﾞ
ORL
AND M70                  ; 自動起動条件
OR M71                   ; 自動運転開始BZ
ANB M51                  ; 即停止異常(緊急停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M71                  ; 自動運転開始BZ
INV
MOV K0 D10               ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
;起動時ﾌﾞｻﾞｰ
LD M71                   ; 自動運転開始BZ
AND> K3 D10              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
MPS
ANB T21                  ; 自動起動ﾃﾞｨﾚｲ
TMR K20 K10              ; 起動前BZ_ONﾀｲﾏ
MPP
AND T20                  ; 起動前BZ_ONﾀｲﾏ
TMR K21 K5               ; 自動起動ﾃﾞｨﾚｲ
@CAL+ D10 K1 D10         ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ  起動時ﾌﾞｻﾞｰｶｳﾝﾄ
LD= D10 K3               ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M72                  ; 自動運転開始
LD M72                   ; 自動運転開始
OUT M73                  ; 主幹起動指令
;<h1/>□□　　原点復帰回路　　□□
LD= D2130 K0             ; 基板収納RB4基板排出中記憶
AND= D3130 K0            ; 乾燥炉へﾗｯｸ排出中記憶
AND= D3630 K0            ; 乾燥炉２ﾗｯｸ排出中記憶
AND= D4130 K0            ; 排出ﾌﾟｯｼｬｰRB7基板排出動作中記憶
AND= D4140 K0            ; 排出ﾘﾌﾀ ﾘﾀｰﾝCVへ排出中記憶
AND= D5130 K0            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
OUT M88                  ; 基板、ﾗｯｸ排出中記憶無し確認
LDB X134F                ; 基板投入部ﾜｰｸ確認
ANB X1350                ; 基板投入部ﾜｰｸｺﾈｸﾀ確認
ANB X1352                ; 振動吸引部ﾜｰｸ確認
ANB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
ANB X1340                ; ｺｰﾃｨﾝｸﾞ部ﾜｰｸ確認
AND X1345                ; 搬送部基板確認
OUT M89                  ; 搬送部基板無し確認
LD= D1612 K0             ; INDX2=基板有無記憶(1=有)
AND= D2112 K0            ; INDX3=基板有無記憶(1=有)
AND= D2612 K0            ; INDX4=基板有無記憶(1=有)
OUT M90                  ; 基板有無記憶　無し状態
LDB M3909                ; 乾燥炉ｽﾄｯﾊﾟｰ1ﾗｯｸ確認
OR M3906                 ; 乾燥炉ｽﾄｯﾊﾟｰ1上昇端
LDB M3910                ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ確認
OR M3908                 ; 乾燥炉ｽﾄｯﾊﾟｰ2上昇端
ANL
LDB M5405                ; ﾘﾀｰﾝCVｴｽｹｰﾌﾟ通過確認
OR M5401                 ; ﾘﾀｰﾝCVｴｽｹﾌﾟ上昇端
ANL
LDB M5406                ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ到着確認
OR M5404                 ; ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ上昇端
ANL
OUT M78                  ; 乾燥炉、ﾘﾀｰﾝCVｽﾄｯﾊﾟｰ原点復帰可
LD= D2130 K0             ; 基板収納RB4基板排出中記憶
AND= D3130 K0            ; 乾燥炉へﾗｯｸ排出中記憶
AND= D3630 K0            ; 乾燥炉２ﾗｯｸ排出中記憶
AND= D3640 K0            ; 乾燥炉内ﾗｯｸ移動中
AND= D4130 K0            ; 排出ﾌﾟｯｼｬｰRB7基板排出動作中記憶
AND= D4140 K0            ; 排出ﾘﾌﾀ ﾘﾀｰﾝCVへ排出中記憶
AND= D5130 K0            ; ﾘﾀｰﾝCVﾗｯｸ排出中記憶
AND= D5140 K0            ; ﾘﾀｰﾝCV内ﾗｯｸ移動中
OUT M79                  ; 排出動作中OFF確認
LDB M72                  ; 自動運転開始
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
AND M31                  ; 手動ﾓｰﾄﾞ
AND X02                  ; 安全扉正常
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND M88                  ; 基板、ﾗｯｸ排出中記憶無し確認
AND M79                  ; 排出動作中OFF確認
OUT M80                  ; 原点復帰可能条件
LD M17                   ; 原点復帰PB
AND M80                  ; 原点復帰可能条件
OR M81                   ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
ANB T15                  ; 強制停止
OUT M81                  ; 原点復帰動作中
MPS
TMR K22 K5               ; 原点復帰開始ﾃﾞｨﾚｲ
MPP
LD M401                  ; 扉閉ﾃﾞｨﾚｰON
AND CR2003               ; 常時OFF
ANL
TMR K23 K1200            ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LDP T22                  ; 原点復帰開始ﾃﾞｨﾚｲ
OR M82                   ; 原点復帰開始
AND M81                  ; 原点復帰動作中
ANB M84                  ; 原点復帰起動確認
OUT M82                  ; 原点復帰開始
LD M82                   ; 原点復帰開始
AND M1484                ; ﾕﾆｯﾄ1原点復帰中
AND M1984                ; ﾕﾆｯﾄ2原点復帰中
AND M2484                ; ﾕﾆｯﾄ3原点復帰中
AND M2984                ; ﾕﾆｯﾄ4原点復帰中
AND M3484                ; ﾕﾆｯﾄ5原点復帰中
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
AND M4484                ; ﾕﾆｯﾄ7原点復帰中
AND M4984                ; ﾕﾆｯﾄ8原点復帰中
AND M5484                ; ﾕﾆｯﾄ9原点復帰中
OUT M83                  ; 原点復帰全起動確認補助
LD M82                   ; 原点復帰開始
AND M83                  ; 原点復帰全起動確認補助
OR M84                   ; 原点復帰起動確認
AND M81                  ; 原点復帰動作中
ANB M85                  ; 原点復帰完了
OUT M84                  ; 原点復帰起動確認
LD M84                   ; 原点復帰起動確認
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
OR M85                   ; 原点復帰完了
AND M81                  ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
OUT M85                  ; 原点復帰完了
LD M85                   ; 原点復帰完了
AND M40                  ; 全ﾕﾆｯﾄ原点
OR M86                   ; 装置全原点確認
AND M81                  ; 原点復帰動作中
OUT M86                  ; 装置全原点確認
;<h1/>□□　　ﾌﾞｻﾞｰ補助回路　　□□
LD M54                   ; 異常ﾌﾞｻﾞｰ
MPS
ANB T30                  ; 異常ﾌﾞｻﾞｰ自動停止
OUT M100                 ; 異常ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K30 K100             ; 異常ﾌﾞｻﾞｰ自動停止
LD M53                   ; 警報(ﾜｰｸ不足等)
MPS
ANB T31                  ; 警報ﾌﾞｻﾞｰ自動停止
OUT M101                 ; 警報ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K31 K100             ; 警報ﾌﾞｻﾞｰ自動停止
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
OUT M110                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LD B1011                 ; ｴﾗｰﾘｾｯﾄPB
OUT M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
;□□　　その他補助回路　　□□
;ﾕﾆｯﾄ別動作停止、ﾕﾆｯﾄﾊﾟｽ設定､生産ｶｳﾝﾀ等装置固有の回路がある場合はここ。
;<h1/>□□　　出力回路　　□□
LD X00                   ; 装置電源入
OUT Y50                  ; 電源入表示
LD X00                   ; 装置電源入
AND M30                  ; 自動ﾓｰﾄﾞ
ANB M140                 ; ﾄﾞｱﾛｯｸ開
LD M5700                 ; 排出ﾘﾌﾀ　ﾗｯｸ交換動作中点滅
ANB M5701                ; 排出ﾘﾌﾀ ﾗｯｸ交換完了待ち点滅
ORL
LDB M5702                ; 排出ﾘﾌﾀ　ﾗｯｸ交換完了動作点滅
ANB M5703                ; ﾘﾀｰﾝCV排出中
INV
ORL
OUT Y51                  ; 安全扉ﾛｯｸ
LD M901                  ; 電源OFF
OUT Y52                  ; 主電源切ﾘﾚｰ
LD X05                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT2
INV
AND CR2003               ; 常時OFF
OUT Y53                  ; ﾗｲﾄｶｰﾃﾝ電源OFF
LD M53                   ; 警報(ﾜｰｸ不足等)
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ANB M54                  ; 異常ﾌﾞｻﾞｰ
OR M1460                 ; 28秒ﾌﾞｻﾞｰ
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M51                   ; 即停止異常(緊急停止)
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LD M60                   ; ｴﾗｰ発生
OR M59                   ; 払出停止異常まとめ
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
ANL
ORL
LDB M54                  ; 異常ﾌﾞｻﾞｰ
LD B09                   ; 安全扉開
AND M30                  ; 自動ﾓｰﾄﾞ
ANB T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND T36                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
ORL
ANL
ORL
LD M71                   ; 自動運転開始BZ
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
AND> K3 D10              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
ORL
LD M13131                ; ﾌﾞｻﾞｰON
ANB M13135               ; BZ停止
ORL
OUT Y2432                ; ﾒｲﾝﾊﾟﾈﾙBZ
LD CR2003                ; 常時OFF
TMR K45 K600             ; 投入要求ﾌﾞｻﾞｰ自動停止
;□□　　画面切替管理　　□□
;※画面切替管理は別ﾓｼﾞｭｰﾙを作成しても良い。
;<h1/>□□　　ﾀｯﾁﾊﾟﾈﾙ表示　　□□
LD M40                   ; 全ﾕﾆｯﾄ原点
ANB M81                  ; 原点復帰動作中
LD M81                   ; 原点復帰動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANB M72                  ; 自動運転開始
OUT B1500                ; 原点復帰TP-PL
LD M30                   ; 自動ﾓｰﾄﾞ
OUT B1501                ; 自動/手動TP-PL
LD M71                   ; 自動運転開始BZ
OR M41                   ; 全ﾕﾆｯﾄ起動中
LD M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB T1                   ; 0.3Secｸﾛｯｸ
ORL
OUT B1502                ; 起動TP-PL
LDB M71                  ; 自動運転開始BZ
ANB M41                  ; 全ﾕﾆｯﾄ起動中
LD M36                   ; 払出ﾓｰﾄﾞ
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M41                  ; 全ﾕﾆｯﾄ起動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1503                ; 停止TP-PL
LDB M53                  ; 警報(ﾜｰｸ不足等)
OR CR2002                ; 常時ON
ANB M54                  ; 異常ﾌﾞｻﾞｰ
INV
LD M13131                ; ﾌﾞｻﾞｰON
ANB M13135               ; BZ停止
ORL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1510                ; ﾌﾞｻﾞｰ停止PL
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
ANB M13131               ; ﾌﾞｻﾞｰON
INV
OR M3998                 ; Oven Fan回転異常一括
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1511                ; ｴﾗｰﾘｾｯﾄPL
LD M130                  ; 一時停止
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OUT B1512                ; 一時停止PL
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1515                ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙ中PL
LD M35                   ; 空運転ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1516                ; DRY_RUN_PL
LD M35                   ; 空運転ﾓｰﾄﾞ
ORB L400                 ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ有効選択記憶
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1517                ; COATING_OFF_PL
LD M35                   ; 空運転ﾓｰﾄﾞ
ORB Y54                  ; ｺｰﾃｨﾝｸﾞ剤加温ﾋｰﾀｰ電源ON
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1518                ; 温調OFF_TP-PL
;メッセージ表示
LDB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M57                  ; 自動起動停止指令(払出停止)
ANB M59                  ; 払出停止異常まとめ
MPS
LDB M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
ANB M81                  ; 原点復帰動作中
ANL
OUT B0B                  ; ｲﾆｼｬﾗｲｽﾞ中表示
MRD
LDB B0B                  ; ｲﾆｼｬﾗｲｽﾞ中表示
ANB M81                  ; 原点復帰動作中
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANL
OUT B0C                  ; 原点復帰要求
MPP
AND M81                  ; 原点復帰動作中
OUT B0D                  ; 原点復帰中
LD B0B                   ; ｲﾆｼｬﾗｲｽﾞ中表示
@DW H000B D0
CON
@FIFOW D0 D900           ; 異常内容
LD B0C                   ; 原点復帰要求
@DW H000C D0
CON
@FIFOW D0 D900           ; 異常内容
LD B0D                   ; 原点復帰中
@DW H000D D0
CON
@FIFOW D0 D900           ; 異常内容
;<h1/>□□　　主幹異常　　□□
;ﾒｲﾝ回路に関連する異常はここにまとめる。
LDB M121                 ; 非常停止正常
AND M50                  ; 運転準備
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2001                ; 非常停止異常BZ停止
AND B01                  ; 非常停止異常
ORL
OUT B01                  ; 非常停止異常
@DW K1 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2001                 ; 非常停止異常BZ停止
AND B01                  ; 非常停止異常
OUT B2001                ; 非常停止異常BZ停止
LD M50                   ; 運転準備
ANB X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2002                ; 圧空低下異常BZ停止
AND B02                  ; 圧空低下異常
ORL
MPS
TMR K50 K10              ; 圧空低下確認
MPP
AND T50                  ; 圧空低下確認
OUT B02                  ; 圧空低下異常
@DW K2 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2002                 ; 圧空低下異常BZ停止
AND B02                  ; 圧空低下異常
OUT B2002                ; 圧空低下異常BZ停止
LD Y51                   ; 安全扉ﾛｯｸ
LD M1492                 ; ﾕﾆｯﾄ1動作無し確認
AND M1992                ; ﾕﾆｯﾄ2動作無し確認
AND M2492                ; ﾕﾆｯﾄ3動作無し確認
INV
LD M2992                 ; ﾕﾆｯﾄ4動作無し確認
AND M3492                ; ﾕﾆｯﾄ5動作無し確認
AND M3992                ; ﾕﾆｯﾄ6動作無し確認
INV
ORL
LD M4492                 ; ﾕﾆｯﾄ7動作無し確認
AND M4992                ; ﾕﾆｯﾄ8動作無し
AND M5492                ; ﾕﾆｯﾄ9動作無し確認
INV
ORL
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M123                 ; 安全扉正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2003                ; 安全扉開異常BZ停止
AND B03                  ; 安全扉開異常
ORL
AND CR2003               ; 常時OFF
OUT B03                  ; 安全扉開異常
@DW K3 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2003                 ; 安全扉開異常BZ停止
AND B03                  ; 安全扉開異常
OUT B2003                ; 安全扉開異常BZ停止
LD M50                   ; 運転準備
AND T23                  ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2004                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
AND B04                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ORL
OUT B04                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
@DW K4 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2004                 ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
AND B04                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OUT B2004                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
LD M50                   ; 運転準備
ANB M122                 ; 外部非常停止正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2005                ; 外部非常停止BZ停止
AND B05                  ; 外部非常停止
ORL
OUT B05                  ; 外部非常停止
@DW K5 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2005                 ; 外部非常停止BZ停止
AND B05                  ; 外部非常停止
OUT B2005                ; 外部非常停止BZ停止
LD CR3905                ; 重度ｴﾗｰ発生中
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2006                ; PLC異常BZ停止
AND B06                  ; PLC異常
ORL
OUT B06                  ; PLC異常
@DW K6 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2006                 ; PLC異常BZ停止
AND B06                  ; PLC異常
OUT B2006                ; PLC異常BZ停止
LD R8400                 ; ｴﾗｰ発生
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2007                ; ﾘﾝｸ異常BZ停止
AND B07                  ; ﾘﾝｸ異常
ORL
OUT B07                  ; ﾘﾝｸ異常
@DW K7 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2007                 ; ﾘﾝｸ異常BZ停止
AND B07                  ; ﾘﾝｸ異常
OUT B2007                ; ﾘﾝｸ異常BZ停止
LD CR2306                ; ﾊﾞｯｸｱｯﾌﾟ電池異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2008                ; PLCﾊﾞｯﾃﾘ低下異常BZ停止
AND B08                  ; PLCﾊﾞｯﾃﾘ低下
ORL
MPS
TMR K51 K18000           ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
MPP
AND T51                  ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
OUT B08                  ; PLCﾊﾞｯﾃﾘ低下
@DW K8 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2008                 ; PLCﾊﾞｯﾃﾘ低下異常BZ停止
AND B08                  ; PLCﾊﾞｯﾃﾘ低下
OUT B2008                ; PLCﾊﾞｯﾃﾘ低下異常BZ停止
LDB X02                  ; 安全扉正常
OUT B2009                ; 安全扉開
LD B2009                 ; 安全扉開
OUT B09                  ; 安全扉開
@DW K9 D0
CON
@FIFOW D0 D900           ; 異常内容
LDP M10                  ; 自動/手動PB
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M71                  ; 自動運転開始BZ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B200A                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
AND B0A                  ; 原点未完ﾓｰﾄﾞ切替不可異常
ORL
OUT B0A                  ; 原点未完ﾓｰﾄﾞ切替不可異常
@DW K10 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B200A                 ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
AND B0A                  ; 原点未完ﾓｰﾄﾞ切替不可異常
OUT B200A                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
LD M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB W00.7                ; PCよりｵﾍﾟﾚｰﾀ登録完了
LDB X130F                ; 投入部NGﾘｾｯﾄSW
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B200E                ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常BZ停止
AND B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
ORL
MPS
TMH K52 K20
MPP
AND T52
OUT B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
@DW K14 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR X130F                 ; 投入部NGﾘｾｯﾄSW
OR B200E                 ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常BZ停止
AND B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
OUT B200E                ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常BZ停止
LDB X1310                ; ｻﾌﾞTP非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2010                ; ｻﾌﾞTP非常停止SW　ON異常BZ停止
AND B010                 ; ｻﾌﾞTP非常停止SW　ON異常
ORL
OUT B010                 ; ｻﾌﾞTP非常停止SW　ON異常
@DW K16 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2010                 ; ｻﾌﾞTP非常停止SW　ON異常BZ停止
AND B010                 ; ｻﾌﾞTP非常停止SW　ON異常
OUT B2010                ; ｻﾌﾞTP非常停止SW　ON異常BZ停止
LDB X1311                ; MAIN TP非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2011                ; MAIN TP非常停止SW　ON異常BZ停止
AND B011                 ; MAIN TP非常停止SW　ON異常
ORL
OUT B011                 ; MAIN TP非常停止SW　ON異常
@DW K17 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2011                 ; MAIN TP非常停止SW　ON異常BZ停止
AND B011                 ; MAIN TP非常停止SW　ON異常
OUT B2011                ; MAIN TP非常停止SW　ON異常BZ停止
LDB X1312                ; ﾜｰｸ投入非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2012                ; ﾜｰｸ投入非常停止SW　ON　BZ停止
AND B012                 ; ﾜｰｸ投入非常停止SW　ON異常
ORL
OUT B012                 ; ﾜｰｸ投入非常停止SW　ON異常
@DW K18 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2012                 ; ﾜｰｸ投入非常停止SW　ON　BZ停止
AND B012                 ; ﾜｰｸ投入非常停止SW　ON異常
OUT B2012                ; ﾜｰｸ投入非常停止SW　ON　BZ停止
LDB X1314                ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2014                ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常BZ停止
AND B014                 ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常
ORL
OUT B014                 ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常
@DW K20 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2014                 ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常BZ停止
AND B014                 ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常
OUT B2014                ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常BZ停止
LDB X1315                ; 乾燥炉非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2015                ; 乾燥炉非常停止SW　ON異常BZ停止
AND B015                 ; 乾燥炉非常停止SW　ON異常
ORL
OUT B015                 ; 乾燥炉非常停止SW　ON異常
@DW K21 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2015                 ; 乾燥炉非常停止SW　ON異常BZ停止
AND B015                 ; 乾燥炉非常停止SW　ON異常
OUT B2015                ; 乾燥炉非常停止SW　ON異常BZ停止
LDB X1316                ; 排出部非常停止SW　ON
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2016                ; 排出部非常停止SW　ON異常BZ停止
AND B016                 ; 排出部非常停止SW　ON異常
ORL
OUT B016                 ; 排出部非常停止SW　ON異常
@DW K22 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2016                 ; 排出部非常停止SW　ON異常BZ停止
AND B016                 ; 排出部非常停止SW　ON異常
OUT B2016                ; 排出部非常停止SW　ON異常BZ停止
LD T71                   ; 自動運転開始PCﾁｪｯｸﾀｲﾑｵｰﾊﾞｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2017                ; 自動起動時PC応答無し異常BZ停止
AND B017                 ; 自動起動時PC応答無し異常
ORL
OUT B017                 ; 自動起動時PC応答無し異常
@DW K23 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2017                 ; 自動起動時PC応答無し異常BZ停止
AND B017                 ; 自動起動時PC応答無し異常
OUT B2017                ; 自動起動時PC応答無し異常BZ停止
LD B01                   ; 非常停止異常
ANB B2001                ; 非常停止異常BZ停止
LD B02                   ; 圧空低下異常
ANB B2002                ; 圧空低下異常BZ停止
ORL
LD B03                   ; 安全扉開異常
ANB B2003                ; 安全扉開異常BZ停止
ORL
LD B04                   ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ANB B2004                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
ORL
LD B05                   ; 外部非常停止
ANB B2005                ; 外部非常停止BZ停止
ORL
LD B06                   ; PLC異常
ANB B2006                ; PLC異常BZ停止
ORL
LD B07                   ; ﾘﾝｸ異常
ANB B2007                ; ﾘﾝｸ異常BZ停止
ORL
OUT M201                 ; ﾒｲﾝ異常BZ-1
LD B08                   ; PLCﾊﾞｯﾃﾘ低下
ANB B2008                ; PLCﾊﾞｯﾃﾘ低下異常BZ停止
LD B0A                   ; 原点未完ﾓｰﾄﾞ切替不可異常
ANB B200A                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
ORL
LD B0E                   ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
ANB B200E                ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常BZ停止
ORL
OUT M202                 ; ﾒｲﾝ異常BZ-2
LD B010                  ; ｻﾌﾞTP非常停止SW　ON異常
ANB B2010                ; ｻﾌﾞTP非常停止SW　ON異常BZ停止
LD B011                  ; MAIN TP非常停止SW　ON異常
ANB B2011                ; MAIN TP非常停止SW　ON異常BZ停止
ORL
LD B012                  ; ﾜｰｸ投入非常停止SW　ON異常
ANB B2012                ; ﾜｰｸ投入非常停止SW　ON　BZ停止
ORL
LD B014                  ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常
ANB B2014                ; ｺｰﾃｨﾝｸﾞ非常停止SW　ON異常BZ停止
ORL
LD B015                  ; 乾燥炉非常停止SW　ON異常
ANB B2015                ; 乾燥炉非常停止SW　ON異常BZ停止
ORL
LD B016                  ; 排出部非常停止SW　ON異常
ANB B2016                ; 排出部非常停止SW　ON異常BZ停止
ORL
LD B017                  ; 自動起動時PC応答無し異常
ANB B2017                ; 自動起動時PC応答無し異常BZ停止
ORL
OUT M203                 ; ﾒｲﾝ異常BZ-3
LDB M201                 ; ﾒｲﾝ異常BZ-1
ANB M202                 ; ﾒｲﾝ異常BZ-2
ANB M203                 ; ﾒｲﾝ異常BZ-3
INV
OUT M200                 ; ﾒｲﾝ異常BZ　まとめ
;異常ﾌﾞｻﾞｰまとめ
LDB M200                 ; ﾒｲﾝ異常BZ　まとめ
ANB M1489                ; ﾕﾆｯﾄ1異常BZ補助
ANB M1989                ; ﾕﾆｯﾄ2異常BZ補助
ANB M2489                ; ﾕﾆｯﾄ3異常BZ補助
ANB M2989                ; ﾕﾆｯﾄ4異常BZ補助
ANB M3489                ; ﾕﾆｯﾄ5異常BZ補助
ANB M3989                ; ﾕﾆｯﾄ6異常BZ補助
ANB M4489                ; ﾕﾆｯﾄ7異常BZ補助
ANB M4989                ; ﾕﾆｯﾄ8異常BZ補助
ANB M5489                ; ﾕﾆｯﾄ9異常BZ補助
INV
OUT M54                  ; 異常ﾌﾞｻﾞｰ
;ﾄﾞﾗｲ運転ﾃﾞｰﾀｾｯﾄ
LD M35                   ; 空運転ﾓｰﾄﾞ
ANP M72                  ; 自動運転開始
@MOV K1 D1051
@MOV K1 D1052
@MOV K7 D1053
@MOV K2 D1054
@MOV K1 D1055
@MOV K7 D1056
@MOV K1 D150             ; INDX1=B面塗布有効設定(1=有効)
@MOV K1 D151             ; INDX1=A面塗布有効設定(1=有効)
;<h1/>タワーランプ
LD M53                   ; 警報(ﾜｰｸ不足等)
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ANB M54                  ; 異常ﾌﾞｻﾞｰ
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M51                   ; 即停止異常(緊急停止)
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LD M60                   ; ｴﾗｰ発生
OR M59                   ; 払出停止異常まとめ
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
ANL
ORL
LD M13131                ; ﾌﾞｻﾞｰON
ANB M13135               ; BZ停止
ORL
OUT Y243C                ; ｼｸﾞﾅﾙﾀﾜｰﾗﾝﾌﾟBZ
LD M51                   ; 即停止異常(緊急停止)
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LD M60                   ; ｴﾗｰ発生
OR M59                   ; 払出停止異常まとめ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y243D                ; ｼｸﾞﾅﾙﾀﾜｰﾗﾝﾌﾟ赤
LDB M53                  ; 警報(ﾜｰｸ不足等)
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT Y243E                ; ｼｸﾞﾅﾙﾀﾜｰﾗﾝﾌﾟ橙
LD M41                   ; 全ﾕﾆｯﾄ起動中
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M57                  ; 自動起動停止指令(払出停止)
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M81                   ; 原点復帰動作中
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
OR M57                   ; 自動起動停止指令(払出停止)
OR M36                   ; 払出ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y243F                ; ｼｸﾞﾅﾙﾀﾜｰﾗﾝﾌﾟ緑
;<h1/>電源OFF処理
LDP X06                  ; 操作ﾊﾟﾈﾙ電源入り切りSW
AND X00                  ; 装置電源入
OR M900                  ; 電源切り受付ON
ANB T73                  ; 電源切りOFF時間
MPS
OUT M900                 ; 電源切り受付ON
MPP
ANB X06                  ; 操作ﾊﾟﾈﾙ電源入り切りSW
TMR K70 K2               ; 電源切りSW OFF確認
LD M900                  ; 電源切り受付ON
AND T70                  ; 電源切りSW OFF確認
OR M901                  ; 電源OFF
AND M900                 ; 電源切り受付ON
MPS
OUT M901                 ; 電源OFF
MPP
ANB X00                  ; 装置電源入
TMR K73 K10              ; 電源切りOFF時間
;<h1/>ﾒｯｾｰｼﾞ言語切替え
LDP B102F                ; MAIN_TPｴﾗｰ表示切替SW
OUT M700                 ; MAIN_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
LD M700                  ; MAIN_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
ANB B152F                ; MAIN_TPｴﾗｰ表示切替日本語
LDB M700                 ; MAIN_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
AND B152F                ; MAIN_TPｴﾗｰ表示切替日本語
ORL
OUT B152F                ; MAIN_TPｴﾗｰ表示切替日本語
LD B152F                 ; MAIN_TPｴﾗｰ表示切替日本語
MOV K1 D70               ; MAIN_TPｴﾗｰ表示切替ﾃﾞﾊﾞｲｽ
LDB B152F                ; MAIN_TPｴﾗｰ表示切替日本語
MOV K0 D70               ; MAIN_TPｴﾗｰ表示切替ﾃﾞﾊﾞｲｽ
LDP B102E                ; SUB_TPｴﾗｰ表示切替SW
OUT M701                 ; SUB_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
LD M701                  ; SUB_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
ANB B152E                ; SUB_TPｴﾗｰ表示切替日本語
LDB M701                 ; SUB_TPｴﾗｰ表示切替SWﾊﾟﾙｽ
AND B152E                ; SUB_TPｴﾗｰ表示切替日本語
ORL
OUT B152E                ; SUB_TPｴﾗｰ表示切替日本語
LD B152E                 ; SUB_TPｴﾗｰ表示切替日本語
MOV K1 D75               ; SUB_TPｴﾗｰ表示切替ﾃﾞﾊﾞｲｽ
LDB B152E                ; SUB_TPｴﾗｰ表示切替日本語
MOV K0 D75               ; SUB_TPｴﾗｰ表示切替ﾃﾞﾊﾞｲｽ
;<h1/>扉開　一時停止処理
LDB X02                  ; 安全扉正常
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OUT M400                 ; 扉開
LD X02                   ; 安全扉正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
MPS
TMR K80 K3               ; 扉閉ﾃﾞｨﾚｰ
MPP
AND T80                  ; 扉閉ﾃﾞｨﾚｰ
OUT M401                 ; 扉閉ﾃﾞｨﾚｰON
LDB B0A6                 ; 収納ﾗｯｸ基板はみだし異常
ANB B0A7                 ; RB8移動中収納ﾗｯｸ基板はみだし異常
MPS
TMR K81 K3               ; RB8ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
MPP
AND T81                  ; RB8ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
OUT M402                 ; RB8ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
LDB B0EC                 ; 排出ﾗｯｸ基板はみだし異常
ANB B0ED                 ; RB9移動中排出ﾗｯｸ基板はみだし異常
MPS
TMR K82 K3               ; RB9ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
MPP
AND T82                  ; RB9ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
OUT M403                 ; RB9ﾗｯｸ基板はみだし異常OFFﾃﾞｨﾚｰ
END
ENDH
