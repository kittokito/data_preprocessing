DEVICE:53
;MODULE:Communication
;MODULE_TYPE:0
;<h1/>ＰＣ動作チェック
LD W00.0                 ; PC動作中
TMR K2500 K20            ; PC正常確認ﾀｲﾏ
LDB W00.0                ; PC動作中
TMR K2501 K20            ; PC正常確認ﾀｲﾏ
LDB T2500                ; PC正常確認ﾀｲﾏ
ANB T2501                ; PC正常確認ﾀｲﾏ
OUT M300                 ; PC通信正常
;<h1/>ｴﾗｰｺｰﾄﾞ送信（ｴﾗｰﾛｸﾞ用)
LD CR2008                ; 運転開始時1ｽｷｬﾝON
FMOV K0 D900 K32         ; 異常内容
DW K0 D900               ; 異常内容
CON
DW K30 D901              ; 格納異常数
LD<> D902 K0             ; ｴﾗｰｺｰﾄﾞ最終
ANB M13020               ; 異常ﾛｸﾞ通信開始
AND= D13000 K0
ANB W00.4                ; ｴﾗｰﾛｸﾞ完了
OR M13020                ; 異常ﾛｸﾞ通信開始
ANB M13015               ; ｴﾗｰﾛｸﾞ完了
OUT M13020               ; 異常ﾛｸﾞ通信開始
LD CR2002                ; 常時ON
FMOV K0 M13000 K1        ; 異常ﾛｸﾞ通信中
LDB M13020               ; 異常ﾛｸﾞ通信開始
MOV K0 D13000
LD M13020                ; 異常ﾛｸﾞ通信開始
MOV D13000 Z1
SET M13000:Z1            ; 異常ﾛｸﾞ通信中
LD M13000                ; 異常ﾛｸﾞ通信中
INC D13000
LD M13001                ; 異常内容読出し
@FIFOR D900 W0112        ; 異常内容  ｴﾗｰｺｰﾄﾞ(ﾛｸﾞ保存用)
INC D13000
LD M13002                ; ｴﾗｰﾛｸﾞ読込み要求ON
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND M300                 ; PC通信正常
ANL
MPS
OUT W0100.5              ; PCへｴﾗｰﾛｸﾞ要求
MPP
AND W00.4                ; ｴﾗｰﾛｸﾞ完了
INC D13000
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
ORB M300                 ; PC通信正常
ANL
INC D13000
LD M13003                ; ﾃﾞｰﾀｸﾘｱｰ
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND M300                 ; PC通信正常
ANB W00.4                ; ｴﾗｰﾛｸﾞ完了
OR M35                   ; 空運転ﾓｰﾄﾞ
ORB M300                 ; PC通信正常
ANL
@MOV K0 W0112            ; ｴﾗｰｺｰﾄﾞ(ﾛｸﾞ保存用)
INC D13000
LD M13004                ; ｴﾗｰﾛｸﾞ完了確認
MOV K15 D13000
;<h1/>ｴﾗｰｺｰﾄﾞ送信（表示用）
LD CR2002                ; 常時ON
ANB M13120               ; 異常発生Ｂｉｔｻｰﾁ開始ON
AND= D13100 K0
OR M13120                ; 異常発生Ｂｉｔｻｰﾁ開始ON
ANB M13115               ; 異常発生Ｂｉｔｻｰﾁ終了
OUT M13120               ; 異常発生Ｂｉｔｻｰﾁ開始ON
LD CR2002                ; 常時ON
FMOV K0 M13100 K1        ; 異常発生Ｂｉｔｻｰﾁ開始
LDB M13120               ; 異常発生Ｂｉｔｻｰﾁ開始ON
MOV K0 D13100
LD M13120                ; 異常発生Ｂｉｔｻｰﾁ開始ON
MOV D13100 Z1
SET M13100:Z1            ; 異常発生Ｂｉｔｻｰﾁ開始
LD M13100                ; 異常発生Ｂｉｔｻｰﾁ開始
INC D13100
LD M13101                ; ｻｰﾁ番号ﾘｾｯﾄ
MOV K0 Z6
INC D13100
LD M13102                ; ﾙｰﾌﾟ開始/ｴﾗｰBitON確認
MPS
AND B00:Z6               ; 0)異常無し
INC D13100
MPP
ANB B00:Z6               ; 0)異常無し
MOV K5 D13100
LD M13103                ; ｴﾗｰｺｰﾄﾞｾｯﾄ
MOV Z6 W0111             ; ｴﾗｰｺｰﾄﾞ(表示用)
INC D13100
LD M13104                ; 表示中
MPS
TMR K2504 K20            ; 表示時間
MPP
AND T2504                ; 表示時間
INC D13100
LD M13105                ; 最終Ｂｉｔ確認
MPS
AND>= Z6 K353
@MOV K7 D13100
MPP
AND< Z6 K353
INC D13100
LD M13106                ; ｻｰﾁ番号ｶｳﾝﾄｱｯﾌﾟﾙｰﾌﾟ開始へ
@INC Z6
MOV K2 D13100
LD M13107                ; 異常発生Ｂｉｔｻｰﾁ終了
MOV K15 D13100
LDB M13104               ; 表示中
MPS
TMR K2505 K25            ; 表示無し確認
MPP
AND T2505                ; 表示無し確認
@MOV K0 W0111            ; ｴﾗｰｺｰﾄﾞ(表示用)
;<h1/>ＰＣからブザー要求
LD W00.2                 ; PC　BUSY
OR M13130                ; PC　BUSY　受付
ANB M13133               ; PC　BUSY　OFF確認
AND M300                 ; PC通信正常
OUT M13130               ; PC　BUSY　受付
LDP M13130               ; PC　BUSY　受付
OR M13131                ; ﾌﾞｻﾞｰON
ANB M13132               ; PCへReset sw　ON
AND M13130               ; PC　BUSY　受付
OUT M13131               ; ﾌﾞｻﾞｰON
LD M13131                ; ﾌﾞｻﾞｰON
ANP M110                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M13135                ; BZ停止
ANB M13133               ; PC　BUSY　OFF確認
AND M13130               ; PC　BUSY　受付
OUT M13135               ; BZ停止
LD M13131                ; ﾌﾞｻﾞｰON
LDP M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORP X130F                ; 投入部NGﾘｾｯﾄSW
ORB W00.2                ; PC　BUSY
ANL
OR M13132                ; PCへReset sw　ON
ANB M13133               ; PC　BUSY　OFF確認
AND M13130               ; PC　BUSY　受付
OUT M13132               ; PCへReset sw　ON
LD M13132                ; PCへReset sw　ON
ANB W00.2                ; PC　BUSY
OR M13133                ; PC　BUSY　OFF確認
AND M13130               ; PC　BUSY　受付
OUT M13133               ; PC　BUSY　OFF確認
;<h1/>ＰＣ　Ｅｒｒｏｒ　処理
LDB M71                  ; 自動運転開始BZ
ANP M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR M13140                ; PCへReset sw　ON
AND W00.1                ; PC Error
AND M300                 ; PC通信正常
OUT M13140               ; PCへReset sw　ON
;<h1/>停止ﾓｰﾄﾞ処理
LD M71                   ; 自動運転開始BZ
OR M13150                ; 自動運転中保持
ANB M13153               ; 停止ﾓｰﾄﾞ処理終了
OUT M13150               ; 自動運転中保持
LD M13150                ; 自動運転中保持
AND<> W010 K0            ; PC　停止ﾓｰﾄﾞ
@MOV W010 W0110          ; PC　停止ﾓｰﾄﾞ  停止ﾓｰﾄﾞ(PLC)
OUT M13151               ; PC→自動運転停止ﾌﾗｸﾞ
LDB T15                  ; 強制停止
ANB M51                  ; 即停止異常(緊急停止)
INV
MEP
OR M13160                ; 即停止異常(緊急停止)記憶
AND M13150               ; 自動運転中保持
OUT M13160               ; 即停止異常(緊急停止)記憶
LDP M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR M13161                ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)記憶
AND M13150               ; 自動運転中保持
OUT M13161               ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)記憶
LDP M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M57                  ; 自動起動停止指令(払出停止)
OR M13162                ; 自動起動停止指令(払出停止)記憶
AND M13150               ; 自動運転中保持
OUT M13162               ; 自動起動停止指令(払出停止)記憶
LD M13150                ; 自動運転中保持
ANF M71                  ; 自動運転開始BZ
OR M13152                ; PCﾃﾞｰﾀ送信
AND M13150               ; 自動運転中保持
OUT M13152               ; PCﾃﾞｰﾀ送信
MPS
LD M13160                ; 即停止異常(緊急停止)記憶
OR M13161                ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)記憶
ANL
@MOV K2 W0110            ; 停止ﾓｰﾄﾞ(PLC)
MPP
LD M13162                ; 自動起動停止指令(払出停止)記憶
ANB M13151               ; PC→自動運転停止ﾌﾗｸﾞ
ANL
@MOV K1 W0110            ; 停止ﾓｰﾄﾞ(PLC)
LD M13152                ; PCﾃﾞｰﾀ送信
ANB W0100.2              ; PLC自動/手動
OR M13153                ; 停止ﾓｰﾄﾞ処理終了
AND M13150               ; 自動運転中保持
MPS
OUT M13153               ; 停止ﾓｰﾄﾞ処理終了
MPP
AND CR2003               ; 常時OFF
@MOV K0 W0110            ; 停止ﾓｰﾄﾞ(PLC)
;<h1/>ﾗｯｸﾃﾞｰﾀｵｰﾙｸﾘｱ処理
LD W05.0                 ; ﾃﾞｰﾀ初期化要求
ANB M13200               ; ﾃﾞｰﾀ初期化要求受付
OR M13200                ; ﾃﾞｰﾀ初期化要求受付
ANB M13203               ; ﾃﾞｰﾀ初期化終了
ANB M71                  ; 自動運転開始BZ
OUT M13200               ; ﾃﾞｰﾀ初期化要求受付
TMH K2510 K5             ; ﾃﾞｰﾀ初期化開始ﾃﾞｨﾚｰ
LDP T2510                ; ﾃﾞｰﾀ初期化開始ﾃﾞｨﾚｰ
OR M13200                ; ﾃﾞｰﾀ初期化要求受付
AND M13200               ; ﾃﾞｰﾀ初期化要求受付
ANB M13202               ; ﾃﾞｰﾀ初期化完了通知
OUT M13201               ; ﾃﾞｰﾀ初期化開始
TMH K2511 K10            ; ﾃﾞｰﾀ初期化終了ﾃﾞｨﾚｰ
@FMOV K0 D3300 K15       ; 基板収納ﾗｯｸ ｲﾝﾃﾞｯｸｽNo.
@FMOV K0 D3600 K15       ; 乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.1記憶
@FMOV K0 D3700 K15       ; 乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.2記憶
@FMOV K0 D4100 K15       ; 排出ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.
LD M13201                ; ﾃﾞｰﾀ初期化開始
AND T2511                ; ﾃﾞｰﾀ初期化終了ﾃﾞｨﾚｰ
OR M13202                ; ﾃﾞｰﾀ初期化完了通知
AND M13200               ; ﾃﾞｰﾀ初期化要求受付
ANB M13203               ; ﾃﾞｰﾀ初期化終了
OUT M13202               ; ﾃﾞｰﾀ初期化完了通知
OUT W0105.0              ; ﾃﾞｰﾀ初期化完了
LD M13202                ; ﾃﾞｰﾀ初期化完了通知
ANB W05.0                ; ﾃﾞｰﾀ初期化要求
OR M13203                ; ﾃﾞｰﾀ初期化終了
AND M13200               ; ﾃﾞｰﾀ初期化要求受付
OUT M13203               ; ﾃﾞｰﾀ初期化終了
;<h1/>ＰＣへの出力
LD CR2002                ; 常時ON
ANB T2503                ; PLC起動ﾀｲﾏ2
TMR K2502 K15            ; PLC起動ﾀｲﾏ1
LD T2502                 ; PLC起動ﾀｲﾏ1
TMR K2503 K15            ; PLC起動ﾀｲﾏ2
LD M1                    ; CPU正常
ANB T2502                ; PLC起動ﾀｲﾏ1
OUT W0100.0              ; PLC動作中
LDB M51                  ; 即停止異常(緊急停止)
ANB T15                  ; 強制停止
INV
OUT W0100.1              ; PLC非常停止異常
LD M71                   ; 自動運転開始BZ
OUT W0100.2              ; PLC自動/手動
LDB M13132               ; PCへReset sw　ON
ANB M13140               ; PCへReset sw　ON
INV
OUT W0100.3              ; PCへﾘｾｯﾄ信号(Ｒｅｓｔ ｓｗ)
LD CR2003                ; 常時OFF
OUT W0100.4              ; PCへﾀﾞｲｱﾛｸﾞ要求
LDB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT W0100.8              ; PCへﾀﾜｰﾗﾝﾌﾟ赤
LDB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT W0100.9              ; PCへﾀﾜｰﾗﾝﾌﾟ橙
LD M71                   ; 自動運転開始BZ
OR M81                   ; 原点復帰動作中
OUT W0100.10             ; PCへﾀﾜｰﾗﾝﾌﾟ緑
LD M3375                 ; 収納ﾘﾌﾀ　　ﾗｯｸ確認
OUT W0101.7              ; 収納ﾗｯｸ_読取可能
LDB T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
OUT W0101.8              ; ﾗｲﾄｶｰﾃﾝ入光(BCRﾀｲﾑｱｳﾄ用)
LDB X1362                ; 収納ﾘﾌﾀｰﾗｯｸ確認
ANB M3403                ; 収納ﾘﾌﾀｰﾗｯｸ到着確認
INV
OUT W0103.10             ; 収納ﾘﾌﾀ　　ﾗｯｸ確認
LD M3909                 ; 乾燥炉ｽﾄｯﾊﾟｰ1ﾗｯｸ確認
OUT W0103.11             ; 乾燥炉ｽﾄｯﾊﾟｰ1ﾗｯｸ確認
LD M3910                 ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ確認
OUT W0103.12             ; 乾燥炉ｽﾄｯﾊﾟｰ2ﾗｯｸ確認
LDB X1376                ; 排出ﾘﾌﾀｰﾗｯｸ確認
ANB M4407                ; 排出ﾘﾌﾀｰﾗｯｸ到着確認
INV
OUT W0103.13             ; 排出ﾘﾌﾀｰﾗｯｸ確認
LD CR2002                ; 常時ON
MOV D3611 W0123          ; 乾燥炉1ﾗｯｸ ID記憶  乾燥炉1ﾗｯｸ ID
MOV D3711 W0124          ; 乾燥炉2ﾗｯｸ ID記憶  乾燥炉2ﾗｯｸ ID
MOV D3600 W013E          ; 乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.1記憶  乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.1
MOV D3700 W013F          ; 乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.2記憶  乾燥炉ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.2
MOV D4111 W0125          ; 排出ﾗｯｸ ID記憶  排出ﾗｯｸ ID
MOV D4100 W0139          ; 排出ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.  排出ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.
LDB X1376                ; 排出ﾘﾌﾀｰﾗｯｸ確認
ANB M4407                ; 排出ﾘﾌﾀｰﾗｯｸ到着確認
ANB Y2428                ; 排出ﾘﾌﾀCV正転ON
ANB Y2429                ; 排出ﾘﾌﾀCV逆転ON
MOV K0 W0125             ; 排出ﾗｯｸ ID
MOV K0 W0139             ; 排出ﾗｯｸ　ｲﾝﾃﾞｯｸｽNo.
END
ENDH
