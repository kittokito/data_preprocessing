DEVICE:53
;MODULE:Vibration
;MODULE_TYPE:0
;□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X1354                 ; 基板投入搬送ﾊﾝﾄﾞ下降端
MPS
TMH K201 K10
MPP
AND T201
OUT M1901                ; 基板投入搬送ﾊﾝﾄﾞ下降端
LD X1355                 ; 基板投入搬送ﾊﾝﾄﾞ上昇端
MPS
TMH K202 K10
MPP
AND T202
OUT M1902                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
LD X1357                 ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
AND X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
MPS
TMH K203 K15
MPP
AND T203
OUT M1903                ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
LD X1356                 ; 振動吸引治具ｸﾗﾝﾌﾟ1開端
AND X1358                ; 振動吸引治具ｸﾗﾝﾌﾟ2開端
MPS
TMH K204 K15
MPP
AND T204
OUT M1904                ; 振動吸引治具ｸﾗﾝﾌﾟ開端
LD X135A                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
MPS
TMH K205 K10
MPP
AND T205
OUT M1905                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
LD X135B                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
MPS
TMH K206 K10
MPP
AND T206
OUT M1906                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
LD X1352                 ; 振動吸引部ﾜｰｸ確認
MPS
TMR K207 K3
MPP
AND T207
OUT M1907                ; 振動吸引部ﾜｰｸ確認
LDB X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
MPS
TMR K208 K5
MPP
AND T208
OUT M1908                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端確認空運転
LD CR2003                ; 常時OFF
MPS
TMR K209 K3
MPP
AND T209
OUT M1909
LD= D1524 K1             ; RB3_完了POS
OUT M1970                ; RB3待機位置
LD= D1524 K2             ; RB3_完了POS
OUT M1972                ; RB3ﾊﾞｷｭｰﾑ終了位置
LD= D1524 K3             ; RB3_完了POS
OUT M1973                ; 基板搬送中間ﾊﾞｯﾌｧ位置
LD= D1524 K4             ; RB3_完了POS
OUT M1974                ; RB3ﾊﾞｷｭｰﾑ終了位置
;■ロボット等機器入力受け
;ﾛﾎﾞｯﾄ現在値
LD X102B                 ; RB3_位置決完(PEND)
AND X102A                ; RB3_原点完了(HEND)
MPS
AND X1020                ; RB3_完了POS1
OUT D1524.0              ; RB3_完了POS1
MRD
AND X1021                ; RB3_完了POS2
OUT D1524.1              ; RB3_完了POS2
MRD
AND X1022                ; RB3_完了POS4
OUT D1524.2              ; RB3_完了POS4
MRD
AND X1023                ; RB3_完了POS8
OUT D1524.3              ; RB3_完了POS8
MRD
AND X1024                ; RB3_完了POS16
OUT D1524.4              ; RB3_完了POS16
MRD
AND X1025                ; RB3_完了POS32
OUT D1524.5              ; RB3_完了POS32
MRD
AND X1026                ; RB3_完了POS64
OUT D1524.6              ; RB3_完了POS64
MRD
AND X1027                ; RB3_完了POS128
OUT D15247               ; RB3_完了POS128
MPP
INV
MOV K0 D1524             ; RB3_完了POS
;□
;□□ 装置状態・動作条件 □□
;□
LD M1990                 ; ﾕﾆｯﾄ2機器準備完了
AND M1970                ; RB3待機位置
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND X135D                ; 集塵切替え下側
AND X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
AND X1357                ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
AND X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
ANB X130A                ; 加震機動作中
OUT M1980                ; ﾕﾆｯﾄ2原点
;□ 非常停止
LDB B040                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
ANB B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
ANB B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
ANB B043                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常
ANB B044                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常
ANB B045                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常
ANB B046                 ; 加震部ﾜｰｸ有り異常
ANB B047                 ; 集塵機負圧異常
ANB B048                 ; 集塵機過負荷異常
ANB B049                 ; 加震機動作異常
ANB B04A                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え異常
ANB B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
ANB B04C                 ; 振動吸引部ﾜｰｸ無し異常
ANB B04D                 ; 振動吸引部ﾜｰｸ残り異常
INV
OUT M1981                ; ﾕﾆｯﾄ2全異常
;□ 起動条件
LD M1980                 ; ﾕﾆｯﾄ2原点
ANB M1981                ; ﾕﾆｯﾄ2全異常
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M1982                ; ﾕﾆｯﾄ2起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
AND M1984                ; ﾕﾆｯﾄ2原点復帰中
ORL
OR M1986                 ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M1983                ; ﾕﾆｯﾄ2起動OFF
;□ ｻｲｸﾙ停止異常
LDB B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
ANB B048                 ; 集塵機過負荷異常
ANB B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
ANB B04D                 ; 振動吸引部ﾜｰｸ残り異常
INV
OUT M1986                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M1987                ; ﾕﾆｯﾄ2払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M1988                ; ﾕﾆｯﾄ2警報補助
;□ 機器準備確認
LD X102A                 ; RB3_原点完了(HEND)
OR CR2002                ; 常時ON
AND X102B                ; RB3_位置決完(PEND)
ANB X1026                ; RB3_完了POS64
ANB X1029                ; RB3_ﾓｰﾄﾞ(RMOD)
AND X102C                ; RB3_運転準備完了(SV)
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1990                ; ﾕﾆｯﾄ2機器準備完了
;□ ｴﾗｰ発生　非常停止扱い
LDB B040                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
ANB B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
ANB B048                 ; 集塵機過負荷異常
ANB B049                 ; 加震機動作異常
ANB B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
INV
AND CR2003               ; 常時OFF
OUT M1991                ; ﾕﾆｯﾄ2ｴﾗｰ発生
;□ 払出しﾜｰｸ無し
LD M36                   ; 払出ﾓｰﾄﾞ
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND M1970                ; RB3待機位置
ANB X1352                ; 振動吸引部ﾜｰｸ確認
LD M35                   ; 空運転ﾓｰﾄﾞ
AND M1970                ; RB3待機位置
AND= D1612 K0            ; INDX2=基板有無記憶(1=有)
ORL
ANL
AND X102B                ; RB3_位置決完(PEND)
ANB X130A                ; 加震機動作中
TMR K214 K10             ; UNIT2払出処理ﾜｰｸ無
LD T214                  ; UNIT2払出処理ﾜｰｸ無
OUT M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
;<h1/>集塵機監視
LD M72                   ; 自動運転開始
AND Y242B                ; 集塵機電源ON
ANB M1985                ; ﾕﾆｯﾄ2起動中
AND M1002                ; 基板ｾｯﾄ待ち
ANB X134F                ; 基板投入部ﾜｰｸ確認
ANB X1350                ; 基板投入部ﾜｰｸｺﾈｸﾀ確認
OUT M1998                ; 投入基板無し状態
LD M1998                 ; 投入基板無し状態
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
MPS
@INC D1580               ; 投入基板無し時間ｶｳﾝﾄ
MPP
AND>= D1580 D1702        ; 投入基板無し時間ｶｳﾝﾄ  投入基板無し時間ｶｳﾝﾄ設定値
OUT M1999                ; 集塵機OFF指示
LDB M1998                ; 投入基板無し状態
MOV K0 D1580             ; 投入基板無し時間ｶｳﾝﾄ
LD M36                   ; 払出ﾓｰﾄﾞ
ANB X134F                ; 基板投入部ﾜｰｸ確認
ANB X1352                ; 振動吸引部ﾜｰｸ確認
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M1400                ; ﾕﾆｯﾄ1動作中
MPS
TMR K140 K20             ; 集塵機OFF確認ﾀｲﾏｰ
MPP
AND T140                 ; 集塵機OFF確認ﾀｲﾏｰ
OUT M1895                ; 払出し集塵機OFF指示
;□ ｽﾃｯﾌﾟ起動
;□ 原点復帰
LDP M82                  ; 原点復帰開始
ANB M1984                ; ﾕﾆｯﾄ2原点復帰中
OR M1984                 ; ﾕﾆｯﾄ2原点復帰中
ANB M1983                ; ﾕﾆｯﾄ2起動OFF
ANB M1991                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M1749                ; ｻｲｸﾙ終了
MPS
OUT M1984                ; ﾕﾆｯﾄ2原点復帰中
MPP
AND M401                 ; 扉閉ﾃﾞｨﾚｰON
TMR K200 K600            ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ
;□ 起動中
LD M73                   ; 主幹起動指令
AND M1354                ; 振動吸引部起動
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1982                ; ﾕﾆｯﾄ2起動条件
OR M1985                 ; ﾕﾆｯﾄ2起動中
ANB M1983                ; ﾕﾆｯﾄ2起動OFF
ANB M1749                ; ｻｲｸﾙ終了
OUT M1985                ; ﾕﾆｯﾄ2起動中
;□□□□□□□□□□□□□□□
;□　　 　UNIT2自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>ﾕﾆｯﾄ2 自動運転回路
LD= D1500 D1505          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ2動作中判定
AND X102B                ; RB3_位置決完(PEND)
ORB M1985                ; ﾕﾆｯﾄ2起動中
TMR K210 K20             ; ﾕﾆｯﾄ2動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D1500 D1505          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ2動作中判定
LD T210                  ; ﾕﾆｯﾄ2動作無し判定ﾀｲﾏ
OUT M1992                ; ﾕﾆｯﾄ2動作無し確認
;□
;□　ﾕﾆｯﾄ2 自動運転ステップ開始
;□
LD CR2002                ; 常時ON
FMOV K0 M1500 K18
LDB M1984                ; ﾕﾆｯﾄ2原点復帰中
ANB M1985                ; ﾕﾆｯﾄ2起動中
MOV K0 D1500             ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1984                 ; ﾕﾆｯﾄ2原点復帰中
OR M1985                 ; ﾕﾆｯﾄ2起動中
MOV D1500 Z1             ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
SET M1500:Z1
LD M1500
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1501
MPS
ANB M1984                ; ﾕﾆｯﾄ2原点復帰中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD M84                   ; 原点復帰起動確認
AND M1984                ; ﾕﾆｯﾄ2原点復帰中
AND M84                  ; 原点復帰起動確認
ANL
MOV K250 D1500           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1502                 ; 振動吸引部ﾜｰｸ確認
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND X1352                ; 振動吸引部ﾜｰｸ確認
AND M1880                ; 振動吸引部ﾜｰｸ確認記憶
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D1612 K1            ; INDX2=基板有無記憶(1=有)
ORL
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1502                 ; 振動吸引部ﾜｰｸ確認
AND X1352                ; 振動吸引部ﾜｰｸ確認
OR M1880                 ; 振動吸引部ﾜｰｸ確認記憶
AND M1985                ; ﾕﾆｯﾄ2起動中
ANB M1543                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給ON
OUT M1880                ; 振動吸引部ﾜｰｸ確認記憶
LD M1503                 ; 振動吸引部起動FLG　OFF
ANB M1354                ; 振動吸引部起動
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1504
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1505
MOV K10 D1500            ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1510
ANB M130                 ; 一時停止
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND X1352                ; 振動吸引部ﾜｰｸ確認
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D1612 K1            ; INDX2=基板有無記憶(1=有)
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1511                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉/集塵上側
ANB M130                 ; 一時停止
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND X135A                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
AND X135C                ; 集塵切替え上側
AND X1352                ; 振動吸引部ﾜｰｸ確認
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
AND M1908                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端確認空運転
AND X135C                ; 集塵切替え上側
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1512                 ; 振動吸引治具ｸﾗﾝﾌﾟ開
ANB M130                 ; 一時停止
AND M1904                ; 振動吸引治具ｸﾗﾝﾌﾟ開端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1513                 ; 加震機動作ON
ANB M1981                ; ﾕﾆｯﾄ2全異常
AND M1850                ; 加震機ON
AND M1851                ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰ
AND X130A                ; 加震機動作中
AND M1381                ; RB2ﾊﾞｷｭｰﾑ開始位置移動安全可
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1513                 ; 加震機動作ON
OR M1850                 ; 加震機ON
ANB M1514                ; 集塵機電源ON
ANB M1519                ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰOFF
AND M1985                ; ﾕﾆｯﾄ2起動中
ANB B049                 ; 加震機動作異常
OUT M1850                ; 加震機ON
LD M1513                 ; 加震機動作ON
AND X130A                ; 加震機動作中
OR M1851                 ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰ
ANB M1519                ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰOFF
AND M1985                ; ﾕﾆｯﾄ2起動中
OUT M1851                ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰ
TMR K271 D1700           ; ﾊﾞｷｭｰﾑ開始ﾃﾞｨﾚｰ  ﾊﾞｷｭｰﾑ開始ﾃﾞｨﾚｰ設定値
LD M1514                 ; 集塵機電源ON
ANB M1850                ; 加震機ON
AND M1870                ; 集塵機ON
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@CAL+ D1605 K10 D1520    ; INDX2=振動吸引ﾛﾎﾞｯﾄNo.記憶  RB3_指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1515                 ; 集塵ﾉｽﾞﾙRB3吸引開始位置移動
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1516
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X102B                ; RB3_位置決完(PEND)
AND= D1524 D1522         ; RB3_完了POS  RB3_転送指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1517
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1518                 ; ﾊﾞｷｭｰﾑ開始待ち
ANB M130                 ; 一時停止
ANB M1981                ; ﾕﾆｯﾄ2全異常
AND X130A                ; 加震機動作中
AND T271                 ; ﾊﾞｷｭｰﾑ開始ﾃﾞｨﾚｰ
AND M1380                ; RB2ﾊﾞｷｭｰﾑ終了位置移動安全可
LD L200                  ; 集塵機有効
AND X135E                ; 集塵機負圧確認
ORB L200                 ; 集塵機有効
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1519                 ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰOFF
ANB M1850                ; 加震機ON
ANB M1851                ; ﾊﾞｷｭｰﾑ開始ﾀｲﾏｰ
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1520                 ; 集塵ﾉｽﾞﾙRB3ﾊﾞｷｭｰﾑ終了位置ｾｯﾄ
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@CAL+ D1605 K50 D1520    ; INDX2=振動吸引ﾛﾎﾞｯﾄNo.記憶  RB3_指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1521                 ; 集塵ﾉｽﾞﾙRB3吸引終了位置移動
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1522
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X102B                ; RB3_位置決完(PEND)
AND= D1524 D1522         ; RB3_完了POS  RB3_転送指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1523
MOV K30 D1500            ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1530                 ; 集塵切替え下側へ
AND X135D                ; 集塵切替え下側
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1531                 ; A面ﾊﾞｷｭｰﾑ
TMR K272 D1704           ; A面ﾊﾞｷｭｰﾑ時間  A面ﾊﾞｷｭｰﾑ時間設定値
CON
AND T272                 ; A面ﾊﾞｷｭｰﾑ時間
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1532                 ; 加震機動作OFF
ANB X130A                ; 加震機動作中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1533                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開
ANB M130                 ; 一時停止
AND X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1534                 ; 振動吸引治具ｸﾗﾝﾌﾟ閉
ANB M130                 ; 一時停止
AND M1903                ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1535
MOV K40 D1500            ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1540                 ; 基板投入搬送ﾊﾝﾄﾞ下降
ANB M130                 ; 一時停止
AND M1901                ; 基板投入搬送ﾊﾝﾄﾞ下降端
AND X1352                ; 振動吸引部ﾜｰｸ確認
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1541                 ; 集塵ﾉｽﾞﾙRB3供給要求
ANB M130                 ; 一時停止
MPS
LDB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
AND= D2112 K0            ; INDX3=基板有無記憶(1=有)
AND M2450                ; 基板収納RB4待機位置
AND M2406                ; 基板収納ﾊﾝﾄﾞ上昇端
AND M2412                ; ﾌﾟｯｼｬｰｼﾌﾄ前後　後退端
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND= D2112 K1            ; INDX3=基板有無記憶(1=有)
ANB M2485                ; ﾕﾆｯﾄ3起動中
ANL
TMR K280 K5              ; ﾕﾆｯﾄ2終了確認ﾀｲﾏｰ
CON
AND T280                 ; ﾕﾆｯﾄ2終了確認ﾀｲﾏｰ
MOV K249 D1500           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1542                 ; 集塵ﾉｽﾞﾙRB3供給可確認
ANB M130                 ; 一時停止
ANB M1861                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了
ANB M2380                ; 基板収納RB4基板収動作中
ANB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1543                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給ON
AND M1860                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給中
ANB M1880                ; 振動吸引部ﾜｰｸ確認記憶
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1543                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給ON
OR M1860                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給中
ANB M1560                ; 基板収納ﾊﾝﾄﾞ下降
AND M1985                ; ﾕﾆｯﾄ2起動中
OUT M1860                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給中
LD M1544
ANB M130                 ; 一時停止
ANB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
AND X1352                ; 振動吸引部ﾜｰｸ確認
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@MOV K3 D1520            ; RB3_指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1545                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧ位置移動
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1546
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X102B                ; RB3_位置決完(PEND)
AND= D1524 K3            ; RB3_完了POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1547                 ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
ANB M130                 ; 一時停止
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
AND M35                  ; 空運転ﾓｰﾄﾞ
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1548                 ; ﾃﾞｰﾀｼﾌﾄ
@BMOV D1600 D2100 K13    ; 基板ｲﾝﾃﾞｯｸｽNO.  基板ｲﾝﾃﾞｯｸｽNO.
@MOV W0131 W0132         ; 振動吸引部ｲﾝﾃﾞｯｸｽNo.  塗布前ﾊﾞｯﾌｧｲﾝﾃﾞｯｸｽNo.
@FMOV K0 D1600 K13       ; 基板ｲﾝﾃﾞｯｸｽNO.
@MOV K0 W0131            ; 振動吸引部ｲﾝﾃﾞｯｸｽNo.
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1549                 ; 搬送CVｼﾌﾄ2-3要求ON
MPS
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT W0103.1              ; 搬送CVｼﾌﾄ2-3要求
MRD
AND W03.1                ; 搬送CVｼﾌﾄ2-3完了
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
AND M35                  ; 空運転ﾓｰﾄﾞ
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1550                 ; 基板投入搬送ﾊﾝﾄﾞ上昇
ANB M130                 ; 一時停止
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1551                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了ON
MPS
LD M71                   ; 自動運転開始BZ
AND M1861                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了
ANL
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
ANB M71                  ; 自動運転開始BZ
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1551                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了ON
OR M1861                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了
ANB M2044                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧ供給完了OFF
AND M71                  ; 自動運転開始BZ
OUT M1861                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給完了
LD M1552
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1553
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1554
ANB M130                 ; 一時停止
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@MOV K1 D1520            ; RB3_指令POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1555                 ; 集塵ﾉｽﾞﾙRB3待機位置移動
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1556
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X102B                ; RB3_位置決完(PEND)
AND= D1524 K1            ; RB3_完了POS
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1557
ANB M130                 ; 一時停止
MPS
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M2485                ; ﾕﾆｯﾄ3起動中
ANL
MOV K249 D1500           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1558
ANB M130                 ; 一時停止
AND M2450                ; 基板収納RB4待機位置
AND M2406                ; 基板収納ﾊﾝﾄﾞ上昇端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1559                 ; ﾌﾟｯｼｬｰｼﾌﾄ前後　前進
ANB M130                 ; 一時停止
AND M2411                ; ﾌﾟｯｼｬｰｼﾌﾄ前後　前進端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1560                 ; 基板収納ﾊﾝﾄﾞ下降
ANB M130                 ; 一時停止
AND M2405                ; 基板収納ﾊﾝﾄﾞ下降端
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1561                 ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給中OFF
ANB M1860                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧへ供給中
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1562                 ; UNIT2ｻｲｸﾙ終了
MOV K249 D1500           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;<h1/>ﾕﾆｯﾄ2原点復帰
LD M1750                 ; ﾕﾆｯﾄ2原点復帰開始
ANB X130A                ; 加震機動作中
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1751                 ; ﾊﾝﾄﾞ上昇、ﾜｰｸｸﾗﾝﾌﾟ開
AND M1902                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND X135D                ; 集塵切替え下側
AND X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1752                 ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
AND M1903                ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
ANB X1352                ; 振動吸引部ﾜｰｸ確認
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1753                 ; ｲﾝﾀｰﾛｯｸ確認
ANB M130                 ; 一時停止
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
LD M1470                 ; RB2待機位置
OR M1380                 ; RB2ﾊﾞｷｭｰﾑ終了位置移動安全可
ANL
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1754
@MOV K1 D1520            ; RB3_指令POS
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1755                 ; 集塵ﾉｽﾞﾙRB3原点復帰_起動
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1756                 ; 集塵ﾉｽﾞﾙRB3原点復帰完了確認
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X102A                ; RB3_原点完了(HEND)
AND X102B                ; RB3_位置決完(PEND)
AND= D1524 K1            ; RB3_完了POS
@INC D1500               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1757                 ; 記憶ﾃﾞｰﾀｸﾘｱ
@FMOV K0 D1600 K13       ; 基板ｲﾝﾃﾞｯｸｽNO.
INC D1500                ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M1758                 ; 集塵ﾉｽﾞﾙRB3原点復帰完了
@MOV K249 D1500          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K249 D1500           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M1062                 ; ﾕﾆｯﾄ1-1ｻｲｸﾙ停止処理
OR CR2003                ; 常時OFF
AND CR2003               ; 常時OFF
@MOV K249 D1500          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;<h1/>UNIT2手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT2手動回路  　　□
;□□□□□□□□□□□□□□□
LD X102A                 ; RB3_原点完了(HEND)
LD M1470                 ; RB2待機位置
ANB X102A                ; RB3_原点完了(HEND)
ORL
OR M1984                 ; ﾕﾆｯﾄ2原点復帰中
AND X102B                ; RB3_位置決完(PEND)
ANB X1026                ; RB3_完了POS64
ANB X1029                ; RB3_ﾓｰﾄﾞ(RMOD)
AND X102C                ; RB3_運転準備完了(SV)
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
LD X1357                 ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
AND X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
AND X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
LD X1355                 ; 基板投入搬送ﾊﾝﾄﾞ上昇端
OR M1973                 ; 基板搬送中間ﾊﾞｯﾌｧ位置
ANL
OUT M1840                ; 加震部基板供給可
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M1984                ; ﾕﾆｯﾄ2原点復帰中
MC
LDB M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANB M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANB M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND<> D1524 K1           ; RB3_完了POS
OUT M1801                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動可
LDP B10A0                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動SW
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
AND M1801                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動可
OR M1802                 ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANFB M1940               ; 集塵ﾉｽﾞﾙRB3動作中
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
@MOV K1 D1520            ; RB3_指令POS
LD M2450                 ; 基板収納RB4待機位置
AND M2406                ; 基板収納ﾊﾝﾄﾞ上昇端
AND M2412                ; ﾌﾟｯｼｬｰｼﾌﾄ前後　後退端
OR M2452                 ; 基板収納RB4収納位置
LDB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
AND M1903                ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
AND M1906                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
OR M1902                 ; 基板投入搬送ﾊﾝﾄﾞ上昇端
ANL
ANB M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANB M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANB M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND<> D1524 K3           ; RB3_完了POS
OUT M1803                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動可
LDP B10A2                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動SW
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
AND M1803                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動可
OR M1804                 ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANFB M1940               ; 集塵ﾉｽﾞﾙRB3動作中
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
@MOV K3 D1520            ; RB3_指令POS
LD M1470                 ; RB2待機位置
OR M1471                 ; RB2ﾊﾞｰｺｰﾄﾞ開始位置
ANB M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANB M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANB M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND M1381                ; RB2ﾊﾞｷｭｰﾑ開始位置移動安全可
OUT M1805                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動可
LDP B10A4                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動SW
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
AND M1805                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動可
OR M1806                 ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANFB M1940               ; 集塵ﾉｽﾞﾙRB3動作中
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
@CAL+ D1050 K10 D1520    ; RB3_指令POS
LD M1470                 ; RB2待機位置
OR M1471                 ; RB2ﾊﾞｰｺｰﾄﾞ開始位置
ANB M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANB M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANB M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
AND X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND M1380                ; RB2ﾊﾞｷｭｰﾑ終了位置移動安全可
OUT M1807                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動可
LDP B10A5                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動SW
AND M1800                ; 集塵ﾉｽﾞﾙRB3動作可条件
AND M1805                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動可
OR M1808                 ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANFB M1940               ; 集塵ﾉｽﾞﾙRB3動作中
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
@CAL+ D1050 K50 D1520    ; RB3_指令POS
LD>= D1524 K51           ; RB3_完了POS
AND<= D1524 K82          ; RB3_完了POS
OUT M1809                ; 基板投入搬送ｶﾞｲﾄﾞ下降可
LDP B10A6                ; 基板投入搬送ｶﾞｲﾄﾞ下降SW
AND M1809                ; 基板投入搬送ｶﾞｲﾄﾞ下降可
OR M1810                 ; 基板投入搬送ｶﾞｲﾄﾞ下降
ANPB B10A7               ; 基板投入搬送ｶﾞｲﾄﾞ上昇SW
OUT M1810                ; 基板投入搬送ｶﾞｲﾄﾞ下降
LD CR2002                ; 常時ON
OUT M1811                ; 基板投入搬送ｶﾞｲﾄﾞ上昇可
LDP B10A7                ; 基板投入搬送ｶﾞｲﾄﾞ上昇SW
AND M1811                ; 基板投入搬送ｶﾞｲﾄﾞ上昇可
OR M1812                 ; 基板投入搬送ｶﾞｲﾄﾞ上昇
ANPB B10A6               ; 基板投入搬送ｶﾞｲﾄﾞ下降SW
OUT M1812                ; 基板投入搬送ｶﾞｲﾄﾞ上昇
LD CR2002                ; 常時ON
OUT M1813                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉可
LDP B10A8                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SW
AND M1813                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉可
OR M1814                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉
ANB M1816                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開
OUT M1814                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉
LD CR2002                ; 常時ON
OUT M1815                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開可
LDB B10A9                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開SW
ANB B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
INV
AND M1815                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開可
OUT M1816                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開
LD CR2002                ; 常時ON
OUT M1817                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開可
LDP B10AA                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SW
AND M1817                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開可
OR M1818                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開
ANB M1820                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉
OUT M1818                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開
LD CR2002                ; 常時ON
OUT M1819                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉可
LD B10AB                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉SW
AND M1819                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉可
OUT M1820                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉
LD M1904                 ; 振動吸引治具ｸﾗﾝﾌﾟ開端
OUT M1821                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON可
LDP B10AC                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON_SW
AND M1821                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON可
OR M1822                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON
ANB X130A                ; 加震機動作中
ANB B10AD                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF_SW
OUT M1822                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON
LD CR2002                ; 常時ON
OUT M1823                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF可
LDP B10AD                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF_SW
AND M1823                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF可
OR M1824                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF
AND X130A                ; 加震機動作中
OUT M1824                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF
LD CR2002                ; 常時ON
OUT M1825                ; 振動吸引集塵上ﾀﾞｸﾄ切替え可
LDP B10AE                ; 振動吸引集塵上ﾀﾞｸﾄ切替えSW
AND M1825                ; 振動吸引集塵上ﾀﾞｸﾄ切替え可
OR M1826                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え
ANB M1828                ; 振動吸引集塵下ﾀﾞｸﾄ切替え
OUT M1826                ; 振動吸引集塵上ﾀﾞｸﾄ切替え
LD CR2002                ; 常時ON
OUT M1827                ; 振動吸引集塵下ﾀﾞｸﾄ切替え可
LD B10AF                 ; 振動吸引集塵下ﾀﾞｸﾄ切替えSW
AND M1827                ; 振動吸引集塵下ﾀﾞｸﾄ切替え可
OUT M1828                ; 振動吸引集塵下ﾀﾞｸﾄ切替え
MCR
LD B103A                 ; 集塵機有効SW
SET L200                 ; 集塵機有効
LD B103B                 ; 集塵機無効SW
RES L200                 ; 集塵機有効
;<h1/>ロボット動作出力
;■ロボット動作出力
;(これはIAIﾛﾎﾞｼﾘﾝﾀﾞ)
LDB M1984                ; ﾕﾆｯﾄ2原点復帰中
AND X102A                ; RB3_原点完了(HEND)
OR CR2002                ; 常時ON
OR M1984                 ; ﾕﾆｯﾄ2原点復帰中
ANB X1026                ; RB3_完了POS64
ANB X1029                ; RB3_ﾓｰﾄﾞ(RMOD)
AND X102B                ; RB3_位置決完(PEND)
AND X102C                ; RB3_運転準備完了(SV)
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1941                ; 集塵ﾉｽﾞﾙRB3動作可条件
LDB M1802                ; 集塵ﾉｽﾞﾙRB3_Standby位置移動
ANB M1804                ; 集塵ﾉｽﾞﾙRB3_Eject位置移動
ANB M1806                ; 集塵ﾉｽﾞﾙRB3_集塵開始位置移動
ANB M1808                ; 集塵ﾉｽﾞﾙRB3_集塵終了位置移動
ANB M1755                ; 集塵ﾉｽﾞﾙRB3原点復帰_起動
ANB M1515                ; 集塵ﾉｽﾞﾙRB3吸引開始位置移動
ANB M1521                ; 集塵ﾉｽﾞﾙRB3吸引終了位置移動
ANB M1545                ; 集塵ﾉｽﾞﾙRB3中間ﾊﾞｯﾌｧ位置移動
ANB M1555                ; 集塵ﾉｽﾞﾙRB3待機位置移動
INV
OUT M1942                ; 集塵ﾉｽﾞﾙRB3起動集合
LD M1942                 ; 集塵ﾉｽﾞﾙRB3起動集合
AND M1941                ; 集塵ﾉｽﾞﾙRB3動作可条件
ANB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
OR M1940                 ; 集塵ﾉｽﾞﾙRB3動作中
ANB M1947                ; 集塵ﾉｽﾞﾙRB3起動終了
AND M50                  ; 運転準備
AND X102D                ; RB3_非常停止(EMG)
AND X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT M1940                ; 集塵ﾉｽﾞﾙRB3動作中
@MOV D1520 D1522         ; RB3_指令POS  RB3_転送指令POS
TMH K230 K10             ; 集塵ﾉｽﾞﾙRB3起動ﾃﾞｨﾚｰ
LDP T230                 ; 集塵ﾉｽﾞﾙRB3起動ﾃﾞｨﾚｰ
OR M1943                 ; 集塵ﾉｽﾞﾙRB3動作開始
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
ANB M1944                ; 集塵ﾉｽﾞﾙRB3起動ｽﾀｰﾄ
OUT M1943                ; 集塵ﾉｽﾞﾙRB3動作開始
LD M1943                 ; 集塵ﾉｽﾞﾙRB3動作開始
ANB X1026                ; RB3_完了POS64
AND X102B                ; RB3_位置決完(PEND)
OR M1944                 ; 集塵ﾉｽﾞﾙRB3起動ｽﾀｰﾄ
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
ANB M1945                ; 集塵ﾉｽﾞﾙRB3起動確認
OUT M1944                ; 集塵ﾉｽﾞﾙRB3起動ｽﾀｰﾄ
LD M1944                 ; 集塵ﾉｽﾞﾙRB3起動ｽﾀｰﾄ
ANB X102B                ; RB3_位置決完(PEND)
OR M1945                 ; 集塵ﾉｽﾞﾙRB3起動確認
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
ANB M1946                ; 集塵ﾉｽﾞﾙRB3起動終了確認
OUT M1945                ; 集塵ﾉｽﾞﾙRB3起動確認
LD M1945                 ; 集塵ﾉｽﾞﾙRB3起動確認
ANB X1026                ; RB3_完了POS64
AND X102B                ; RB3_位置決完(PEND)
AND= D1520 D1524         ; RB3_指令POS  RB3_完了POS
OR M1946                 ; 集塵ﾉｽﾞﾙRB3起動終了確認
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
ANB M1947                ; 集塵ﾉｽﾞﾙRB3起動終了
OUT M1946                ; 集塵ﾉｽﾞﾙRB3起動終了確認
LD M1946                 ; 集塵ﾉｽﾞﾙRB3起動終了確認
OR M1947                 ; 集塵ﾉｽﾞﾙRB3起動終了
AND M1940                ; 集塵ﾉｽﾞﾙRB3動作中
OUT M1947                ; 集塵ﾉｽﾞﾙRB3起動終了
LD CR2002                ; 常時ON
MPS
AND D1522.0              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS1
OUT Y2020                ; RB3_指令POS1
MRD
AND D1522.1              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS2
OUT Y2021                ; RB3_指令POS2
MRD
AND D1522.2              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS4
OUT Y2022                ; RB3_指令POS4
MRD
AND D1522.3              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS8
OUT Y2023                ; RB3_指令POS8
MRD
AND D1522.4              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS16
OUT Y2024                ; RB3_指令POS16
MRD
AND D1522.5              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS32
OUT Y2025                ; RB3_指令POS32
MRD
AND D1522.6              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS64
OUT Y2026                ; RB3_指令POS64
MPP
AND D1522.7              ; 集塵ﾉｽﾞﾙPP RB_転送指令POS128
OUT Y2027                ; RB3_指令POS128
LD M5804                 ; ROBOT　2〜7軸手動ﾓｰﾄﾞON
OUT Y202A                ; RB3_運転ﾓｰﾄﾞ(RMOD)
LD X02                   ; 安全扉正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ANB M400                 ; 扉開
ORB M1940                ; 集塵ﾉｽﾞﾙRB3動作中
OUT Y202C                ; RB3_一時停止(STP)
LD M1944                 ; 集塵ﾉｽﾞﾙRB3起動ｽﾀｰﾄ
OUT Y202D                ; RB3_ｽﾀｰﾄ(CSTR)
LD M16                   ; ﾘｾｯﾄPB
ANB X102E                ; RB3_ｱﾗｰﾑ(ARM)
OUT Y202E                ; RB3_ﾘｾｯﾄ(RESET)
LD M50                   ; 運転準備
OUT Y202F                ; RB3_ｻｰﾎﾞON(SON)
;<h1/>出力(シリンダ等)
LDB M1814                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉
ANB M1511                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉/集塵上側
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OR Y2500                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
ANB M1816                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開
ANB M1751                ; ﾊﾝﾄﾞ上昇、ﾜｰｸｸﾗﾝﾌﾟ開
ANB M1533                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開
ANB B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
OUT Y2500                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
LDB M1818                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開
ANB M1512                ; 振動吸引治具ｸﾗﾝﾌﾟ開
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OR Y2502                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB M1820                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ閉
ANB M1751                ; ﾊﾝﾄﾞ上昇、ﾜｰｸｸﾗﾝﾌﾟ開
ANB M1534                ; 振動吸引治具ｸﾗﾝﾌﾟ閉
OUT Y2502                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
LDB M1810                ; 基板投入搬送ｶﾞｲﾄﾞ下降
ANB M1540                ; 基板投入搬送ﾊﾝﾄﾞ下降
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
LD Y2504                 ; 基板投入搬送ﾊﾝﾄﾞ下降SOL
ANB Y2505                ; 基板投入搬送ﾊﾝﾄﾞ上昇SOL
ORL
OUT Y2504                ; 基板投入搬送ﾊﾝﾄﾞ下降SOL
LDB M1812                ; 基板投入搬送ｶﾞｲﾄﾞ上昇
ANB M1751                ; ﾊﾝﾄﾞ上昇、ﾜｰｸｸﾗﾝﾌﾟ開
ANB M1550                ; 基板投入搬送ﾊﾝﾄﾞ上昇
ANB CR2003               ; 常時OFF
INV
LD Y2505                 ; 基板投入搬送ﾊﾝﾄﾞ上昇SOL
ANB Y2504                ; 基板投入搬送ﾊﾝﾄﾞ下降SOL
ORL
OUT Y2505                ; 基板投入搬送ﾊﾝﾄﾞ上昇SOL
LDB M1826                ; 振動吸引集塵上ﾀﾞｸﾄ切替え
ANB M1511                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉/集塵上側
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OR Y2506                 ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
ANB M1828                ; 振動吸引集塵下ﾀﾞｸﾄ切替え
ANB M1751                ; ﾊﾝﾄﾞ上昇、ﾜｰｸｸﾗﾝﾌﾟ開
ANB M1530                ; 集塵切替え下側へ
OUT Y2506                ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
LDB M1822                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀON
ANB M1850                ; 加震機ON
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT Y242D                ; 加震機動作ON
LDB M1824                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀOFF
ANB M1532                ; 加震機動作OFF
ANB CR2003               ; 常時OFF
INV
LD M51                   ; 即停止異常(緊急停止)
AND X130A                ; 加震機動作中
ORL
OUT Y242E                ; 加震機動作OFF
LD M1871                 ; 集塵負圧計ｾﾞﾛｼﾌﾄON
OR B1186                 ; TP　ｾﾞﾛｼﾌﾄSW
OUT Y2434                ; 集塵機負圧計ｾﾞﾛｼﾌﾄ
;<h1/>パネル表示
;□□　　パネル表示　　□□
LD= D1524 K1             ; RB3_完了POS
LD= D1522 K1             ; RB3_転送指令POS
AND X102A                ; RB3_原点完了(HEND)
ANB X102B                ; RB3_位置決完(PEND)
ANB M51                  ; 即停止異常(緊急停止)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A0                ; UNIT2-1-1 TP-PL
LD= D1524 K3             ; RB3_完了POS
LD= D1522 K3             ; RB3_転送指令POS
AND X102A                ; RB3_原点完了(HEND)
ANB X102B                ; RB3_位置決完(PEND)
ANB M51                  ; 即停止異常(緊急停止)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A2                ; UNIT2-1-3 TP-PL
LD>= D1524 K11           ; RB3_完了POS
AND<= D1524 K42          ; RB3_完了POS
LD>= D1522 K11           ; RB3_転送指令POS
AND<= D1522 K42          ; RB3_転送指令POS
AND X102A                ; RB3_原点完了(HEND)
ANB X102B                ; RB3_位置決完(PEND)
ANB M51                  ; 即停止異常(緊急停止)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A4                ; UNIT2-1-5 TP-PL
LD>= D1524 K51           ; RB3_完了POS
AND<= D1524 K82          ; RB3_完了POS
LD>= D1522 K51           ; RB3_転送指令POS
AND<= D1522 K82          ; RB3_転送指令POS
AND X102A                ; RB3_原点完了(HEND)
ANB X102B                ; RB3_位置決完(PEND)
ANB M51                  ; 即停止異常(緊急停止)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A5                ; UNIT2-1-6 TP-PL
LD X1354                 ; 基板投入搬送ﾊﾝﾄﾞ下降端
LD Y2504                 ; 基板投入搬送ﾊﾝﾄﾞ下降SOL
ANB X1354                ; 基板投入搬送ﾊﾝﾄﾞ下降端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A6                ; UNIT2-1-7 TP-PL
LD X1355                 ; 基板投入搬送ﾊﾝﾄﾞ上昇端
LD Y2505                 ; 基板投入搬送ﾊﾝﾄﾞ上昇SOL
ANB X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A7                ; UNIT2-1-8 TP-PL
LD X135A                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
LD Y2500                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
ANB X135A                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A8                ; UNIT2-1-9 TP-PL
LD X135B                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
LDB Y2500                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
ANB X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15A9                ; UNIT2-1-10 TP-PL
LD X1356                 ; 振動吸引治具ｸﾗﾝﾌﾟ1開端
AND X1358                ; 振動吸引治具ｸﾗﾝﾌﾟ2開端
LD Y2502                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1356                ; 振動吸引治具ｸﾗﾝﾌﾟ1開端
ANB X1358                ; 振動吸引治具ｸﾗﾝﾌﾟ2開端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AA                ; UNIT2-1-11 TP-PL
LD X1357                 ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
AND X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
LDB Y2502                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1357                ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
ANB X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AB                ; UNIT2-1-12 TP-PL
LD X130A                 ; 加震機動作中
LD Y242D                 ; 加震機動作ON
ANB X130A                ; 加震機動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AC                ; UNIT2-1-13 TP-PL
LDB X130A                ; 加震機動作中
LD Y242E                 ; 加震機動作OFF
ANB X130A                ; 加震機動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AD                ; UNIT2-1-14 TP-PL
LD X135C                 ; 集塵切替え上側
LD Y2506                 ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
ANB X135C                ; 集塵切替え上側
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AE                ; UNIT2-1-15 TP-PL
LD X135D                 ; 集塵切替え下側
LDB Y2506                ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
ANB X135D                ; 集塵切替え下側
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15AF                ; UNIT2-1-16 TP-PL
LD L200                  ; 集塵機有効
OUT B153A                ; 集塵機有効PL
LDB L200                 ; 集塵機有効
OUT B153B                ; 集塵機無効PL
;<h1/>異常検出
;□□　　異常検出　　□□
LD M50                   ; 運転準備
AND T200                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2040                ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
AND B040                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B040                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
@DW K64 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2040                 ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
AND B040                 ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
OUT B2040                ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
LD M50                   ; 運転準備
ANB X102E                ; RB3_ｱﾗｰﾑ(ARM)
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2041                ; RB3_ｱﾗｰﾑ(ALM)異常BZ停止
AND B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
ORL
MPS
TMH K251 K10             ; RB3_ｱﾗｰﾑ(ALM)確認
MPP
AND T251                 ; RB3_ｱﾗｰﾑ(ALM)確認
OUT B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
@DW K65 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2041                 ; RB3_ｱﾗｰﾑ(ALM)異常BZ停止
AND B041                 ; RB3_ｱﾗｰﾑ(ALM)異常
OUT B2041                ; RB3_ｱﾗｰﾑ(ALM)異常BZ停止
LD X1352                 ; 振動吸引部ﾜｰｸ確認
AND Y2500                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
ANB X135A                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉端
LDB Y2500                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ閉SOL
ANB X135B                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2042                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常BZ停止
AND B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
ORL
MPS
TMR K252 K50             ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS確認
MPP
AND T252                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS確認
OUT B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
@DW K66 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常BZ停止
AND B042                 ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
OUT B2042                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常BZ停止
LD Y2502                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1356                ; 振動吸引治具ｸﾗﾝﾌﾟ1開端
LDB Y2502                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1357                ; 振動吸引治具ｸﾗﾝﾌﾟ1閉端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2043                ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常BZ停止
AND B043                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常
ORL
MPS
TMR K253 K50             ; 加振固定ｸﾗﾝﾌﾟ右開閉LS確認
MPP
AND T253                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS確認
OUT B043                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常
@DW K67 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2043                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常BZ停止
AND B043                 ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常
OUT B2043                ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常BZ停止
LD Y2502                 ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1358                ; 振動吸引治具ｸﾗﾝﾌﾟ2開端
LDB Y2502                ; 振動吸引ﾊﾞｲﾌﾞﾚｰﾀｸﾗﾝﾌﾟ開SOL
ANB X1359                ; 振動吸引治具ｸﾗﾝﾌﾟ2閉端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2044                ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常BZ停止
AND B044                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常
ORL
MPS
TMR K254 K50             ; 加振固定ｸﾗﾝﾌﾟ左開閉LS確認
MPP
AND T254                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS確認
OUT B044                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常
@DW K68 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2044                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常BZ停止
AND B044                 ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常
OUT B2044                ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常BZ停止
LD Y2504                 ; 基板投入搬送ﾊﾝﾄﾞ下降SOL
ANB X1354                ; 基板投入搬送ﾊﾝﾄﾞ下降端
LD Y2505                 ; 基板投入搬送ﾊﾝﾄﾞ上昇SOL
ANB X1355                ; 基板投入搬送ﾊﾝﾄﾞ上昇端
ORL
AND X03                  ; 圧空元圧正常
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2045                ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常BZ停止
AND B045                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常
ORL
MPS
TMR K255 K50             ; 基板投入搬送ﾊﾝﾄﾞ上下LS確認
MPP
AND T255                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS確認
OUT B045                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常
@DW K69 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2045                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常BZ停止
AND B045                 ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常
OUT B2045                ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常BZ停止
LD M1752                 ; 振動吸引治具ｸﾗﾝﾌﾟ閉端
AND X1352                ; 振動吸引部ﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2046                ; 加震部ﾜｰｸ有り異常BZ停止
OR X1352                 ; 振動吸引部ﾜｰｸ確認
AND B046                 ; 加震部ﾜｰｸ有り異常
ORL
OUT B046                 ; 加震部ﾜｰｸ有り異常
@DW K70 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2046                 ; 加震部ﾜｰｸ有り異常BZ停止
AND B046                 ; 加震部ﾜｰｸ有り異常
OUT B2046                ; 加震部ﾜｰｸ有り異常BZ停止
LD Y242B                 ; 集塵機電源ON
ANB X135E                ; 集塵機負圧確認
AND M1985                ; ﾕﾆｯﾄ2起動中
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2047                ; 集塵機負圧異常BZ停止
AND B047                 ; 集塵機負圧異常
ORL
MPS
AND L200                 ; 集塵機有効
TMR K256 K20             ; 集塵機負圧確認ﾃﾞｨﾚｰ
MPP
AND T256                 ; 集塵機負圧確認ﾃﾞｨﾚｰ
OUT B047                 ; 集塵機負圧異常
@DW K71 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2047                 ; 集塵機負圧異常BZ停止
AND B047                 ; 集塵機負圧異常
OUT B2047                ; 集塵機負圧異常BZ停止
LD M50                   ; 運転準備
ANB X1300                ; 集塵機過負荷
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2048                ; 集塵機過負荷異常BZ停止
AND B048                 ; 集塵機過負荷異常
ORL
MPS
TMH K257 K10             ; 加震機動作異常確認
MPP
AND T257                 ; 加震機動作異常確認
OUT B048                 ; 集塵機過負荷異常
@DW K72 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2048                 ; 集塵機過負荷異常BZ停止
AND B048                 ; 集塵機過負荷異常
OUT B2048                ; 集塵機過負荷異常BZ停止
LD M1850                 ; 加震機ON
ANB X130A                ; 加震機動作中
LD M1532                 ; 加震機動作OFF
AND X130A                ; 加震機動作中
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2049                ; 加震機動作異常BZ停止
AND B049                 ; 加震機動作異常
ORL
MPS
TMR K258 K30
MPP
AND T258
OUT B049                 ; 加震機動作異常
@DW K73 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2049                 ; 加震機動作異常BZ停止
AND B049                 ; 加震機動作異常
OUT B2049                ; 加震機動作異常BZ停止
LD Y2506                 ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
ANB X135C                ; 集塵切替え上側
LDB Y2506                ; 振動吸引集塵上ﾀﾞｸﾄ切替えSOL
ANB X135D                ; 集塵切替え下側
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B204A                ; 振動吸引集塵上ﾀﾞｸﾄ切替異常BZ停止
AND B04A                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え異常
ORL
MPS
TMR K259 K50             ; 振動吸引集塵上ﾀﾞｸﾄ切替え確認
MPP
AND T259                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え確認
OUT B04A                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え異常
@DW K74 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B204A                 ; 振動吸引集塵上ﾀﾞｸﾄ切替異常BZ停止
AND B04A                 ; 振動吸引集塵上ﾀﾞｸﾄ切替え異常
OUT B204A                ; 振動吸引集塵上ﾀﾞｸﾄ切替異常BZ停止
LD M1547                 ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANB X1353                ; 中間ﾊﾞｯﾌｧﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B204B                ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常BZ停止
AND B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
ORL
OUT B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
@DW K75 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B204B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常BZ停止
AND B04B                 ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
OUT B204B                ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常BZ停止
LD M1880                 ; 振動吸引部ﾜｰｸ確認記憶
ANB X1352                ; 振動吸引部ﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B204C                ; 振動吸引部ﾜｰｸ無し異常BZ停止
AND B04C                 ; 振動吸引部ﾜｰｸ無し異常
ORL
MPS
TMR K260 K5              ; 振動吸引部ﾜｰｸ無し確認
MPP
AND T260                 ; 振動吸引部ﾜｰｸ無し確認
OUT B04C                 ; 振動吸引部ﾜｰｸ無し異常
@DW K76 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B204C                 ; 振動吸引部ﾜｰｸ無し異常BZ停止
AND B04C                 ; 振動吸引部ﾜｰｸ無し異常
OUT B204C                ; 振動吸引部ﾜｰｸ無し異常BZ停止
LDB M35                  ; 空運転ﾓｰﾄﾞ
AND M72                  ; 自動運転開始
ANB M130                 ; 一時停止
AND X100B                ; RB2_位置決完(PEND)
AND= D1612 K0            ; INDX2=基板有無記憶(1=有)
AND X1352                ; 振動吸引部ﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B204D                ; 振動吸引部ﾜｰｸ残り異常BZ停止
AND B04D                 ; 振動吸引部ﾜｰｸ残り異常
ORL
MPS
TMR K261 K5              ; 振動吸引部ﾜｰｸ残り確認
MPP
AND T261                 ; 振動吸引部ﾜｰｸ残り確認
OUT B04D                 ; 振動吸引部ﾜｰｸ残り異常
@DW K76 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B204D                 ; 振動吸引部ﾜｰｸ残り異常BZ停止
AND B04D                 ; 振動吸引部ﾜｰｸ残り異常
OUT B204D                ; 振動吸引部ﾜｰｸ残り異常BZ停止
LD B040                  ; 振動吸引部原点復帰ﾀｲﾑｱｳﾄ異常
ANB B2040                ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
LD B041                  ; RB3_ｱﾗｰﾑ(ALM)異常
ANB B2041                ; RB3_ｱﾗｰﾑ(ALM)異常BZ停止
ORL
LD B042                  ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常
ANB B2042                ; 振動吸引ﾜｰｸｸﾗﾝﾌﾟ開閉LS異常BZ停止
ORL
LD B043                  ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常
ANB B2043                ; 加振固定ｸﾗﾝﾌﾟ右開閉LS異常BZ停止
ORL
LD B044                  ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常
ANB B2044                ; 加振固定ｸﾗﾝﾌﾟ左開閉LS異常BZ停止
ORL
LD B045                  ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常
ANB B2045                ; 基板投入搬送ﾊﾝﾄﾞ上下LS異常BZ停止
ORL
LD B046                  ; 加震部ﾜｰｸ有り異常
ANB B2046                ; 加震部ﾜｰｸ有り異常BZ停止
ORL
LD B047                  ; 集塵機負圧異常
ANB B2047                ; 集塵機負圧異常BZ停止
ORL
OUT M1891                ; ﾕﾆｯﾄ2異常BZ補助1
LD B048                  ; 集塵機過負荷異常
ANB B2048                ; 集塵機過負荷異常BZ停止
LD B049                  ; 加震機動作異常
ANB B2049                ; 加震機動作異常BZ停止
ORL
LD B04A                  ; 振動吸引集塵上ﾀﾞｸﾄ切替え異常
ANB B204A                ; 振動吸引集塵上ﾀﾞｸﾄ切替異常BZ停止
ORL
LD B04B                  ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常
ANB B204B                ; 中間ﾊﾞｯﾌｧﾜｰｸ落下異常BZ停止
ORL
LD B04C                  ; 振動吸引部ﾜｰｸ無し異常
ANB B204C                ; 振動吸引部ﾜｰｸ無し異常BZ停止
ORL
LD B04D                  ; 振動吸引部ﾜｰｸ残り異常
ANB B204D                ; 振動吸引部ﾜｰｸ残り異常BZ停止
ORL
OUT M1892                ; ﾕﾆｯﾄ2異常BZ補助2
LDB M1891                ; ﾕﾆｯﾄ2異常BZ補助1
ANB M1892                ; ﾕﾆｯﾄ2異常BZ補助2
INV
OUT M1989                ; ﾕﾆｯﾄ2異常BZ補助
END
ENDH
