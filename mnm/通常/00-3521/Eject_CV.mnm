DEVICE:53
;MODULE:Eject_CV
;MODULE_TYPE:0
;□□　　入力受け　　□□
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
;通常は入力ﾕﾆｯﾄに入力時定数(ﾃﾞｨﾚｲ)があるので直接使用しても問題は無い。
LD X1380                 ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
MPS
TMH K801 K5
MPP
AND T801
OUT M4901                ; 排出CVｴﾘｱ内ﾜｰｸなし確認１
LD X1381                 ; 排出CVｴﾘｱ内基板有り確認２
MPS
TMH K802 K5
MPP
AND T802
OUT M4902                ; 排出CVｴﾘｱ内ﾜｰｸなし確認２
LD X1382                 ; 排出CVﾜｰｸ確認
MPS
TMH K803 K5
MPP
AND T803
OUT M4903                ; 排出CVﾜｰｸ確認
LD CR2003                ; 常時OFF
MPS
TMR K804 K3
MPP
AND T804
OUT M4904
;□
;□□ 装置状態・動作条件 □□
;□
LDB Y2424                ; 排出CV移動ON
OUT M4980                ; ﾕﾆｯﾄ8原点
;□ 非常停止
LDB B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0102                ; 基板有り異常
INV
OUT M4981                ; ﾕﾆｯﾄ8全異常
;□ 起動条件
LD M4980                 ; ﾕﾆｯﾄ8原点
ANB M4981                ; ﾕﾆｯﾄ8全異常
ANB M4985                ; ﾕﾆｯﾄ8起動中
OUT M4982                ; ﾕﾆｯﾄ8起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
LD M60                   ; ｴﾗｰ発生
AND M4984                ; ﾕﾆｯﾄ8原点復帰中
ORL
OR M4986                 ; ﾕﾆｯﾄ8ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M4983                ; ﾕﾆｯﾄ8起動OFF
;□ ｻｲｸﾙ停止異常
LDB B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
INV
OUT M4986                ; ﾕﾆｯﾄ8ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M4987                ; ﾕﾆｯﾄ8払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M4988                ; ﾕﾆｯﾄ8警報補助
;□ 機器準備確認
LD CR2002                ; 常時ON
OUT M4990                ; ﾕﾆｯﾄ8機器準備完了
;□ ｴﾗｰ発生　非常停止扱い
LDB B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
ANB B0102                ; 基板有り異常
INV
AND CR2003               ; 常時OFF
OUT M4991                ; ﾕﾆｯﾄ8ｴﾗｰ発生
;□ 払出しﾜｰｸ無し
LD M36                   ; 払出ﾓｰﾄﾞ
ANB Y2424                ; 排出CV移動ON
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D4610 K0            ; 排出CV排出しﾜｰｸ確認記憶
ORL
ANL
TMR K814 K10             ; UNIT8払出処理ﾜｰｸ無
LD T814                  ; UNIT8払出処理ﾜｰｸ無
OUT M4993                ; ﾕﾆｯﾄ8ﾜｰｸ無し
LD M1493                 ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
AND M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
AND M2993                ; ﾕﾆｯﾄ4ﾜｰｸ無し
AND M3493                ; ﾕﾆｯﾄ5ﾜｰｸ無し
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し
OUT M4994                ; 前工程ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
;□ 原点復帰
LDP M82                  ; 原点復帰開始
ANB M4984                ; ﾕﾆｯﾄ8原点復帰中
OR M4984                 ; ﾕﾆｯﾄ8原点復帰中
ANB M4983                ; ﾕﾆｯﾄ8起動OFF
ANB M4991                ; ﾕﾆｯﾄ8ｴﾗｰ発生
ANB M4749                ; ｻｲｸﾙ終了
MPS
OUT M4984                ; ﾕﾆｯﾄ8原点復帰中
MPP
AND M401                 ; 扉閉ﾃﾞｨﾚｰON
TMR K800 K100            ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ
;□ 起動中
LD M73                   ; 主幹起動指令
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OR M4485                 ; ﾕﾆｯﾄ7起動中
AND M4982                ; ﾕﾆｯﾄ8起動条件
OR M4985                 ; ﾕﾆｯﾄ8起動中
ANB M4983                ; ﾕﾆｯﾄ8起動OFF
ANB M4749                ; ｻｲｸﾙ終了
OUT M4985                ; ﾕﾆｯﾄ8起動中
;□□□□□□□□□□□□□□□
;□　　 　UNIT8自動回路  　　□
;□□□□□□□□□□□□□□□
;ﾕﾆｯﾄ8 停止判定
LD= D1000 D1005          ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ1-1動作中判定
ANB Y2424                ; 排出CV移動ON
ORB M4985                ; ﾕﾆｯﾄ8起動中
TMR K810 K20             ; ﾕﾆｯﾄ8動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D4500 D4505          ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ8動作中判定
LD T810                  ; ﾕﾆｯﾄ8動作無し判定ﾀｲﾏ
OUT M4992                ; ﾕﾆｯﾄ8動作無し
;<h1/>ﾕﾆｯﾄ1 自動運転ステップ開始
LD CR2002                ; 常時ON
FMOV K0 M4500 K18
LDB M4984                ; ﾕﾆｯﾄ8原点復帰中
ANB M4985                ; ﾕﾆｯﾄ8起動中
MOV K0 D4500             ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4984                 ; ﾕﾆｯﾄ8原点復帰中
OR M4985                 ; ﾕﾆｯﾄ8起動中
MOV D4500 Z1             ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
SET M4500:Z1
LD M4500
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4501
ANB M130                 ; 一時停止
MPS
ANB M4984                ; ﾕﾆｯﾄ8原点復帰中
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
LD M84                   ; 原点復帰起動確認
AND M4984                ; ﾕﾆｯﾄ8原点復帰中
AND M84                  ; 原点復帰起動確認
ANL
MOV K250 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4502
ANB M130                 ; 一時停止
MPS
LD X1382                 ; 排出CVﾜｰｸ確認
OR= D4710 K1             ; 排出CV取り出しﾜｰｸ確認記憶
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MRD
LD= D4710 K1             ; 排出CV取り出しﾜｰｸ確認記憶
AND M35                  ; 空運転ﾓｰﾄﾞ
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MRD
LD X1380                 ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
OR X1381                 ; 排出CVｴﾘｱ内基板有り確認２
OR= D4610 K1             ; 排出CV排出しﾜｰｸ確認記憶
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
@MOV K20 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MRD
LD= D4610 K1             ; 排出CV排出しﾜｰｸ確認記憶
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
AND M35                  ; 空運転ﾓｰﾄﾞ
ANL
@MOV K20 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M4485                ; ﾕﾆｯﾄ7起動中
AND= D4610 K0            ; 排出CV排出しﾜｰｸ確認記憶
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ANB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
ANL
TMR K784 K5
CON
AND T784
MOV K249 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4503
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4504
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4505                 ; 基板取り出し要求ON
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
LD X1382                 ; 排出CVﾜｰｸ確認
AND M4850                ; 基板取り出し要求１
OR= D4710 K1             ; 排出CV取り出しﾜｰｸ確認記憶
LDB X1382                ; 排出CVﾜｰｸ確認
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ORL
ANL
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D4710 K1            ; 排出CV取り出しﾜｰｸ確認記憶
AND M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4505                 ; 基板取り出し要求ON
OR M4850                 ; 基板取り出し要求１
ANB M4509                ; 基板取り出し要求OFF
AND X1382                ; 排出CVﾜｰｸ確認
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT M4850                ; 基板取り出し要求１
LD M4505                 ; 基板取り出し要求ON
OR M4851                 ; 基板取り出し要求ﾄﾞﾗｲRUN用
ANB M4509                ; 基板取り出し要求OFF
AND= D4710 K1            ; 排出CV取り出しﾜｰｸ確認記憶
AND M35                  ; 空運転ﾓｰﾄﾞ
OUT M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
LD M4506
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB X1382                ; 排出CVﾜｰｸ確認
LD M35                   ; 空運転ﾓｰﾄﾞ
AND X138F                ; 排出部NGﾘｾｯﾄSW
ORL
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M4485                ; ﾕﾆｯﾄ7起動中
AND X1382                ; 排出CVﾜｰｸ確認
ANL
TMR K880 K5              ; ﾕﾆｯﾄ8終了確認
CON
AND T880                 ; ﾕﾆｯﾄ8終了確認
MOV K249 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4507
MPS
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT W0103.9              ; 排出CV2基板取出し要求
MRD
AND W03.9                ; 排出CV2基板取出し完了
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
AND M35                  ; 空運転ﾓｰﾄﾞ
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4508
MPS
MOV K0 D4710             ; 排出CV取り出しﾜｰｸ確認記憶
MPP
LDB X1382                ; 排出CVﾜｰｸ確認
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4509                 ; 基板取り出し要求OFF
ANB M4850                ; 基板取り出し要求１
ANB M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4510
MOV K249 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4520
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4521
ANB M130                 ; 一時停止
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB X1382                ; 排出CVﾜｰｸ確認
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ANB M4471                ; 排出ﾌﾟｯｼｬｰRB7基板押出し位置
ANB M4360                ; 排出CVへ基板排出中
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
LD M35                   ; 空運転ﾓｰﾄﾞ
AND= D4710 K0            ; 排出CV取り出しﾜｰｸ確認記憶
ANB M4471                ; 排出ﾌﾟｯｼｬｰRB7基板押出し位置
ANL
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4522                 ; 排出CV　回転ON
AND Y2424                ; 排出CV移動ON
MPS
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
ANL
MPS
AND M4903                ; 排出CVﾜｰｸ確認
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
TMR K871 K60
CON
AND T871
ANB X1382                ; 排出CVﾜｰｸ確認
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
AND M35                  ; 空運転ﾓｰﾄﾞ
TMR K872 K70
CON
AND T872
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4523
ANB M130                 ; 一時停止
@MOV D4610 D4710         ; 排出CV排出しﾜｰｸ確認記憶  排出CV取り出しﾜｰｸ確認記憶
@MOV K0 D4610            ; 排出CV排出しﾜｰｸ確認記憶
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4524
MPS
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT W0103.8              ; 排出CV1基板ｼﾌﾄ要求
MRD
AND W03.8                ; 排出CV1基板ｼﾌﾄ完了
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
MPP
AND M35                  ; 空運転ﾓｰﾄﾞ
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4525
MOV K249 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
;<h1/>ﾕﾆｯﾄ8原点復帰
LD M4750
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4751                 ; 基板確認
ANB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
ANB X1382                ; 排出CVﾜｰｸ確認
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4752                 ; ｺﾝﾍﾞｱ回転
AND Y2424                ; 排出CV移動ON
TMR K870 K70
CON
AND T870
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4753                 ; 基板確認
ANB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
ANB X1382                ; 排出CVﾜｰｸ確認
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4754
MOV K0 D4610             ; 排出CV排出しﾜｰｸ確認記憶
MOV K0 D4710             ; 排出CV取り出しﾜｰｸ確認記憶
INC D4500                ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
LD M4755
MOV K249 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND CR2003               ; 常時OFF
MOV K62 D4500            ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M1062                 ; ﾕﾆｯﾄ1-1ｻｲｸﾙ停止処理
OR CR2003                ; 常時OFF
AND CR2003               ; 常時OFF
@MOV K63 D4500           ; ﾕﾆｯﾄ8自動運転ｽﾃｯﾌﾟ
;<h1/>UNIT8手動回路
LD CR2002                ; 常時ON
OUT M4800
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M4985                ; ﾕﾆｯﾄ8起動中
ANB M4984                ; ﾕﾆｯﾄ8原点復帰中
MC
LD CR2002                ; 常時ON
OUT M4801                ; 排出CV　ON可
LDP B1160                ; 排出CV　ON_SW
AND M4801                ; 排出CV　ON可
OR M4802                 ; 排出CV　ON
ANB M4804                ; 排出CV　OFF
ANB X1382                ; 排出CVﾜｰｸ確認
OUT M4802                ; 排出CV　ON
LD CR2002                ; 常時ON
OUT M4803                ; 排出CV　OFF可
LD B1161                 ; 排出CV　OFF_SW
AND M4803                ; 排出CV　OFF可
OUT M4804                ; 排出CV　OFF
MCR
;<h1/>出力(シリンダ等)
LDB M4802                ; 排出CV　ON
ANB M4522                ; 排出CV　回転ON
ANB M4752                ; ｺﾝﾍﾞｱ回転
ANB M4361                ; 基板排出排出CV回転ON
INV
OUT Y2424                ; 排出CV移動ON
LDB M4850                ; 基板取り出し要求１
ANB M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
INV
OUT Y2440                ; 排出CV基板取り出し可PL
LDB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT Y2441                ; 排出CV基板取り出し不可PL
LDB M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
ANB B0101                ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT Y2442                ; 排出CVBZﾘｾｯﾄPL
LDB M4851                ; 基板取り出し要求ﾄﾞﾗｲRUN用
ANB M4989                ; ﾕﾆｯﾄ8異常BZ補助
ANB M1460                ; 28秒ﾌﾞｻﾞｰ
INV
OUT Y2443                ; 排出CVBZ
;□□　　パネル表示　　□□
LD Y2424                 ; 排出CV移動ON
OUT B1660
LDB Y2424                ; 排出CV移動ON
OUT B1661
;□□　　異常検出　　□□
LD M50                   ; 運転準備
AND T800                 ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
AND B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
@DW K256 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2100                 ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
AND B0100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
OUT B2100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
LD M72                   ; 自動運転開始
AND M4850                ; 基板取り出し要求１
LDB X138F                ; 排出部NGﾘｾｯﾄSW
AND B0101                ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
ORL
MPS
AND X1382                ; 排出CVﾜｰｸ確認
TMR K851 D4550           ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ  排出CVﾜｰｸ取出し要求ﾀｲﾑ設定値
MPP
AND T851                 ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
OUT B0101                ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
@DW K257 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR X138F                 ; 排出部NGﾘｾｯﾄSW
OR B2101
AND B0101                ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
OUT B2101
LDB X1380                ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
ANB X1381                ; 排出CVｴﾘｱ内基板有り確認２
ANB X1382                ; 排出CVﾜｰｸ確認
INV
LD M4753                 ; 基板確認
OR M4751                 ; 基板確認
OR M17                   ; 原点復帰PB
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORB B2102                ; 基板有り異常BZ停止
OR X1380                 ; 排出CVｴﾘｱ内ﾜｰｸ基板有り確認１
OR X1381                 ; 排出CVｴﾘｱ内基板有り確認２
OR X1382                 ; 排出CVﾜｰｸ確認
AND B0102                ; 基板有り異常
ORL
OUT B0102                ; 基板有り異常
@DW K258 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR B2102                 ; 基板有り異常BZ停止
AND B0102                ; 基板有り異常
OUT B2102                ; 基板有り異常BZ停止
LD B0100                 ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常
ANB B2100                ; ﾕﾆｯﾄ8原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
LD B0101                 ; 排出CVﾜｰｸ取出し要求ﾀｲﾑｵｰﾊﾞｰ
ANB B2101
ORL
LD B0102                 ; 基板有り異常
ANB B2102                ; 基板有り異常BZ停止
ORL
OUT M4989                ; ﾕﾆｯﾄ8異常BZ補助
END
ENDH
