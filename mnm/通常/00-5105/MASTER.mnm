DEVICE:51
;MODULE:MASTER
;MODULE_TYPE:0
LD CR2002                ; 常時ON
MOV #127 DM51            ; PLCB接続局番指定
LD CR2008                ; 運転開始時1ｽｷｬﾝON
MOV #0 DM3008            ; _MainTP　切替ﾍﾟｰｼﾞNo
LDB MR102                ; 手動中
ANB MR109                ; 運転待機
ANB MR107                ; 運転待機完了
ANB MR110                ; 自動運転中
ANB MR113                ; 自動終了中
ANB MR200                ; 停止要求待ち
AND MR011                ; 運転準備
OUT MR114                ; 停止中
;手動
LDB MR110                ; 自動運転中
AND MR114                ; 停止中
OUT MR115                ; TP手動移行OK
LDP MR101                ; 手動開始(TPSW）
LD<= #2 DM3000           ; _MainTP　表示中ﾍﾟｰｼﾞNo
AND>= #9 DM3000          ; _MainTP　表示中ﾍﾟｰｼﾞNo
ORL
OR MR102                 ; 手動中
ANB MR100                ; 停止要求ON
ANB MR107                ; 運転待機完了
ANB MR109                ; 運転待機
ANB MR110                ; 自動運転中
ANB MR113                ; 自動終了中
AND<> #0 DM3000          ; _MainTP　表示中ﾍﾟｰｼﾞNo
ANB MR010                ; 装置非常停止
AND MR011                ; 運転準備
OUT MR102                ; 手動中
LDP MR101                ; 手動開始(TPSW）
ANB MR110                ; 自動運転中
ANB MR113                ; 自動終了中
MOV #2 DM3008            ; _MainTP　切替ﾍﾟｰｼﾞNo
;自動
LD MR109                 ; 運転待機
AND MR100                ; 停止要求ON
@SET @MR002
LD MR6711                ; ﾘﾓｰﾄ局Ready
AND MR6911               ; ﾘﾓｰﾄ局Ready:入口ﾛｰﾀﾘ
AND MR7111               ; ﾘﾓｰﾄ局Ready:乾燥用
AND MR7311               ; ﾘﾓｰﾄ局Ready:炭化用
AND MR7511               ; ﾘﾓｰﾄ局Ready:出口ﾛｰﾀﾘ
AND MR7711               ; ﾘﾓｰﾄ局Ready:移送
AND MR7911               ; ﾘﾓｰﾄ局Ready:消火･冷却
OUT @MR003               ; ﾘﾓｰﾄOK
LDP MR000                ; 運転入力
AND MR114                ; 停止中
AND @MR003               ; ﾘﾓｰﾄOK
LD MR109                 ; 運転待機
ANB MR107                ; 運転待機完了
ORL
ANB MR102                ; 手動中
ANB MR100                ; 停止要求ON
ANB MR3000               ; 異常発生
ANB MR108                ; 自動終了SW
ANB MR010                ; 装置非常停止
AND MR011                ; 運転準備
OUT MR109                ; 運転待機
LD MR109                 ; 運転待機
OR MR107                 ; 運転待機完了
ANB MR102                ; 手動中
ANB MR100                ; 停止要求ON
ANB MR3000               ; 異常発生
ANB MR108                ; 自動終了SW
ANB MR110                ; 自動運転中
AND MR002                ; 供給用ﾛｰﾀﾘｰﾊﾞﾙﾌﾞ定位置
ANB MR010                ; 装置非常停止
OUT MR107                ; 運転待機完了
LD MR107                 ; 運転待機完了
OR MR110                 ; 自動運転中
ANB MR100                ; 停止要求ON
ANB MR3000               ; 異常発生
ANB MR108                ; 自動終了SW
ANB MR010                ; 装置非常停止
AND MR011                ; 運転準備
OUT MR110                ; 自動運転中
@SET MR200               ; 停止要求待ち
LD MR200                 ; 停止要求待ち
LD MR100                 ; 停止要求ON
ORB MR110                ; 自動運転中
ANL
ORP @MR002
LD MR113                 ; 自動終了中
ANB @MR001
ORL
ANB MR010                ; 装置非常停止
AND MR011                ; 運転準備
OUT MR113                ; 自動終了中
RES MR200                ; 停止要求待ち
LD MR113                 ; 自動終了中
MPS
C #1 DM13 CR2006         ; 自動停止後ｽｸﾘｭｰ停止  回転終了設定時間  1sｸﾛｯｸﾊﾟﾙｽ
MPP
ANP CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LDA DM9                  ; 終了経過時間現在値
CON
ADD #1
CON
STA DM9                  ; 終了経過時間現在値
LD CR2002                ; 常時ON
LDA DM13                 ; 回転終了設定時間
CON
SUB DM9                  ; 終了経過時間現在値
CON
STA DM12                 ; 終了残り時間
LD C1                    ; 自動停止後ｽｸﾘｭｰ停止
SET @MR001
LDF MR113                ; 自動終了中
MPS
RES @MR002
CON
RES @MR001
MPP
MOV #0 DM9               ; 終了経過時間現在値
;動作中
LD MR110                 ; 自動運転中
OR MR113                 ; 自動終了中
OR MR107                 ; 運転待機完了
OR MR109                 ; 運転待機
OUT MR503                ; 運転中ﾗﾝﾌﾟON
END
ENDH
