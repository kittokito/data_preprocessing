DEVICE:128
;MODULE:U02
;MODULE_TYPE:0
;SCRIPT_TYPE:
;  ▽_入力受け
;<h1/>□_装置状態・動作条件
LD X409                  ; ﾊﾟﾚｯﾄ残留確認1(前半部)
OR X40A                  ; ﾊﾟﾚｯﾄ残留確認2(後半部)
OR= D20080 K1            ; CV 除材部1在荷(1=有)
OUT M2590                ; 冷却CV内ﾊﾟﾚｯﾄ有
LD X40B                  ; 取出しﾊﾟﾚｯﾄ検出1(内)
OR X40C                  ; 取出しﾊﾟﾚｯﾄ検出2(外)
TMH K211 K50             ; 取出し部ﾊﾟﾚｯﾄ検出
LDB X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
ANB X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
TMR K212 K20             ; 取出し部ﾊﾟﾚｯﾄ無し
LDP M74                  ; 自動運転中
MPS
LDA.D D20810             ; 冷却時間(0.01s)
CON
SUB.D K200
CON
STA.D D2106              ; Shit開始1s前
CON
SUB.D K200
CON
STA.D D2104              ; Shit開始2s前
MPP
MOV.D K260 D2120         ; 一時停止有効時間
LD M74                   ; 自動運転中
LD X40B                  ; 取出しﾊﾟﾚｯﾄ検出1(内)
OR X40C                  ; 取出しﾊﾟﾚｯﾄ検出2(外)
ANL
MPS
ANB T241                 ; 除材督促    第二形態
TMR K240 K100            ; 除材督促    第一形態
MRD
TMR K241 K200            ; 除材督促    第二形態
MPP
LDB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
ORB X409                 ; ﾊﾟﾚｯﾄ残留確認1(前半部)
ORB X40A                 ; ﾊﾟﾚｯﾄ残留確認2(後半部)
ANL
TMR K242 D20122          ; 督促ﾌﾞｻﾞｰOFF  冷却CVﾌﾞｻﾞｰOFF時間(0.1s)
LD= D20090 K1            ; CV 除材部2在荷(1=有)
AND T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
DW K0 D20090             ; CV 除材部2在荷(1=有)
;  ▽_原点
LD M2800                 ; Cycle1原点
LD M2820                 ; Cycle2原点
OR CR2002                ; 常時ON
ANL
OUT M2980                ; ﾕﾆｯﾄ2原点
;  ▽_即停止異常
LDB M2801                ; Cycle1即停止異常
ANB M2821                ; Cycle2即停止異常
INV
OUT M2981                ; ﾕﾆｯﾄ2即停止異常
;  ▽_ﾕﾆｯﾄ起動条件
LD CR2003                ; 常時OFF
OUT M2982                ; ﾕﾆｯﾄ2起動条件
;  ▽_原点復帰OFF
LDB M50                  ; 運転準備
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
INV
ORL
OR T15                   ; 強制停止ONﾀｲﾏ
OUT M2983                ; ﾕﾆｯﾄ2_原点復帰起動OFF
;  ▽_原点復帰中
LD M82                   ; 原点復帰開始
ANB M2984                ; ﾕﾆｯﾄ2原点復帰中
OR M2984                 ; ﾕﾆｯﾄ2原点復帰中
ANB M2983                ; ﾕﾆｯﾄ2_原点復帰起動OFF
ANB M2969                ; Org_StepEnd
OUT M2984                ; ﾕﾆｯﾄ2原点復帰中
;  ▽_サイクル起動中
LDB M2804                ; Cycle1起動中
ANB M2824                ; Cycle2起動中
OUB M2985                ; ﾕﾆｯﾄ2起動中
;  ▽_サイクル停止異常
LDB M2805                ; Cycle1ｻｲｸﾙ停止異常
ANB M2825                ; Cycle2ｻｲｸﾙ停止異常
OUB M2986                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
;  ▽_払い出し後に停止する異常
LDB CR2003               ; 常時OFF
OUB M2987                ; ﾕﾆｯﾄ2払出後停止異常
;  ▽_警報ﾌﾞｻﾞｰ
LDB B047                 ; 071)冷却FAN7回転異常
ANB B05C                 ; 092)
OUB M2988                ; ﾕﾆｯﾄ2警報補助
;  ▽_機器準備完了
LD X40F                  ; 冷却CV電源正常
ANB X40E                 ; 冷却CV ﾄﾘｯﾌﾟ出力
OUT M2990                ; ﾕﾆｯﾄ2機器準備完了
;  ▽_一時停止異常
LDB M2806                ; Cycle1ｴﾗｰ発生(一時停止処理)
ANB M2826                ; Cycle2ｴﾗｰ発生(一時停止処理)
OUB M2991                ; ﾕﾆｯﾄ2ｴﾗｰ発生
;  ▽_ﾕﾆｯﾄ停止中ﾌﾗｸﾞ
LD M2809                 ; Cycle1動作無し完了
LD M2827                 ; Cycle2動作無し
OR CR2002                ; 常時ON
ANL
OUT M2992                ; ﾕﾆｯﾄ2動作無し確認
;  ▽_ﾕﾆｯﾄ1ﾜｰｸ無確認
LDB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
ANB X409                 ; ﾊﾟﾚｯﾄ残留確認1(前半部)
ANB X40A                 ; ﾊﾟﾚｯﾄ残留確認2(後半部)
ANB X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
ANB X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
OUT M2993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
;<h1/>□_自動回路
;<h1/>▽_Cycle1
LD CR2002                ; 常時ON
OUT M2800                ; Cycle1原点
LDB B040                 ; 064)冷却CV異常
INV
OUT M2801                ; Cycle1即停止異常
LD M50                   ; 運転準備
ANB M2803                ; Cycle1起動OFF
ANB M2804                ; Cycle1起動中
ANB M130                 ; 一時停止
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M2808                ; U2_Cycle1払出完了
OUT M2802                ; Cycle1起動条件
LDB M50                  ; 運転準備
OR M2986                 ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
OR M51                   ; 即停止異常(緊急停止)
OR T15                   ; 強制停止ONﾀｲﾏ
OUT M2803                ; Cycle1起動OFF
LD M73                   ; 各工程起動指令
AND M2802                ; Cycle1起動条件
OR M2804                 ; Cycle1起動中
ANB M2803                ; Cycle1起動OFF
ANB M2199                ; U2C1_S199__動作終了
OUT M2804                ; Cycle1起動中
LDB CR2003               ; 常時OFF
INV
OUT M2805                ; Cycle1ｻｲｸﾙ停止異常
LDB B050                 ; 080)
INV
OUT M2806                ; Cycle1ｴﾗｰ発生(一時停止処理)
LDB CR2003               ; 常時OFF
INV
OUT M2813                ; Cycle1ｴﾗｰ発生(動作継続、RBPrgはｽ
LD CR2002                ; 常時ON
OUT M2895                ; UT2 ｼﾘﾝﾀﾞ動作中無し
LDB M2000                ; U2C1_S000
ANB M2190                ; U2C1_S190
INV
AND M130                 ; 一時停止
OUT M2807                ; Cycle1動作無し
LD M2807                 ; Cycle1動作無し
AND M2895                ; UT2 ｼﾘﾝﾀﾞ動作中無し
ANB CR2003               ; 常時OFF
ORB M2804                ; Cycle1起動中
OR M2808                 ; U2_Cycle1払出完了
AND M130                 ; 一時停止
OUT M2809                ; Cycle1動作無し完了
;ｻｲｸﾙﾀｲﾑ計測
LD M2804                 ; Cycle1起動中
LD>= D2000 K10           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
AND<= D2000 K71          ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD>= D2000 K100          ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
AND<= D2000 K141         ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
ORL
ANL
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC D2899               ; ｻｲｸﾙﾀｲﾑ計測
LDF M2804                ; Cycle1起動中
BMOV D2899 D2900 K10     ; ｻｲｸﾙﾀｲﾑ計測  ｻｲｸﾙﾀｲﾑ
DW K0 D2899              ; ｻｲｸﾙﾀｲﾑ計測
;  ▽_Cycle1ｽﾃｯﾌﾟ回路
LD CR2002                ; 常時ON
ZRES M2000 M2199         ; U2C1_S000  U2C1_S199__動作終了
LDB M2804                ; Cycle1起動中
MOV K0 D2000             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M2804                 ; Cycle1起動中
MOV D2000 Z1             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
SET M2000:Z1             ; U2C1_S000
;<h1/>▽_Cycle1動作回路
LD M2000                 ; U2C1_S000
ANB M130                 ; 一時停止
MPS
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
MPS
ANB M2598                ; 初回ﾜｰｸ確認済
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND M2598                ; 初回ﾜｰｸ確認済
MOV K10 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
MOV K190 D2000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>  ▽_S001 初期ﾜｰｸ除材
LD M2001                 ; U2C1_S001
OR M2598                 ; 初回ﾜｰｸ確認済
AND M74                  ; 自動運転中
OUT M2598                ; 初回ﾜｰｸ確認済
LD M2001                 ; U2C1_S001
AND M2598                ; 初回ﾜｰｸ確認済
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPS
AND T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
DW K0 D20090             ; CV 除材部2在荷(1=有)
MPP
LDB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
ANB X409                 ; ﾊﾟﾚｯﾄ残留確認1(前半部)
ANB X40A                 ; ﾊﾟﾚｯﾄ残留確認2(後半部)
ANL
FMOV K0 D20020 K60       ; CV 前半部1在荷(1=有)
;送り中断確認 / Shift interrupt check
LD M2002                 ; U2C1_S002
MPS
AND=.D D20110 K0         ; 移動時間ｶｳﾝﾄ
MOV K10 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND<>.D D20110 K0        ; 移動時間ｶｳﾝﾄ
RES B1021                ; 動作ﾘｾｯﾄsw
CON
RES B1022                ; 確認sw
CON
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M2003                 ; U2C1_S003
MPS
AND B1022                ; 確認sw
MPS
ANB B1021                ; 動作ﾘｾｯﾄsw
MOV K10 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND B1021                ; 動作ﾘｾｯﾄsw
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M2595                ; Reset画面表示要求
LD M2004                 ; U2C1_S004
DW.D K0 D20110           ; 移動時間ｶｳﾝﾄ
BMOV D20010 D20020 K80   ; CV 投入部在荷(1=有)  CV 前半部1在荷(1=有)
MOV K10 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>  ▽_S010 CV送り判断 / CV shift condition check
LD M2010                 ; U2C1_S010
MPS
ANB M2590                ; 冷却CV内ﾊﾟﾚｯﾄ有
DW.D K0 D20112           ; 冷却時間ｶｳﾝﾄ
CON
MOV K20 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND M2590                ; 冷却CV内ﾊﾟﾚｯﾄ有
MOV K30 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>  ▽_S020 ﾊﾟﾚｯﾄ投入待ち / Waiting for Palette supply
LD M2020                 ; U2C1_S020
MPS
AND X408                 ; 投入部ﾊﾟﾚｯﾄ検出
TMR K230 K10             ; 投入ｳｪｲﾄ
CON
AND T230                 ; 投入ｳｪｲﾄ
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LDB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
AND M36                  ; 払出ﾓｰﾄﾞ
AND M1808                ; U1_Cycle1払出完了
ANL
MOV K190 D2000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M2570                ; ﾊﾟﾚｯﾄ投入待ち
LD M2021                 ; U2C1_S021
MPS
AND X408                 ; 投入部ﾊﾟﾚｯﾄ検出
MPS
AND T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
MOV K50 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
ANB T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
OUT M2572                ; 出口ﾊﾟﾚｯﾄ除材待ち
MPP
ANB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
MOV K20 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>  ▽_S030 ﾊﾟﾚｯﾄ冷却完了待ち / Waiting for Palette cooling time up
LD M2003                 ; U2C1_S003
OR M2030                 ; U2C1_S030
AND CR2004               ; 10msｸﾛｯｸﾊﾟﾙｽ
@INC.D D20112            ; 冷却時間ｶｳﾝﾄ
LD M2030                 ; U2C1_S030
MPS
AND<=.D D20810 D20112    ; 冷却時間(0.01s)  冷却時間ｶｳﾝﾄ
MPS
AND T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
ANB T212                 ; 取出し部ﾊﾟﾚｯﾄ無し
OUT M2585                ; CV出口渋滞中1
MRD
AND>.D D20810 D20112     ; 冷却時間(0.01s)  冷却時間ｶｳﾝﾄ
MPS
AND<=.D D2104 D20112     ; Shit開始2s前  冷却時間ｶｳﾝﾄ
OUT M2593                ; Shift開始2s表示
MPP
AND<=.D D2106 D20112     ; Shit開始1s前  冷却時間ｶｳﾝﾄ
OUT M2594                ; Shift開始1s表示
MPP
ANB M2594                ; Shift開始1s表示
OUT M2571                ; 冷却中
;ｶｳﾝﾀ操作 / Counter setting
LD M2031                 ; U2C1_S031
MOV K50 D2000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPS
ANB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
LDA.D D20112             ; 冷却時間ｶｳﾝﾄ
CON
SUB.D D20810             ; 冷却時間(0.01s)
CON
STA.D D20112             ; 冷却時間ｶｳﾝﾄ
MPP
AND X408                 ; 投入部ﾊﾟﾚｯﾄ検出
DW.D K0 D20112           ; 冷却時間ｶｳﾝﾄ
;<h1/>  ▽_S050 冷却CV1ﾋﾟｯﾁｼﾌﾄ / 1pitch shift
LD M2050                 ; U2C1_S050
MPS
AND M2401                ; CV1ﾋﾟｯﾁ搬送完了
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
OUT M2402                ; CV1ﾋﾟｯﾁ搬送動作
MPP
AND X408                 ; 投入部ﾊﾟﾚｯﾄ検出
DW.D K0 D20112           ; 冷却時間ｶｳﾝﾄ
LD M2051                 ; U2C1_S051
ANB M2401                ; CV1ﾋﾟｯﾁ搬送完了
MOV K190 D2000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>  ▽_ｻｲｸﾙ停止
LD M2190                 ; U2C1_S190
AND M2993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
OR M2808                 ; U2_Cycle1払出完了
AND M36                  ; 払出ﾓｰﾄﾞ
OUT M2808                ; U2_Cycle1払出完了
LD M2190                 ; U2C1_S190
ANB M130                 ; 一時停止
MPS
ANB M2808                ; U2_Cycle1払出完了
MOV K199 D2000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND M2808                ; U2_Cycle1払出完了
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;
LD M2191                 ; U2C1_S191
MPS
ANB Y450                 ; 冷却ﾌｧﾝ 電源ON
INC D2000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M2612                ; Fan停止
LD M2192                 ; U2C1_S192
MOV K199 D2000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>□_原点復帰回路
LD CR2002                ; 常時ON
ZRES M2900 M2969         ; Org_S00  Org_StepEnd
LDB M2984                ; ﾕﾆｯﾄ2原点復帰中
MOV K0 D2010             ; 原点復帰ｽﾃｯﾌﾟ
LD M2984                 ; ﾕﾆｯﾄ2原点復帰中
MOV D2010 Z1             ; 原点復帰ｽﾃｯﾌﾟ
SET M2900:Z1             ; Org_S00
;  ▽_原点復帰開始
;FAN停止 / FAN stop
LD M2900                 ; Org_S00
MPS
ANB Y450                 ; 冷却ﾌｧﾝ 電源ON
INC D2010                ; 原点復帰ｽﾃｯﾌﾟ
MPP
OUT M2613                ; Fan停止
;ｺﾝﾍﾞﾔ内在荷ﾁｪｯｸ / Palette check in Cooling-CV
LD M2901                 ; Org_S01
MPS
ANB X409                 ; ﾊﾟﾚｯﾄ残留確認1(前半部)
FMOV K0 D20020 K30       ; CV 前半部1在荷(1=有)
MRD
ANB X40A                 ; ﾊﾟﾚｯﾄ残留確認2(後半部)
FMOV K0 D20050 K30       ; CV 後半部1在荷(1=有)
MRD
LDB X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
ANB X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
ANL
FMOV K0 D20090 K10       ; CV 除材部2在荷(1=有)
MPP
INC D2010                ; 原点復帰ｽﾃｯﾌﾟ
;
LD M2902                 ; Org_S02
MOV K31 D2010            ; 原点復帰ｽﾃｯﾌﾟ
;終了
LD M2931                 ; Org_S31
AND M2980                ; ﾕﾆｯﾄ2原点
ANB M82                  ; 原点復帰開始
MOV K69 D2010            ; 原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M162                 ; ｴﾘｱS除外手動操作可能条件
ANB M2984                ; ﾕﾆｯﾄ2原点復帰中
ANB M2985                ; ﾕﾆｯﾄ2起動中
ANB M2986                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ANB M2805                ; Cycle1ｻｲｸﾙ停止異常
ANB M2813                ; Cycle1ｴﾗｰ発生(動作継続、RBPrgはｽ
MC
;<h1/>    Fan
LDP B10A0                ; UNIT2-1-1 TP-PB
ANB B10A1                ; UNIT2-1-2 TP-PB
OUT M2600                ; Fan回転
LDP B10A1                ; UNIT2-1-2 TP-PB
ANB B10A0                ; UNIT2-1-1 TP-PB
OUT M2611                ; Fan停止
;<h1/>    冷却ｺﾝﾍﾞﾔ / Cooling conveyor
LD B10A2                 ; UNIT2-1-3 TP-PB
OR B10A3                 ; UNIT2-1-4 TP-PB
OUT M2620                ; 冷却CV 回転
LD B10A3                 ; UNIT2-1-4 TP-PB
OUT M2640                ; 冷却CV逆回転
;<h1/>    1cycle
;搬送1cycle / 1 pitch shift 1cycle
LDP B10A4                ; UNIT2-1-5 TP-PB
OR M2541                 ; 1ﾋﾟｯﾁｼﾌﾄ開始
ANB M2542                ; 1ﾋﾟｯﾁｼﾌﾄ終了
OUT M2541                ; 1ﾋﾟｯﾁｼﾌﾄ開始
OUT M2403                ; CV1ﾋﾟｯﾁ搬送動作
LD M2401                 ; CV1ﾋﾟｯﾁ搬送完了
AND M2541                ; 1ﾋﾟｯﾁｼﾌﾄ開始
OUT M2542                ; 1ﾋﾟｯﾁｼﾌﾄ終了
MCR
;<h1/>○  Subﾙｰﾁﾝ  ○
;<h1/>  冷却CV 1ﾋﾟｯﾁ搬送 / Cooling-CV 1pitch shift
LD CR2002                ; 常時ON
ZRES M2300 M2319         ; U2Sb1_S00  U2Sb1_S19 完了
LDB M2402                ; CV1ﾋﾟｯﾁ搬送動作
ANB M2403                ; CV1ﾋﾟｯﾁ搬送動作
INV
OUT M2400                ; CV1ﾋﾟｯﾁ搬送中
LDB M2400                ; CV1ﾋﾟｯﾁ搬送中
MOV K0 D2005             ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M2400                 ; CV1ﾋﾟｯﾁ搬送中
MOV D2005 Z3             ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MPS
SET M2300:Z3             ; U2Sb1_S00
MPP
AND M2319                ; U2Sb1_S19 完了
OUT M2401                ; CV1ﾋﾟｯﾁ搬送完了
;
LD M2300                 ; U2Sb1_S00
MPS
INC D2005                ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND=.D D20110 K0         ; 移動時間ｶｳﾝﾄ
MPS
AND X408                 ; 投入部ﾊﾟﾚｯﾄ検出
DW K1 D20010             ; CV 投入部在荷(1=有)
MPP
ANB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
DW K0 D20010             ; CV 投入部在荷(1=有)
LD M2301                 ; U2Sb1_S01
MPS
AND<=.D D20812 D20110    ; CV送り時間   (0.01s)  移動時間ｶｳﾝﾄ
MOV K10 D2005            ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LD>.D D20812 D20110      ; CV送り時間   (0.01s)  移動時間ｶｳﾝﾄ
AND X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
AND X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
AND<.D D20110 D2120      ; 移動時間ｶｳﾝﾄ  一時停止有効時間
ANL
INC D2005                ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
OUT M2621                ; 冷却CV 回転
MPP
AND CR2004               ; 10msｸﾛｯｸﾊﾟﾙｽ
@INC.D D20110            ; 移動時間ｶｳﾝﾄ
;途中停止により移動量補正 / Movement amount correction by Pause
;D20110.L = (D20110.L - 50) / 1.5		'移動開始遅れ時間800ms+移動時間/2
;IF D20110.L < 0 THEN					'ﾏｲﾅｽ回避
;	D20110.D = 0
;END IF
LD M2302                 ; U2Sb1_S02
INC D2005                ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
NCJ K1000
;D20110.L = (D20110.L - 50) / 1.5		'移動開始遅れ時間800ms+移動時間/2
;
LD CR2002                ; 常時ON
LDA.L D20110             ; 移動時間ｶｳﾝﾄ
CON
SUB.L +50
CON
FLOAT.L
CON
DIV.F +1.5
CON
INTG.L
CON
STA.L D20110             ; 移動時間ｶｳﾝﾄ
;IF D20110.L < 0 THEN					'ﾏｲﾅｽ回避
;
LD<.L D20110 +0          ; 移動時間ｶｳﾝﾄ
NCJ K2000
;	D20110.D = 0
;
LD CR2002                ; 常時ON
MOV.D K0 D20110          ; 移動時間ｶｳﾝﾄ
;END IF
LABEL K2000
LABEL K1000
;
;取出し部にパレット検出  一時停止 / Pause by Palette at exit
LD M2303                 ; U2Sb1_S03
MPS
LDB X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
ANB X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
ANL
TMR K220 K10             ; 再起動ﾃﾞｨﾚｲ
CON
AND T220                 ; 再起動ﾃﾞｨﾚｲ
MOV K1 D2005             ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LD X40B                  ; 取出しﾊﾟﾚｯﾄ検出1(内)
OR X40C                  ; 取出しﾊﾟﾚｯﾄ検出2(外)
ANL
OUT M2586                ; CV出口渋滞中2
;ｺﾝﾍﾞﾔ停止ﾃﾞｨﾚｲ/ CV stop delay
LD M2310                 ; U2Sb1_S10
TMR K221 K30             ; CV停止ﾃﾞｨﾚｲ
CON
AND T221                 ; CV停止ﾃﾞｨﾚｲ
INC D2005                ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
;移動完了/ Shit end
LD M2311                 ; U2Sb1_S11
MPS
DW.D K0 D20110           ; 移動時間ｶｳﾝﾄ
CON
MOV K18 D2005            ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
BMOV D20010 D20020 K80   ; CV 投入部在荷(1=有)  CV 前半部1在荷(1=有)
MPP
DW K0 D20010             ; CV 投入部在荷(1=有)
;End
LD M2318                 ; U2Sb1_S18
MOV K19 D2005            ; sub1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>□_出力回路
;
LD M50                   ; 運転準備
AND M123                 ; 安全扉正常
ANB M51                  ; 即停止異常(緊急停止)
MC
;
;<h1/>____▼_Fan
LD B041                  ; 065)冷却FAN1回転異常
AND B042                 ; 066)冷却FAN2回転異常
AND B043                 ; 067)冷却FAN3回転異常
AND B044                 ; 068)冷却FAN4回転異常
AND B045                 ; 069)冷却FAN5回転異常
AND B046                 ; 070)冷却FAN6回転異常
AND B047                 ; 071)冷却FAN7回転異常
AND B048                 ; 072)冷却FAN8回転異常
INV
ANB X408                 ; 投入部ﾊﾟﾚｯﾄ検出
ANB X409                 ; ﾊﾟﾚｯﾄ残留確認1(前半部)
ANB X40A                 ; ﾊﾟﾚｯﾄ残留確認2(後半部)
LDB X40B                 ; 取出しﾊﾟﾚｯﾄ検出1(内)
ANB X40C                 ; 取出しﾊﾟﾚｯﾄ検出2(外)
OR CR2002                ; 常時ON
ANL
AND M74                  ; 自動運転中
TMR K209 K200            ; FAN停止
LDB M2600                ; Fan回転
ANB M2601                ; Fan回転
ANB M2602                ; Fan回転
ANB M2603                ; Fan回転
ANB M2604                ; Fan回転
ANB M2605                ; Fan回転
INV
LD>= D2000 K1            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
AND< D2000 K190          ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB M2610                ; Fan停止補助
ORL
ANB T209                 ; FAN停止
ORL
OUT Y450                 ; 冷却ﾌｧﾝ 電源ON
LDB M2611                ; Fan停止
ANB M2612                ; Fan停止
ANB M2613                ; Fan停止
ANB M2614                ; Fan停止
ANB M2615                ; Fan停止
ANB M2616                ; Fan停止
INV
OUT M2610                ; Fan停止補助
;<h1/>____▼_冷却CV / Coolong-conveyor
LDB M2620                ; 冷却CV 回転
ANB M2621                ; 冷却CV 回転
ANB M2622                ; 冷却CV 回転
ANB M2623                ; 冷却CV 回転
ANB M2624                ; 冷却CV 回転
ANB M2625                ; 冷却CV 回転
INV
LD Y452                  ; 冷却CV RUN
ANB M2630                ; 冷却CV 停止補助
AND CR2003               ; 常時OFF
ORL
OUT Y452                 ; 冷却CV RUN
LDB M2631                ; 冷却CV 停止
ANB M2632                ; 冷却CV 停止
ANB M2633                ; 冷却CV 停止
ANB M2634                ; 冷却CV 停止
ANB M2635                ; 冷却CV 停止
ANB M2636                ; 冷却CV 停止
INV
OUT M2630                ; 冷却CV 停止補助
LDB M2640                ; 冷却CV逆回転
ANB M2641                ; 冷却CV逆回転
ANB M2642                ; 冷却CV逆回転
ANB M2643                ; 冷却CV逆回転
INV
LD Y453                  ; 冷却CV 回転方向逆
ANB M2650                ; 冷却CV逆回転解除補助
AND CR2003               ; 常時OFF
ORL
OUT Y453                 ; 冷却CV 回転方向逆
LDB M2651                ; 冷却CV逆回転解除
ANB M2652                ; 冷却CV逆回転解除
ANB M2652                ; 冷却CV逆回転解除
ANB M2653                ; 冷却CV逆回転解除
ANB M2654                ; 冷却CV逆回転解除
INV
OUT M2650                ; 冷却CV逆回転解除補助
;<h1/>____▼_投入表示灯
LD M2594                 ; Shift開始1s表示
FLIK K12 K12 M2580       ; Fliker1
LD M2593                 ; Shift開始2s表示
FLIK K40 K40 M2581       ; Fliker2
LD T240                  ; 除材督促    第一形態
OR T241                  ; 除材督促    第二形態
LD X408                  ; 投入部ﾊﾟﾚｯﾄ検出
OR X409                  ; ﾊﾟﾚｯﾄ残留確認1(前半部)
OR X40A                  ; ﾊﾟﾚｯﾄ残留確認2(後半部)
ANL
OUT M2792                ; 出口除材督促中
LD M2400                 ; CV1ﾋﾟｯﾁ搬送中
LD M2594                 ; Shift開始1s表示
AND M2580                ; Fliker1
ORL
LD M2593                 ; Shift開始2s表示
ANB M2594                ; Shift開始1s表示
AND M2581                ; Fliker2
ORL
OUT M2790                ; 冷却CV共通表示 赤
LD M2790                 ; 冷却CV共通表示 赤
LDB M2585                ; CV出口渋滞中1
ANB M2586                ; CV出口渋滞中2
INV
ORL
OUT Y458                 ; 冷却CV 投入側表示灯 赤
LDB L29                  ; KTR仕様
LDB M2792                ; 出口除材督促中
LDB M2593                ; Shift開始2s表示
AND M2571                ; 冷却中
AND<> D20080 K1          ; CV 除材部1在荷(1=有)
OR M2790                 ; 冷却CV共通表示 赤
ANL
LD M2792                 ; 出口除材督促中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
LD L29                   ; KTR仕様
LD T240                  ; 除材督促    第一形態
OR T241                  ; 除材督促    第二形態
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y45C                 ; 冷却CV 排出側表示灯 赤
;
LD M2594                 ; Shift開始1s表示
AND CR2003               ; 常時OFF
LDB M2585                ; CV出口渋滞中1
ANB M2586                ; CV出口渋滞中2
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT M2791                ; 冷却CV共通表示 緑
LD M2791                 ; 冷却CV共通表示 緑
LDB M2570                ; ﾊﾟﾚｯﾄ投入待ち
LDB M2571                ; 冷却中
OR Y458                  ; 冷却CV 投入側表示灯 赤
ANL
INV
ORL
OUT Y459                 ; 冷却CV 投入側表示灯 緑
LDB L29                  ; KTR仕様
LDB M2792                ; 出口除材督促中
ANB M2593                ; Shift開始2s表示
LD M2570                 ; ﾊﾟﾚｯﾄ投入待ち
OR M2571                 ; 冷却中
ANL
LD= D20080 K1            ; CV 除材部1在荷(1=有)
OR= D20090 K1            ; CV 除材部2在荷(1=有)
ANL
LD M2792                 ; 出口除材督促中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
LD L29                   ; KTR仕様
LD M2400                 ; CV1ﾋﾟｯﾁ搬送中
OR T240                  ; 除材督促    第一形態
OR T241                  ; 除材督促    第二形態
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LDB M2400                ; CV1ﾋﾟｯﾁ搬送中
ANB T240                 ; 除材督促    第一形態
ANB T241                 ; 除材督促    第二形態
AND M74                  ; 自動運転中
ORL
LD M2572                 ; 出口ﾊﾟﾚｯﾄ除材待ち
AND CR2003               ; 常時OFF
ORL
ANL
ORL
OUT Y45D                 ; 冷却CV 排出側表示灯 緑
;
MCR
;<h1/>____▼_Tp出力
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
OUT B15A0                ; UNIT2-1-1 TP-PL
LDB Y450                 ; 冷却ﾌｧﾝ 電源ON
OUT B15A1                ; UNIT2-1-2 TP-PL
LD Y452                  ; 冷却CV RUN
MPS
ANB Y453                 ; 冷却CV 回転方向逆
OUT B15A2                ; UNIT2-1-3 TP-PL
MPP
AND Y453                 ; 冷却CV 回転方向逆
OUT B15A3                ; UNIT2-1-4 TP-PL
LD M2541                 ; 1ﾋﾟｯﾁｼﾌﾄ開始
OUT B15A4                ; UNIT2-1-5 TP-PL
;<h1/>□_異常回路
LD M50                   ; 運転準備
AND X40E                 ; 冷却CV ﾄﾘｯﾌﾟ出力
LD B040                  ; 064)冷却CV異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B040                 ; 064)冷却CV異常
LDP B040                 ; 064)冷却CV異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0040 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X400                 ; 冷却ﾌｧﾝ1 回転警報
LD B041                  ; 065)冷却FAN1回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K291 K50             ; Error delay
MPP
AND T291                 ; Error delay
OUT B041                 ; 065)冷却FAN1回転異常
LDP B041                 ; 065)冷却FAN1回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0041 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X401                 ; 冷却ﾌｧﾝ2 回転警報
LD B042                  ; 066)冷却FAN2回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K292 K50             ; Error delay
MPP
AND T292                 ; Error delay
OUT B042                 ; 066)冷却FAN2回転異常
LDP B042                 ; 066)冷却FAN2回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0042 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X402                 ; 冷却ﾌｧﾝ3 回転警報
LD B043                  ; 067)冷却FAN3回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K293 K50             ; Error delay
MPP
AND T293                 ; Error delay
OUT B043                 ; 067)冷却FAN3回転異常
LDP B043                 ; 067)冷却FAN3回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0043 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X403                 ; 冷却ﾌｧﾝ4 回転警報
LD B044                  ; 068)冷却FAN4回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K294 K50             ; Error delay
MPP
AND T294                 ; Error delay
OUT B044                 ; 068)冷却FAN4回転異常
LDP B044                 ; 068)冷却FAN4回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0044 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X404                 ; 冷却ﾌｧﾝ5 回転警報
LD B045                  ; 069)冷却FAN5回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K295 K50             ; Error delay
MPP
AND T295                 ; Error delay
OUT B045                 ; 069)冷却FAN5回転異常
LDP B045                 ; 069)冷却FAN5回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0045 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X405                 ; 冷却ﾌｧﾝ6 回転警報
LD B046                  ; 070)冷却FAN6回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K296 K50             ; Error delay
MPP
AND T296                 ; Error delay
OUT B046                 ; 070)冷却FAN6回転異常
LDP B046                 ; 070)冷却FAN6回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0046 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X406                 ; 冷却ﾌｧﾝ7 回転警報
LD B047                  ; 071)冷却FAN7回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K297 K50             ; Error delay
MPP
AND T297                 ; Error delay
OUT B047                 ; 071)冷却FAN7回転異常
LDP B047                 ; 071)冷却FAN7回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0047 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD Y450                  ; 冷却ﾌｧﾝ 電源ON
ANB X407                 ; 冷却ﾌｧﾝ8 回転警報
LD B048                  ; 072)冷却FAN8回転異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K298 K50             ; Error delay
MPP
AND T298                 ; Error delay
OUT B048                 ; 072)冷却FAN8回転異常
LDP B048                 ; 072)冷却FAN8回転異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0048 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B049                  ; 073)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B049                 ; 073)
LDP B049                 ; 073)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0049 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04A                  ; 074)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04A                 ; 074)
LDP B04A                 ; 074)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004A D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04B                  ; 075)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04B                 ; 075)
LDP B04B                 ; 075)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004B D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04C                  ; 076)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04C                 ; 076)
LDP B04C                 ; 076)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004C D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04D                  ; 077)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04D                 ; 077)
LDP B04D                 ; 077)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004D D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04E                  ; 078)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04E                 ; 078)
LDP B04E                 ; 078)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004E D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B04F                  ; 079)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B04F                 ; 079)
LDP B04F                 ; 079)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H004F D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B050                  ; 080)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B050                 ; 080)
LDP B050                 ; 080)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0050 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B051                  ; 081)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B051                 ; 081)
LDP B051                 ; 081)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0051 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B052                  ; 082)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B052                 ; 082)
LDP B052                 ; 082)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0052 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B053                  ; 083)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B053                 ; 083)
LDP B053                 ; 083)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0053 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B054                  ; 084)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B054                 ; 084)
LDP B054                 ; 084)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0054 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B055                  ; 085)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B055                 ; 085)
LDP B055                 ; 085)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0055 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B056                  ; 086)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B056                 ; 086)
LDP B056                 ; 086)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0056 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B057                  ; 087)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B057                 ; 087)
LDP B057                 ; 087)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0057 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B058                  ; 088)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B058                 ; 088)
LDP B058                 ; 088)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0058 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B059                  ; 089)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B059                 ; 089)
LDP B059                 ; 089)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0059 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05A                  ; 090)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05A                 ; 090)
LDP B05A                 ; 090)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005A D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05B                  ; 091)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05B                 ; 091)
LDP B05B                 ; 091)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005B D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05C                  ; 092)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05C                 ; 092)
LDP B05C                 ; 092)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005C D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05D                  ; 093)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05D                 ; 093)
LDP B05D                 ; 093)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005D D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05E                  ; 094)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05E                 ; 094)
LDP B05E                 ; 094)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005E D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B05F                  ; 095)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B05F                 ; 095)
LDP B05F                 ; 095)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H005F D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾌﾞｻﾞｰ発生回路
LD<>.D B040 D2012        ; 064)冷却CV異常  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
AND<>.D B040 K0          ; 064)冷却CV異常
OUT M2989                ; ﾕﾆｯﾄ2異常BZ補助
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
MOV.D B040 D2012         ; 064)冷却CV異常  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
LD=.D B040 K0            ; 064)冷却CV異常
@MOV.D K0 D2012          ; ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
END
ENDH
