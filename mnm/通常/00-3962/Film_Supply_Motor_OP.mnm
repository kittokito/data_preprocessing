DEVICE:53
;MODULE:Film_Supply_Motor_OP
;MODULE_TYPE:0
;<h1/>通常 巻出し動作 (巻出しﾀﾞﾝｻﾛｰﾗ動作有効)
LD MR16006               ; 巻出し部ｴﾗｰ発生
OR R46200                ; _安全ｶﾊﾞｰ1開中
OR MR16014               ; 巻出し　ﾌｨﾙﾑ切れ
OUT MR16009              ; 巻出しﾓｰﾀ回転不可
LDP MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
SET @MR100
LDF MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
RES MR16005              ; 強制　巻きだしﾓｰﾀ動作ON
CON
RES MR6008               ; DRV-1 正転
CON
ZRES @MR100 @MR115
LD CR2002                ; 常時ON
AND R46100               ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
LD MR7313                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 上側
ORB R7312                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 上OT
ANL
TMR #691 #10
CON
AND T691
RES MR19700              ; 巻出し動作中
CON
RES @MR113               ; 動作中
CON
SET MR16005              ; 強制　巻きだしﾓｰﾀ動作ON
STG MR16005              ; 強制　巻きだしﾓｰﾀ動作ON
MPS
ANB @MR113               ; 動作中
MPS
LDB MR7314               ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下側
AND R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
ANL
RES @MR100
CON
RES @MR101
CON
SET @MR102
CON
ENDS
MPP
LD MR7314                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下側
ORB R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
ANL
TMR #1210 #1
CON
AND T1210
ENDS
MPP
AND @MR113               ; 動作中
TMR #1213 #1
CON
AND T1213
ENDS
LD MR16005               ; 強制　巻きだしﾓｰﾀ動作ON
SET MR16915
STG @MR100
ANB @MR113               ; 動作中
JMP @MR101
STG @MR101
LD MR7313                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 上側
ORB R7312                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 上OT
ANL
SET MR19700              ; 巻出し動作中
CON
SET @MR113               ; 動作中
CON
JMP @MR102
STG @MR102
SET MR11214              ; 巻き出しﾌｨﾙﾑ押え 上昇
CON
MPS
LDB MR7304               ; F巻出し部 巻出しF押え 上限ｴﾗｰ
ANB R7305                ; F巻出し部 巻出しF押え 下限
AND R7304                ; F巻出し部 巻出しF押え 上限
ANL
JMP @MR103
MPP
AND MR7304               ; F巻出し部 巻出しF押え 上限ｴﾗｰ
OUT @MR111               ; ｾﾝｻｴﾗｰ ﾎﾟｰｽﾞ要求
STG @MR103
SET MR6008               ; DRV-1 正転
CON
AND MR6008               ; DRV-1 正転
JMP @MR104
STG @MR104
MPS
LD MR7314                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下側
ORB R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
ANL
JMP @MR106               ; SW2
MPP
LDB MR7314               ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下側
AND R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
AND R6008                ; DRV-1 正転
ANL
TMR #699 #50
CON
AND T699
RES MR6008               ; DRV-1 正転
CON
JMP @MR105               ; SW1
STG @MR105               ; SW1
ANB R39004               ; ｴﾗｰ中
ZRES @MR100 @MR109
CON
SET R39004               ; ｴﾗｰ中
CON
DW #105 @DM1
CON
DW #1000 @DM9
CON
SET @R100
CON
JMP @MR115
STG @MR115
AND @R101
RES @R101
CON
RES R39004               ; ｴﾗｰ中
CON
DW #0 DM60009
CON
AND @R105                ; SW1
JMP @MR102
STG @MR106               ; SW2
RES MR6008               ; DRV-1 正転
CON
ANB MR6008               ; DRV-1 正転
TMR #681 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T681                 ; ｳｪｲﾄﾀｲﾏ
JMP @MR107
STG @MR107
RES MR11214              ; 巻き出しﾌｨﾙﾑ押え 上昇
CON
MPS
LDB MR7305               ; F巻出し部 巻出しF押え 下限ｴﾗｰ
ANB R7304                ; F巻出し部 巻出しF押え 上限
AND R7305                ; F巻出し部 巻出しF押え 下限
ANL
JMP @MR108
MPP
AND MR7305               ; F巻出し部 巻出しF押え 下限ｴﾗｰ
RES MR7305               ; F巻出し部 巻出しF押え 下限ｴﾗｰ
CON
JMP @MR108
STG @MR108
TMR #684 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T684                 ; ｳｪｲﾄﾀｲﾏ
RES MR19700              ; 巻出し動作中
CON
RES @MR113               ; 動作中
CON
JMP @MR100
LD @MR110                ; ｾﾝｻｴﾗｰ ﾎﾟｰｽﾞ要求
OR @MR111                ; ｾﾝｻｴﾗｰ ﾎﾟｰｽﾞ要求
OR @MR112                ; ｾﾝｻｴﾗｰ ﾎﾟｰｽﾞ要求
OUT MR16008              ; 巻き出しﾌｨﾙﾑ押え ｾﾝｻｰ異常
LD CR2002                ; 常時ON
ANB MR16009              ; 巻出しﾓｰﾀ回転不可
AND R6008                ; DRV-1 正転
MPS
LDB MR7314               ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下側
AND R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
ANL
TMR #682 #200            ; 巻出しﾓｰﾀ回転監視
CON
AND T682                 ; 巻出しﾓｰﾀ回転監視
RES MR19700              ; 巻出し動作中
CON
RES @MR113               ; 動作中
CON
RES MR6008               ; DRV-1 正転
CON
SET MR16006              ; 巻出し部ｴﾗｰ発生
MRD
ANB R7315                ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 下OT
TMR #689 #200
CON
AND T689
RES MR19700              ; 巻出し動作中
CON
RES @MR113               ; 動作中
CON
SET MR16014              ; 巻出し　ﾌｨﾙﾑ切れ
MPP
ANB MR7312               ; F巻出し部 ﾀﾞﾝｻﾛｰﾗ位置 上OT
TMR #685 #100            ; 巻出しﾓｰﾀ回転監視
CON
AND T685                 ; 巻出しﾓｰﾀ回転監視
RES MR19700              ; 巻出し動作中
CON
RES @MR113               ; 動作中
CON
SET MR16006              ; 巻出し部ｴﾗｰ発生
LD MR16006               ; 巻出し部ｴﾗｰ発生
ANB R39004               ; ｴﾗｰ中
RES MR16007              ; 巻きだしﾀﾞﾝｻﾛｰﾗ制御ｵﾝ
CON
RES MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
CON
ZRES @MR100 @MR109
CON
SET R39004               ; ｴﾗｰ中
CON
DW #105 @DM1
CON
DW #1000 @DM9
CON
RES MR16006              ; 巻出し部ｴﾗｰ発生
CON
SET @R100
CON
SET @R000
STG @R000
AND @R101
RES @R101
CON
RES R39004               ; ｴﾗｰ中
CON
DW #0 DM60009
CON
AND @R105                ; SW1
SET MR16007              ; 巻きだしﾀﾞﾝｻﾛｰﾗ制御ｵﾝ
CON
SET MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
CON
JMP @R001
STG @R001
TMR #686 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T686                 ; ｳｪｲﾄﾀｲﾏ
SET MR16005              ; 強制　巻きだしﾓｰﾀ動作ON
CON
ENDS
LD MR16014               ; 巻出し　ﾌｨﾙﾑ切れ
ANB R39004               ; ｴﾗｰ中
RES MR16007              ; 巻きだしﾀﾞﾝｻﾛｰﾗ制御ｵﾝ
CON
RES MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
CON
ZRES @MR100 @MR109
CON
SET R39004               ; ｴﾗｰ中
CON
DW #107 @DM1
CON
DW #1000 @DM9
CON
SET @R100
CON
SET @R002
STG @R002
RES MR16014              ; 巻出し　ﾌｨﾙﾑ切れ
CON
AND @R101
RES @R101
CON
RES R39004               ; ｴﾗｰ中
CON
DW #0 DM60009
CON
AND @R105                ; SW1
SET MR16007              ; 巻きだしﾀﾞﾝｻﾛｰﾗ制御ｵﾝ
CON
SET MR16004              ; 巻だしﾀﾞﾝｻﾛｰﾗ動作ON
CON
JMP @R003
STG @R003
TMR #690 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T690                 ; ｳｪｲﾄﾀｲﾏ
SET MR16005              ; 強制　巻きだしﾓｰﾀ動作ON
CON
ENDS
;MyTask Error Process Substance/ｴﾗｰ処理実体
STG @R100
ANB R39002               ; ERROR_DISP Busy
MPS
ZRES @R105 @R108         ; SW1
CON
LDA @DM9
CON
TBCD
CON
STA @DM10
CON
DISN @DM10 @DM3 #4
MRD
DW #5 @DM0
CON
BMOV @DM0 DM60000 #7     ; TaskNo /ERROR_DISP
MPP
SET R39000               ; ERROR_DISP Start
CON
SET @R102
CON
JMP @R103
STG @R103
AND R39002               ; ERROR_DISP Busy
AND= DM60008 @DM1        ; ReturnNo /ERROR_DISP
MPS
AND= DM60007 @DM6        ; ReturnSW /ERROR_DISP
SET @R105                ; SW1
CON
JMP @R104
MPP
AND= DM60007 @DM5        ; ReturnSW /ERROR_DISP
SET @R106                ; SW2
CON
JMP @R104
STG @R104
ANB R39002               ; ERROR_DISP Busy
RES @R102
CON
SET @R101
CON
ENDS
;
END
ENDH
