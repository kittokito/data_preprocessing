DEVICE:53
;MODULE:UT1_Case_Supply
;MODULE_TYPE:0
;[][]    Received input    [][]
LD X1021                 ; 投入部ﾜｰｸ確認
TMR K110 K3              ; 投入部ﾜｰｸ確認
LD X1000                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
TMR K111 K3              ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
LD X1001                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ浮き確認1
TMR K112 K3              ; 給材ﾜｰｸ浮き確認1
LD X1002                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ浮き確認2
TMR K113 K3              ; 給材ﾜｰｸ浮き確認2
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
TMR K116 K3              ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
TMR K117 K3              ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
;[] ｾｯﾄ治具ID
;Set JIG ID
LD Y2100                 ; 投入ﾃｰﾌﾞﾙ時計回SV
AND T116                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD Y2101                 ; 投入ﾃｰﾌﾞﾙ反時計回SV
AND T117                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
MPS
AND X1003                ; 給材ﾃｰﾌﾞﾙ治具ID-1
SET W0120.0
MRD
ANB X1003                ; 給材ﾃｰﾌﾞﾙ治具ID-1
RES W0120.0
MRD
AND X1004                ; 給材ﾃｰﾌﾞﾙ治具ID-2
SET W0120.1
MRD
ANB X1004                ; 給材ﾃｰﾌﾞﾙ治具ID-2
RES W0120.1
MRD
AND X1005                ; 給材ﾃｰﾌﾞﾙ治具ID-3
SET W0120.2
MPP
ANB X1005                ; 給材ﾃｰﾌﾞﾙ治具ID-3
RES W0120.2
;[]
;[][] Device status and operation conditions [][]
;[]
LDB X1000                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANB X1021                ; 投入部ﾜｰｸ確認
OR M81                   ; 原点復帰動作中
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ORL
ANL
OUT M1480                ; ﾕﾆｯﾄ1原点
;[] 非常停止
;Emergency stop
LDB CR2003               ; 常時OFF
INV
OUT M1481                ; ﾕﾆｯﾄ1全異常
;[] 起動条件
;Start-up conditions
LD M1480                 ; ﾕﾆｯﾄ1原点
ANB M36                  ; 払出ﾓｰﾄﾞ
OUT M1482                ; ﾕﾆｯﾄ1起動条件
;[] 起動OFF
;Start OFF
LDB M50                  ; 運転準備
OR M1486                 ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M1483                ; ﾕﾆｯﾄ1起動OFF
;[] 原点復帰
;Origin
LD M82                   ; 原点復帰開始
LDB M82                  ; 原点復帰開始
ANP B1099                ; UNIT1-2-10 TP-PB
ORL
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OR M1484                 ; ﾕﾆｯﾄ1原点復帰中
ANB M1483                ; ﾕﾆｯﾄ1起動OFF
ANB M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M1299                ; ﾕﾆｯﾄ1原点復帰完了確認
MPS
OUT M1484                ; ﾕﾆｯﾄ1原点復帰中
MPP
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1599                ; UNIT1-2-10 TP-PL
;[] 起動中
;Running
LD M73                   ; 各工程起動指令
AND M1482                ; ﾕﾆｯﾄ1起動条件
OR M1485                 ; ﾕﾆｯﾄ1起動中
ANB M1483                ; ﾕﾆｯﾄ1起動OFF
LDB M1049                ; ﾕﾆｯﾄ1-1ｻｲｸﾙ終了
ORB M1099                ; ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ終了
ANL
OUT M1485                ; ﾕﾆｯﾄ1起動中
;[] ｻｲｸﾙ停止異常
;Cycle stop error
LDB B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
ANB B023                 ; 原点復帰ﾜｰｸ有異常
ANB B024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ無し異常
INV
OUT M1486                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
;[] 払出停止異常
;Payout stop error
LDB CR2003               ; 常時OFF
INV
OUT M1487                ; ﾕﾆｯﾄ1払出停止異常
;[] 警報補助
;Auxiliary alarm
LDB B021                 ; ﾌﾚｰﾑ投入不可異常
OR B0521
LDB B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
OR B0525
ANL
LDB B026                 ; ﾗﾍﾞﾙ発行済みﾌﾚｰﾑ投入要求
OR B0526
ANL
ANB M1450                ; Err100:ﾌﾚｰﾑｺｰﾄﾞNG
ANB M1451                ; Err101:ﾌﾚｰﾑ浮きNG
INV
OUT M1488                ; ﾕﾆｯﾄ1警報補助
;[] 機器準備確認
;Equipment ready
LD CR2002                ; 常時ON
OUT M1490                ; ﾕﾆｯﾄ1機器準備完了
;[] ｴﾗｰ発生　一時停止
;Error, pause
LDB B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
INV
OUT M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
;[] 動作中ﾕﾆｯﾄ無し
;No action unit
LD M130                  ; 全体PAUSE
LD M1000                 ; ｹｰｽ投入部ｽﾃｯﾌﾟ開始
AND M5000                ; 基板投入部ｻｲｸﾙ開始
ANB M5069                ; ﾗﾍﾞﾙ発行完､ｹｰｽ投入可､照合判定
ORL
ORB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
TMR K101 K20             ; ﾕﾆｯﾄ1-1動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D1000 D1005          ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD T101                  ; ﾕﾆｯﾄ1-1動作無し判定ﾀｲﾏ
OUT M1492                ; ﾕﾆｯﾄ1動作無し確認
;[] 払出しﾜｰｸ無し
;Without paying out work
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ORL
ANB X1000                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANB X1021                ; 投入部ﾜｰｸ確認
TMR K105 K10             ; UNIT1払出処理ﾜｰｸ無
LD T105                  ; UNIT1払出処理ﾜｰｸ無
OUT M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
;[] ｽﾃｯﾌﾟ起動
;Step start-up
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M1479                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
;[][][][][][][][][][][][][][][]
;[]  UNIT13 Auto operation   []
;[][][][][][][][][][][][][][][]
;<h1/>[] Automatic circuit
;<h1/>/ Case supply Unit
;[] UNIT1-1 Case supply
LD CR2002                ; 常時ON
ZRES M1000 M1049         ; ｹｰｽ投入部ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ1-1ｻｲｸﾙ終了
LDB M1485                ; ﾕﾆｯﾄ1起動中
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
DW K0 D1200              ; for UT1 main STEP monitor
LD M1485                 ; ﾕﾆｯﾄ1起動中
MOV D1000 Z1             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
SET M1000:Z1             ; ｹｰｽ投入部ｽﾃｯﾌﾟ開始
CAL+ D1000 K1000 D1200   ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ  for UT1 main STEP monitor
;ｹｰｽ有無確認
;Case with or without check
LD M1000                 ; ｹｰｽ投入部ｽﾃｯﾌﾟ開始
MPS
AND M5069                ; ﾗﾍﾞﾙ発行完､ｹｰｽ投入可､照合判定
MOV K3 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB M5069                ; ﾗﾍﾞﾙ発行完､ｹｰｽ投入可､照合判定
AND T110                 ; 投入部ﾜｰｸ確認
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1001                 ; ｹｰｽ投入不可
ANB T110                 ; 投入部ﾜｰｸ確認
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｹｰｽ供給確認
;ｹｰｽ supply check
LD M1003                 ; ｹｰｽ投入要求､確認
MPS
LD T110                  ; 投入部ﾜｰｸ確認
AND X04                  ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
AND X06                  ; 投入ﾗｲﾄｶｰﾃﾝ2_OUT1
ANL
TMR K115 K10             ; ﾌﾚｰﾑ投入確認ﾃﾞｨﾚｲ
MRD
LD T115                  ; ﾌﾚｰﾑ投入確認ﾃﾞｨﾚｲ
ANB W01.8                ; 投入部ﾌﾚｰﾑ照合完了
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB T115                 ; ﾌﾚｰﾑ投入確認ﾃﾞｨﾚｲ
ANP B1025                ; ﾗﾍﾞﾙ再発行PB
ANL
MOV K8 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｹｰｽｺｰﾄﾞ照合
;Case code verification
LD M1004                 ; ｹｰｽ投入確認､ｺｰﾄﾞ照合要求
MPS
OUT W0101.8              ; 投入部ﾌﾚｰﾑ照合要求
MPP
AND W01.8                ; 投入部ﾌﾚｰﾑ照合完了
MPS
AND W01.9                ; 投入部ﾌﾚｰﾑ不一致
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB W01.9                ; 投入部ﾌﾚｰﾑ不一致
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
@BMOV D5400 D15000 K100  ; UT9_ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT1-1ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
MPS
ANB M154                 ; PCB screw check MODE
@MOV K1 D15062           ; UT1-1ﾜｰｸOK/NGﾃﾞｰﾀ
MPP
AND M154                 ; PCB screw check MODE
@MOV K2 D15062           ; UT1-1ﾜｰｸOK/NGﾃﾞｰﾀ
;ｹｰｽｺｰﾄﾞ照合NG (ﾗﾍﾞﾙ再発行PB B1025)
;Case code verification NG (Reissue label TP_PB B1025)
LD M1005                 ; ｹｰｽｺｰﾄﾞ照合NG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1006                 ; ｹｰｽｺｰﾄﾞ照合NG中
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
OUT M1450                ; Err100:ﾌﾚｰﾑｺｰﾄﾞNG
MPP
LDB T110                 ; 投入部ﾜｰｸ確認
AND X04                  ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
AND X06                  ; 投入ﾗｲﾄｶｰﾃﾝ2_OUT1
ANL
MPS
ANP B1025                ; ﾗﾍﾞﾙ再発行PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANP X1020                ; ﾜｰｸ投入部確認PB
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K3 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1007                 ; ｹｰｽｺｰﾄﾞ照合NG解除
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾗﾍﾞﾙ再発行要求
;Label reissue request
LD M1008                 ; ﾗﾍﾞﾙ再発行要求
MPS
OUT W0101.10             ; 投入部ﾌﾚｰﾑ再発行要求
MPP
AND W01.10               ; 投入部ﾌﾚｰﾑﾗﾍﾞﾙ再発行完了
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1009                 ; ﾗﾍﾞﾙ再発行完了
ANB W01.10               ; 投入部ﾌﾚｰﾑﾗﾍﾞﾙ再発行完了
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｹｰｽ照合完了､基板CVへ搬送可ON
;Transport Allowed ON Case verification completed, the substrate CV
LD M1015                 ; ｹｰｽ照合完了､基板搬送可
AND M5070                ; ｹｰｽ投入､照合完了確認
ANB M154                 ; PCB screw check MODE
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1016                 ; 基板投入Ans確認
ANB M5070                ; ｹｰｽ投入､照合完了確認
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾃｰﾌﾞﾙ回転
;Table rotation
LD M1017                 ; 取出部ｹｰｽ無､要求確認
AND M1051                ; ｹｰｽ供給要求
ANB M1604                ; ｼﾌﾄ投入開始(RB)
ANB M1605                ; ｼﾌﾄ投入開始確認
ANB M130                 ; 全体PAUSE
MPS
LDB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K19 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1018                 ; ﾃｰﾌﾞﾙ1供給回転
AND X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
MOV K20 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1019                 ; ﾃｰﾌﾞﾙ2供給回転
ANB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮き確認
;Work float check
LD M1020                 ; 給材ﾃｰﾌﾞﾙ旋回確認
TMR K120 K5              ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
MPS
LD T120                  ; ﾜｰｸ浮き確認ﾃﾞｨﾚｲ
AND X1000                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANL
MPS
LD T112                  ; 給材ﾜｰｸ浮き確認1
AND T113                 ; 給材ﾜｰｸ浮き確認2
ANL
MOV K30 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB T112                 ; 給材ﾜｰｸ浮き確認1
ORB T113                 ; 給材ﾜｰｸ浮き確認2
ANL
MPS
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K22 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LDB M250                 ; 20141010   koike
ANB X1000                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
ANL
MOV K23 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ浮きNG
;Work float NG
LD M1021                 ; ﾃｰﾌﾞﾙ1浮きNG回転戻
ANB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
AND X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
MOV K23 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1022                 ; ﾃｰﾌﾞﾙ2浮きNG回転戻
AND X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｹｰｽ浮きNG再ｾｯﾄ要求
;Case float NG re-set request
LD M1023                 ; ｹｰｽ浮きNG
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
DW K100 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1024                 ; ｹｰｽ浮きNG中
ANB M130                 ; 全体PAUSE
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
OUT M1451                ; Err101:ﾌﾚｰﾑ浮きNG
MPP
LDP X1020                ; ﾜｰｸ投入部確認PB
ORP B1024                ; ﾘﾄﾗｲ確認TP-PB
ANB T110                 ; 投入部ﾜｰｸ確認
AND X04                  ; 投入ﾗｲﾄｶｰﾃﾝ1_OUT1
AND X06                  ; 投入ﾗｲﾄｶｰﾃﾝ2_OUT1
ANL
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1025                 ; ｹｰｽ再ｾｯﾄ確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
MOV K17 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾃﾞｰﾀｼﾌﾄ要求(PLC→PC)
;Shift data request
LD M1030                 ; 浮きOKﾃﾞｰﾀｼﾌﾄ要求
MPS
OUT W0102.2              ; ﾌﾚｰﾑ供給部反転要求
MPP
AND W02.2                ; ｹｰｽ供給部反転完了
@BMOV D15000 D15100 K100 ; UT1-1ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT1-2ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
@FMOV K0 D15000 K100     ; UT1-1ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1031                 ; ｹｰｽ供給完了
ANB W02.2                ; ｹｰｽ供給部反転完了
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1032                 ; ｹｰｽ供給要求OFF確認
ANB M1051                ; ｹｰｽ供給要求
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1000                ; ｹｰｽ投入部ｽﾃｯﾌﾟ開始
MOV K48 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
;Cycle end
LD M1048                 ; ﾕﾆｯﾄ1-1ｻｲｸﾙ停止処理
@MOV K49 D1000           ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>/ Frame extraction
;[] UNIT1-2 ｹｰｽ取出自動
;UNIT1-2 Case extraction, automatic
LD CR2002                ; 常時ON
ZRES M1050 M1099         ; ｹｰｽ取出部ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ終了
LDB M1485                ; ﾕﾆｯﾄ1起動中
MOV K0 D1001
DW K0 D1201              ; for UT1 sub STEP 1 monitor
LD M1485                 ; ﾕﾆｯﾄ1起動中
MOV D1001 Z1
SET M1050:Z1             ; ｹｰｽ取出部ｽﾃｯﾌﾟ開始
CAL+ D1001 K1050 D1201   ; for UT1 sub STEP 1 monitor
;ｹｰｽ有無判定
;Case with or without check
LD M1050                 ; ｹｰｽ取出部ｽﾃｯﾌﾟ開始
MPS
ANB T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
@INC D1001
MPP
AND T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
MOV K4 D1001
;ｹｰｽ供給要求､完了確認
;Case supply demand, supply check
LD M1051                 ; ｹｰｽ供給要求
AND M1031                ; ｹｰｽ供給完了
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
@INC D1001
LD M1052                 ; ｹｰｽ供給完了OFF確認
ANB M1031                ; ｹｰｽ供給完了
@INC D1001
;ｹｰｽ取出可､完了確認
;Case extraction permit, take-out completion check
LD M1053                 ; ｹｰｽ取出可ON(RBへ)
AND M1508                ; 供給部取出完了確認(RB)
ANB M130                 ; 全体PAUSE
@INC D1001
LD M1054                 ; ｹｰｽ取出完了確認
ANB M1508                ; 供給部取出完了確認(RB)
ANB T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
MOV K0 D1001
;ｻｲｸﾙ停止処理
;Cycle end check
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M1051                ; ｹｰｽ供給要求
MOV K48 D1001
;ｻｲｸﾙ終了
;Cycle end
LD M1098                 ; ﾕﾆｯﾄ1-1ｻﾌﾞｻｲｸﾙ停止処理
@MOV K49 D1001
;<h1/>[] Origin
;[][][][][][][][][][][][][][][]
;[][]     UNIT1 Origin     [][]
;[][][][][][][][][][][][][][][]
LD CR2002                ; 常時ON
ZRES M1250 M1299         ; ﾕﾆｯﾄ1原点復帰開始  ﾕﾆｯﾄ1原点復帰完了確認
LDB M1484                ; ﾕﾆｯﾄ1原点復帰中
MOV K0 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
DW K0 D1210
LD M1484                 ; ﾕﾆｯﾄ1原点復帰中
MOV D1010 Z1             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
SET M1250:Z1             ; ﾕﾆｯﾄ1原点復帰開始
CAL+ D1010 K1250 D1210   ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;原点復帰ｻｲｸﾙ監視
;Origin cycle monitoring
LD M1484                 ; ﾕﾆｯﾄ1原点復帰中
TMR K100 K300            ; ﾕﾆｯﾄ1原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
;Origin step start
LD M1250                 ; ﾕﾆｯﾄ1原点復帰開始
AND M1980                ; ﾕﾆｯﾄ2原点
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
@FMOV K0 D15000 K200     ; UT1-1ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
;旋回ﾃｰﾌﾞﾙ回転端確認
;Turntable rotation end check
LD M1251                 ; 旋回ﾃｰﾌﾞﾙ確認
MPS
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
OR X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
MOV K3 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
LDB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1252                 ; ﾃｰﾌﾞﾙ1側旋回
AND X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
@INC D1010               ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
;Origin end
LD M1253                 ; 原点確認
AND M1480                ; ﾕﾆｯﾄ1原点
@MOV K48 D1010           ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M1298                 ; ﾕﾆｯﾄ1原点復帰完了
ANB M82                  ; 原点復帰開始
@MOV K49 D1010           ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;<h1/>[] Manual Oprate
;[][][][][][][][][][][][][][][]
;[] 　　   UNIT1 Manual      []
;[][][][][][][][][][][][][][][]
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
ANB X3236                ; Blow_RB_自動運転中
LD X3218                 ; Blow_RB_作業原点復帰完了
OR X3219                 ; Blow_RB_待機位置
ANL
MC
;旋回ﾃｰﾌﾞﾙ回転
;Turntable rotation
LD T60                   ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB X3236                ; Blow_RB_自動運転中
ANB M1821                ; 供給取出開始(RB)
ANB M1826                ; ﾌﾞﾛｰ1供給開始(RB)
ANB M1831                ; ﾌﾞﾛｰ2供給開始(RB)
ANB M1836                ; ﾌﾞﾛｰ1取出開始(RB)
ANB M1841                ; ﾌﾞﾛｰ2取出開始(RB)
ANB M1846                ; ｼﾌﾄ供給開始(RB)
OUT M1300                ; ﾃｰﾌﾞﾙ1供給旋回可
LD B1080                 ; UNIT1-1-1 TP-PB
AND M1300                ; ﾃｰﾌﾞﾙ1供給旋回可
OR M1301                 ; ﾃｰﾌﾞﾙ1供給旋回
ANPB M1303               ; ﾃｰﾌﾞﾙ2供給旋回
OUT M1301                ; ﾃｰﾌﾞﾙ1供給旋回
LD T60                   ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB X3236                ; Blow_RB_自動運転中
ANB M1821                ; 供給取出開始(RB)
ANB M1826                ; ﾌﾞﾛｰ1供給開始(RB)
ANB M1831                ; ﾌﾞﾛｰ2供給開始(RB)
ANB M1836                ; ﾌﾞﾛｰ1取出開始(RB)
ANB M1841                ; ﾌﾞﾛｰ2取出開始(RB)
ANB M1846                ; ｼﾌﾄ供給開始(RB)
OUT M1302                ; ﾃｰﾌﾞﾙ2供給旋回可
LD B1081                 ; UNIT1-1-2 TP-PB
AND M1302                ; ﾃｰﾌﾞﾙ2供給旋回可
OR M1303                 ; ﾃｰﾌﾞﾙ2供給旋回
ANPB M1301               ; ﾃｰﾌﾞﾙ1供給旋回
OUT M1303                ; ﾃｰﾌﾞﾙ2供給旋回
MCR
;<h1/>[] Output
;[][]    Output    [][]
;Solenoid valve output
;旋回ﾃｰﾌﾞﾙ回転
;Turntable rotation
LDB M1018                ; ﾃｰﾌﾞﾙ1供給回転
ANB M1022                ; ﾃｰﾌﾞﾙ2浮きNG回転戻
ANB M1252                ; ﾃｰﾌﾞﾙ1側旋回
ANB M1301                ; ﾃｰﾌﾞﾙ1供給旋回
INV
LDP M50                  ; 運転準備
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LDB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
ANL
ORL
OR M1320                 ; 投入ﾃｰﾌﾞﾙ時計周補助
ANPB M1321               ; 投入ﾃｰﾌﾞﾙ反時計周補助
OUT M1320                ; 投入ﾃｰﾌﾞﾙ時計周補助
LD M1320                 ; 投入ﾃｰﾌﾞﾙ時計周補助
LD M123                  ; 安全扉正常
AND T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB M130                 ; 全体PAUSE
OR X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
ANL
OUT Y2100                ; 投入ﾃｰﾌﾞﾙ時計回SV
LDB M1019                ; ﾃｰﾌﾞﾙ2供給回転
ANB M1021                ; ﾃｰﾌﾞﾙ1浮きNG回転戻
ANB M1303                ; ﾃｰﾌﾞﾙ2供給旋回
INV
LDP M50                  ; 運転準備
AND X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
OR M1321                 ; 投入ﾃｰﾌﾞﾙ反時計周補助
ANPB M1320               ; 投入ﾃｰﾌﾞﾙ時計周補助
OUT M1321                ; 投入ﾃｰﾌﾞﾙ反時計周補助
LD M1321                 ; 投入ﾃｰﾌﾞﾙ反時計周補助
LD M123                  ; 安全扉正常
AND T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
ANB M130                 ; 全体PAUSE
OR X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ANL
OUT Y2101                ; 投入ﾃｰﾌﾞﾙ反時計回SV
LDB B0E                  ; PCよりｵﾍﾟﾚｰﾀ登録未完了異常
ANB M1450                ; Err100:ﾌﾚｰﾑｺｰﾄﾞNG
ANB M1451                ; Err101:ﾌﾚｰﾑ浮きNG
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT Y203A                ; ﾜｰｸ投入表示
;<h1/>[] Tuch Panel Display
;[][]    Panel Display    [][]
LD M1003                 ; ｹｰｽ投入要求､確認
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
OUT M1100                ; Label reissue Window
LD X1006                 ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1580                ; UNIT1-1-1 TP-PL
LD X1007                 ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1581                ; UNIT1-1-2 TP-PL
;<h1/>[] Error
;[][]    Error Detect    [][]
;B020(K32)--B02F(K47)  B030(K48)--B03F(K63)
LD Y2100                 ; 投入ﾃｰﾌﾞﾙ時計回SV
ANB X1006                ; 給材ﾃｰﾌﾞﾙ1側供給旋回端
LD Y2101                 ; 投入ﾃｰﾌﾞﾙ反時計回SV
ANB X1007                ; 給材ﾃｰﾌﾞﾙ2側供給旋回端
ORL
AND M50                  ; 運転準備
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
ORL
MPS
TMR K150 K50             ; 給材ﾃｰﾌﾞﾙ旋回確認
MPP
AND T150                 ; 給材ﾃｰﾌﾞﾙ旋回確認
OUT B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
@DW K32 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0520
AND B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
OUT B0520
LD M1001                 ; ｹｰｽ投入不可
OUT B021                 ; ﾌﾚｰﾑ投入不可異常
@DW K33 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0521
AND B021                 ; ﾌﾚｰﾑ投入不可異常
OUT B0521
LD M1054                 ; ｹｰｽ取出完了確認
AND T111                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
ORL
OUT B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
@DW K34 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0522
AND B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
OUT B0522
LD M17                   ; 原点復帰PB
ANB M41                  ; 全ﾕﾆｯﾄ起動中
LD M1253                 ; 原点確認
AND M1484                ; ﾕﾆｯﾄ1原点復帰中
ORL
LD X1000                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
OR X1021                 ; 投入部ﾜｰｸ確認
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B023                 ; 原点復帰ﾜｰｸ有異常
ORL
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OUT B023                 ; 原点復帰ﾜｰｸ有異常
@DW K35 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0523
AND B023                 ; 原点復帰ﾜｰｸ有異常
OUT B0523
LD M250                  ; 20141010   koike
LD M1020                 ; 給材ﾃｰﾌﾞﾙ旋回確認
ANB X1000                ; 給材ﾃｰﾌﾞﾙﾜｰｸ確認
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ無し異常
ORL
ANL
OUT B024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ無し異常
@DW K36 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0524
AND B024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ無し異常
OUT B0524
LDB T60                  ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LD>= D12000 K250         ; 表示画面No.
AND< D12000 K300         ; 表示画面No.
LD>= D12200 K250
AND< D12200 K300
ORL
ANL
MPS
TMR K151 K80             ; 投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
MPP
AND T151                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光ﾀｲﾏ
OUT B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
@DW K37 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0525
AND B025                 ; 投入部ﾗｲﾄｶｰﾃﾝ遮光
OUT B0525
LD M1003                 ; ｹｰｽ投入要求､確認
MPS
TMR K152 K300            ; ﾗﾍﾞﾙ発行完了ﾀｲﾏ
MPP
AND T152                 ; ﾗﾍﾞﾙ発行完了ﾀｲﾏ
OUT B026                 ; ﾗﾍﾞﾙ発行済みﾌﾚｰﾑ投入要求
@DW K38 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0526
AND B026                 ; ﾗﾍﾞﾙ発行済みﾌﾚｰﾑ投入要求
OUT B0526
;異常ﾌﾞｻﾞｰまとめ
LDB B020                 ; 給材ﾃｰﾌﾞﾙ旋回異常
OR B0520
LDB B022                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ取出異常
OR B0522
ANL
LDB B023                 ; 原点復帰ﾜｰｸ有異常
OR B0523
ANL
LDB B024                 ; 給材ﾃｰﾌﾞﾙﾜｰｸ無し異常
OR B0524
ANL
INV
OUT M1489                ; ﾕﾆｯﾄ1異常BZ補助
END
ENDH
