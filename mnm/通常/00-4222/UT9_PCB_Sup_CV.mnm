DEVICE:53
;MODULE:UT9_PCB_Sup_CV
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD X1260                 ; 搬送上流ｽﾄｯﾊﾟ下降端
TMR K910 K5              ; 搬送上流ｽﾄｯﾊﾟ下降端
LD X1261                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
TMR K911 K3              ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD X1262                 ; 搬送下流ｽﾄｯﾊﾟ下降端
TMR K912 K3              ; 搬送下流ｽﾄｯﾊﾟ下降端
LD X1263                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
TMR K913 K3              ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD X1264                 ; 搬送位置決め閉端
TMR K914 K5              ; 搬送位置決め閉端
LD X1265                 ; 搬送位置決め開端
TMR K915 K3              ; 搬送位置決め開端
LD X1266                 ; 基板搬送送端
TMR K916 K3              ; 基板搬送送端
LD X1267                 ; 基板搬送戻端
TMR K917 K3              ; 基板搬送戻端
LD X1268                 ; 基板搬送爪下降端
TMR K918 K3              ; 基板搬送爪下降端
LD X1269                 ; 基板搬送爪上昇端
TMR K919 K3              ; 基板搬送爪上昇端
LD X127E                 ; 基板搬送CV装置側引戸
AND X127F                ; 基板搬送CV投入側引戸
TMR K920 K5              ; 基板CV扉閉確認
LDB X126D                ; 基板投入CVﾜｰｸ確認
TMR K921 K40             ; 基板投入ﾜｰｸ無確認
LD X126E                 ; 基板投入CV読取ﾜｰｸ確認
TMH K923 K20             ; 基板投入CV読取ﾜｰｸ有
LDB X126E                ; 基板投入CV読取ﾜｰｸ確認
TMH K924 K20             ; 基板投入CV読取ﾜｰｸ無
LD X1236                 ; E-cap sensor input
TMR K922 K30             ; E-cap sensor
;□ ﾕﾆｯﾄ原点
LD T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
OR CR2002                ; 常時ON
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
AND T915                 ; 搬送位置決め開端
AND T917                 ; 基板搬送戻端
AND T919                 ; 基板搬送爪上昇端
OUT M5480                ; ﾕﾆｯﾄ9原点
;□ 非常停止
LDB CR2003               ; 常時OFF
INV
OUT M5481                ; ﾕﾆｯﾄ9全異常
;□ 起動条件
LD M5480                 ; ﾕﾆｯﾄ9原点
OUT M5482                ; ﾕﾆｯﾄ9起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
OR M5486                 ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M5483                ; ﾕﾆｯﾄ9起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
OR M5484                 ; ﾕﾆｯﾄ9原点復帰中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
ANB M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
ANB M5299                ; ﾕﾆｯﾄ9原点終了
OUT M5484                ; ﾕﾆｯﾄ9原点復帰中
;□ 起動中
LD M73                   ; 各工程起動指令
AND M5482                ; ﾕﾆｯﾄ9起動条件
OR M5485                 ; ﾕﾆｯﾄ9起動中
ANB M5483                ; ﾕﾆｯﾄ9起動OFF
LDB M5099                ; ﾕﾆｯﾄ9-1ｻｲｸﾙ停止
ORB M5149                ; ﾕﾆｯﾄ9-3ｻｲｸﾙ終了
ANL
OUT M5485                ; ﾕﾆｯﾄ9起動中
;□ ｻｲｸﾙ停止異常
LDB B0121                ; 基板供給CV画像検査異常
ANB B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
ANB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
INV
OUT M5486                ; ﾕﾆｯﾄ9ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB M5033                ; ID_NG払出ｻｲｸﾙ停止
ANB CR2003               ; 常時OFF
INV
OUT M5487                ; ﾕﾆｯﾄ9払出停止異常
;□ 警報補助
LDB M5010                ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
INV
OUT M5488                ; ﾕﾆｯﾄ9警報補助
;□ 機器準備確認
LD B2015                 ; PA検査ﾚﾃﾞｨ
OUT M5490                ; ﾕﾆｯﾄ9機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
ANB B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
ANB B0125                ; 基板搬送CV位置決異常
ANB B0126                ; 基板搬送爪送り異常
ANB B0127                ; 基板搬送爪上下異常
ANB B012A                ; 基板CV取出位置PCB行方不明
ANB B012B                ; PA検査IV応答無し
INV
OUT M5491                ; ﾕﾆｯﾄ9ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD= D5001 D5006          ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-2動作中判定
AND= D5002 D5007         ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-3動作中判定
ORB M5485                ; ﾕﾆｯﾄ9起動中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
TMR K901 K20             ; ﾕﾆｯﾄ9動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D5001 D5006          ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-2動作中判定
MOV D5002 D5007          ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ9-3動作中判定
LD T901                  ; ﾕﾆｯﾄ9動作無し判定ﾀｲﾏ
OUT M5492                ; ﾕﾆｯﾄ9動作無し確認
;□ 払出しﾜｰｸ無し
LDB X126E                ; 基板投入CV読取ﾜｰｸ確認
ANB X126F                ; 基板投入CV取出ﾜｰｸ確認
ANB X1270                ; 基板投入CVﾜｰｸ有
LD T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
OR M5004                 ; 基板ｺﾝﾍﾞｱ回転
ANL
TMR K905 K10             ; UNIT9払出処理ﾜｰｸ無し
LD T905                  ; UNIT9払出処理ﾜｰｸ無し
OUT M5493                ; ﾕﾆｯﾄ9ﾜｰｸ無し
;□基板取出し部ﾜｰｸ確認
LD X126F                 ; 基板投入CV取出ﾜｰｸ確認
TMH K990 K20             ; 基板投入CV取出ﾜｰｸ有
LDB X126F                ; 基板投入CV取出ﾜｰｸ確認
TMH K991 K20             ; 基板投入CV取出ﾜｰｸ無
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M5479                ; ﾕﾆｯﾄ9ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　 　UNIT9自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>■自動回路 
;<h1/>/ 基板投入部
LD CR2002                ; 常時ON
ZRES M5000 M5099         ; 基板投入部ｻｲｸﾙ開始  ﾕﾆｯﾄ9-1ｻｲｸﾙ停止
LDB M5485                ; ﾕﾆｯﾄ9起動中
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
DW K0 D5201
FMOV K0 W0130 K6         ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
LD M5485                 ; ﾕﾆｯﾄ9起動中
MOV D5001 Z1             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
SET M5000:Z1             ; 基板投入部ｻｲｸﾙ開始
CAL+ D5001 K5000 D5201   ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;払出･投入抑制判定
;Discharge or Outage decision
LD M5000                 ; 基板投入部ｻｲｸﾙ開始
ANB M36                  ; 払出ﾓｰﾄﾞ
MPS
LDB M153                 ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M90                  ; 払出ﾓｰﾄﾞｼﾌﾄ動作開始
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
LD M153                  ; Re-Issue MODE
OR M3710                 ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
OR M3711                 ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANL
MOV K90 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5090                 ; 投入抑制中
MPS
LDB M153                 ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
LD M5110                 ; 基板搬送完了(投入部へ)
ORB M91                  ; 投入ﾓｰﾄﾞﾘﾀｰﾝｼﾌﾄ動作開始
ANL
ANL
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MRD
LD M153                  ; Re-Issue MODE
ANB M3710                ; 再投入ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANB M3711                ; 日常点検ﾓｰﾄﾞ開始ﾌﾗｸﾞ
ANL
OUT W0107.12             ; ﾗﾍﾞﾙ再発行品読取要求
MPP
AND W0A.12               ; ﾗﾍﾞﾙ再発行品読取完了
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5091                 ; ﾗﾍﾞﾙ再発行品読取完了確認
ANB W0A.12               ; ﾗﾍﾞﾙ再発行品読取完了
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;初期動作ｺﾝﾍﾞｱ回転
LD M5001                 ; ｺﾝﾍﾞｱ投入回転
MPS
AND Y2310                ; 基板供給CV正転
TMH K935 K50             ; 基板無確認
MPP
LD T935                  ; 基板無確認
AND X1267                ; 基板搬送戻端
ANL
MPS
ANB X126E                ; 基板投入CV読取ﾜｰｸ確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND X126E                ; 基板投入CV読取ﾜｰｸ確認
MOV K6 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ無引込
LD M5002                 ; 引込可能確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5003                 ; 基板投入ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5004                 ; 基板ｺﾝﾍﾞｱ回転
MPS
AND X126D                ; 基板投入CVﾜｰｸ確認
TMR K930 K10             ; ﾜｰｸ投入確認
MPP
LD T930                  ; ﾜｰｸ投入確認
OR M5201                 ; 基板投入確認
ANL
OUT M5201                ; 基板投入確認
LD M5004                 ; 基板ｺﾝﾍﾞｱ回転
MPS
LDB M151                 ; Bonding Daily Check MODE
ANB M152                 ; Re-Enter MODE
ANB M153                 ; Re-Issue MODE
ANL
MPS
AND X1267                ; 基板搬送戻端
MPS
AND X126E                ; 基板投入CV読取ﾜｰｸ確認
TMH K936 K50             ; 基板到着確認
MPP
LD M5201                 ; 基板投入確認
ANB X126E                ; 基板投入CV読取ﾜｰｸ確認
ANL
TMR K937 K100            ; 基板未到着
MPP
LD T936                  ; 基板到着確認
LD T921                  ; 基板投入ﾜｰｸ無確認
OR T911                  ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND M36                  ; 払出ﾓｰﾄﾞ
ORL
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
LD M151                  ; Bonding Daily Check MODE
OR M152                  ; Re-Enter MODE
OR M153                  ; Re-Issue MODE
ANL
MOV K90 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ﾓｰﾄﾞ判定
;Mode decision
LD M5005                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
MPS
ANB M150                 ; CC Master Check MODE
MPS
ANB M36                  ; 払出ﾓｰﾄﾞ
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND M36                  ; 払出ﾓｰﾄﾞ
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND M150                 ; CC Master Check MODE
MOV K45 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｲﾝﾃﾞｯｸｽNoｾｯﾄ
LD M5006                 ; 搬送CVｲﾝﾃﾞｯｸｽNoｾｯﾄ
ANB W01.0                ; 基板読込完了
MPS
AND>= K0 D5030           ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
AND<= K10 D5030          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
MOV D5030 W0130          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ  基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;<h1/>/ 基板ｺｰﾄﾞ読取
;基板投入不可､IDｴﾗｰ発生時はﾀﾞｲｱﾛｸﾞ表示で処理選択
LD M5007                 ; 基板ｺｰﾄﾞ読込要求
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
LD W07.1                 ; 放熱部接着剤塗布有無
AND T922                 ; E-cap sensor
LDB W07.1                ; 放熱部接着剤塗布有無
ANB T922                 ; E-cap sensor
ORL
OR M97                   ; ｺﾝﾃﾞﾝｻｷｬｯﾌﾟｾﾝｻ無
ANL
ANL
MOV K40 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
@BMOV W020 D5400 K100    ; ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID  UT9_ﾌﾚｰﾑ投入ﾊﾟﾚｯﾄID
@MOV W07 D5463           ; 8_機種情報BITﾃﾞｰﾀ  UT9機種情報
MPP
LD W01.1                 ; 基板投入不可
LD W07.1                 ; 放熱部接着剤塗布有無
ANB T922                 ; E-cap sensor
LDB W07.1                ; 放熱部接着剤塗布有無
AND T922                 ; E-cap sensor
ORL
ANB M97                  ; ｺﾝﾃﾞﾝｻｷｬｯﾌﾟｾﾝｻ無
ORL
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
LD W01.2                 ; 製品IDｴﾗｰ､段取
AND= D200 K0             ; ｴﾗｰｺｰﾄﾞNo.
ANL
DW K900 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K10 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5008                 ; 投入不可確認
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;製品IDｴﾗｰ･ﾀﾞｲｱﾛｸﾞ選択
LD M5010                 ; IDｴﾗｰ確認､処理選択　(ﾀﾞｲｱﾛｸﾞ)
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
MPS
AND B1020                ; ﾀﾞｲｱﾛｸﾞ選択PB1
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MRD
AND B1021                ; ﾀﾞｲｱﾛｸﾞ選択PB2
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K12 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND B1022                ; ﾀﾞｲｱﾛｸﾞ選択PB3
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K25 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･基板取出処理(B1020)
LD M5011                 ; ﾜｰｸ取出選択
MPS
OUT W0101.2              ; ID異常時取出選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･ﾜｰｸ治具交換選択(B1021)
LD M5012                 ; 治具交換選択
AND M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
AND M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5013                 ; 段取一時停止処理
AND M2056                ; 段取ﾌﾞﾛｰﾃｰﾌﾞﾙ1後退確認
AND M2156                ; 段取ﾌﾞﾛｰﾃｰﾌﾞﾙ2後退確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5014                 ; 段取時ﾌﾞﾛｰﾃｰﾌﾞﾙ後退確認､POSE要求
AND M130                 ; 全体PAUSE
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5015                 ; 一時停止確認
MPS
OUT W0101.3              ; ID異常時段取選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5016                 ; IDｴﾗｰ解除確認
ANB M130                 ; 全体PAUSE
MOV K7 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;治具IDｴﾗｰ･基板取出後ｻｲｸﾙ停止(B1022)
LD M5025                 ; ｻｲｸﾙ停止選択
MPS
OUT W0101.1              ; ID異常時ｻｲｸﾙ停止選択
MPP
ANB W01.2                ; 製品IDｴﾗｰ､段取
MPS
AND X126D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB X126D                ; 基板投入CVﾜｰｸ確認
MOV K27 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5026                 ; 投入位置ﾜｰｸ有取出要求
ANB X126D                ; 基板投入CVﾜｰｸ確認
ANP X126C                ; 基板供給確認PB
MOV K25 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5027                 ; ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5028                 ; ｺﾝﾍﾞｱ排出逆転
MPS
TMR K931 K80             ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
MPP
LD T931                  ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
ORF X126D                ; 基板投入CVﾜｰｸ確認
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5029                 ; 基板取出要求
ANP X126C                ; 基板供給確認PB
ANB X126D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5030                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5031                 ; 基板取出要求ON
AND W0101.15             ; 基板投入部取出要求
AND W01.15               ; 基板投入部取出確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5032                 ; 基板取出確認
ANB W0101.15             ; 基板投入部取出要求
ANB W01.15               ; 基板投入部取出確認
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND= W0130 K0            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5033                 ; ID_NG払出ｻｲｸﾙ停止
AND M36                  ; 払出ﾓｰﾄﾞ
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;<h1/>/ ｸﾘｽﾀﾙｷｬｯﾌﾟ検査
;検査有無判定
;Inspection presence or absence
LD M5040                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
MPS
AND D5463.7              ; UT9ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB D5463.7              ; UT9ｸﾘｽﾀﾙｷｬｯﾌﾟ検査有無
MOV K50 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;検査有効･無効判定
;Inspection enable or disable
LD M5041                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
MPS
LDB W08.12               ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査無効
AND W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
ANL
MPS
AND W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
MOV K50 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND W08.12               ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査無効
MOV K50 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｸﾘｽﾀﾙｷｬｯﾌﾟ検査NG 基板取出へ
;Crystal Cap Inspection NG Return
LD M5042                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査NG確認
ANB W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｸﾘｽﾀﾙｷｬｯﾌﾟ日常点検ﾓｰﾄﾞ
;Crystal Cap Daily check MODE
LD M5045                 ; ｲﾝﾃﾞｯｸｽNo. ｾｯﾄ
ANB W01.0                ; 基板読込完了
MPS
AND>= K0 D5030           ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
AND<= K10 D5030          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MOV K1 D5030             ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
MRD
MOV D5030 W0130          ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ  基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5046                 ; 基板ｺｰﾄﾞ読込要求
MPS
AND W01.0                ; 基板読込完了
MPS
LDB W01.1                ; 基板投入不可
ANB W01.2                ; 製品IDｴﾗｰ､段取
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND W01.1                ; 基板投入不可
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND W01.2                ; 製品IDｴﾗｰ､段取
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5047                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
AND W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
LD W05.5                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
ORB W05.5                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査結果
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5048                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査完了
ANB W05.4                ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査完了
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;<h1/>/ PA検査
LD M5050                 ; PA検査開始
MPS
AND D5463.3              ; UT9PA検査有無
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB D5463.3              ; UT9PA検査有無
MOV K67 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;PA検査Prg_No.切替
LD M5051                 ; PA検査開始ON
AND W03.0                ; PA検査開始受付
MPS
AND W08.0                ; PA画像検査無効
MOV K64 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
LDB W08.0                ; PA画像検査無効
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MOV D5414 W05C0          ; UT9_PA画像検査No.  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
LD M5052                 ; PA検査　PRO_No切替指令
MPS
AND B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
TMR K942 K200            ; PA_IV Prog切替応答無し
LD M5053                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5054                 ; PA検査　PRO_No切替確認
ANB B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M5055                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2010                ; PA検査結果取得可能
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;PA検査実行
LD M5056                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5057                 ; PA検査　指令
MPS
AND B2012                ; PA検査ﾋﾞｼﾞｰ
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
TMR K943 K200            ; PA_IV検査開始応答無し
;検査完了結果判定
LD M5058                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
MPS
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
AND B2020                ; PA検査総合判定(OFF:NG ON:OK)
MOV K64 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;PA検査OK
LD M5064                 ; PA検査OK
AND W0103.1              ; PA検査結果
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5065                 ; PA検査OK動作中OFF
ANB W0103.0              ; PA検査開始
ANB W03.0                ; PA検査開始受付
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5066                 ; PA検査結果OFF
ANB W0103.1              ; PA検査結果
ANB W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;<h1/>/ ﾗﾍﾞﾙ発行&照合
;ﾌﾚｰﾑﾗﾍﾞﾙ発行
LD M5067                 ; ﾗﾍﾞﾙ発行要求
MPS
OUT W0101.7              ; ﾌﾚｰﾑﾗﾍﾞﾙ発行要求
MPP
AND W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5068                 ; ﾗﾍﾞﾙ発行確認
ANB W01.7                ; ﾌﾚｰﾑﾗﾍﾞﾙ発行完了
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｹｰｽ投入部より照合判定待ち
LD M5069                 ; ﾗﾍﾞﾙ発行完､ｹｰｽ投入可､照合判定
AND M1015                ; ｹｰｽ照合完了､基板搬送可
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5070                 ; ｹｰｽ投入､照合完了確認
ANB M1015                ; ｹｰｽ照合完了､基板搬送可
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;基板搬送可･完了確認
LD M5071                 ; 照合完了搬送可(基板搬送へ)
AND M5110                ; 基板搬送完了(投入部へ)
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5072                 ; 基板搬送確認
ANB M5110                ; 基板搬送完了(投入部へ)
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;基板取出
LD M5080                 ; NG基板排出
ANB W0103.0              ; PA検査開始
MPS
AND X126D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
MPP
ANB X126D                ; 基板投入CVﾜｰｸ確認
MOV K82 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5081                 ; 投入位置ﾜｰｸ有取出要求
ANB X126D                ; 基板投入CVﾜｰｸ確認
ANP X126C                ; 基板供給確認PB
MOV K80 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5082                 ; ｽﾄｯﾊﾟ下降
AND T910                 ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5083                 ; ｺﾝﾍﾞｱ排出逆転
MPS
TMR K932 K80             ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
MPP
LD T932                  ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
ORF X126D                ; 基板投入CVﾜｰｸ確認
ANL
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5084                 ; 読込NG取出要求
LDP X126C                ; 基板供給確認PB
LD W01.2                 ; 製品IDｴﾗｰ､段取
AND W01.1                ; 基板投入不可
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
AND CR2003               ; 常時OFF
ORL
ANL
ANB X126D                ; 基板投入CVﾜｰｸ確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5085                 ; ｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5086                 ; 基板取出要求ON
AND W0101.15             ; 基板投入部取出要求
AND W01.15               ; 基板投入部取出確認
@INC D5001               ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD M5087                 ; 基板取出確認
ANB W0101.15             ; 基板投入部取出要求
ANB W01.15               ; 基板投入部取出確認
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND= W0130 K0            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MOV K0 D5001             ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M5000                 ; 基板投入部ｻｲｸﾙ開始
OR M5004                 ; 基板ｺﾝﾍﾞｱ回転
OR M5071                 ; 照合完了搬送可(基板搬送へ)
ANL
MOV K98 D5001            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M5098                 ; ﾕﾆｯﾄ9-1ｻｲｸﾙ停止処理
@MOV K99 D5001           ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
;<h1/>□ 搬送ｺﾝﾍﾞｱ
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M5100 M5149         ; 基板CV搬送開始  ﾕﾆｯﾄ9-3ｻｲｸﾙ終了
LDB M5485                ; ﾕﾆｯﾄ9起動中
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
DW K0 D5202
LD M5485                 ; ﾕﾆｯﾄ9起動中
MOV D5002 Z1             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
SET M5100:Z1             ; 基板CV搬送開始
CAL+ D5002 K5100 D5202   ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;自動運転ステップ開始
;取出部基板確認
LD M5100                 ; 基板CV搬送開始
MPS
AND T991                 ; 基板投入CV取出ﾜｰｸ無
MOV K3 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
AND T990                 ; 基板投入CV取出ﾜｰｸ有
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板有取出要求
LD M5101                 ; 基板取出可(嵌合RBへ)
AND M5550                ; 基板取出完了(基板CVへ)
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5102                 ; 基板取出確認
ANB M5550                ; 基板取出完了(基板CVへ)
AND T991                 ; 基板投入CV取出ﾜｰｸ無
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5103                 ; 搬送可待ち
AND M3650                ; 再投入ﾓｰﾄﾞ基板搬送可
OR M5495                 ; 基板空送り中
OUT M5495                ; 基板空送り中
LD M5103                 ; 搬送可待ち
LD M5071                 ; 照合完了搬送可(基板搬送へ)
LD M3650                 ; 再投入ﾓｰﾄﾞ基板搬送可
AND M5495                ; 基板空送り中
ORL
OR M3680                 ; 点検投入ﾓｰﾄﾞ基板搬送可
LD X126E                 ; 基板投入CV読取ﾜｰｸ確認
AND M256                 ; debug
LD X1270                 ; 基板投入CVﾜｰｸ有
AND T991                 ; 基板投入CV取出ﾜｰｸ無
ORL
LD M3690                 ; 排出ｼﾌﾄ払出ﾓｰﾄﾞ基板搬送可
ANB M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
LD M2530                 ; 供給ｼﾌﾄ払出ﾓｰﾄﾞ基板搬送可
LDB M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
ORB M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し
ORB M2493                ; ﾕﾆｯﾄ3ﾜｰｸ無し
ORB X126E                ; 基板投入CV読取ﾜｰｸ確認
AND M2552                ; 投入ｼﾌﾄ投入可(ﾘﾆｺﾝへ)
ANB M3010                ; ｷｬﾘｱ投入要求(ｼﾌﾄ投入へ)
AND M36                  ; 払出ﾓｰﾄﾞ
ORL
AND M95                  ; 能拡対応ﾓｰﾄﾞﾌﾗｸﾞ(ONで能拡ﾓｰﾄﾞ)
ORL
ANL
ORL
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5104                 ; 搬送可確認
ANB M3650                ; 再投入ﾓｰﾄﾞ基板搬送可
ANB M3680                ; 点検投入ﾓｰﾄﾞ基板搬送可
AND T991                 ; 基板投入CV取出ﾜｰｸ無
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板搬送動作
LD M5105                 ; 搬送爪閉
AND T918                 ; 基板搬送爪下降端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5106                 ; 基板CV下流ｽﾄｯﾊﾟ下降
AND T912                 ; 搬送下流ｽﾄｯﾊﾟ下降端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;搬送爪送りでｲﾝﾃﾞｯｸｽｶｳﾝﾄｱｯﾌﾟとｲﾝﾃﾞｯｸｽﾃﾞｰﾀをｼﾌﾄ
LD M5107                 ; 搬送爪送
@INC D5030               ; ｲﾝﾃﾞｯｸｽｶｳﾝﾀ
@BMOV W0130 W0131 K6     ; 基板投入部ｲﾝﾃﾞｯｸｽNo.  基板搬送1ｲﾝﾃﾞｯｸｽNo.
MPS
@MOV K0 W0130            ; 基板投入部ｲﾝﾃﾞｯｸｽNo.
MPP
AND T916                 ; 基板搬送送端
MPS
LD<> W0135 K0            ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
AND T990                 ; 基板投入CV取出ﾜｰｸ有
OR= W0135 K0             ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
ORB X1270                ; 基板投入CVﾜｰｸ有
AND T920                 ; 基板CV扉閉確認
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
LD<> W0135 K0            ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
ANB T990                 ; 基板投入CV取出ﾜｰｸ有
AND X1270                ; 基板投入CVﾜｰｸ有
ANL
TMR K940 K30             ; PCB行方不明ERR
CON
AND B012A                ; 基板CV取出位置PCB行方不明
DW K901 D200             ; ｴﾗｰｺｰﾄﾞNo.
CON
MOV K20 D5002            ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;*** PCB搬送異常
LD M5120                 ; 基板搬送異常
MPS
LDB X127E                ; 基板搬送CV装置側引戸
OR M5440                 ; 除材扉開確認
ANL
OUT M5440                ; 除材扉開確認
MPP
LD M5440                 ; 除材扉開確認
AND T920                 ; 基板CV扉閉確認
ANB T990                 ; 基板投入CV取出ﾜｰｸ有
AND<> D200 K0            ; ｴﾗｰｺｰﾄﾞNo.
ANP B1023                ; 基板取出確認TP-PB
ANL
DW K0 W0135              ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
CON
DW K0 D200               ; ｴﾗｰｺｰﾄﾞNo.
CON
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5121                 ; 搬送異常基板除材(CV→Assy)
AND M5580                ; 除材処理受諾
MOV K8 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;***
LD M5108                 ; 基板CV下流ｽﾄｯﾊﾟ上昇＆搬送爪開
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
AND T919                 ; 基板搬送爪上昇端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LDB M5109                ; 搬送爪戻
ANB M5110                ; 基板搬送完了(投入部へ)
ANB M5111                ; 搬送爪戻確認
INV
OUT M5350                ; 搬送爪戻_補助
LD M5109                 ; 搬送爪戻
AND Y2404                ; 基板搬送戻りSV
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;基板投入へ基板搬送完了ON
LD M5110                 ; 基板搬送完了(投入部へ)
ANB M5071                ; 照合完了搬送可(基板搬送へ)
ANB M5090                ; 投入抑制中
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5111                 ; 搬送爪戻確認
AND T917                 ; 基板搬送戻端
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
LD M5112                 ; 取出部ﾜｰｸ確認
MPS
LD T990                  ; 基板投入CV取出ﾜｰｸ有
AND T920                 ; 基板CV扉閉確認
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
AND T991                 ; 基板投入CV取出ﾜｰｸ無
MPS
AND= W0135 K0            ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
MPP
AND<> W0135 K0           ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
TMR K941 K10             ; PCB行方不明ERR
;基板取出可･確認
LD M5113                 ; 基板取出可ON(基板嵌合へ)
LD M5518                 ; 基板取出完了(照合NG)
OR M5528                 ; 基板取出完了(塗布検査NG)
OR M5550                 ; 基板取出完了(基板CVへ)
OR M5582                 ; NG_Case有 基板除材完了
ANL
@INC D5002               ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
@MOV K0 W0135            ; 基板取出部ｲﾝﾃﾞｯｸｽNo.
LD M5114                 ; 基板取出完了確認
ANB M5518                ; 基板取出完了(照合NG)
ANB M5528                ; 基板取出完了(塗布検査NG)
ANB M5550                ; 基板取出完了(基板CVへ)
MOV K0 D5002             ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M5103                 ; 搬送可待ち
LD M5113                 ; 基板取出可ON(基板嵌合へ)
ANB X126F                ; 基板投入CV取出ﾜｰｸ確認
ORL
ANL
MOV K48 D5002            ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M5148                 ; ﾕﾆｯﾄ9-3ｻｲｸﾙ停止
@MOV K49 D5002           ; ﾕﾆｯﾄ9-3自動運転ｽﾃｯﾌﾟ
;<h1/>■工程処理 PLC→PC I/F
;Code Read / CC Check
LD M5007                 ; 基板ｺｰﾄﾞ読込要求
OR M5046                 ; 基板ｺｰﾄﾞ読込要求
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0101.0              ; 基板読込要求
LD M5041                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
OR M5047                 ; ｸﾘｽﾀﾙｷｬｯﾌﾟ検査開始
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0105.4              ; ｸﾘｽﾀﾙｷｬｯﾌﾟ画像検査開始
;PA検査開始･結果
LDB M5031                ; 基板取出要求ON
ANB M5086                ; 基板取出要求ON
INV
OUT W0101.15             ; 基板投入部取出要求
LD M5051                 ; PA検査開始ON
OR W0103.0               ; PA検査開始
ANB M5065                ; PA検査OK動作中OFF
ANB M5080                ; NG基板排出
AND M5485                ; ﾕﾆｯﾄ9起動中
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0103.0              ; PA検査開始
LD M5064                 ; PA検査OK
OR W0103.1               ; PA検査結果
ANB M5066                ; PA検査結果OFF
AND M5485                ; ﾕﾆｯﾄ9起動中
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
OUT W0103.1              ; PA検査結果
;投入許可表示ﾌﾗｸﾞ
LD M253                  ; 20141021 　koike
LD M5005                 ; ｽﾄｯﾊﾟ上昇
OR M5200                 ; 基板投入不可
ANL
ANB M5064                ; PA検査OK
ANB M5072                ; 基板搬送確認
ANB M5087                ; 基板取出確認
AND M5485                ; ﾕﾆｯﾄ9起動中
OUT M5200                ; 基板投入不可
;<h1/>■原点復帰
;□□□□□□□□□□□□□□□
;□　　 UNIT9原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
ZRES M5250 M5299         ; 原点復帰可能確認  ﾕﾆｯﾄ9原点終了
LDB M5484                ; ﾕﾆｯﾄ9原点復帰中
MOV K0 D5010             ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
DW K0 D5210
LD M5484                 ; ﾕﾆｯﾄ9原点復帰中
MOV D5010 Z1             ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
SET M5250:Z1             ; 原点復帰可能確認
CAL+ D5010 K5250 D5210   ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
;原点復帰ｻｲｸﾙ監視
LD M5484                 ; ﾕﾆｯﾄ9原点復帰中
TMR K900 K300            ; UNIT9原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M5250                 ; 原点復帰可能確認
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5251                 ; 基板搬送爪開
AND T919                 ; 基板搬送爪上昇端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5252                 ; 基板搬送爪戻
AND T917                 ; 基板搬送戻端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5253                 ; 位置決開
AND T915                 ; 搬送位置決め開端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5254                 ; 基板CVｽﾄｯﾊﾟ上昇
AND T911                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND T913                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
@INC D5010               ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M5255                 ; 基板搬送原点完了
AND M5480                ; ﾕﾆｯﾄ9原点
@MOV K48 D5010           ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
LD M5298                 ; ﾕﾆｯﾄ9原点確認
ANB M82                  ; 原点復帰開始
@MOV K49 D5010           ; ﾕﾆｯﾄ9原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT9手動回路  　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND T920                 ; 基板CV扉閉確認
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M5485                ; ﾕﾆｯﾄ9起動中
ANB M5484                ; ﾕﾆｯﾄ9原点復帰中
ANB M5811                ; 基板嵌合RB1ｻｲｸﾙ
ANB M5841                ; 基板照合1ｻｲｸﾙ
MC
;基板CV上流ｽﾄｯﾊﾟ上下
LD X1263                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
OUT M5300                ; 基板CV上流ｽﾄｯﾊﾟ下降可
LD B1180                 ; UNIT9-1-1 TP-PB
AND M5300                ; 基板CV上流ｽﾄｯﾊﾟ下降可
OR M5301                 ; 基板CV上流ｽﾄｯﾊﾟ下昇
ANPB M5303               ; 基板CV上流ｽﾄｯﾊﾟ上昇
OUT M5301                ; 基板CV上流ｽﾄｯﾊﾟ下昇
LD CR2002                ; 常時ON
OUT M5302                ; 基板CV上流ｽﾄｯﾊﾟ上昇可
LD B1181                 ; UNIT9-1-2 TP-PB
AND M5302                ; 基板CV上流ｽﾄｯﾊﾟ上昇可
OR M5303                 ; 基板CV上流ｽﾄｯﾊﾟ上昇
ANPB M5301               ; 基板CV上流ｽﾄｯﾊﾟ下昇
OUT M5303                ; 基板CV上流ｽﾄｯﾊﾟ上昇
;基板CV下流ｽﾄｯﾊﾟ上下
LD X1261                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
OUT M5304                ; 基板CV下流ｽﾄｯﾊﾟ下降可
LD B1182                 ; UNIT9-1-3 TP-PB
AND M5304                ; 基板CV下流ｽﾄｯﾊﾟ下降可
OR M5305                 ; 基板CV下流ｽﾄｯﾊﾟ下昇
ANPB M5307               ; 基板CV下流ｽﾄｯﾊﾟ上昇
OUT M5305                ; 基板CV下流ｽﾄｯﾊﾟ下昇
LD X1266                 ; 基板搬送送端
OR X1267                 ; 基板搬送戻端
OR X1269                 ; 基板搬送爪上昇端
OUT M5306                ; 基板CV下流ｽﾄｯﾊﾟ上昇可
LD B1183                 ; UNIT9-1-4 TP-PB
AND M5306                ; 基板CV下流ｽﾄｯﾊﾟ上昇可
OR M5307                 ; 基板CV下流ｽﾄｯﾊﾟ上昇
ANPB M5305               ; 基板CV下流ｽﾄｯﾊﾟ下昇
OUT M5307                ; 基板CV下流ｽﾄｯﾊﾟ上昇
;基板搬送爪上下
LD CR2002                ; 常時ON
OUT M5308                ; 基板搬送爪閉可
LD B1184                 ; UNIT9-1-5 TP-PB
AND M5308                ; 基板搬送爪閉可
OR M5309                 ; 基板搬送爪閉
ANPB M5311               ; 基板搬送爪開
OUT M5309                ; 基板搬送爪閉
LD CR2002                ; 常時ON
OUT M5310                ; 基板搬送爪開可
LD B1185                 ; UNIT9-1-6 TP-PB
AND M5310                ; 基板搬送爪開可
OR M5311                 ; 基板搬送爪開
ANPB M5309               ; 基板搬送爪閉
OUT M5311                ; 基板搬送爪開
;基板搬送爪送戻
LDB X126F                ; 基板投入CV取出ﾜｰｸ確認
AND X1265                ; 搬送位置決め開端
AND X1262                ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1269                 ; 基板搬送爪上昇端
LD X3562                 ; 基板嵌合RB原点(42)
OR CR2002                ; 常時ON
ANL
OUT M5312                ; 基板搬送爪送可
LD B1186                 ; UNIT9-1-7 TP-PB
AND M5312                ; 基板搬送爪送可
OR M5313                 ; 基板搬送爪送
ANPB M5315               ; 基板搬送爪戻
OUT M5313                ; 基板搬送爪送
LD X1262                 ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1269                 ; 基板搬送爪上昇端
LD X3562                 ; 基板嵌合RB原点(42)
OR CR2002                ; 常時ON
ANL
OUT M5314                ; 基板搬送爪戻可
LD B1187                 ; UNIT9-1-8 TP-PB
AND M5314                ; 基板搬送爪戻可
OR M5315                 ; 基板搬送爪戻
ANPB M5313               ; 基板搬送爪送
OUT M5315                ; 基板搬送爪戻
;基板CV正転･逆転
LD CR2002                ; 常時ON
OUT M5316                ; 基板搬送CV位置決閉可
LD B1188                 ; UNIT9-1-9 TP-PB
AND M5316                ; 基板搬送CV位置決閉可
OR M5317                 ; 基板搬送CV位置決閉
ANPB M5319               ; 基板搬送CV位置決開
OUT M5317                ; 基板搬送CV位置決閉
LD CR2002                ; 常時ON
OUT M5318                ; 基板搬送CV位置決開可
LD B1189                 ; UNIT9-1-10 TP-PB
AND M5318                ; 基板搬送CV位置決開可
OR M1519
ANPB M5317               ; 基板搬送CV位置決閉
OUT M5319                ; 基板搬送CV位置決開
;基板CV正転･逆転
LD CR2002                ; 常時ON
ANB M5323                ; 基板CV逆転
OUT M5320                ; 基板CV正転可
LD B118A                 ; UNIT9-1-11 TP-PB
AND M5320                ; 基板CV正転可
OUT M5321                ; 基板CV正転
LD CR2002                ; 常時ON
ANB M5321                ; 基板CV正転
OUT M5322                ; 基板CV逆転可
LD B118B                 ; UNIT9-1-12 TP-PB
AND M5322                ; 基板CV逆転可
OUT M5323                ; 基板CV逆転
;PA検査
LD CR2002                ; 常時ON
AND B2015                ; PA検査ﾚﾃﾞｨ
OUT M5330                ; PA検査開始可
LDP B118C                ; UNIT9-1-13 TP-PB
AND M5330                ; PA検査開始可
OR M5331                 ; PA検査　PRO_No切替指令
ANB M5332                ; PA検査　PRO_No切替確認
MOV D5100 W05C0          ; 手動PA検査No  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
OUT M5331                ; PA検査　PRO_No切替指令
LD M5331                 ; PA検査　PRO_No切替指令
AND B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
OR M5332                 ; PA検査　PRO_No切替確認
ANB M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
OUT M5332                ; PA検査　PRO_No切替確認
LD M5332                 ; PA検査　PRO_No切替確認
ANB B2002                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5333                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB M5334                ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
OUT M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M5333                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2010                ; PA検査結果取得可能
ANB B2020                ; PA検査総合判定(OFF:NG ON:OK)
OR M5334                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB M5335                ; PA検査　指令
OUT M5334                ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
;PA検査実行
LD M5334                 ; PA検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5335                 ; PA検査　指令
ANB M5336                ; PA検査　BUSY確認
OUT M5335                ; PA検査　指令
LD M5335                 ; PA検査　指令
AND B2012                ; PA検査ﾋﾞｼﾞｰ
OR M5336                 ; PA検査　BUSY確認
ANB M5337                ; PA検査完了
OUT M5336                ; PA検査　BUSY確認
;検査完了結果判定
LD M5336                 ; PA検査　BUSY確認
ANB B2012                ; PA検査ﾋﾞｼﾞｰ
LDB B2020                ; PA検査総合判定(OFF:NG ON:OK)
OR B2020                 ; PA検査総合判定(OFF:NG ON:OK)
ANL
OUT M5337                ; PA検査完了
MCR
;<h1/>■出力
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;基板CV上流ｽﾄｯﾊﾟ上下
LDB M5003                ; 基板投入ｽﾄｯﾊﾟ下降
ANB M5027                ; ｽﾄｯﾊﾟ下降
ANB M5082                ; ｽﾄｯﾊﾟ下降
ANB M5301                ; 基板CV上流ｽﾄｯﾊﾟ下昇
INV
OR Y2400                 ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB M5005                ; ｽﾄｯﾊﾟ上昇
ANB M5030                ; ｽﾄｯﾊﾟ上昇
ANB M5085                ; ｽﾄｯﾊﾟ上昇
ANB M5254                ; 基板CVｽﾄｯﾊﾟ上昇
ANB M5303                ; 基板CV上流ｽﾄｯﾊﾟ上昇
OUT Y2400                ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
;基板CV下流ｽﾄｯﾊﾟ上下
LDB M5106                ; 基板CV下流ｽﾄｯﾊﾟ下降
ANB M5305                ; 基板CV下流ｽﾄｯﾊﾟ下昇
INV
OR Y2401                 ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB M5108                ; 基板CV下流ｽﾄｯﾊﾟ上昇＆搬送爪開
ANB M5254                ; 基板CVｽﾄｯﾊﾟ上昇
ANB M5307                ; 基板CV下流ｽﾄｯﾊﾟ上昇
OUT Y2401                ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
;基板CV位置決開閉
LDB CR2003               ; 常時OFF
ANB M5317                ; 基板搬送CV位置決閉
ANPB X356C               ; 基板押さえ閉要求
INV
OR Y2402                 ; 基板搬送CV位置決出
ANB M5523                ; 塗布検査NG発生中
ANB M5253                ; 位置決開
ANB M5319                ; 基板搬送CV位置決開
ANB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
ANB M5510                ; 照合NG発生中
ANPB X356D               ; 基板押さえ開要求
ANB M5514                ; 照合NGﾜｰｸ取出確認位置決め開
OUT Y2402                ; 基板搬送CV位置決出
;基板搬送爪送戻
LDB M5107                ; 搬送爪送
ANB M5313                ; 基板搬送爪送
INV
LD Y2403                 ; 基板搬送送りSV
ANB Y2404                ; 基板搬送戻りSV
ORL
LD T920                  ; 基板CV扉閉確認
OR X1266                 ; 基板搬送送端
ANL
ANB M130                 ; 全体PAUSE
OUT Y2403                ; 基板搬送送りSV
LDB M5350                ; 搬送爪戻_補助
ANB M5252                ; 基板搬送爪戻
ANB M5315                ; 基板搬送爪戻
INV
LD Y2404                 ; 基板搬送戻りSV
ANB Y2403                ; 基板搬送送りSV
ORL
LD T920                  ; 基板CV扉閉確認
OR X1267                 ; 基板搬送戻端
ANL
ANB M130                 ; 全体PAUSE
OUT Y2404                ; 基板搬送戻りSV
;基板搬送爪開閉
LDB M5105                ; 搬送爪閉
ANB M5309                ; 基板搬送爪閉
INV
OR Y2405                 ; 基板搬送爪閉SV
ANB M5108                ; 基板CV下流ｽﾄｯﾊﾟ上昇＆搬送爪開
ANB M5251                ; 基板搬送爪開
ANB M5311                ; 基板搬送爪開
OUT Y2405                ; 基板搬送爪閉SV
;基板CV
LDB M5001                ; ｺﾝﾍﾞｱ投入回転
ANB M5004                ; 基板ｺﾝﾍﾞｱ回転
ANB M5321                ; 基板CV正転
INV
AND T920                 ; 基板CV扉閉確認
OUT Y2310                ; 基板供給CV正転
LDB M5028                ; ｺﾝﾍﾞｱ排出逆転
ANB M5083                ; ｺﾝﾍﾞｱ排出逆転
ANB M5323                ; 基板CV逆転
INV
AND T920                 ; 基板CV扉閉確認
OUT Y2311                ; 基板供給CV逆転
;基板投入部表示･BZ
LDB M5081                ; 投入位置ﾜｰｸ有取出要求
ANB M5084                ; 読込NG取出要求
ANB M5029                ; 基板取出要求
INV
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2320                ; 基板投入確認PL
LDB B0120                ; 払出ﾓｰﾄﾞ基板投入不可
ANB B012C                ; 基板未到着
ANB M5081                ; 投入位置ﾜｰｸ有取出要求
ANB M5084                ; 読込NG取出要求
ANB M5029                ; 基板取出要求
INV
LD M5200                 ; 基板投入不可
OR M36                   ; 払出ﾓｰﾄﾞ
AND X126D                ; 基板投入CVﾜｰｸ確認
ORL
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2321                ; 基板投入ﾌﾞｻﾞｰ
LD M5485                 ; ﾕﾆｯﾄ9起動中
MPS
LDB M36                  ; 払出ﾓｰﾄﾞ
ANB M5200                ; 基板投入不可
AND>= D5001 K1           ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
AND<= D5001 K4           ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
ANL
OUT Y2322                ; 基板投入OK_PL
MPP
LD>= D5001 K8            ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
AND<= D5001 K39          ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
LD>= D5001 K80           ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
AND<= D5001 K87          ; ﾕﾆｯﾄ9-2自動運転ｽﾃｯﾌﾟ
ORL
OR M5200                 ; 基板投入不可
OR M36                   ; 払出ﾓｰﾄﾞ
ANL
OUT Y2323                ; 基板投入NG_PL
;PA検査ｶﾒﾗ
LDB M5057                ; PA検査　指令
ANB M5335                ; PA検査　指令
INV
OUT B2040                ; PA検査ﾄﾘｶﾞ要求
LDB M5052                ; PA検査　PRO_No切替指令
ANB M5331                ; PA検査　PRO_No切替指令
INV
OUT B2042                ; PA検査ﾌﾟﾛｸﾞﾗﾑ切替要求
LDB CR2003               ; 常時OFF
INV
OUT B2043                ; PA検査ﾜｰﾆﾝｸﾞｸﾘｱ要求
LDB M5055                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
ANB M5333                ; PA検査ﾊﾞｯﾌｧｸﾘｱ
INV
OUT B2045                ; PA検査ﾊﾞｯﾌｧｸﾘｱ要求
;電解ｺﾝﾃﾞﾝｻ検出線さ　→　PCへ
LDB X127D                ; 電解ｺﾝﾃﾞﾝｻ検出      (有=OFF)
TMH K960 K10             ; 検出ﾃﾞｨﾚｲ
CON
AND T960                 ; 検出ﾃﾞｨﾚｲ
OUT W0102.15
;<h1/>■表示
;□□　　パネル表示　　□□
LD X1260                 ; 搬送上流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1680                ; UNIT9-1-1 TP-PL
LD X1261                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1681                ; UNIT9-1-2 TP-PL
LD X1262                 ; 搬送下流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1682                ; UNIT9-1-3 TP-PL
LD X1263                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1683                ; UNIT9-1-4 TP-PL
LD X1268                 ; 基板搬送爪下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1684                ; UNIT9-1-5 TP-PL
LD X1269                 ; 基板搬送爪上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1685                ; UNIT9-1-6 TP-PL
LD X1266                 ; 基板搬送送端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1686                ; UNIT9-1-7 TP-PL
LD X1267                 ; 基板搬送戻端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1687                ; UNIT9-1-8 TP-PL
LD X1264                 ; 搬送位置決め閉端
OUT B1688                ; UNIT9-1-9 TP-PL
LD X1265                 ; 搬送位置決め開端
OUT B1689                ; UNIT9-1-10 TP-PL
LD Y2310                 ; 基板供給CV正転
OUT B168A                ; UNIT9-1-11 TP-PL
LD Y2311                 ; 基板供給CV逆転
OUT B168B                ; UNIT9-1-12 TP-PL
;<h1/>■異常
;□□　　異常検出　　□□
;B120(K289)--B12F(K304)  B130(K305)--B13F(K320)
LD X126D                 ; 基板投入CVﾜｰｸ確認
AND M5000                ; 基板投入部ｻｲｸﾙ開始
AND M36                  ; 払出ﾓｰﾄﾞ
OUT B0120                ; 払出ﾓｰﾄﾞ基板投入不可
@DW K288 D0
CON
@FIFOW D0 D900           ; 異常内容
LD X123A                 ; 塗布検査画像ｴﾗｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0121                ; 基板供給CV画像検査異常
ORL
AND M50                  ; 運転準備
OUT B0121                ; 基板供給CV画像検査異常
@DW K289 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0621
AND B0121                ; 基板供給CV画像検査異常
OUT B0621
LD B201F                 ; PA検査ｴﾗｰ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
ORL
AND M50                  ; 運転準備
OUT B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
@DW K290 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0622
AND B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
OUT B0622
LD Y2400                 ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB X1260                ; 搬送上流ｽﾄｯﾊﾟ下降端
LDB Y2400                ; 基板搬送CV上流ｽﾄｯﾊﾟ下降
ANB X1261                ; 搬送上流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K950 K50             ; 基板搬送CV上流ｽﾄｯﾊﾟ確認
MPP
AND T950                 ; 基板搬送CV上流ｽﾄｯﾊﾟ確認
OUT B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
@DW K291 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0623
AND B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
OUT B0623
LD Y2401                 ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB X1262                ; 搬送下流ｽﾄｯﾊﾟ下降端
LDB Y2401                ; 基板搬送CV下流ｽﾄｯﾊﾟ下降
ANB X1263                ; 搬送下流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K951 K50             ; 基板搬送CV下流ｽﾄｯﾊﾟ確認
MPP
AND T951                 ; 基板搬送CV下流ｽﾄｯﾊﾟ確認
OUT B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
@DW K292 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0624
AND B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
OUT B0624
LD Y2402                 ; 基板搬送CV位置決出
ANB X1264                ; 搬送位置決め閉端
LDB Y2402                ; 基板搬送CV位置決出
ANB X1265                ; 搬送位置決め開端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0125                ; 基板搬送CV位置決異常
ORL
AND M50                  ; 運転準備
MPS
TMR K952 K50             ; 基板搬送CV位置決確認
MPP
AND T952                 ; 基板搬送CV位置決確認
OUT B0125                ; 基板搬送CV位置決異常
@DW K293 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0625
AND B0125                ; 基板搬送CV位置決異常
OUT B0625
LD Y2403                 ; 基板搬送送りSV
ANB X1266                ; 基板搬送送端
LD Y2404                 ; 基板搬送戻りSV
ANB X1267                ; 基板搬送戻端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0126                ; 基板搬送爪送り異常
ORL
AND M50                  ; 運転準備
MPS
TMR K953 K50             ; 基板搬送爪送り確認
MPP
AND T953                 ; 基板搬送爪送り確認
OUT B0126                ; 基板搬送爪送り異常
@DW K294 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0626
AND B0126                ; 基板搬送爪送り異常
OUT B0626
LD Y2405                 ; 基板搬送爪閉SV
ANB X1268                ; 基板搬送爪下降端
LDB Y2405                ; 基板搬送爪閉SV
ANB X1269                ; 基板搬送爪上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0127                ; 基板搬送爪上下異常
ORL
AND M50                  ; 運転準備
MPS
TMR K954 K50             ; 基板搬送爪上下確認
MPP
AND T954                 ; 基板搬送爪上下確認
OUT B0127                ; 基板搬送爪上下異常
@DW K295 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0627
AND B0127                ; 基板搬送爪上下異常
OUT B0627
LD M17                   ; 原点復帰PB
ORF M81                  ; 原点復帰動作中
LD X126E                 ; 基板投入CV読取ﾜｰｸ確認
LD X126F                 ; 基板投入CV取出ﾜｰｸ確認
OR X1270                 ; 基板投入CVﾜｰｸ有
ANB X3560                ; 基板取出部領域(40)
ORL
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
ORL
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M1484                ; ﾕﾆｯﾄ1原点復帰中
OUT B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
@DW K296 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0628
AND B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
OUT B0628
LDB T920                 ; 基板CV扉閉確認
AND M30                  ; 自動ﾓｰﾄﾞ
OUT B0129                ; 基板搬送CV扉開
@DW K297 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0629                 ; 基板搬送CV扉開BZ停止
AND B0129                ; 基板搬送CV扉開
OUT B0629                ; 基板搬送CV扉開BZ停止
LD T940                  ; PCB行方不明ERR
AND M30                  ; 自動ﾓｰﾄﾞ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B012A                ; 基板CV取出位置PCB行方不明
ORL
AND M50                  ; 運転準備
OUT B012A                ; 基板CV取出位置PCB行方不明
@DW K298 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B062A
AND B012A                ; 基板CV取出位置PCB行方不明
OUT B062A
LD T942                  ; PA_IV Prog切替応答無し
OR T943                  ; PA_IV検査開始応答無し
AND M30                  ; 自動ﾓｰﾄﾞ
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B012B                ; PA検査IV応答無し
ORL
AND M50                  ; 運転準備
OUT B012B                ; PA検査IV応答無し
@DW K299 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B062B
AND B012B                ; PA検査IV応答無し
OUT B062B
LD T937                  ; 基板未到着
OUT B012C                ; 基板未到着
@DW K300 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B062C
AND B012C                ; 基板未到着
OUT B062C
LD CR2003                ; 常時OFF
AND M5084                ; 読込NG取出要求
OUT B012E                ; Board reject
MPS
@DW H012F D0
CON
@FIFOW D0 D900           ; 異常内容
MRD
AND M5450                ; PCBｺｰﾄﾞ読み取りNG
OUT B012F                ; ｺｰﾄﾞ読み取り異常
@DW H0130 D0
CON
@FIFOW D0 D900           ; 異常内容
MRD
AND M5451                ; CC検査NG
OUT B0130                ; CC検査NG
@DW H0131 D0
CON
@FIFOW D0 D900           ; 異常内容
MRD
AND M5452                ; PA検査NG
OUT B0131                ; PA検査NG
@DW H0132 D0
CON
@FIFOW D0 D900           ; 異常内容
MPP
AND M5453                ; ｺﾝﾃﾞﾝｻｷｬｯﾌﾟ異常
OUT B0132                ; UNIT9異常19
@DW H0133 D0
CON
@FIFOW D0 D900           ; 異常内容
;異常ﾌﾞｻﾞｰまとめ
LDB B0121                ; 基板供給CV画像検査異常
OR B0621
LDB B0122                ; 基板供給CV_PA検査ｶﾒﾗ異常
OR B0622
ANL
LDB B0123                ; 基板搬送CV上流ｽﾄｯﾊﾟ異常
OR B0623
ANL
LDB B0124                ; 基板搬送CV下流ｽﾄｯﾊﾟ異常
OR B0624
ANL
LDB B0125                ; 基板搬送CV位置決異常
OR B0625
ANL
LDB B0126                ; 基板搬送爪送り異常
OR B0626
ANL
LDB B0127                ; 基板搬送爪上下異常
OR B0627
ANL
LDB B0128                ; 基板C/V原点復帰時ﾜｰｸ有異常
OR B0628
ANL
LDB B012A                ; 基板CV取出位置PCB行方不明
OR B062A
ANL
LDB B012B                ; PA検査IV応答無し
OR B062B
ANL
INV
OUT M5489                ; ﾕﾆｯﾄ9異常BZ補助
END
ENDH
