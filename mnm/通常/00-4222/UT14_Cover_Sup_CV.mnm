DEVICE:53
;MODULE:UT14_Cover_Sup_CV
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD X149C                 ; 搬送上流ｽﾄｯﾊﾟ下降端
TMR K1410 K5             ; 搬送上流ｽﾄｯﾊﾟ下降端
LD X149D                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
TMR K1411 K3             ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD X149E                 ; 搬送下流ｽﾄｯﾊﾟ下降端
TMR K1412 K3             ; 搬送下流ｽﾄｯﾊﾟ下降端
LD X149F                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
TMR K1413 K3             ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD X1490                 ; 搬送位置決め閉端
TMR K1414 K5             ; 搬送位置決め閉端
LD X1491                 ; 搬送位置決め開端
TMR K1415 K3             ; 搬送位置決め開端
LD X1492                 ; ｶﾊﾞｰ搬送送端
TMR K1416 K3             ; ｶﾊﾞｰ搬送送端
LD X1493                 ; ｶﾊﾞｰ搬送戻端
TMR K1417 K3             ; ｶﾊﾞｰ搬送戻端
LD X1494                 ; ｶﾊﾞｰ搬送爪閉端
TMR K1418 K3             ; ｶﾊﾞｰ搬送爪下降端
LD X1495                 ; ｶﾊﾞｰ搬送爪開端
TMR K1419 K3             ; ｶﾊﾞｰ搬送爪上昇端
LD X1496                 ; ｶﾊﾞｰ搬送CV装置側ｶﾊﾞｰ
AND X1497                ; ｶﾊﾞｰ搬送CV投入側ｶﾊﾞｰ
OR CR2002                ; 常時ON
TMR K1420 K5             ; ｶﾊﾞｰCV扉閉確認
LD X1496                 ; ｶﾊﾞｰ搬送CV装置側ｶﾊﾞｰ
OR CR2002                ; 常時ON
TMR K1421 K5             ; ｶﾊﾞｰCV扉閉確認(RB用)
;□ ﾕﾆｯﾄ原点
LD T1411                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
OR CR2002                ; 常時ON
AND T1413                ; 搬送下流ｽﾄｯﾊﾟ上昇端
AND T1415                ; 搬送位置決め開端
AND T1417                ; ｶﾊﾞｰ搬送戻端
AND T1419                ; ｶﾊﾞｰ搬送爪上昇端
OUT M7980                ; ﾕﾆｯﾄ14原点
;□ 非常停止
LDB CR2003               ; 常時OFF
INV
OUT M7981                ; ﾕﾆｯﾄ14全異常
;□ 起動条件
LD M7980                 ; ﾕﾆｯﾄ14原点
OUT M7982                ; ﾕﾆｯﾄ14起動条件
;□ 起動OFF
LDB M50                  ; 運転準備
OR M7986                 ; ﾕﾆｯﾄ14ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M7983                ; ﾕﾆｯﾄ14起動OFF
;□ 原点復帰
LD M82                   ; 原点復帰開始
ANB M7984                ; ﾕﾆｯﾄ14原点復帰中
OR M7984                 ; ﾕﾆｯﾄ14原点復帰中
ANB M7983                ; ﾕﾆｯﾄ14起動OFF
ANB M7991                ; ﾕﾆｯﾄ14ｴﾗｰ発生
ANB M7799                ; ﾕﾆｯﾄ14原点終了
OUT M7984                ; ﾕﾆｯﾄ14原点復帰中
;□ 起動中
LD M73                   ; 各工程起動指令
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
AND CR2003               ; 常時OFF
ORL
AND M7982                ; ﾕﾆｯﾄ14起動条件
OR M7985                 ; ﾕﾆｯﾄ14起動中
ANB M7983                ; ﾕﾆｯﾄ14起動OFF
LDB M7549                ; ﾕﾆｯﾄ14-1ｻｲｸﾙ停止
ORB M7599                ; ﾕﾆｯﾄ14-2ｻｲｸﾙ停止
ANL
OUT M7985                ; ﾕﾆｯﾄ14起動中
;□ ｻｲｸﾙ停止異常
LDB B01C0                ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ異常
INV
OUT M7986                ; ﾕﾆｯﾄ14ｻｲｸﾙ停止異常
;□ 払出停止異常
LDB CR2003               ; 常時OFF
INV
OUT M7987                ; ﾕﾆｯﾄ14払出停止異常
;□ 警報補助
LDB CR2003               ; 常時OFF
INV
OUT M7988                ; ﾕﾆｯﾄ14警報補助
;□ 機器準備確認
LD B2015                 ; PA検査ﾚﾃﾞｨ
OUT M7990                ; ﾕﾆｯﾄ14機器準備完了
;□ ｴﾗｰ発生　一時停止
LDB B01C2                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ異常
ANB B01C3                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ異常
ANB B01C4                ; ｶﾊﾞｰ送CV位置決異常
ANB B01C5                ; ｶﾊﾞｰ搬送爪送り異常
ANB B01C6                ; ｶﾊﾞｰ送爪上下異常
ANB B01C8                ; VentPlug挿入装置応答異常
ANB B01C9                ; Vent_Plug検査NG
INV
OUT M7991                ; ﾕﾆｯﾄ14ｴﾗｰ発生
;□ 動作中ﾕﾆｯﾄ無し
LD= D7500 D7505          ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
ORB M7985                ; ﾕﾆｯﾄ14起動中
ANB M7984                ; ﾕﾆｯﾄ14原点復帰中
TMR K1401 K20            ; ﾕﾆｯﾄ14動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D7500 D7505          ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD T1401                 ; ﾕﾆｯﾄ14動作無し判定ﾀｲﾏ
OUT M7992                ; ﾕﾆｯﾄ14動作無し確認
;□ 払出しﾜｰｸ無し
LD CR2002                ; 常時ON
TMR K1405 K10            ; UNIT14払出処理ﾜｰｸ無し
LD T1405                 ; UNIT14払出処理ﾜｰｸ無し
OUT M7993                ; ﾕﾆｯﾄ14ﾜｰｸ無し
;□ ｽﾃｯﾌﾟ起動
LD M30                   ; 自動ﾓｰﾄﾞ
LD M31                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M7979                ; ﾕﾆｯﾄ14ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　  UNIT14自動回路  　　□
;□□□□□□□□□□□□□□□
;<h1/>■自動回路 
;<h1/>/ ｶﾊﾞｰ投入部
LD CR2002                ; 常時ON
ZRES M7500 M7549         ; ｶﾊﾞｰ搬送開始  ﾕﾆｯﾄ14-1ｻｲｸﾙ停止
LDB M7985                ; ﾕﾆｯﾄ14起動中
MOV K0 D7500             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
DW K0 D7700
LD M7985                 ; ﾕﾆｯﾄ14起動中
MOV D7500 Z1             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
SET M7500:Z1             ; ｶﾊﾞｰ搬送開始
CAL+ D7500 K7500 D7700   ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7500                 ; ｶﾊﾞｰ搬送開始
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7501                 ; NP
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7502                 ; ｺﾝﾍﾞｱ回転
MPS
AND Y2530                ; 基板供給CV正転
TMR K1435 K20            ; ｶﾊﾞｰ無確認
MPP
AND T1435                ; ｶﾊﾞｰ無確認
MPS
ANB X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
MOV K7 D7500             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ無引込
LD CR2002                ; 常時ON
MPS
DW K0 D7544              ; Cover必要数
CON
DW K0 D7546              ; Cover収納数
MRD
LDB X1243                ; 基板ﾈｼﾞ締めﾜｰｸ確認
AND= D16562 K1           ; UT12ﾜｰｸOK/NGﾃﾞｰﾀ
ANL
INC D7544                ; Cover必要数
MRD
LD X1480                 ; 接着剤塗布2ﾜｰｸ確認
AND= D16662 K1           ; UT13ﾜｰｸOK/NGﾃﾞｰﾀ
ANL
INC D7544                ; Cover必要数
MRD
AND X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
INC D7546                ; Cover収納数
MPP
AND X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
INC D7546                ; Cover収納数
LD M7503                 ; 引込可能確認
LDB M36                  ; 払出ﾓｰﾄﾞ
LD M36                   ; 払出ﾓｰﾄﾞ
AND> D7544 D7546         ; Cover必要数  Cover収納数
ORL
ANL
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7504                 ; ｶﾊﾞｰ投入ｽﾄｯﾊﾟ下降
AND T1410                ; 搬送上流ｽﾄｯﾊﾟ下降端
MPS
ANB X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
MOV K6 D7500             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7505                 ; ｶﾊﾞｰ供給待ち
MPS
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MPS
ANB X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
TMR K1434 K200           ; VentPlug応答なし
MPP
AND X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
TMR K1433 K50            ; 要求OFF後wait1
LD M7506                 ; ｶﾊﾞｰ検査位置到達待ち
MPS
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
MPS
AND X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
TMR K1436 K70            ; ｶﾊﾞｰ引っ掛かりNG
MRD
AND X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
TMR K1437 K10            ; ｶﾊﾞｰ到着確認
MPP
AND T1437                ; ｶﾊﾞｰ到着確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
TMR K1432 K50            ; 要求OFF後wait2
LD M7507                 ; ｽﾄｯﾊﾟ上昇
AND T1411                ; 搬送上流ｽﾄｯﾊﾟ上昇端
MPS
AND D5463.5              ; UT9ﾍﾞﾝﾄﾌﾟﾗｸﾞ画像検査有無
MOV K10 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
ANB D5463.5              ; UT9ﾍﾞﾝﾄﾌﾟﾗｸﾞ画像検査有無
MOV K40 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7510                 ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査有確認
MPS
ANB W08.10               ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査無効
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND W08.10               ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査無効
MOV K40 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査Prg_No.切替
LD M7511                 ; ﾍﾞﾝﾄ検査開始ON
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MOV K7 W0684             ; IV Series[2]ﾌﾟﾛｸﾞﾗﾑ番号
LD M7512                 ; ﾍﾞﾝﾄ検査　PRO_No切替指令
AND B2062                ; ﾍﾞﾝﾄ検査ﾌﾟﾛｸﾞﾗﾑ切替応答
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7513                 ; ﾍﾞﾝﾄ検査　BUSY確認
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7514                 ; ﾍﾞﾝﾄ検査　PRO_No切替確認
ANB B2062                ; ﾍﾞﾝﾄ検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M7515                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2070                ; ﾍﾞﾝﾄ検査結果取得可能
ANB B2080                ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査実行
LD M7516                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7517                 ; ﾍﾞﾝﾄ検査　指令
AND B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;検査完了結果判定
LD M7518                 ; ﾍﾞﾝﾄ検査　BUSY確認
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
MPS
ANB B2080                ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
MOV K20 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
AND B2080                ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
MOV K40 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査NG取出
LD M7520                 ; 基板排出確認
MPS
AND X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
MPP
ANB X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
MOV K22 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7521                 ; 投入位置ﾜｰｸ有取出要求
ANB X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
ANP X1499                ; ｶﾊﾞｰ供給確認PB
MOV K20 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7522                 ; ｽﾄｯﾊﾟ下降
AND T1410                ; 搬送上流ｽﾄｯﾊﾟ下降端
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7523                 ; ｺﾝﾍﾞｱ排出逆転
MPS
TMR K1431 K80            ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
MPP
LD T1431                 ; ｺﾝﾍﾞｱ停止ﾀｲﾏ
ORF X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
ANL
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7524                 ; 読込NG取出要求
ANP X1499                ; ｶﾊﾞｰ供給確認PB
ANB X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
LD M7525                 ; ｽﾄｯﾊﾟ上昇
AND T1411                ; 搬送上流ｽﾄｯﾊﾟ上昇端
MOV K0 D7500             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ搬送可･完了確認
LD M7540                 ; 検査完了搬送可(ｶﾊﾞｰ搬送へ)
AND M7561                ; ｶﾊﾞｰ搬送完了(投入部へ)
@INC D7500               ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
@CAL+ W0145 K1 W0145     ; COV_C/V_Number  COV_C/V_Number
LD M7541                 ; ｶﾊﾞｰ搬送確認
ANB M7561                ; ｶﾊﾞｰ搬送完了(投入部へ)
MOV K0 D7500             ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M7503                 ; 引込可能確認
LD M7505                 ; ｶﾊﾞｰ供給待ち
AND T1433                ; 要求OFF後wait1
ORL
LD M7506                 ; ｶﾊﾞｰ検査位置到達待ち
AND T1432                ; 要求OFF後wait2
ORL
LDB M8485                ; ﾕﾆｯﾄ15起動中
AND M7540                ; 検査完了搬送可(ｶﾊﾞｰ搬送へ)
ORL
ANL
MOV K48 D7500            ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M7548                 ; ﾕﾆｯﾄ14-1ｻｲｸﾙ停止処理
@MOV K49 D7500           ; ﾕﾆｯﾄ14-1自動運転ｽﾃｯﾌﾟ
;<h1/>/ 搬送コンベア
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M7550 M7599         ; ｶﾊﾞｰCV搬送開始  ﾕﾆｯﾄ14-2ｻｲｸﾙ停止
LDB M7985                ; ﾕﾆｯﾄ14起動中
MOV K0 D7501             ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
DW K0 D7701
LD M7985                 ; ﾕﾆｯﾄ14起動中
MOV D7501 Z1             ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
SET M7550:Z1             ; ｶﾊﾞｰCV搬送開始
CAL+ D7501 K7550 D7701   ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;自動運転ステップ開始
;取出部ｶﾊﾞｰ確認
LD M7550                 ; ｶﾊﾞｰCV搬送開始
MPS
ANB X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
MPP
AND X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
MOV K13 D7501            ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7551                 ; NOP
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7552                 ; NOP
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7553                 ; NOP
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7554                 ; 搬送可待ち
AND M7540                ; 検査完了搬送可(ｶﾊﾞｰ搬送へ)
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ搬送動作
LD M7555                 ; 搬送爪閉
AND T1418                ; ｶﾊﾞｰ搬送爪下降端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7556                 ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下降
AND T1412                ; 搬送下流ｽﾄｯﾊﾟ下降端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7557                 ; 搬送爪送
AND T1416                ; ｶﾊﾞｰ搬送送端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7558                 ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
AND T1413                ; 搬送下流ｽﾄｯﾊﾟ上昇端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7559                 ; 搬送爪開
AND T1419                ; ｶﾊﾞｰ搬送爪上昇端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7560                 ; 搬送爪戻
AND T1417                ; ｶﾊﾞｰ搬送戻端
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ投入へｶﾊﾞｰ搬送完了ON
LD M7561                 ; ｶﾊﾞｰ搬送完了(投入部へ)
ANB M7540                ; 検査完了搬送可(ｶﾊﾞｰ搬送へ)
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7562                 ; 取出部ﾜｰｸ確認
MPS
AND X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
MPP
ANB X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
MOV K0 D7501             ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ取出可･確認
LD M7563                 ; ｶﾊﾞｰ取出可ON(ｶﾊﾞｰ嵌合へ)
LD M8050                 ; ｶﾊﾞｰ取出完了(ｶﾊﾞｰCVへ)
OR M8042                 ; ｶﾊﾞｰ取出NG完了(ｶﾊﾞｰCVへ)
ANL
@INC D7501               ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
LD M7564                 ; ｶﾊﾞｰ取出完了確認
ANB M8050                ; ｶﾊﾞｰ取出完了(ｶﾊﾞｰCVへ)
@CAL- W0145 K1 W0145     ; COV_C/V_Number  COV_C/V_Number
MOV K0 D7501             ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ停止処理
LD M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M7554                 ; 搬送可待ち
AND M7549                ; ﾕﾆｯﾄ14-1ｻｲｸﾙ停止
LDB M8485                ; ﾕﾆｯﾄ15起動中
AND M7563                ; ｶﾊﾞｰ取出可ON(ｶﾊﾞｰ嵌合へ)
ORL
ANL
MOV K48 D7501            ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M7598                 ; ﾕﾆｯﾄ14-2ｻｲｸﾙ停止処理
@MOV K49 D7501           ; ﾕﾆｯﾄ14-2自動運転ｽﾃｯﾌﾟ
;投入許可表示ﾌﾗｸﾞ
LD M7507                 ; ｽﾄｯﾊﾟ上昇
OR M7700                 ; ｶﾊﾞｰ投入不可
ANB M7540                ; 検査完了搬送可(ｶﾊﾞｰ搬送へ)
ANB M7541                ; ｶﾊﾞｰ搬送確認
AND M7985                ; ﾕﾆｯﾄ14起動中
OUT M7700                ; ｶﾊﾞｰ投入不可
;<h1/>■原点復帰
;□□□□□□□□□□□□□□□
;□　　 UNIT9原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
ZRES M7750 M7799         ; 原点復帰可能確認  ﾕﾆｯﾄ14原点終了
LDB M7984                ; ﾕﾆｯﾄ14原点復帰中
MOV K0 D7510             ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
DW K0 D7710
LD M7984                 ; ﾕﾆｯﾄ14原点復帰中
MOV D7510 Z1             ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
SET M7750:Z1             ; 原点復帰可能確認
CAL+ D7510 K7750 D7710   ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
;原点復帰ｻｲｸﾙ監視
LD M7984                 ; ﾕﾆｯﾄ14原点復帰中
TMR K1400 K300           ; UNIT14原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M7750                 ; 原点復帰可能確認
@INC D7510               ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
LD M7751                 ; ｶﾊﾞｰ搬送爪開
AND T1419                ; ｶﾊﾞｰ搬送爪上昇端
@INC D7510               ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
LD M7752                 ; ｶﾊﾞｰ搬送爪戻
AND T1417                ; ｶﾊﾞｰ搬送戻端
@INC D7510               ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
LD M7753                 ; 位置決開
AND T1415                ; 搬送位置決め開端
@INC D7510               ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
LD M7754                 ; ｶﾊﾞｰCVｽﾄｯﾊﾟ上昇
AND T1411                ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND T1413                ; 搬送下流ｽﾄｯﾊﾟ上昇端
@INC D7510               ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M7755                 ; ｶﾊﾞｰ搬送原点完了
AND M7980                ; ﾕﾆｯﾄ14原点
@MOV K48 D7510           ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
LD M7798                 ; ﾕﾆｯﾄ14原点確認
ANB M82                  ; 原点復帰開始
@MOV K49 D7510           ; ﾕﾆｯﾄ14原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路
;□□□□□□□□□□□□□□□
;□　　 　UNIT14手動回路 　　□
;□□□□□□□□□□□□□□□
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M7985                ; ﾕﾆｯﾄ14起動中
ANB M8485                ; ﾕﾆｯﾄ15起動中
ANB M7984                ; ﾕﾆｯﾄ14原点復帰中
MC
;基板CV上流ｽﾄｯﾊﾟ上下
LD X149F                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
OUT M7800                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下降可
LD B1220                 ; UNIT14-1-1 TP-PB
AND M7800                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下降可
OR M7801                 ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下昇
ANPB M7803               ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇
OUT M7801                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下昇
LD CR2002                ; 常時ON
OUT M7802                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇可
LD B1221                 ; UNIT14-1-2 TP-PB
AND M7802                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇可
OR M7803                 ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇
ANPB M7801               ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下昇
OUT M7803                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇
;基板CV下流ｽﾄｯﾊﾟ上下
LD X149D                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
OUT M7804                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下降可
LD B1222                 ; UNIT14-1-3 TP-PB
AND M7804                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下降可
OR M7805                 ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下昇
ANPB M7807               ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
OUT M7805                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下昇
LD X1492                 ; ｶﾊﾞｰ搬送送端
OR X1493                 ; ｶﾊﾞｰ搬送戻端
OR X1495                 ; ｶﾊﾞｰ搬送爪開端
OUT M7806                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇可
LD B1223                 ; UNIT14-1-4 TP-PB
AND M7806                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇可
OR M7807                 ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
ANPB M7805               ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下昇
OUT M7807                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
;基板搬送爪上下
LD CR2002                ; 常時ON
OUT M7808                ; ｶﾊﾞｰ搬送爪閉可
LD B1224                 ; UNIT14-1-5 TP-PB
AND M7808                ; ｶﾊﾞｰ搬送爪閉可
OR M7809                 ; ｶﾊﾞｰ搬送爪閉
ANPB M7811               ; ｶﾊﾞｰ搬送爪開
OUT M7809                ; ｶﾊﾞｰ搬送爪閉
LD CR2002                ; 常時ON
OUT M7810                ; ｶﾊﾞｰ搬送爪開可
LD B1225                 ; UNIT14-1-6 TP-PB
AND M7810                ; ｶﾊﾞｰ搬送爪開可
OR M7811                 ; ｶﾊﾞｰ搬送爪開
ANPB M7809               ; ｶﾊﾞｰ搬送爪閉
OUT M7811                ; ｶﾊﾞｰ搬送爪開
;基板搬送爪送戻
LDB X149B                ; ｶﾊﾞｰ投入CV取出ﾜｰｸ確認
AND X1491                ; 搬送位置決め開端
AND X149E                ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1495                 ; ｶﾊﾞｰ搬送爪開端
OUT M7812                ; ｶﾊﾞｰ搬送爪送可
LD B1226                 ; UNIT14-1-7 TP-PB
AND M7812                ; ｶﾊﾞｰ搬送爪送可
OR M7813                 ; ｶﾊﾞｰ搬送爪送
ANPB M7815               ; ｶﾊﾞｰ搬送爪戻
OUT M7813                ; ｶﾊﾞｰ搬送爪送
LD X149E                 ; 搬送下流ｽﾄｯﾊﾟ下降端
OR X1495                 ; ｶﾊﾞｰ搬送爪開端
OUT M7814                ; ｶﾊﾞｰ搬送爪戻可
LD B1227                 ; UNIT14-1-8 TP-PB
AND M7814                ; ｶﾊﾞｰ搬送爪戻可
OR M7815                 ; ｶﾊﾞｰ搬送爪戻
ANPB M7813               ; ｶﾊﾞｰ搬送爪送
OUT M7815                ; ｶﾊﾞｰ搬送爪戻
;基板CV正転･逆転
LD CR2002                ; 常時ON
OUT M7816                ; ｶﾊﾞｰ搬送CV位置決閉可
LD B1228                 ; UNIT14-1-9 TP-PB
AND M7816                ; ｶﾊﾞｰ搬送CV位置決閉可
OR M7817                 ; ｶﾊﾞｰ搬送CV位置決閉
ANPB M7819               ; ｶﾊﾞｰ搬送CV位置決開
ANB M8311                ; ｶﾊﾞｰ嵌合RB1ｻｲｸﾙ
OUT M7817                ; ｶﾊﾞｰ搬送CV位置決閉
LD CR2002                ; 常時ON
OUT M7818                ; ｶﾊﾞｰ搬送CV位置決開可
LD B1229                 ; UNIT14-1-10 TP-PB
AND M7818                ; ｶﾊﾞｰ搬送CV位置決開可
OR M7819                 ; ｶﾊﾞｰ搬送CV位置決開
ANPB M7817               ; ｶﾊﾞｰ搬送CV位置決閉
ANB M8311                ; ｶﾊﾞｰ嵌合RB1ｻｲｸﾙ
OUT M7819                ; ｶﾊﾞｰ搬送CV位置決開
;基板CV正転･逆転
LD CR2002                ; 常時ON
ANB M7823                ; ｶﾊﾞｰCV逆転
OUT M7820                ; ｶﾊﾞｰCV正転可
LD B122A                 ; UNIT14-1-11 TP-PB
AND M7820                ; ｶﾊﾞｰCV正転可
OUT M7821                ; ｶﾊﾞｰCV正転
LD CR2002                ; 常時ON
ANB M7821                ; ｶﾊﾞｰCV正転
OUT M7822                ; ｶﾊﾞｰCV逆転可
LD B122B                 ; UNIT14-1-12 TP-PB
AND M7822                ; ｶﾊﾞｰCV逆転可
OUT M7823                ; ｶﾊﾞｰCV逆転
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査
LD CR2002                ; 常時ON
OUT M7830                ; ﾍﾞﾝﾄ検査開始可
LDP B122C                ; UNIT14-1-13 TP-PB
AND M7830                ; ﾍﾞﾝﾄ検査開始可
OR M7831                 ; ﾍﾞﾝﾄ検査　PRO_No切替指令
ANB M7832                ; ﾍﾞﾝﾄ検査　PRO_No切替確認
MOV D7100 W0684          ; 手動機種ID  IV Series[2]ﾌﾟﾛｸﾞﾗﾑ番号
OUT M7831                ; ﾍﾞﾝﾄ検査　PRO_No切替指令
LD M7831                 ; ﾍﾞﾝﾄ検査　PRO_No切替指令
AND B2062                ; ﾍﾞﾝﾄ検査ﾌﾟﾛｸﾞﾗﾑ切替応答
OR M7832                 ; ﾍﾞﾝﾄ検査　PRO_No切替確認
ANB M7833                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
OUT M7832                ; ﾍﾞﾝﾄ検査　PRO_No切替確認
LD M7832                 ; ﾍﾞﾝﾄ検査　PRO_No切替確認
ANB B2062                ; ﾍﾞﾝﾄ検査ﾌﾟﾛｸﾞﾗﾑ切替応答
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
OR M7833                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
ANB M7834                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ確認
OUT M7833                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
;計測結果ﾊﾞｯﾌｧｸﾘｱ
LD M7833                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
ANB B2070                ; ﾍﾞﾝﾄ検査結果取得可能
ANB B2080                ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
OR M7834                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB M7835                ; ﾍﾞﾝﾄ検査　指令
OUT M7834                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ確認
;PA検査実行
LD M7834                 ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ確認
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
OR M7835                 ; ﾍﾞﾝﾄ検査　指令
ANB M7836                ; ﾍﾞﾝﾄ検査　BUSY確認
OUT M7835                ; ﾍﾞﾝﾄ検査　指令
LD M7835                 ; ﾍﾞﾝﾄ検査　指令
AND B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
OR M7836                 ; ﾍﾞﾝﾄ検査　BUSY確認
ANB M7837                ; ﾍﾞﾝﾄ検査完了
OUT M7836                ; ﾍﾞﾝﾄ検査　BUSY確認
;検査完了結果判定
LD M7836                 ; ﾍﾞﾝﾄ検査　BUSY確認
ANB B2072                ; ﾍﾞﾝﾄ検査ﾋﾞｼﾞｰ
LDB B2080                ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
OR B2080                 ; ﾍﾞﾝﾄ検査総合判定(OFF:NG ON:OK)
ANL
OUT M7837                ; ﾍﾞﾝﾄ検査完了
MCR
;<h1/>■出力
;□□　　出力回路　　□□
;ソレノイドバルブ出力
;ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上下
LDB M7504                ; ｶﾊﾞｰ投入ｽﾄｯﾊﾟ下降
ANB M7522                ; ｽﾄｯﾊﾟ下降
ANB M7801                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ下昇
INV
OR Y2640                 ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ下降
ANB M7507                ; ｽﾄｯﾊﾟ上昇
ANB M7525                ; ｽﾄｯﾊﾟ上昇
ANB M7754                ; ｶﾊﾞｰCVｽﾄｯﾊﾟ上昇
ANB M7803                ; ｶﾊﾞｰCV上流ｽﾄｯﾊﾟ上昇
OUT Y2640                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ下降
;ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上下
LDB M7556                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下降
ANB M7805                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ下昇
INV
OR Y2641                 ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ下降
ANB M7558                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
ANB M7754                ; ｶﾊﾞｰCVｽﾄｯﾊﾟ上昇
ANB M7807                ; ｶﾊﾞｰCV下流ｽﾄｯﾊﾟ上昇
OUT Y2641                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ下降
;ｶﾊﾞｰCV位置決開閉
LDB CR2003               ; 常時OFF
ANB M7817                ; ｶﾊﾞｰ搬送CV位置決閉
ANPB X384C               ; ｶﾊﾞｰ押さえ閉要求(54)
INV
OR Y2642                 ; ｶﾊﾞｰ搬送CV位置決出
ANB CR2003               ; 常時OFF
ANB M7753                ; 位置決開
ANB M7819                ; ｶﾊﾞｰ搬送CV位置決開
ANPB X384D               ; ｶﾊﾞｰ押さえ開要求(55)
OUT Y2642                ; ｶﾊﾞｰ搬送CV位置決出
;ｶﾊﾞｰ搬送爪送戻
LDB M7557                ; 搬送爪送
ANB M7813                ; ｶﾊﾞｰ搬送爪送
INV
AND T1420                ; ｶﾊﾞｰCV扉閉確認
ANB M130                 ; 全体PAUSE
LD Y2643                 ; ｶﾊﾞｰ搬送送りSV
ANB Y2644                ; ｶﾊﾞｰ搬送戻りSV
ORL
OUT Y2643                ; ｶﾊﾞｰ搬送送りSV
LDB M7560                ; 搬送爪戻
ANB M7751                ; ｶﾊﾞｰ搬送爪開
ANB M7815                ; ｶﾊﾞｰ搬送爪戻
INV
AND T1420                ; ｶﾊﾞｰCV扉閉確認
ANB M130                 ; 全体PAUSE
LD Y2644                 ; ｶﾊﾞｰ搬送戻りSV
ANB Y2643                ; ｶﾊﾞｰ搬送送りSV
ORL
OUT Y2644                ; ｶﾊﾞｰ搬送戻りSV
;ｶﾊﾞｰ搬送爪開閉
LDB M7555                ; 搬送爪閉
ANB M7809                ; ｶﾊﾞｰ搬送爪閉
INV
OR Y2645                 ; ｶﾊﾞｰ搬送爪閉SV
ANB M7559                ; 搬送爪開
ANB M7751                ; ｶﾊﾞｰ搬送爪開
ANB M7811                ; ｶﾊﾞｰ搬送爪開
OUT Y2645                ; ｶﾊﾞｰ搬送爪閉SV
;ｶﾊﾞｰCV
LDB M7502                ; ｺﾝﾍﾞｱ回転
ANB M7505                ; ｶﾊﾞｰ供給待ち
ANB M7506                ; ｶﾊﾞｰ検査位置到達待ち
ANB M7821                ; ｶﾊﾞｰCV正転
INV
AND T1420                ; ｶﾊﾞｰCV扉閉確認
OUT Y2530                ; 基板供給CV正転
LDB M7523                ; ｺﾝﾍﾞｱ排出逆転
ANB M7823                ; ｶﾊﾞｰCV逆転
INV
AND T1420                ; ｶﾊﾞｰCV扉閉確認
OUT Y2531                ; 基板供給CV逆転
;基板投入部表示･BZ
LDB M7521                ; 投入位置ﾜｰｸ有取出要求
ANB M7524                ; 読込NG取出要求
INV
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2540                ; ｶﾊﾞｰ投入確認PL
LDB M7521                ; 投入位置ﾜｰｸ有取出要求
ANB M7524                ; 読込NG取出要求
ANB T1436                ; ｶﾊﾞｰ引っ掛かりNG
INV
LD M7985                 ; ﾕﾆｯﾄ14起動中
ANB T1411                ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
AND X149A                ; ｶﾊﾞｰ投入CVﾜｰｸ確認
ORL
ANB T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1(1secON､1secOFF)
OUT Y2541                ; ｶﾊﾞｰ投入ﾌﾞｻﾞｰ
LD M7985                 ; ﾕﾆｯﾄ14起動中
MPS
LD T1411                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
ORB X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
ANB M7700                ; ｶﾊﾞｰ投入不可
ANL
OUT Y2542                ; ｶﾊﾞｰ投入OK_PL
MPP
LDB T1411                ; 搬送上流ｽﾄｯﾊﾟ上昇端
AND X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
OR M7700                 ; ｶﾊﾞｰ投入不可
ANL
OUT Y2543                ; ｶﾊﾞｰ投入NG_PL
;ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ
LDB M7517                ; ﾍﾞﾝﾄ検査　指令
ANB M7835                ; ﾍﾞﾝﾄ検査　指令
INV
OUT B20A0                ; ﾍﾞﾝﾄ検査ﾄﾘｶﾞ要求
LDB M7512                ; ﾍﾞﾝﾄ検査　PRO_No切替指令
ANB M7831                ; ﾍﾞﾝﾄ検査　PRO_No切替指令
INV
OUT B20A2                ; ﾍﾞﾝﾄ検査ﾌﾟﾛｸﾞﾗﾑ切替要求
LDB CR2003               ; 常時OFF
INV
OUT B20A3                ; ﾍﾞﾝﾄ検査ﾜｰﾆﾝｸﾞｸﾘｱ要求
LDB M7515                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
ANB M7833                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ
INV
OUT B20A5                ; ﾍﾞﾝﾄ検査ﾊﾞｯﾌｧｸﾘｱ要求
;Vent Plug 挿入装置
LD M7505                 ; ｶﾊﾞｰ供給待ち
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M260                 ; 仮
OUT Y2548                ; VentへCover要求
;<h1/>■表示
;□□　　パネル表示　　□□
LD X149C                 ; 搬送上流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1720                ; UNIT14-1-1 TP-PL
LD X149D                 ; 搬送上流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1721                ; UNIT14-1-2 TP-PL
LD X149E                 ; 搬送下流ｽﾄｯﾊﾟ下降端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1722                ; UNIT14-1-3 TP-PL
LD X149F                 ; 搬送下流ｽﾄｯﾊﾟ上昇端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1723                ; UNIT14-1-4 TP-PL
LD X1494                 ; ｶﾊﾞｰ搬送爪閉端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1724                ; UNIT14-1-5 TP-PL
LD X1495                 ; ｶﾊﾞｰ搬送爪開端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1725                ; UNIT14-1-6 TP-PL
LD X1492                 ; ｶﾊﾞｰ搬送送端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1726                ; UNIT14-1-7 TP-PL
LD X1493                 ; ｶﾊﾞｰ搬送戻端
LD CR2003                ; 常時OFF
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1727                ; UNIT14-1-8 TP-PL
LD X1490                 ; 搬送位置決め閉端
OUT B1728                ; UNIT14-1-9 TP-PL
LD X1491                 ; 搬送位置決め開端
OUT B1729                ; UNIT14-1-10 TP-PL
LD Y2530                 ; 基板供給CV正転
OUT B172A                ; UNIT14-1-11 TP-PL
LD Y2531                 ; 基板供給CV逆転
OUT B172B                ; UNIT14-1-12 TP-PL
;<h1/>■異常
;□□　　異常検出　　□□
;B1C0(K449)--B1CF(K464)  B1D0(K465)--B1DF(K480)
LD B207F                 ; ﾍﾞﾝﾄ検査ｴﾗｰ
AND M50                  ; 運転準備
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C0                ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ異常
ORL
AND M50                  ; 運転準備
OUT B01C0                ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ異常
@DW K448 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C0
AND B01C0                ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ異常
OUT B06C0
LD M7554                 ; 搬送可待ち
ANB X1498                ; ｶﾊﾞｰ投入CV読取ﾜｰｸ確認
AND Y2530                ; 基板供給CV正転
AND M8485                ; ﾕﾆｯﾄ15起動中
AND M50                  ; 運転準備
MPS
TMR K1450 K50            ; ｶﾊﾞｰ投入待ち
MPP
AND T1450                ; ｶﾊﾞｰ投入待ち
OUT B01C1                ; ｶﾊﾞｰ投入要求
@DW K449 D0
CON
@FIFOW D0 D900           ; 異常内容
LD Y2640                 ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ下降
ANB X149C                ; 搬送上流ｽﾄｯﾊﾟ下降端
LDB Y2640                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ下降
ANB X149D                ; 搬送上流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C2                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1451 K50            ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ確認
MPP
AND T1451                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ確認
OUT B01C2                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ異常
@DW K450 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C2
AND B01C2                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ異常
OUT B06C2
LD Y2641                 ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ下降
ANB X149E                ; 搬送下流ｽﾄｯﾊﾟ下降端
LDB Y2641                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ下降
ANB X149F                ; 搬送下流ｽﾄｯﾊﾟ上昇端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C3                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1452 K50            ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ確認
MPP
AND T1452                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ確認
OUT B01C3                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ異常
@DW K451 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C3
AND B01C3                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ異常
OUT B06C3
LD Y2642                 ; ｶﾊﾞｰ搬送CV位置決出
ANB X1490                ; 搬送位置決め閉端
LDB Y2642                ; ｶﾊﾞｰ搬送CV位置決出
ANB X1491                ; 搬送位置決め開端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C4                ; ｶﾊﾞｰ送CV位置決異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1453 K50            ; ｶﾊﾞｰ送CV位置決確認
MPP
AND T1453                ; ｶﾊﾞｰ送CV位置決確認
OUT B01C4                ; ｶﾊﾞｰ送CV位置決異常
@DW K452 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C4
AND B01C4                ; ｶﾊﾞｰ送CV位置決異常
OUT B06C4
LD Y2643                 ; ｶﾊﾞｰ搬送送りSV
ANB X1492                ; ｶﾊﾞｰ搬送送端
LD Y2644                 ; ｶﾊﾞｰ搬送戻りSV
ANB X1493                ; ｶﾊﾞｰ搬送戻端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C5                ; ｶﾊﾞｰ搬送爪送り異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1454 K50            ; ｶﾊﾞｰ搬送爪送り確認
MPP
AND T1454                ; ｶﾊﾞｰ搬送爪送り確認
OUT B01C5                ; ｶﾊﾞｰ搬送爪送り異常
@DW K453 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C5
AND B01C5                ; ｶﾊﾞｰ搬送爪送り異常
OUT B06C5
LD Y2645                 ; ｶﾊﾞｰ搬送爪閉SV
ANB X1494                ; ｶﾊﾞｰ搬送爪閉端
LDB Y2645                ; ｶﾊﾞｰ搬送爪閉SV
ANB X1495                ; ｶﾊﾞｰ搬送爪開端
ORL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C6                ; ｶﾊﾞｰ送爪上下異常
ORL
AND M50                  ; 運転準備
MPS
TMR K1455 K50            ; ｶﾊﾞｰ搬送爪上下確認
MPP
AND T1455                ; ｶﾊﾞｰ搬送爪上下確認
OUT B01C6                ; ｶﾊﾞｰ送爪上下異常
@DW K454 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C6
AND B01C6                ; ｶﾊﾞｰ送爪上下異常
OUT B06C6
LD CR2003                ; 常時OFF
ANB T1420                ; ｶﾊﾞｰCV扉閉確認
AND M30                  ; 自動ﾓｰﾄﾞ
OUT B01C7                ; ｶﾊﾞｰ搬送CV扉開
@DW K455 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C7                 ; ｶﾊﾞｰ搬送CV扉開BZ停止
AND B01C7                ; ｶﾊﾞｰ搬送CV扉開
OUT B06C7                ; ｶﾊﾞｰ搬送CV扉開BZ停止
LD M30                   ; 自動ﾓｰﾄﾞ
AND T1434                ; VentPlug応答なし
OUT B01C8                ; VentPlug挿入装置応答異常
@DW K456 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C8
AND B01C8                ; VentPlug挿入装置応答異常
OUT B06C8
LD M30                   ; 自動ﾓｰﾄﾞ
AND M7524                ; 読込NG取出要求
OUT B01C9                ; Vent_Plug検査NG
@DW K457 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B06C9
AND B01C9                ; Vent_Plug検査NG
OUT B06C9
;異常ﾌﾞｻﾞｰまとめ
LDB B01C0                ; ﾍﾞﾝﾄﾌﾟﾗｸﾞ検査ｶﾒﾗ異常
OR B06C0
LDB B01C2                ; ｶﾊﾞｰ搬送CV上流ｽﾄｯﾊﾟ異常
OR B06C2
ANL
LDB B01C3                ; ｶﾊﾞｰ搬送CV下流ｽﾄｯﾊﾟ異常
OR B06C3
ANL
LDB B01C4                ; ｶﾊﾞｰ送CV位置決異常
OR B06C4
ANL
LDB B01C5                ; ｶﾊﾞｰ搬送爪送り異常
OR B06C5
ANL
LDB B01C6                ; ｶﾊﾞｰ送爪上下異常
OR B06C6
ANL
LDB B01C8                ; VentPlug挿入装置応答異常
OR B06C8
ANL
LDB B01C9                ; Vent_Plug検査NG
OR B06C9
ANL
INV
OUT M7989                ; ﾕﾆｯﾄ14異常BZ補助
END
ENDH
