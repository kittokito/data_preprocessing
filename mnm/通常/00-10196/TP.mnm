DEVICE:128
;MODULE:TP
;MODULE_TYPE:0
;<h1/>ﾒｲﾝ画面表示
LD= W08 K1               ; TP_切替ﾍﾟｰｼﾞNo.
MEP
ANB M51                  ; 自動中
OR M52                   ; ｽﾃｯﾌﾟ終了
MOV.S -1 D902            ; ｽﾃｯﾌﾟ現在回数
MOV K0 D900              ; ｽﾃｯﾌﾟ回数設定値
LD B1007                 ; 運用品種選択
LD= W08 K1               ; TP_切替ﾍﾟｰｼﾞNo.
MEP
ORL
ANB M51                  ; 自動中
BMOV D20000:Z1 D0 K500   ; 品種1品種設定1  運用品種設定1
;実行中ｽﾃｯﾌﾟ表示
LDP M51                  ; 自動中
FMOV.S -1 D12000 K300    ; ｶｳﾝﾄ履歴ﾃﾞｰﾀ
FMOV K0 D600 K10         ; 圧力到達時間
LDP M53                  ; 1ｽﾃｯﾌﾟ
MOV D902 D606            ; ｽﾃｯﾌﾟ現在回数  ｽﾃｯﾌﾟ番号
LD M204                  ; 減圧中
OR M254                  ; 加圧中
MPS
ANB @M6                  ; 圧力到達時間1sｸﾛｯｸ
TMR @3 K10               ; 圧力到達時間ｸﾛｯｸ
MPP
AND @T3                  ; 圧力到達時間ｸﾛｯｸ
OUT @M6                  ; 圧力到達時間1sｸﾛｯｸ
LD @M6                   ; 圧力到達時間1sｸﾛｯｸ
INC D600                 ; 圧力到達時間
LD M201                  ; 減圧保持開始
OR M251                  ; 加圧保持開始
MPS
ANB @M7                  ; 保持時間1sｸﾛｯｸ
TMR @4 K10               ; 保持時間ｸﾛｯｸ
MPP
AND @T4                  ; 保持時間ｸﾛｯｸ
OUT @M7                  ; 保持時間1sｸﾛｯｸ
LD @M7                   ; 保持時間1sｸﾛｯｸ
INC D602                 ; 保持時間
LD M302                  ; 常圧制御中
MPS
ANB @M8                  ; 復圧時間1sｸﾛｯｸ
TMR @5 K10               ; 復圧時間ｸﾛｯｸ
MPP
AND @T5                  ; 復圧時間ｸﾛｯｸ
OUT @M8                  ; 復圧時間1sｸﾛｯｸ
LD @M8                   ; 復圧時間1sｸﾛｯｸ
INC D604                 ; 復圧時間
LD M303                  ; 常圧保持中
MPS
ANB @M9                  ; 常圧保持時間1sｸﾛｯｸ
TMR @6 K10               ; 常圧保持時間ｸﾛｯｸ
MPP
AND @T6                  ; 常圧保持時間ｸﾛｯｸ
OUT @M9                  ; 常圧保持時間1sｸﾛｯｸ
LD @M9                   ; 常圧保持時間1sｸﾛｯｸ
INC D608                 ; 常圧保持時間
;履歴ｽﾃｯﾌﾟ表示
LD M51                   ; 自動中
MEP
MPS
AND<=.S D606 +1          ; ｽﾃｯﾌﾟ番号
FMOV.S -1 D700 K10       ; 履歴1-1
MRD
AND<=.S D706 +1          ; 履歴1ｽﾃｯﾌﾟ番号
FMOV.S -1 D710 K10       ; 履歴2-1
MRD
AND<=.S D716 +1          ; 履歴2ｽﾃｯﾌﾟ番号
FMOV.S -1 D720 K10       ; 履歴3-1
MRD
AND<=.S D726 +1          ; 履歴3ｽﾃｯﾌﾟ番号
FMOV.S -1 D730 K10       ; 履歴4-1
MPP
AND<=.S D736 +1          ; 履歴4ｽﾃｯﾌﾟ番号
FMOV.S -1 D740 K10       ; 履歴5-1
LD M65                   ; ｽﾃｯﾌﾟ完了
@WSR D12000 K290 K10     ; ｶｳﾝﾄ履歴ﾃﾞｰﾀ
BMOV D600 D12000 K10     ; 圧力到達時間  ｶｳﾝﾄ履歴ﾃﾞｰﾀ
MPS
AND<= D606 K5            ; ｽﾃｯﾌﾟ番号
@WSR D700 K40 K10        ; 履歴1-1
BMOV D600 D700 K10       ; 圧力到達時間  履歴1-1
MRD
AND> D606 K5             ; ｽﾃｯﾌﾟ番号
BMOV D12000 D700 K50     ; ｶｳﾝﾄ履歴ﾃﾞｰﾀ  履歴1-1
MRD
FMOV K0 D600 K10         ; 圧力到達時間
MPP
MOV K0 D760              ; 履歴ｼﾌﾄ±10
LD B1008                 ; 履歴ﾎﾞﾀﾝ上SW
OR B1009                 ; 履歴ﾎﾞﾀﾝ下SW
MOV D760 Z4              ; 履歴ｼﾌﾄ±10
BMOV D12000:Z4 D700 K50  ; ｶｳﾝﾄ履歴ﾃﾞｰﾀ  履歴1-1
LD B100A                 ; 履歴ｸﾘｱSW
TMR K10 K5               ; 履歴ｸﾘｱ長押し
CON
AND T10                  ; 履歴ｸﾘｱ長押し
FMOV.S -1 D700 K50       ; 履歴1-1
FMOV K0 D600 K10         ; 圧力到達時間
FMOV.S -1 D12000 K300    ; ｶｳﾝﾄ履歴ﾃﾞｰﾀ
;<h1/>品種設定
LD M25                   ; 手動選択中
AND>= W08 K501           ; TP_切替ﾍﾟｰｼﾞNo.
AND<= W08 K503           ; TP_切替ﾍﾟｰｼﾞNo.
ANB M51                  ; 自動中
OUT M1000                ; 品種書込み可
LD M0                    ; 常時ON
MPS
LDA D11000               ; 現在品種番号
CON
SUB K1
CON
MUL K500
CON
STA D11002               ; (現在品種番号-1)*200
MRD
MOV D11002 Z1            ; (現在品種番号-1)*200
MPP
AND B100D                ; 品種読み出しPB
BMOV D20000:Z1 D1000 K500; 品種1品種設定1  品種設定変更値1
LDP B100E                ; 品種書込みPB
ANB M51                  ; 自動中
BMOV D1000 D20000:Z1 K500; 品種設定変更値1  品種1品種設定1
;<h1/>品種コピー
LD= W08 K500             ; TP_切替ﾍﾟｰｼﾞNo.
AND M25                  ; 手動選択中
ANB M51                  ; 自動中
OUT M400                 ; 品種ｺﾋﾟｰ可
LD M400                  ; 品種ｺﾋﾟｰ可
MPS
LDA D11050               ; 品種ｺﾋﾟｰ元番号
CON
SUB K1
CON
MUL K500
CON
STA D11052               ; (品種ｺﾋﾟｰ元番号-1)*500
MRD
MOV D11052 Z2            ; (品種ｺﾋﾟｰ元番号-1)*500
MRD
BMOV D20450:Z2 D11060 K11; 品種1品種名称  ｺﾋﾟｰ元名称表示
MRD
LDA D11054               ; 品種ｺﾋﾟｰ先番号
CON
SUB K1
CON
MUL K500
CON
STA D11056               ; (品種ｺﾋﾟｰ先番号-1)*500
MRD
MOV D11056 Z3            ; (品種ｺﾋﾟｰ先番号-1)*500
MPP
BMOV D20450:Z2 D11080 K11; 品種1品種名称  ｺﾋﾟｰ先名称
LDP B100B                ; 品種ｺﾋﾟｰ開始PB
BMOV D20000:Z2 D20000:Z3 K500;品種1品種設定1  品種1品種設定1
;<h1/>操作盤出力
LD M51                   ; 自動中
LD M50                   ; ｽﾃｯﾌﾟ運転開始条件
AND M3                   ; 1sｸﾛｯｸ
ANB M51                  ; 自動中
ORL
OR M20                   ; 運転入力
OUT B1100                ; 運転LP
LD M30                   ; 異常発生中
OUT B1102                ; ﾘｾｯﾄLP
LD M45                   ; 全原位置
LD M42                   ; 原点復帰指令
AND M3                   ; 1sｸﾛｯｸ
ORL
ANB M41                  ; 原点復帰完了
OUT B1103                ; 原点復帰LP
LD M24                   ; 自動選択中
OUT B1104                ; 自動選択中LP
LD M25                   ; 手動選択中
OUT B1105                ; 手動選択中LP
LD Y1504                 ; ｼｸﾞﾅﾙﾀﾜｰ ﾌﾞｻﾞｰ
AND M3                   ; 1sｸﾛｯｸ
OUT B1106                ; ﾌﾞｻﾞｰ停止LP
LD M1000                 ; 品種書込み可
OUT B1120                ; 品種読み出しLP
OUT B1121                ; 品種書込みLP
;状態表示ﾗﾝﾌﾟ
LDB M51                  ; 自動中
MOV K0 D910              ; 運転状態LP
LD M303                  ; 常圧保持中
MOV K1 D910              ; 運転状態LP
LD M204                  ; 減圧中
MOV K2 D910              ; 運転状態LP
LD M201                  ; 減圧保持開始
MOV K3 D910              ; 運転状態LP
LD M60                   ; 減圧制御要求
AND M302                 ; 常圧制御中
MOV K4 D910              ; 運転状態LP
LD M254                  ; 加圧中
MOV K5 D910              ; 運転状態LP
LD M251                  ; 加圧保持開始
MOV K6 D910              ; 運転状態LP
LD M62                   ; 加圧制御要求
AND M302                 ; 常圧制御中
MOV K7 D910              ; 運転状態LP
END
ENDH
