DEVICE:128
;MODULE:出力
;MODULE_TYPE:0
;出力補助
LDPB B1010               ; 手動_真空ﾎﾟﾝﾌﾟON_SW
ANB M20                  ; 運転入力
INV
ANB B1011                ; 手動_真空ﾎﾟﾝﾌﾟOFF_SW
OUT @R000                ; 真空ﾎﾟﾝﾌﾟON補助
LDPB B1011               ; 手動_真空ﾎﾟﾝﾌﾟOFF_SW
ANB M52                  ; ｽﾃｯﾌﾟ終了
ANB M30                  ; 異常発生中
INV
ANB B1010                ; 手動_真空ﾎﾟﾝﾌﾟON_SW
OUT @R001                ; 真空ﾎﾟﾝﾌﾟOFF補助
LDPB B1012               ; 手動_ｼｬｯﾀｰ上昇_SW
ANB M20                  ; 運転入力
INV
AND M28                  ; IL_共通
LD B1016                 ; 手動_加圧配管弁開端_SW
AND M27                  ; IL_ｼｬｯﾀｰ
ORL
ANB B1012                ; 手動_ｼｬｯﾀｰ上昇_SW
OUT @R002                ; ｼｬｯﾀｰ上昇補助
LDPB B1013               ; 手動_ｼｬｯﾀｰ下降_SW
ANB M43                  ; ﾊﾞﾙﾌﾞ原位置確認
ANB M52                  ; ｽﾃｯﾌﾟ終了
LDPB M351                ; 手動操作中大気圧
ORB Y150A                ; 大気開放弁 開
ANL
INV
AND M28                  ; IL_共通
ANB B1013                ; 手動_ｼｬｯﾀｰ下降_SW
OUT @R003                ; ｼｬｯﾀｰ下降補助
LDPB B1018               ; 手動_大気開放弁開端_SW
ANB M302                 ; 常圧制御中
ANB M2                   ; RUN後1ｽｷｬﾝON
ANB M42                  ; 原点復帰指令
INV
AND M26                  ; IL_大気開放
ANB B1019                ; 手動_大気開放弁閉端_SW
OUT @R004                ; 大気開放弁開補助
LDPB B1019               ; 手動_大気開放弁閉端_SW
ANPB M60                 ; 減圧制御要求
ANPB M62                 ; 加圧制御要求
ANB M30                  ; 異常発生中
INV
AND M26                  ; IL_大気開放
ANB B1018                ; 手動_大気開放弁開端_SW
OUT @R005                ; 大気開放弁閉補助
LDPB B1014               ; 手動_真空配管弁開端_SW
ANB M204                 ; 減圧中
ANB M207                 ; 再減圧開始
INV
AND M29                  ; IL_真空配管
ANB B1015                ; 手動_真空配管弁閉端_SW
OUT @R006                ; 真空配管弁開補助
LDPB B1015               ; 手動_真空配管弁閉端_SW
ANB M42                  ; 原点復帰指令
ANB M205                 ; 設定圧到達(減圧制御)
ANB M208                 ; 再減圧到達
ANB M206                 ; 常圧制御要求(減圧中)
ANB M30                  ; 異常発生中
INV
ANB B1014                ; 手動_真空配管弁開端_SW
OUT @R007                ; 真空配管弁閉補助
LDPB B1016               ; 手動_加圧配管弁開端_SW
ANB M254                 ; 加圧中
ANB M257                 ; 再加圧開始
INV
AND M27                  ; IL_ｼｬｯﾀｰ
ANB B1017                ; 手動_加圧配管弁閉端_SW
OUT @R008                ; 加圧配管弁開補助
LDPB B1017               ; 手動_加圧配管弁閉端_SW
ANB M42                  ; 原点復帰指令
ANB M255                 ; 設定圧到達(加圧制御)
ANB M258                 ; 再加圧到達
ANB M256                 ; 常圧制御要求(加圧中)
ANB M30                  ; 異常発生中
INV
ANB B1016                ; 手動_加圧配管弁開端_SW
OUT @R009                ; 加圧配管弁閉補助
;真空ﾎﾟﾝﾌﾟ
LD @R000                 ; 真空ﾎﾟﾝﾌﾟON補助
OR Y1500                 ; 真空ﾎﾟﾝﾌﾟ 電源ON
ANB @R001                ; 真空ﾎﾟﾝﾌﾟOFF補助
OUT Y1500                ; 真空ﾎﾟﾝﾌﾟ 電源ON
;ｼｸﾞﾅﾙﾀﾜｰ
LD M30                   ; 異常発生中
OUT Y1501                ; ｼｸﾞﾅﾙﾀﾜｰ 赤
LD M0                    ; 常時ON
ANB M51                  ; 自動中
ANB M30                  ; 異常発生中
OUT Y1502                ; ｼｸﾞﾅﾙﾀﾜｰ 黄
LD M51                   ; 自動中
OUT Y1503                ; ｼｸﾞﾅﾙﾀﾜｰ 緑
LDP M30                  ; 異常発生中
OR Y1504                 ; ｼｸﾞﾅﾙﾀﾜｰ ﾌﾞｻﾞｰ
ANB B1006                ; ﾌﾞｻﾞｰ停止SW
ANB M31                  ; ﾘｾｯﾄ要求
OUT Y1504                ; ｼｸﾞﾅﾙﾀﾜｰ ﾌﾞｻﾞｰ
;ﾊﾞﾙﾌﾞ出力
LD @R002                 ; ｼｬｯﾀｰ上昇補助
OR Y1508                 ; ｼｬｯﾀｰ 上昇
ANB @R003                ; ｼｬｯﾀｰ下降補助
OUT Y1508                ; ｼｬｯﾀｰ 上昇
LD @R003                 ; ｼｬｯﾀｰ下降補助
OR Y1509                 ; ｼｬｯﾀｰ 下降
ANB @R002                ; ｼｬｯﾀｰ上昇補助
OUT Y1509                ; ｼｬｯﾀｰ 下降
LD @R004                 ; 大気開放弁開補助
OR Y150A                 ; 大気開放弁 開
ANB @R005                ; 大気開放弁閉補助
OR M52                   ; ｽﾃｯﾌﾟ終了
OUT Y150A                ; 大気開放弁 開
LD @R006                 ; 真空配管弁開補助
OR Y150C                 ; 真空配管弁 開
ANB @R007                ; 真空配管弁閉補助
OUT Y150C                ; 真空配管弁 開
LD @R008                 ; 加圧配管弁開補助
OR Y150E                 ; 加圧配管弁 開
ANB @R009                ; 加圧配管弁閉補助
OUT Y150E                ; 加圧配管弁 開
END
ENDH
