DEVICE:128
;MODULE:Main
;MODULE_TYPE:0
;<h1/>■PLC正常
LD CR2002                ; 常時ON
TMR K0 K10               ; PLC_RUN DLY
;<h1/>■入力
;<h1/>∟ｲﾝﾊﾞｰﾀ
;上流
LD X02                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
OUT M2                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
LDB X03                  ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M3                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
LD X04                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ 運転中
OUT M4                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ 運転中
LDB X05                  ; 上流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M5                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
LD X200                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
OUT M20                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
LDB X201                 ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M21                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
;下流
LD X08                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
OUT M8                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
LDB X09                  ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M9                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
LD X0A                   ; 下流側連結CV ｲﾝﾊﾞｰﾀ 運転中
OUT M10                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ 運転中
LDB X0B                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M11                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
;<h1/>∟PB
LD X107                  ; 非常停止PB状態入力
OUT M107                 ; 非常停止 正常
LD M107                  ; 非常停止 正常
MC
LD X100                  ; 上流運転PB
OUT M100                 ; 上流運転PB
LD X102                  ; 下流運転PB
OUT M102                 ; 下流運転PB
MCR
LD X101                  ; 上流停止PB
OUT M101                 ; 上流停止PB
LD X103                  ; 下流停止PB
OUT M103                 ; 下流停止PB
LD X104                  ; ﾘｾｯﾄPB
OUT M104                 ; ﾘｾｯﾄPB
;上流速度選択スイッチ
LD X108                  ; ｾﾚｸﾀｽｲｯﾁ1-1
MPS
LD X109                  ; ｾﾚｸﾀｽｲｯﾁ1-2
ANB X10A                 ; ｾﾚｸﾀｽｲｯﾁ1-3
ANL
OUT M108                 ; ｾﾚｸﾀｽｲｯﾁ1 低速選択
MPP
LDB X109                 ; ｾﾚｸﾀｽｲｯﾁ1-2
AND X10A                 ; ｾﾚｸﾀｽｲｯﾁ1-3
ANL
OUT M109                 ; ｾﾚｸﾀｽｲｯﾁ1 中速選択
LDB X108                 ; ｾﾚｸﾀｽｲｯﾁ1-1
AND X109                 ; ｾﾚｸﾀｽｲｯﾁ1-2
AND X10A                 ; ｾﾚｸﾀｽｲｯﾁ1-3
OUT M110                 ; ｾﾚｸﾀｽｲｯﾁ1 高速選択
;下流速度選択スイッチ
LD X10B                  ; ｾﾚｸﾀｽｲｯﾁ2-1
MPS
LD X10C                  ; ｾﾚｸﾀｽｲｯﾁ2-2
ANB X10D                 ; ｾﾚｸﾀｽｲｯﾁ2-3
ANL
OUT M111                 ; ｾﾚｸﾀｽｲｯﾁ2 低速選択
MPP
LDB X10C                 ; ｾﾚｸﾀｽｲｯﾁ2-2
AND X10D                 ; ｾﾚｸﾀｽｲｯﾁ2-3
ANL
OUT M112                 ; ｾﾚｸﾀｽｲｯﾁ2 中速選択
LDB X10B                 ; ｾﾚｸﾀｽｲｯﾁ2-1
AND X10C                 ; ｾﾚｸﾀｽｲｯﾁ2-2
AND X10D                 ; ｾﾚｸﾀｽｲｯﾁ2-3
OUT M113                 ; ｾﾚｸﾀｽｲｯﾁ2 高速選択
;<h1/>∟上流巻締装置
LD X0E                   ; 1号機より 装置運転中
OUT M14                  ; 1号機より 装置運転中
;<h1/>∟下流巻締装置
LD X0F                   ; 2号機より 装置運転中
OUT M15                  ; 2号機より 装置運転中
;<h1/>■出力
;<h1/>∟ｲﾝﾊﾞｰﾀ
;上流
LDB M120                 ; 上流CV運転開始
INV
OUT Y54                  ; 上流側排出CV2 正転
CON
OUT Y58                  ; 上流側連結CV 正転
CON
OUT Y250                 ; 上流側連結下流CV 正転
LD M110                  ; ｾﾚｸﾀｽｲｯﾁ1 高速選択
OUT Y55                  ; 上流側排出CV2 高速選択
CON
OUT Y59                  ; 上流側連結CV 高速選択
CON
OUT Y251                 ; 上流側連結下流CV 高速選択
LD M109                  ; ｾﾚｸﾀｽｲｯﾁ1 中速選択
OUT Y56                  ; 上流側排出CV2 中速選択
CON
OUT Y5A                  ; 上流側連結CV 中速選択
CON
OUT Y252                 ; 上流側連結下流CV 中速選択
LD M108                  ; ｾﾚｸﾀｽｲｯﾁ1 低速選択
OUT Y57                  ; 上流側排出CV2 低速選択
CON
OUT Y5B                  ; 上流側連結CV 低速選択
CON
OUT Y253                 ; 上流側連結下流CV 低速選択
LD M104                  ; ﾘｾｯﾄPB
MPS
AND M3                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT Y255                 ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑﾘｾｯﾄ
MRD
AND M5                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT Y256                 ; 上流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑﾘｾｯﾄ
MPP
AND M21                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT Y257                 ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑﾘｾｯﾄ
;下流
LDB M130                 ; 下流CV運転開始
INV
OUT Y154                 ; 下流側排出CV2 正転
CON
OUT Y158                 ; 下流側連結CV 正転
LD M113                  ; ｾﾚｸﾀｽｲｯﾁ2 高速選択
OUT Y155                 ; 下流側排出CV2 高速選択
CON
OUT Y159                 ; 下流側連結CV 高速選択
LD M112                  ; ｾﾚｸﾀｽｲｯﾁ2 中速選択
OUT Y156                 ; 下流側排出CV2 中速選択
CON
OUT Y15A                 ; 下流側連結CV 中速選択
LD M111                  ; ｾﾚｸﾀｽｲｯﾁ2 低速選択
OUT Y157                 ; 下流側排出CV2 低速選択
CON
OUT Y15B                 ; 下流側連結CV 低速選択
LD M104                  ; ﾘｾｯﾄPB
MPS
AND M9                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT Y259                 ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑﾘｾｯﾄ
MPP
AND M11                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT Y25A                 ; 下流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑﾘｾｯﾄ
;<h1/>∟SOL
LD M20                   ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
OUT Y50                  ; 連結CV乗継部 ｴｱﾌﾞﾛｰON
;<h1/>∟PL・BZ
LDB M140                 ; 上流CVｱﾗｰﾑ発生
ANB M141                 ; 下流CVｱﾗｰﾑ発生
INV
OUT M142                 ; ｱﾗｰﾑ発生中まとめ
LD M142                  ; ｱﾗｰﾑ発生中まとめ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT Y5E                  ; ﾘｾｯﾄPL 
LD M142                  ; ｱﾗｰﾑ発生中まとめ
MEP
LD Y15E                  ; ﾌﾞｻﾞｰ 
ANPB M104                ; ﾘｾｯﾄPB
ORL
OUT Y15E                 ; ﾌﾞｻﾞｰ 
;上流
LDB M2                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
ANB M4                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ 運転中
ANB M20                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
INV
LDB M140                 ; 上流CVｱﾗｰﾑ発生
ANB M101                 ; 上流停止PB
AND M130                 ; 下流CV運転開始
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
AND X107                 ; 非常停止PB状態入力
OUT Y5C                  ; 上流運転PL 
LDB M2                   ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
ANB M4                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ 運転中
ANB M20                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
OUT Y5D                  ; 上流停止PL 
;下流
LDB M8                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
ANB M10                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ 運転中
INV
LDB M140                 ; 上流CVｱﾗｰﾑ発生
ANB M103                 ; 下流停止PB
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
AND X107                 ; 非常停止PB状態入力
OUT Y15C                 ; 下流運転PL 
LDB M8                   ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
ANB M10                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ 運転中
OUT Y15D                 ; 下流停止PL 
;<h1/>∟上流巻締装置
LD M2                    ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
AND M4                   ; 上流側連結CV ｲﾝﾊﾞｰﾀ 運転中
AND M20                  ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ 運転中
OUT Y5F                  ; 1号機へ CV運転中
;<h1/>∟下流巻締装置
LD M8                    ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ 運転中
AND M10                  ; 下流側連結CV ｲﾝﾊﾞｰﾀ 運転中
OUT Y15F                 ; 2号機へ CV運転中
;<h1/>■運転
LDP M100                 ; 上流運転PB
OR M120                  ; 上流CV運転開始
ANB M140                 ; 上流CVｱﾗｰﾑ発生
LDB M101                 ; 上流停止PB
OR M14                   ; 1号機より 装置運転中
ANL
AND M130                 ; 下流CV運転開始
AND X107                 ; 非常停止PB状態入力
OUT M120                 ; 上流CV運転開始
LDP M102                 ; 下流運転PB
OR M130                  ; 下流CV運転開始
ANB M140                 ; 上流CVｱﾗｰﾑ発生
LDB M103                 ; 下流停止PB
OR M14                   ; 1号機より 装置運転中
OR M15                   ; 2号機より 装置運転中
ANL
AND X107                 ; 非常停止PB状態入力
OUT M130                 ; 下流CV運転開始
LD M3                    ; 上流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OR M5                    ; 上流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OR M21                   ; 上流側連結下流CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M140                 ; 上流CVｱﾗｰﾑ発生
LD M9                    ; 下流側排出CV2 ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OR M11                   ; 下流側連結CV ｲﾝﾊﾞｰﾀ ｱﾗｰﾑ
OUT M141                 ; 下流CVｱﾗｰﾑ発生
END
ENDH
