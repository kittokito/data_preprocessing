DEVICE:128
;MODULE:Main
;MODULE_TYPE:0
;
LD CR3905                ; 重度ｴﾗｰ発生中
INV
OUT M1                   ; CPU正常
LD CR2008                ; 運転開始時1ｽｷｬﾝON
OUT M2                   ; 1ｽｷｬﾝON
LD CR2002                ; 常時ON
ANB T111                 ; OFF_0.3secﾀｲﾏ
MPS
TMR K110 K3              ; ON_0.3secﾀｲﾏ
MPP
AND T110                 ; ON_0.3secﾀｲﾏ
TMR K111 K3              ; OFF_0.3secﾀｲﾏ
;<h1/>INPUT
LD Y54                   ; ｸﾗﾝﾌﾟﾛｯｸ
TMR K120 K5              ; ｸﾗﾝﾌﾟﾛｯｸ確認ﾀｲﾏ
LDB Y54                  ; ｸﾗﾝﾌﾟﾛｯｸ
TMR K121 K5              ; ｸﾗﾝﾌﾟ解除確認ﾀｲﾏ
LD X03                   ; NG_PB
TMR K135 K20             ; NG確認ﾃﾞｨﾚｲ
;<h1/>Main_Cycle
LD M300                  ; PC通信正常
AND W00.4                ; ｽﾃｰｼﾞ有効/無効
ANB M1049                ; ｻｲｸﾙ終了
OUT M100                 ; 起動指令
LD CR2002                ; 常時ON
ZRES M1000 M1049         ; 動作開始  ｻｲｸﾙ終了
LDB M100                 ; 起動指令
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MOV K0 W02F              ; PLC動作ｽﾃｯﾌﾟ
LD M100                  ; 起動指令
MOV D1000 Z1             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
SET M1000:Z1             ; 動作開始
CAL+ D1000 K1000 W02F    ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ  PLC動作ｽﾃｯﾌﾟ
LD M1000                 ; 動作開始
MPS
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
OR= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND W01.2                ; 槽取出し可能
LD T10                   ; ﾋｰﾀｰ温度到達
ORB W00.5                ; 低温起動中
ANL
ANB X0A                  ; ﾜｰｸ検出
AND X09                  ; ｸﾗﾝﾌﾟ原点
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND= W02 K3              ; 工程処理    1:Lo 2:Hi   3:電特
MOV K5 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1001                 ; 取出し開始要求ON
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND Y58                  ; 低温槽取出要求
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND Y59                  ; 高温槽取出要求
ORL
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1002                 ; 取出開始要求確認
MPS
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
ANF X01                  ; 低温槽扉閉
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
ANF X07                  ; 高温温槽扉閉
ORL
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND X01                  ; 低温槽扉閉
ANP X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND X07                  ; 高温温槽扉閉
ANP X06                  ; 高温温槽取出開始_PB
ORL
ANL
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾁｬﾝﾊﾞｰ開･時間計測開始
;Chamber open timer measurement start
LD M1003                 ; ﾁｬﾝﾊﾞｰ取出可能確認
AND W011.0               ; 扉開（計時開始要求）
AND W01.0                ; 計時開始受付
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1004                 ; 計時開始確認
AND M1051                ; 時間計測開始
MPS
AND W00.6                ; 時間計測ﾀｲﾑｱｳﾄ
MOV K14 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MRD
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND X01                  ; 低温槽扉閉
ANP X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND X07                  ; 高温温槽扉閉
ANP X06                  ; 高温温槽取出開始_PB
ORL
ANL
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ無し確認
;Work check
LD M1005                 ; ﾜｰｸ無し確認
MPS
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
OR= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND W00.6                ; 時間計測ﾀｲﾑｱｳﾄ
ANL
MOV K14 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MRD
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND X01                  ; 低温槽扉閉
ANP X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND X07                  ; 高温温槽扉閉
ANP X06                  ; 高温温槽取出開始_PB
ORL
ANL
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD X09                   ; ｸﾗﾝﾌﾟ原点
ANB X0A                  ; ﾜｰｸ検出
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸｾｯﾄ要求
;Work set request
LD M1006                 ; ﾜｰｸｾｯﾄ要求
MPS
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
OR= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND W00.6                ; 時間計測ﾀｲﾑｱｳﾄ
ANL
MOV K14 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MRD
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND X01                  ; 低温槽扉閉
ANP X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND X07                  ; 高温温槽扉閉
ANP X06                  ; 高温温槽取出開始_PB
ORL
ANL
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD X09                   ; ｸﾗﾝﾌﾟ原点
AND X0A                  ; ﾜｰｸ検出
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸｸﾗﾝﾌﾟ要求
;Work clamp request
LD M1007                 ; ﾜｰｸｸﾗﾝﾌﾟ要求
MPS
AND W00.6                ; 時間計測ﾀｲﾑｱｳﾄ
MOV K14 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MRD
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
AND X01                  ; 低温槽扉閉
ANP X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
AND X07                  ; 高温温槽扉閉
ANP X06                  ; 高温温槽取出開始_PB
ORL
ANL
MOV K15 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND X08                  ; ｸﾗﾝﾌﾟ閉
MPS
ANB X0A                  ; ﾜｰｸ検出
MOV K16 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
AND X0A                  ; ﾜｰｸ検出
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｺｰﾄﾞ読取完了
;Code reading completion
LD M1008                 ; ﾊﾞｰｺｰﾄﾞﾃﾞｰﾀ待ち
MPS
LDB W01.3                ; 読取済ﾊﾞｰｺｰﾄﾞﾃﾞｰﾀあり
AND X09                  ; ｸﾗﾝﾌﾟ原点
ANB X0A                  ; ﾜｰｸ検出
ANL
MOV K6 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
LD W01.3                 ; 読取済ﾊﾞｰｺｰﾄﾞﾃﾞｰﾀあり
AND X08                  ; ｸﾗﾝﾌﾟ閉
ANL
MPS
AND X0A                  ; ﾜｰｸ検出
MOV K20 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB X0A                  ; ﾜｰｸ検出
MOV K10 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ未検出
;Work undetected
LD M1010                 ; ﾜｰｸ未検出検査開始不可NG
ANB W01.3                ; 読取済ﾊﾞｰｺｰﾄﾞﾃﾞｰﾀあり
AND X09                  ; ｸﾗﾝﾌﾟ原点
MOV K7 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ﾀｲﾑｱｳﾄ異常
;Time_out_NG
LD M1014                 ; ﾀｲﾑｱｳﾄNG
AND M1050                ; NG点灯補助
ANB M1051                ; 時間計測開始
ANB Y58                  ; 低温槽取出要求
AND T135                 ; NG確認ﾃﾞｨﾚｲ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;槽取出ｷｬﾝｾﾙ処理
;Chamber take-out cancel
LD M1015                 ; 槽取出ｷｬﾝｾﾙ
AND W011.1               ; 槽取出しｷｬﾝｾﾙ要求
AND W01.1                ; 槽取出しｷｬﾝｾﾙ受付
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
ANB X00                  ; 低温槽取出開始_PB
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
ANB X06                  ; 高温温槽取出開始_PB
ORL
ANL
ANB M1050                ; NG点灯補助
ANB W00.6                ; 時間計測ﾀｲﾑｱｳﾄ
MOV K49 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1016                 ; ﾜｰｸ未検出ｸﾗﾝﾌﾟ
AND X09                  ; ｸﾗﾝﾌﾟ原点
MOV K6 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｸﾗﾝﾌﾟﾛｯｸ確認
;Clamp lock check
LD M1020                 ; ｸﾗﾝﾌﾟﾛｯｸ
AND T120                 ; ｸﾗﾝﾌﾟﾛｯｸ確認ﾀｲﾏ
MPS
AND X08                  ; ｸﾗﾝﾌﾟ閉
MOV K23 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB X08                  ; ｸﾗﾝﾌﾟ閉
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1021                 ; ｸﾗﾝﾌﾟﾛｯｸﾐｽ､ﾛｯｸ解除
AND T121                 ; ｸﾗﾝﾌﾟ解除確認ﾀｲﾏ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1022                 ; ｸﾗﾝﾌﾟﾛｯｸｴﾗｰ
AND X09                  ; ｸﾗﾝﾌﾟ原点
MOV K7 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;ｸﾞﾗﾝﾄﾞ接続後検査開始
;Grand connection after the test start
;EX10はｸﾞﾗﾝﾄﾞ無しの為ｽｷｯﾌﾟ
LD M1023                 ; ｸﾞﾗﾝﾄﾞ接続
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1024                 ; 検査開始要求
ANB M1051                ; 時間計測開始
AND W011.4               ; 検査開始要求
AND W01.4                ; 検査中
LD= W02 K1               ; 工程処理    1:Lo 2:Hi   3:電特
ANB Y58                  ; 低温槽取出要求
LD= W02 K2               ; 工程処理    1:Lo 2:Hi   3:電特
ANB Y59                  ; 高温槽取出要求
ORL
OR= W02 K3               ; 工程処理    1:Lo 2:Hi   3:電特
ANL
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1025                 ; 検査開始確認
ANB W01.4                ; 検査中
MPS
AND W01.5                ; 検査結果OK
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB W01.5                ; 検査結果OK
MOV K30 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;検査OK
;Test OK
LD M1026                 ; 検査OKｸﾞﾗﾝﾄﾞ解放
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1027                 ; ｸﾗﾝﾌﾟﾛｯｸ解除
AND T121                 ; ｸﾗﾝﾌﾟ解除確認ﾀｲﾏ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1028                 ; OKﾜｰｸ取出
AND X09                  ; ｸﾗﾝﾌﾟ原点
ANB X0A                  ; ﾜｰｸ検出
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1029                 ; OKﾜｰｸ取出確認
ANB Y52                  ; OK_PL
ANB Y53                  ; NG_PL
MOV K49 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;検査NG
;Test NG
LD M1030                 ; 検査NG
ANP T135                 ; NG確認ﾃﾞｨﾚｲ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1031                 ; 検査NGｸﾞﾗﾝﾄﾞ解放
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1032                 ; ｸﾗﾝﾌﾟﾛｯｸ解除
AND T121                 ; ｸﾗﾝﾌﾟ解除確認ﾀｲﾏ
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1033                 ; NGﾜｰｸ取出
AND X09                  ; ｸﾗﾝﾌﾟ原点
ANB X0A                  ; ﾜｰｸ検出
ANB W00.1                ; PC Busy
@INC D1000               ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1034                 ; NGﾜｰｸ取出確認
ANB Y52                  ; OK_PL
ANB Y53                  ; NG_PL
MOV K49 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>Heater
LD W00.5                 ; 低温起動中
AND W00.7                ; ﾋｰﾀｰON
AND Y57                  ; ﾋｰﾀｰﾌﾞﾛｰ_ON
OR M1070                 ; ﾋｰﾀｰ起動  ｴｱﾌﾞﾛｰON
ANB T130                 ; ﾋｰﾀｰ冷却ﾀｲﾏ
OUT M1070                ; ﾋｰﾀｰ起動  ｴｱﾌﾞﾛｰON
LD M1070                 ; ﾋｰﾀｰ起動  ｴｱﾌﾞﾛｰON
AND Y5C                  ; ﾋｰﾀｰ温調停止*
TMR K130 K600            ; ﾋｰﾀｰ冷却ﾀｲﾏ
;<h1/>PLC→PC I/F
LD CR2002                ; 常時ON
ANB T303                 ; PLC startup timer 2
TMR K302 K15             ; PLC startup timer 1
LD T302                  ; PLC startup timer 1
TMR K303 K15             ; PLC startup timer 2
LD M1                    ; CPU正常
ANB T302                 ; PLC startup timer 1
OUT W010.0               ; PLC動作中
LD M300                  ; PC通信正常
MPS
LD W00.1                 ; PC Busy
AND T135                 ; NG確認ﾃﾞｨﾚｲ
ANL
OUT W010.1               ; Reset SW受付
MRD
LD X09                   ; ｸﾗﾝﾌﾟ原点
ANB Y54                  ; ｸﾗﾝﾌﾟﾛｯｸ
ANL
OUT W010.4               ; ｸﾗﾝﾌﾟ原点
MRD
AND X0A                  ; ﾜｰｸ検出
OUT W010.5               ; ﾜｰｸ検出
MPP
AND T10                  ; ﾋｰﾀｰ温度到達
OUT W010.6               ; ﾋｰﾀｰ温度到達
;
LD M1003                 ; ﾁｬﾝﾊﾞｰ取出可能確認
OUT W011.0               ; 扉開（計時開始要求）
LD M1015                 ; 槽取出ｷｬﾝｾﾙ
OUT W011.1               ; 槽取出しｷｬﾝｾﾙ要求
LD M1024                 ; 検査開始要求
OUT W011.4               ; 検査開始要求
;<h1/>OUT_PUT
LD M1004                 ; 計時開始確認
OR M1051                 ; 時間計測開始
ANB M1015                ; 槽取出ｷｬﾝｾﾙ
ANB M1014                ; ﾀｲﾑｱｳﾄNG
ANB M1024                ; 検査開始要求
AND M100                 ; 起動指令
OUT M1051                ; 時間計測開始
LDB W00.1                ; PC Busy
ANB B014                 ; ｺｰﾄﾞ読取中
ANB B015                 ; ﾜｰｸ未検出検査開始不可
ANB B018                 ; 検査NGﾜｰｸ取出要求
ANB B019                 ; 不明ﾜｰｸ有り取出要求
ANB B01A                 ; ｸﾗﾝﾌﾟﾛｯｸｴﾗｰ
ANB B01B                 ; ﾋｰﾀｰ過昇異常
ANB B01D                 ; ﾋｰﾀｰ温度低下異常
ANB B01E                 ; ﾋｰﾀｰｴｱ流量異常
INV
OUT M1100                ; BZ補助
LD M1100                 ; BZ補助
OUT Y50                  ; ﾌﾞｻﾞｰ
LD M1004                 ; 計時開始確認
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LD> D1000 K4             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
AND< D1000 K8            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ORL
OR M1025                 ; 検査開始確認
AND CR2003               ; 常時OFF
OUT Y51
LD M1026                 ; 検査OKｸﾞﾗﾝﾄﾞ解放
OR Y52                   ; OK_PL
ANB M1029                ; OKﾜｰｸ取出確認
AND M100                 ; 起動指令
OUT Y52                  ; OK_PL
LD M1014                 ; ﾀｲﾑｱｳﾄNG
OR M1030                 ; 検査NG
OR M1050                 ; NG点灯補助
ANB M1015                ; 槽取出ｷｬﾝｾﾙ
ANB M1034                ; NGﾜｰｸ取出確認
AND M100                 ; 起動指令
OUT M1050                ; NG点灯補助
LD M1050                 ; NG点灯補助
LD W00.1                 ; PC Busy
OR B014                  ; ｺｰﾄﾞ読取中
OR B01B                  ; ﾋｰﾀｰ過昇異常
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD B01C                  ; ｸﾗﾝﾌﾟ開要求
OR B019                  ; 不明ﾜｰｸ有り取出要求
OR B01A                  ; ｸﾗﾝﾌﾟﾛｯｸｴﾗｰ
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y53                  ; NG_PL
LD M1001                 ; 取出し開始要求ON
AND= W02 K1              ; 工程処理    1:Lo 2:Hi   3:電特
OR M1080                 ; 低温槽取出要求補助
ANB M1014                ; ﾀｲﾑｱｳﾄNG
ANB M1015                ; 槽取出ｷｬﾝｾﾙ
ANB M1024                ; 検査開始要求
AND M300                 ; PC通信正常
AND W00.4                ; ｽﾃｰｼﾞ有効/無効
OUT M1080                ; 低温槽取出要求補助
LD M1080                 ; 低温槽取出要求補助
LDB M1002                ; 取出開始要求確認
OR CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
OUT Y58                  ; 低温槽取出要求
LD M1001                 ; 取出し開始要求ON
AND= W02 K2              ; 工程処理    1:Lo 2:Hi   3:電特
OR M1081                 ; 高温槽取出要求補助
ANB M1014                ; ﾀｲﾑｱｳﾄNG
ANB M1015                ; 槽取出ｷｬﾝｾﾙ
ANB M1024                ; 検査開始要求
AND M300                 ; PC通信正常
AND W00.4                ; ｽﾃｰｼﾞ有効/無効
OUT M1081                ; 高温槽取出要求補助
LD M1081                 ; 高温槽取出要求補助
LDB M1002                ; 取出開始要求確認
OR CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
OUT Y59                  ; 高温槽取出要求
LD M1020                 ; ｸﾗﾝﾌﾟﾛｯｸ
OR Y54                   ; ｸﾗﾝﾌﾟﾛｯｸ
ANB M1021                ; ｸﾗﾝﾌﾟﾛｯｸﾐｽ､ﾛｯｸ解除
ANB M1027                ; ｸﾗﾝﾌﾟﾛｯｸ解除
ANB M1032                ; ｸﾗﾝﾌﾟﾛｯｸ解除
AND M100                 ; 起動指令
OUT Y54                  ; ｸﾗﾝﾌﾟﾛｯｸ
;Heater
LDB B01B                 ; ﾋｰﾀｰ過昇異常
INV
OUT Y56                  ; ﾋｰﾀ-電源_OFF(EMS)*
LDB Y5C                  ; ﾋｰﾀｰ温調停止*
AND X0C                  ; ﾋｰﾀｰ温度範囲内
TMR K10 K30              ; ﾋｰﾀｰ温度到達
LDB W00.5                ; 低温起動中
ANB M1070                ; ﾋｰﾀｰ起動  ｴｱﾌﾞﾛｰON
INV
OUT Y57                  ; ﾋｰﾀｰﾌﾞﾛｰ_ON
LD W00.7                 ; ﾋｰﾀｰON
AND W00.5                ; 低温起動中
AND Y57                  ; ﾋｰﾀｰﾌﾞﾛｰ_ON
ANB B01B                 ; ﾋｰﾀｰ過昇異常
ANB Y56                  ; ﾋｰﾀ-電源_OFF(EMS)*
INV
OUT Y5C                  ; ﾋｰﾀｰ温調停止*
;<h1/>Message
LD M1002                 ; 取出開始要求確認
OUT B010                 ; ﾜｰｸ取出可能
@DW H0010 D0
CON
@FIFOW D0 D900           ; 異常内容
LD CR2003                ; 常時OFF
OUT B011
@DW H0011 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1006                 ; ﾜｰｸｾｯﾄ要求
OUT B012                 ; ﾜｰｸｾｯﾄ要求
@DW H0012 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1007                 ; ﾜｰｸｸﾗﾝﾌﾟ要求
OUT B013                 ; ﾜｰｸｸﾗﾝﾌﾟ要求
@DW H0013 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1008                 ; ﾊﾞｰｺｰﾄﾞﾃﾞｰﾀ待ち
OUT B014                 ; ｺｰﾄﾞ読取中
@DW H0014 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1010                 ; ﾜｰｸ未検出検査開始不可NG
OR M1016                 ; ﾜｰｸ未検出ｸﾗﾝﾌﾟ
OUT B015                 ; ﾜｰｸ未検出検査開始不可
@DW H0015 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1025                 ; 検査開始確認
OUT B016                 ; 検査中
@DW H0016 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1028                 ; OKﾜｰｸ取出
OUT B017                 ; 検査OKﾜｰｸ取出要求
@DW H0017 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1033                 ; NGﾜｰｸ取出
OUT B018                 ; 検査NGﾜｰｸ取出要求
@DW H0018 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1000                 ; 動作開始
OR M1001                 ; 取出し開始要求ON
OR M1002                 ; 取出開始要求確認
OR M1003                 ; ﾁｬﾝﾊﾞｰ取出可能確認
OR M1004                 ; 計時開始確認
OR M1005                 ; ﾜｰｸ無し確認
AND X0A                  ; ﾜｰｸ検出
OUT B019                 ; 不明ﾜｰｸ有り取出要求
@DW H0019 D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1022                 ; ｸﾗﾝﾌﾟﾛｯｸｴﾗｰ
OUT B01A                 ; ｸﾗﾝﾌﾟﾛｯｸｴﾗｰ
@DW H001A D0
CON
@FIFOW D0 D900           ; 異常内容
LD X0E                   ; ﾋｰﾀｰ過昇警報
LD B01B                  ; ﾋｰﾀｰ過昇異常
ANB X03                  ; NG_PB
ORL
AND W00.5                ; 低温起動中
OUT B01B                 ; ﾋｰﾀｰ過昇異常
@DW H001B D0
CON
@FIFOW D0 D900           ; 異常内容
LD M1000                 ; 動作開始
OR M1001                 ; 取出し開始要求ON
OR M1002                 ; 取出開始要求確認
OR M1003                 ; ﾁｬﾝﾊﾞｰ取出可能確認
OR M1004                 ; 計時開始確認
OR M1005                 ; ﾜｰｸ無し確認
OR M1006                 ; ﾜｰｸｾｯﾄ要求
ANB X09                  ; ｸﾗﾝﾌﾟ原点
TMR K140 K20             ; ClampOpenCHK
MPS
AND T140                 ; ClampOpenCHK
OUT B01C                 ; ｸﾗﾝﾌﾟ開要求
MPP
@DW H001C D0
CON
@FIFOW D0 D900           ; 異常内容
LD X0D                   ; ﾋｰﾀｰ温度低下警報
AND W00.5                ; 低温起動中
OUT B01D                 ; ﾋｰﾀｰ温度低下異常
@DW H001D D0
CON
@FIFOW D0 D900           ; 異常内容
LD X0F                   ; ﾋｰﾀｰｴｱ流量低下警報
AND W00.7                ; ﾋｰﾀｰON
LD B01E                  ; ﾋｰﾀｰｴｱ流量異常
ANB X03                  ; NG_PB
ORL
OUT B01E                 ; ﾋｰﾀｰｴｱ流量異常
@DW H001E D0
CON
@FIFOW D0 D900           ; 異常内容
END
ENDH
