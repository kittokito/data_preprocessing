DEVICE:53
;MODULE:UNIT1
;MODULE_TYPE:0
;□_入力受け / INPUT
;関連する入力受け(ｾﾝｻのﾃﾞｨﾚｲﾀｲﾏ等)を必要があれば作成する。
LD X02                   ; CX1 ﾜｰｸ浮き確認1(正常:ON)
AND X03                  ; CX1 ﾜｰｸ浮き確認2(正常:ON)
TMR K101 K3              ; CX1 WORK FLOAT CHECK OK
INV
TMR K102 K3              ; CX1 WORK FLOAT CHECK NG
LD CR2003                ; 常時OFF
TMR K103 K3              ; 入力受け3
LD CR2003                ; 常時OFF
TMR K104 K3              ; 入力受け4
;□_ﾍﾞﾝﾄﾌﾟﾗｸﾞ圧入ﾛﾎﾞｯﾄ現在値 / Vent plug press-fit robot current Position
LD X108A                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
MPS
AND X1080                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS1
OUT D1028.0              ; ﾌﾟﾗｸﾞ圧入 RB完了POS1
MRD
AND X1081                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS2
OUT D1028.1              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS2
MRD
AND X1082                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS4
OUT D1028.2              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS4
MRD
AND X1083                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS8
OUT D1028.3              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS8
MRD
AND X1084                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS16
OUT D1028.4              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS16
MRD
AND X1085                ; ﾌﾟﾗｸﾞ圧入 RB_完了POS32
OUT D1028.5              ; ﾌﾟﾗｸﾞ圧入 RB_完了POS32
MPP
INV
MOV K0 D1028             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
;□_画像検査ｼｬｯﾀｰﾛﾎﾞｯﾄ現在値 / Image inspection shutter robot current Position
LD X109A                 ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
MPS
AND X1090                ; 画像ｼｬｯﾀｰ RB_完了POS1
OUT D1038.0              ; 画像ｼｬｯﾀｰ RB完了POS1
MRD
AND X1091                ; 画像ｼｬｯﾀｰ RB_完了POS2
OUT D1038.1              ; 画像ｼｬｯﾀｰ RB完了POS2
MRD
AND X1092                ; 画像ｼｬｯﾀｰ RB_完了POS4
OUT D1038.2              ; 画像ｼｬｯﾀｰ RB完了POS4
MRD
AND X1093                ; 画像ｼｬｯﾀｰ RB_完了POS8
OUT D1038.3              ; 画像ｼｬｯﾀｰ RB完了POS8
MRD
AND X1094                ; 画像ｼｬｯﾀｰ RB_完了POS16
OUT D1038.4              ; 画像ｼｬｯﾀｰ RB完了POS16
MRD
AND X1095                ; 画像ｼｬｯﾀｰ RB_完了POS32
OUT D1038.5              ; 画像ｼｬｯﾀｰ RB完了POS32
MPP
INV
MOV K0 D1038             ; 画像ｼｬｯﾀｰ RB_完了POS
;□ 装置状態・動作条件 /  conditions and operating conditions
LD= D1028 K1             ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
AND= D1038 K1            ; 画像ｼｬｯﾀｰ RB_完了POS
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
OUT M2384                ; ﾕﾆｯﾄ1原点
LDB B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
ANB B021                 ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
ANB B022                 ; 画像検査  機器_異常
ANB B023                 ; ﾕﾆｯﾄ1_異常4
ANB B024                 ; ﾕﾆｯﾄ1_異常5
ANB B025                 ; ﾕﾆｯﾄ1_異常6
INV
OUT M2385                ; ﾕﾆｯﾄ1全異常
LD M2384                 ; ﾕﾆｯﾄ1原点
ANB M1600                ; S00_圧入前位置(P2)
AND X01                  ; ｾｯﾄ部ﾜｰｸ有確認
LD M1001                 ; CX1 JIG 以外
LD M1000                 ; CX1 JIG
AND T101                 ; CX1 WORK FLOAT CHECK OK
ORL
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OR M997                  ; CX1追加改造
ANL
AND<= K7 D2010           ; ﾃﾞｼﾞｽｲｯﾁ 選択No
OUT M2386                ; ﾕﾆｯﾄ1起動条件
LDB M64                  ; 運転準備
OR M65                   ; 即停止異常(緊急停止)
OR M2385                 ; ﾕﾆｯﾄ1全異常
OR T15                   ; 強制停止
OUT M2387                ; ﾕﾆｯﾄ1起動OFF
LD M98                   ; 原点復帰開始
ANB M2388                ; ﾕﾆｯﾄ1原点復帰中
OR M2388                 ; ﾕﾆｯﾄ1原点復帰中
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M2079                ; Ori79_ﾕﾆｯﾄ1原点復帰完了確認
OUT M2388                ; ﾕﾆｯﾄ1原点復帰中
LD M80                   ; 自動起動条件
LDP X1564                ; ｽﾀｰﾄPB
LD M201                  ; START SW PASS MODE中
AND M200                 ; START SW PASS MODE
ORL
ANL
AND M2386                ; ﾕﾆｯﾄ1起動条件
OR M2389                 ; ﾕﾆｯﾄ1起動中
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M1679                ; S79_STEPEND
OUT M2389                ; ﾕﾆｯﾄ1起動中
LD X01                   ; ｾｯﾄ部ﾜｰｸ有確認
LD M1001                 ; CX1 JIG 以外
LD M1000                 ; CX1 JIG
AND T101                 ; CX1 WORK FLOAT CHECK OK
ORL
OR M997                  ; CX1追加改造
ANL
ANB M2389                ; ﾕﾆｯﾄ1起動中
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
AND M201                 ; START SW PASS MODE中
OUT M200                 ; START SW PASS MODE
LD X1564                 ; ｽﾀｰﾄPB
AND X1563                ; 自動/手動SW
TMR K200 K15             ; ON DELAY TIMER
LD T200                  ; ON DELAY TIMER
SET M201                 ; START SW PASS MODE中
LD X1567                 ; ｴﾗｰﾘｾｯﾄPB
ORB X1563                ; 自動/手動SW
OR X1565                 ; NGﾘｾｯﾄPB
OR X1566                 ; 原点復帰PB
OR T201                  ; 原点未時間
OR T202                  ; WORK SETされなかった時間
RES M201                 ; START SW PASS MODE中
LDB Y2575                ; 原点復帰ﾗﾝﾌﾟ
TMR K201 K1000           ; 原点未時間
LDB X01                  ; ｾｯﾄ部ﾜｰｸ有確認
ANB X1564                ; ｽﾀｰﾄPB
TMR K202 K1000           ; WORK SETされなかった時間
LD X1563                 ; 自動/手動SW
AND X1564                ; ｽﾀｰﾄPB
ANB M2386                ; ﾕﾆｯﾄ1起動条件
ANB M999                 ; 暫定修正　小池
OUT M2390                ; ｽﾀｰﾄ条件不足ﾌﾞｻﾞｰ
LD M80                   ; 自動起動条件
AND M2386                ; ﾕﾆｯﾄ1起動条件
ANB M2387                ; ﾕﾆｯﾄ1起動OFF
ANB M1679                ; S79_STEPEND
ANB M2389                ; ﾕﾆｯﾄ1起動中
ANB M999                 ; 暫定修正　小池
OUT M2391                ; ｽﾀｰﾄ要求
LDB M1681                ; 圧入NGﾌﾞｻﾞｰ
ANB M1683                ; 画像検査NGﾌﾞｻﾞｰ
LDB M2390                ; ｽﾀｰﾄ条件不足ﾌﾞｻﾞｰ
OR M999                  ; 暫定修正　小池
ANL
INV
OUT M2392                ; ﾕﾆｯﾄ1警報補助
;□ステップ起動用フラグ
LD M32                   ; 自動ﾓｰﾄﾞ
LD M33                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M2368                ; ﾕﾆｯﾄ1ｽﾃｯﾌﾟ動作
;□_自動回路 / AUTO
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
FMOV K0 M1600 K5         ; S00_圧入前位置(P2)
LDB M2389                ; ﾕﾆｯﾄ1起動中
MOV K0 D1000             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M2389                 ; ﾕﾆｯﾄ1起動中
MOV D1000 Z1             ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
SET M1600:Z1             ; S00_圧入前位置(P2)
;<h1/>□_自動運転ステップ開始 / AUTO START
;<h1/>__S00 プラグ圧入開始位置下降 / Plug press-fit start position lowered
LD M1600                 ; S00_圧入前位置(P2)
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K2 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1601                 ; S01_圧入開始位置下降指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S02_プラグ圧入下降開始 / Start of plug press down
LD M1602                 ; S02_開始位置移動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@CAL+ D2020 K10 D1024    ; 動作設定No (自動ﾓｰﾄﾞで確定)  ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1603                 ; S03_圧入下降指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S04_プラグ圧入判定 / Plug press-fit judgment
LD M1604                 ; S04_起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
MPS
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
MOV K10 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
ANB X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S05_プラグ圧入判定ＮＧ処理 / NG processing for plug press-fit determination
;ＮＧオペコール、ＰＢ確認 / NG operation call, PB confirmation
LD M2389                 ; ﾕﾆｯﾄ1起動中
MPS
LDP M1605                ; S05_圧入失敗
OR M1680                 ; 圧入NGﾗﾝﾌﾟ表示
ANB M1635                ; S35_NGｽｲｯﾁ確認(終了)
ANL
OUT M1680                ; 圧入NGﾗﾝﾌﾟ表示
MRD
LDP M1605                ; S05_圧入失敗
OR M1681                 ; 圧入NGﾌﾞｻﾞｰ
ANB M1635                ; S35_NGｽｲｯﾁ確認(終了)
ANB T120                 ; NGﾘｾｯﾄ復帰処理開始
ANL
OUT M1681                ; 圧入NGﾌﾞｻﾞｰ
MPP
LD M1680                 ; 圧入NGﾗﾝﾌﾟ表示
LD CR2005                ; 100msｸﾛｯｸﾊﾟﾙｽ
ORB M1605                ; S05_圧入失敗
ANL
ANB M999                 ; 暫定修正　小池
ANL
OUT M1685                ; 圧入NGﾗﾝﾌﾟON
LD M1605                 ; S05_圧入失敗
MPS
AND X1565                ; NGﾘｾｯﾄPB
MPS
ANB X00                  ; NGﾜｰｸ確認
TMR K120 K10             ; NGﾘｾｯﾄ復帰処理開始
MPP
AND X00                  ; NGﾜｰｸ確認
OUT M1636                ; S36_圧入NGBOX排出要求
MPP
AND T120                 ; NGﾘｾｯﾄ復帰処理開始
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M1606                 ; S06_圧入上昇指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1607                 ; S07_起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1608                 ; S08_位置決完了確認
MOV K27 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S10_圧入判定ＯＫ--シャッター開 / Press-fit judgment--OK shutter open
LD M1610                 ; S10_圧入OK
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K2 D1034            ; 画像ｼｬｯﾀｰ  RB_POS確定
LD M1611                 ; S11_ｼｬｯﾀ開指令
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1612                 ; S12_起動確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S13_画像計測プログラム番号切替 / Image measurement program number switching
LD M1613                 ; S13_位置決完了確認
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
ANB B0A02                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替応答)
MOV D2020 W01C0          ; 動作設定No (自動ﾓｰﾄﾞで確定)  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1614                 ; S14_画像計測PRO_No切替指令
AND B0A02                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替応答)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1615                 ; S15_画像計測　BUSY確認
AND= D2020 W0104         ; 動作設定No (自動ﾓｰﾄﾞで確定)  IV Series[1]現在ﾌﾟﾛｸﾞﾗﾑ番号
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1616                 ; S16_画像計測PRO_No切替確認
ANB B0A02                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替応答)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;__S17_画像計測ﾊﾞｯﾌｧｸﾘｱ / Image measurement buffer clear
LD M1617                 ; S17_画像計測ﾊﾞｯﾌｧｸﾘｱ
ANB B0A10                ; 画像検査(結果取得可能)
ANB B0A20                ; 画像検査(総合判定)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;__S18_画像計測 / Image measurement
LD M1618                 ; S18_画像計測ﾊﾞｯﾌｧｸﾘｱ確認
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1619                 ; S19_画像計測　指令
AND B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1620                 ; S20_画像計測　BUSY確認
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S21_画像計測完了､シャッター閉 / Image measurement complete, shutter closed
LD M1621                 ; S21_画像計測完了確認
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1034            ; 画像ｼｬｯﾀｰ  RB_POS確定
LD M1622                 ; S22_ｼｬｯﾀ閉指令
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S23_計測結果判定 / Measurement result determination
LD M1623                 ; S23_起動確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND B0A10                ; 画像検査(結果取得可能)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
MPS
AND B0A20                ; 画像検査(総合判定)
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
MPP
ANB B0A20                ; 画像検査(総合判定)
MOV K28 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S24_判定ＯＫ--圧入上昇 / Judgment OK--Press-fit rise
LD M1624                 ; S24_圧入上昇指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1625                 ; S25_起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;__S26_ＯＫワーク取出確認(終了処理) / OK work take-out confirmation (end processing)
LD M1626                 ; S26_判定OK
OR M1684                 ; 判定OKﾗﾝﾌﾟ表示
ANB M1679                ; S79_STEPEND
AND M2389                ; ﾕﾆｯﾄ1起動中
OUT M1684                ; 判定OKﾗﾝﾌﾟ表示
LD M1626                 ; S26_判定OK
LDB X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
ORB X01                  ; ｾｯﾄ部ﾜｰｸ有確認
ANL
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1627                 ; S27_OK取出開始確認(遮光)
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
ANB X01                  ; ｾｯﾄ部ﾜｰｸ有確認
MOV K79 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>__S28_画像判定ＮＧ処理 /  determination NG processing
;  ＮＧオペコール、ＰＢ確認 /  opcall, PB confirmation
LD M2389                 ; ﾕﾆｯﾄ1起動中
MPS
LDP M1628                ; S28_画像NG
OR M1682                 ; 画像検査NGﾗﾝﾌﾟ表示
ANB M1635                ; S35_NGｽｲｯﾁ確認(終了)
ANL
OUT M1682                ; 画像検査NGﾗﾝﾌﾟ表示
MRD
LDP M1628                ; S28_画像NG
OR M1683                 ; 画像検査NGﾌﾞｻﾞｰ
ANB M1635                ; S35_NGｽｲｯﾁ確認(終了)
ANB T121                 ; 画像NGﾘｾｯﾄ復帰処理開始
ANL
OUT M1683                ; 画像検査NGﾌﾞｻﾞｰ
MPP
LD M1682                 ; 画像検査NGﾗﾝﾌﾟ表示
LD CR2005                ; 100msｸﾛｯｸﾊﾟﾙｽ
ORB M1628                ; S28_画像NG
ANL
ANB M999                 ; 暫定修正　小池
ANL
OUT M1686                ; 画像検査NGﾗﾝﾌﾟON
LD M1628                 ; S28_画像NG
MPS
AND X1565                ; NGﾘｾｯﾄPB
MPS
ANB X00                  ; NGﾜｰｸ確認
TMR K121 K10             ; 画像NGﾘｾｯﾄ復帰処理開始
MPP
AND X00                  ; NGﾜｰｸ確認
OUT M1637                ; S37_画像NGBOX排出要求
MPP
AND T121                 ; 画像NGﾘｾｯﾄ復帰処理開始
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
;__S29_判定ＮＧ圧入上昇 / Determination NG press-fit increase
LD M1629                 ; S29_圧入上昇指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1630                 ; S30_起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
MOV K32 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;__S32_ＮＧワーク取出確認(終了処理) / NG work take-out confirmation (end processing)
;  画像､圧入ＮＧ兼用処理 /  press-fit NG combined processing
LD M1632                 ; S32_NG取出開始待ち
LDB X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
ORB X01                  ; ｾｯﾄ部ﾜｰｸ有確認
ANL
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1633                 ; S33_NG取出開始確認(遮光)
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
ANB X01                  ; ｾｯﾄ部ﾜｰｸ有確認
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1634                 ; S34_NG取出完了確認(受光)
AND X00                  ; NGﾜｰｸ確認
INC D1000                ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
LD M1635                 ; S35_NGｽｲｯﾁ確認(終了)
ANB M1680                ; 圧入NGﾗﾝﾌﾟ表示
ANB M1682                ; 画像検査NGﾗﾝﾌﾟ表示
MOV K79 D1000            ; ﾕﾆｯﾄ1自動運転ｽﾃｯﾌﾟ
;<h1/>□_OUTPUT
LDB M999                 ; 暫定修正　小池
AND M1632                ; S32_NG取出開始待ち
OR M1633                 ; S33_NG取出開始確認(遮光)
OR M1634                 ; S34_NG取出完了確認(受光)
OR M1636                 ; S36_圧入NGBOX排出要求
OR M1637                 ; S37_画像NGBOX排出要求
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
LD X00                   ; NGﾜｰｸ確認
ANB M1636                ; S36_圧入NGBOX排出要求
ANB M1637                ; S37_画像NGBOX排出要求
ORL
OUT Y50                  ; NGﾜｰｸ確認　ランプ
;<h1/>□_原点復帰回路 / ORIGIN
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
FMOV K0 M2000 K5         ; Ori00_ﾕﾆｯﾄ1原点復帰開始
LDB M2388                ; ﾕﾆｯﾄ1原点復帰中
MOV K0 D1010             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2388                 ; ﾕﾆｯﾄ1原点復帰中
MOV D1010 Z1             ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
SET M2000:Z1             ; Ori00_ﾕﾆｯﾄ1原点復帰開始
;原点復帰ステップ開始
LD M2000                 ; Ori00_ﾕﾆｯﾄ1原点復帰開始
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
@MOV K1 D1034            ; 画像ｼｬｯﾀｰ  RB_POS確定
LD M2001                 ; Ori01_ｼｬｯﾀｰ開閉 RB_原点復帰指令
LD M2346                 ; 画像ｼｬｯﾀｰ RB_原点完了
OR M2350                 ; 画像ｼｬｯﾀｰ RB_原点復帰完了
ANL
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2002                 ; Ori02_起動確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2003                 ; Ori03_位置決完了確認
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
MPP
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2004                 ; Ori04_ﾌﾟﾗｸﾞ圧入 RB_原点復帰指令
LD M2336                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
OR M2340                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
ANL
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2005                 ; Ori05_起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
LD M2006                 ; Ori06_位置決完了確認
AND M2384                ; ﾕﾆｯﾄ1原点
INC D1010                ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M2007                 ; Ori07_ﾕﾆｯﾄ1原点確認
ANB M98                  ; 原点復帰開始
MOV K79 D1010            ; ﾕﾆｯﾄ1原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路 / MANUAL
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
ANB M2389                ; ﾕﾆｯﾄ1起動中
ANB M2388                ; ﾕﾆｯﾄ1原点復帰中
MC
;<h1/>__圧入ﾛﾎﾞｯﾄ下降 / Press-fit robot descends
LD= D2021 K1             ; 手動操作設定No
AND X108A                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT M2080                ; ﾍﾞﾝﾄ圧入   RB_下降可
LDP X1564                ; ｽﾀｰﾄPB
AND M2080                ; ﾍﾞﾝﾄ圧入   RB_下降可
OR M2081                 ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2084                ; 位置決完了確認
MPS
OUT M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
MPP
ANB M2083                ; 起動確認
OUT M2082                ; ﾍﾞﾝﾄ圧入   RB_下降指令
@MOV K2 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2082                 ; ﾍﾞﾝﾄ圧入   RB_下降指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
OR M2083                 ; 起動確認
AND M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
OUT M2083                ; 起動確認
LD M2083                 ; 起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
OR M2084                 ; 位置決完了確認
AND M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
OUT M2084                ; 位置決完了確認
;<h1/>__圧入ﾛﾎﾞｯﾄ圧入下降 / Press-fit robot press-fit down
LD= D2021 K2             ; 手動操作設定No
AND= D1028 K2            ; ﾌﾟﾗｸﾞ圧入 RB_完了POS
AND X108A                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT M2085                ; ﾍﾞﾝﾄ圧入   RB_圧入可
LDP X1564                ; ｽﾀｰﾄPB
AND M2085                ; ﾍﾞﾝﾄ圧入   RB_圧入可
OR M2086                 ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2089                ; 位置決完了確認
MPS
OUT M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
MPP
ANB M2088                ; 起動確認
OUT M2087                ; ﾍﾞﾝﾄ圧入   RB_圧入指令
@CAL+ D2020 K10 D1024    ; 動作設定No (自動ﾓｰﾄﾞで確定)  ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2087                 ; ﾍﾞﾝﾄ圧入   RB_圧入指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
OR M2088                 ; 起動確認
AND M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
OUT M2088                ; 起動確認
LD M2088                 ; 起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
OR M2089                 ; 位置決完了確認
AND M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
OUT M2089                ; 位置決完了確認
;<h1/>__圧入ﾛﾎﾞｯﾄ上昇 / Press-fit robot rise
LD= D2021 K3             ; 手動操作設定No
AND X108A                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
ANB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT M2090                ; ﾍﾞﾝﾄ圧入   RB_上昇可
LDP X1564                ; ｽﾀｰﾄPB
AND M2090                ; ﾍﾞﾝﾄ圧入   RB_上昇可
OR M2091                 ; ﾍﾞﾝﾄ圧入   RB_上昇
ANB M2094                ; 位置決完了確認
MPS
OUT M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
MPP
ANB M2093                ; 起動確認
OUT M2092                ; ﾍﾞﾝﾄ圧入   RB_上昇指令
@MOV K1 D1024            ; ﾌﾟﾗｸﾞ圧入  RB_POS確定
LD M2092                 ; ﾍﾞﾝﾄ圧入   RB_上昇指令
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
OR M2093                 ; 起動確認
AND M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
OUT M2093                ; 起動確認
LD M2093                 ; 起動確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
OR M2094                 ; 位置決完了確認
AND M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
OUT M2094                ; 位置決完了確認
;<h1/>__ｼｬｯﾀｰ開閉ﾛﾎﾞｯﾄ開 / Shutter opening and closing robot open 
LD= D2021 K4             ; 手動操作設定No
AND X109A                ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
ANB M2102                ; ｼｬｯﾀｰ開閉  RB_閉
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT M2096                ; ｼｬｯﾀｰ開閉  RB_開可
LDP X1564                ; ｽﾀｰﾄPB
AND M2096                ; ｼｬｯﾀｰ開閉  RB_開可
OR M2097                 ; ｼｬｯﾀｰ開閉  RB_閉
ANB M2100                ; 位置決完了確認
MPS
OUT M2097                ; ｼｬｯﾀｰ開閉  RB_閉
MPP
ANB M2099                ; 起動確認
OUT M2098                ; ｼｬｯﾀｰ開閉  RB_閉指令
@MOV K3 D1034            ; 画像ｼｬｯﾀｰ  RB_POS確定
LD M2098                 ; ｼｬｯﾀｰ開閉  RB_閉指令
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
OR M2099                 ; 起動確認
AND M2097                ; ｼｬｯﾀｰ開閉  RB_閉
OUT M2099                ; 起動確認
LD M2099                 ; 起動確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
OR M2100                 ; 位置決完了確認
AND M2097                ; ｼｬｯﾀｰ開閉  RB_閉
OUT M2100                ; 位置決完了確認
;<h1/>__ｼｬｯﾀｰ開閉ﾛﾎﾞｯﾄ閉 / Shutter opening and closing robot CLOSE
LD= D2021 K5             ; 手動操作設定No
AND X109A                ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
ANB M2097                ; ｼｬｯﾀｰ開閉  RB_閉
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT M2101                ; ｼｬｯﾀｰ開閉  RB_閉可
LDP X1564                ; ｽﾀｰﾄPB
AND M2101                ; ｼｬｯﾀｰ開閉  RB_閉可
OR M2102                 ; ｼｬｯﾀｰ開閉  RB_閉
ANB M2105                ; 位置決完了確認
MPS
OUT M2102                ; ｼｬｯﾀｰ開閉  RB_閉
MPP
ANB M2104                ; 起動確認
OUT M2103                ; ｼｬｯﾀｰ開閉  RB_閉指令
@MOV K1 D1034            ; 画像ｼｬｯﾀｰ  RB_POS確定
LD M2103                 ; ｼｬｯﾀｰ開閉  RB_閉指令
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
OR M2104                 ; 起動確認
AND M2102                ; ｼｬｯﾀｰ開閉  RB_閉
OUT M2104                ; 起動確認
LD M2104                 ; 起動確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
OR M2105                 ; 位置決完了確認
AND M2102                ; ｼｬｯﾀｰ開閉  RB_閉
OUT M2105                ; 位置決完了確認
;<h1/>__画像計測
LD= D2021 K6             ; 手動操作設定No
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
OUT M2111                ; 画像計測可
LDP X1564                ; ｽﾀｰﾄPB
AND M2111                ; 画像計測可
OR M2112                 ; 画像計測開始
ANB M2120                ; 画像計測終了確認
OUT M2112                ; 画像計測開始
;画像計測プログラム番号切替
LDP M2112                ; 画像計測開始
OR M2113                 ; 画像計測PRO_No切替指令
AND M2112                ; 画像計測開始
ANB M2114                ; 画像計測PRO_No切替確認
MOV D2020 W01C0          ; 動作設定No (自動ﾓｰﾄﾞで確定)  IV Series[1]ﾌﾟﾛｸﾞﾗﾑ番号
OUT M2113                ; 画像計測PRO_No切替指令
LD M2113                 ; 画像計測PRO_No切替指令
AND B0A02                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替応答)
OR M2114                 ; 画像計測PRO_No切替確認
AND M2112                ; 画像計測開始
ANB M2115                ; 画像計測PRO_No確認
OUT M2114                ; 画像計測PRO_No切替確認
LD M2114                 ; 画像計測PRO_No切替確認
AND= D2020 W0104         ; 動作設定No (自動ﾓｰﾄﾞで確定)  IV Series[1]現在ﾌﾟﾛｸﾞﾗﾑ番号
OR M2115                 ; 画像計測PRO_No確認
AND M2112                ; 画像計測開始
ANB M2116                ; 画像計測ﾊﾞｯﾌｧｸﾘｱ指令
OUT M2115                ; 画像計測PRO_No確認
;画像計測ﾊﾞｯﾌｧｸﾘｱ
LD M2115                 ; 画像計測PRO_No確認
ANB B0A02                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替応答)
OR M2116                 ; 画像計測ﾊﾞｯﾌｧｸﾘｱ指令
AND M2112                ; 画像計測開始
ANB M2117                ; 画像計測ﾊﾞｯﾌｧｸﾘｱ確認
OUT M2116                ; 画像計測ﾊﾞｯﾌｧｸﾘｱ指令
LD M2116                 ; 画像計測ﾊﾞｯﾌｧｸﾘｱ指令
ANB B0A10                ; 画像検査(結果取得可能)
ANB B0A20                ; 画像検査(総合判定)
OR M2117                 ; 画像計測ﾊﾞｯﾌｧｸﾘｱ確認
AND M2112                ; 画像計測開始
ANB M2118                ; 画像計測指令
OUT M2117                ; 画像計測ﾊﾞｯﾌｧｸﾘｱ確認
;画像計測
LD M2117                 ; 画像計測ﾊﾞｯﾌｧｸﾘｱ確認
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
OR M2118                 ; 画像計測指令
AND M2112                ; 画像計測開始
ANB M2119                ; 画像計測開始確認
OUT M2118                ; 画像計測指令
LD M2118                 ; 画像計測指令
AND B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
OR M2119                 ; 画像計測開始確認
AND M2112                ; 画像計測開始
ANB M2120                ; 画像計測終了確認
OUT M2119                ; 画像計測開始確認
LD M2119                 ; 画像計測開始確認
ANB B0A12                ; 画像検査(ﾋﾞｼﾞｰ)
OR M2120                 ; 画像計測終了確認
AND M2112                ; 画像計測開始
OUT M2120                ; 画像計測終了確認
LDB M2081                ; ﾍﾞﾝﾄ圧入   RB_下降
ANB M2086                ; ﾍﾞﾝﾄ圧入   RB_圧入
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
ANB M2097                ; ｼｬｯﾀｰ開閉  RB_閉
ANB M2102                ; ｼｬｯﾀｰ開閉  RB_閉
OUT M2130                ; 手動操作中
MCR
;<h1/>□_出力回路 / OUTPUT
;<h1/>__プラグ圧入ロボット原点動作 /  press-fit robot origin operation
LDB M2004                ; Ori04_ﾌﾟﾗｸﾞ圧入 RB_原点復帰指令
INV
MPS
LD X108A                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
OR M2336                 ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANB M2337                ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
ANL
OUT M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
MPP
LDB X108A                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
OR M2337                 ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
ANB M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANL
OUT M2337                ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
LD M2337                 ; ﾌﾟﾗｸﾞ圧入 RB_原点未完了
MPS
ANB M2339                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
OUT M2338                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
MRD
LD M2338                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
AND X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
OR M2339                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
ANL
OUT M2339                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
MPP
LD M2339                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始確認
ANB X1088                ; ﾌﾟﾗｸﾞ圧入 RB_ﾋﾞｼﾞｰ　(BUSY)
AND X108A                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了(SETON)
AND X108B                ; ﾌﾟﾗｸﾞ圧入 RB_位置決完(INP)
OR M2340                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
ANL
OUT M2340                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
;<h1/>__シャッター開閉ロボット原点動作 / Shutter opening and closing robot origin operation
LDB M2001                ; Ori01_ｼｬｯﾀｰ開閉 RB_原点復帰指令
INV
MPS
LD X109A                 ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
OR M2346                 ; 画像ｼｬｯﾀｰ RB_原点完了
ANB M2347                ; 画像ｼｬｯﾀｰ RB_原点未完了
ANL
OUT M2346                ; 画像ｼｬｯﾀｰ RB_原点完了
MPP
LDB X109A                ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
OR M2347                 ; 画像ｼｬｯﾀｰ RB_原点未完了
ANB M2346                ; 画像ｼｬｯﾀｰ RB_原点完了
ANL
OUT M2347                ; 画像ｼｬｯﾀｰ RB_原点未完了
LD M2347                 ; 画像ｼｬｯﾀｰ RB_原点未完了
MPS
ANB M2349                ; 画像ｼｬｯﾀｰ RB_原点復帰開始確認
OUT M2348                ; 画像ｼｬｯﾀｰ RB_原点復帰開始
MRD
LD M2348                 ; 画像ｼｬｯﾀｰ RB_原点復帰開始
AND X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
OR M2349                 ; 画像ｼｬｯﾀｰ RB_原点復帰開始確認
ANL
OUT M2349                ; 画像ｼｬｯﾀｰ RB_原点復帰開始確認
MPP
LD M2349                 ; 画像ｼｬｯﾀｰ RB_原点復帰開始確認
ANB X1098                ; 画像ｼｬｯﾀｰ RB_ﾋﾞｼﾞｰ(BUSY)
AND X109A                ; 画像ｼｬｯﾀｰ RB_原点完了(SETON)
AND X109B                ; 画像ｼｬｯﾀｰ RB_位置決完(INP)
OR M2350                 ; 画像ｼｬｯﾀｰ RB_原点復帰完了
ANL
OUT M2350                ; 画像ｼｬｯﾀｰ RB_原点復帰完了
;<h1/>__プラグ圧入ロボット動作出力 /  press-fit robot operation output
LDB M1601                ; S01_圧入開始位置下降指令
ANB M1603                ; S03_圧入下降指令
ANB M1606                ; S06_圧入上昇指令
ANB M1624                ; S24_圧入上昇指令
ANB M1629                ; S29_圧入上昇指令
INV
LDB M2336                ; ﾌﾟﾗｸﾞ圧入 RB_原点完了
ANB M2340                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰完了
INV
ORL
LDB M2082                ; ﾍﾞﾝﾄ圧入   RB_下降指令
ANB M2087                ; ﾍﾞﾝﾄ圧入   RB_圧入指令
ANB M2091                ; ﾍﾞﾝﾄ圧入   RB_上昇
INV
ORL
@MOV D1024 D1026         ; ﾌﾟﾗｸﾞ圧入  RB_POS確定  ﾌﾟﾗｸﾞ圧入 RB_指令POS
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
TMR K190 K2              ; ﾌﾟﾗｸﾞ圧入 RB_POS確定ﾃﾞｨﾚｲ
MPP
AND T190                 ; ﾌﾟﾗｸﾞ圧入 RB_POS確定ﾃﾞｨﾚｲ
OUT Y208A                ; ﾌﾟﾗｸﾞ圧入 RB_ｽﾀｰﾄ(DRIVE)
LD CR2002                ; 常時ON
MPS
AND D1026.0              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS1
OUT Y2080                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS1
MRD
AND D1026.1              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS2
OUT Y2081                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS2
MRD
AND D1026.2              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS4
OUT Y2082                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS4
MRD
AND D1026.3              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS8
OUT Y2083                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS8
MRD
AND D1026.4              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS16
OUT Y2084                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS16
MPP
AND D1026.5              ; ﾌﾟﾗｸﾞ圧入 RB_指令POS32
OUT Y2085                ; ﾌﾟﾗｸﾞ圧入 RB_指令POS32
LDB X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT Y2088                ; ﾌﾟﾗｸﾞ圧入 RB_一時停止(HOLD)
LD M64                   ; 運転準備
OUT Y2089                ; ﾌﾟﾗｸﾞ圧入 RB_ｻｰﾎﾞON(SVON)
LD M16                   ; ﾘｾｯﾄPB
AND X108F                ; ﾌﾟﾗｸﾞ圧入 RB_ｱﾗｰﾑ
OUT Y208B                ; ﾌﾟﾗｸﾞ圧入 RB_ﾘｾｯﾄ(RESET)
LD M2338                 ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰開始
OUT Y208C                ; ﾌﾟﾗｸﾞ圧入 RB_原点復帰(SETON)
;<h1/>__シャッター開閉ロボット動作出力 / Shutter opening and closing robot operation output
LDB M1611                ; S11_ｼｬｯﾀ開指令
ANB M1622                ; S22_ｼｬｯﾀ閉指令
INV
LDB M2346                ; 画像ｼｬｯﾀｰ RB_原点完了
ANB M2350                ; 画像ｼｬｯﾀｰ RB_原点復帰完了
INV
ORL
LDB M2098                ; ｼｬｯﾀｰ開閉  RB_閉指令
ANB M2103                ; ｼｬｯﾀｰ開閉  RB_閉指令
INV
ORL
@MOV D1034 D1036         ; 画像ｼｬｯﾀｰ  RB_POS確定  画像ｼｬｯﾀｰ RB_指令POS
MPS
AND X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
TMR K191 K2              ; ｼｬｯﾀｰ開閉 RB_POS確定ﾃﾞｨﾚｲ
MPP
AND T191                 ; ｼｬｯﾀｰ開閉 RB_POS確定ﾃﾞｨﾚｲ
OUT Y209A                ; 画像ｼｬｯﾀｰ RB_ｽﾀｰﾄ(DRIVE)
LD CR2002                ; 常時ON
MPS
AND D1036.0              ; ｼｬｯﾀｰ開閉 RB_指令POS1
OUT Y2090                ; 画像ｼｬｯﾀ- RB_指令POS1
MRD
AND D1036.1              ; ｼｬｯﾀｰ開閉 RB_指令POS2
OUT Y2091                ; 画像ｼｬｯﾀｰ RB_指令POS2
MRD
AND D1036.2              ; ｼｬｯﾀｰ開閉 RB_指令POS4
OUT Y2092                ; 画像ｼｬｯﾀｰ RB_指令POS4
MRD
AND D1036.3              ; ｼｬｯﾀｰ開閉 RB_指令POS8
OUT Y2093                ; 画像ｼｬｯﾀｰ RB_指令POS8
MRD
AND D1036.4              ; ｼｬｯﾀｰ開閉 RB_指令POS16
OUT Y2094                ; 画像ｼｬｯﾀｰ RB_指令POS16
MPP
AND D1036.5              ; ｼｬｯﾀｰ開閉 RB_指令POS32
OUT Y2095                ; 画像ｼｬｯﾀｰ RB_指令POS32
LDB X1561                ; ﾗｲﾄｶｰﾃﾝOUT1(遮光)
OUT Y2098                ; 画像ｼｬｯﾀｰ RB_一時停止(HOLD)
LD M64                   ; 運転準備
OUT Y2099                ; 画像ｼｬｯﾀｰ RB_ｻｰﾎﾞON(SVON)
LD M16                   ; ﾘｾｯﾄPB
AND X109F                ; 画像ｼｬｯﾀｰ RB_ｱﾗｰﾑ
OUT Y209B                ; 画像ｼｬｯﾀｰ RB_ﾘｾｯﾄ(RESET)
LD M2348                 ; 画像ｼｬｯﾀｰ RB_原点復帰開始
OUT Y209C                ; 画像ｼｬｯﾀｰ RB_原点復帰(SETON)
;<h1/>__画像計測出力 /  measurement output
LDB M1619                ; S19_画像計測　指令
ANB M2118                ; 画像計測指令
INV
OUT B0A40                ; 画像検査(ﾄﾘｶﾞ要求)
LD CR2003                ; 常時OFF
OUT B0A41                ; 画像検査(ﾏｽﾀｰ登録要求)
LDB M1614                ; S14_画像計測PRO_No切替指令
ANB M2113                ; 画像計測PRO_No切替指令
INV
OUT B0A42                ; 画像検査(ﾌﾟﾛｸﾞﾗﾑ切替要求)
LD M123                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OUT B0A43                ; 画像検査(ﾜｰﾆﾝｸﾞｸﾘｱ要求)
LD CR2003                ; 常時OFF
OUT B0A44                ; 画像検査(統計ﾘｾｯﾄ要求)
LDB M1617                ; S17_画像計測ﾊﾞｯﾌｧｸﾘｱ
ANB M2116                ; 画像計測ﾊﾞｯﾌｧｸﾘｱ指令
INV
OUT B0A45                ; 画像検査(ﾊﾞｯﾌｧｸﾘｱ要求)
;<h1/>□_パネル表示 / PANEL DISPLAY
LD M1684                 ; 判定OKﾗﾝﾌﾟ表示
LD M2391                 ; ｽﾀｰﾄ要求
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ANB M999                 ; 暫定修正　小池
ORL
LDB M63                  ; 運転準備完了
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB M999                 ; 暫定修正　小池
ORL
LD CR2005                ; 100msｸﾛｯｸﾊﾟﾙｽ
AND M201                 ; START SW PASS MODE中
AND X1563                ; 自動/手動SW
ORL
OUT Y2571                ; ｽﾀｰﾄ/OKﾗﾝﾌﾟ
LD M1685                 ; 圧入NGﾗﾝﾌﾟON
OR M1686                 ; 画像検査NGﾗﾝﾌﾟON
LDB M63                  ; 運転準備完了
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB M999                 ; 暫定修正　小池
ORL
OUT Y2572                ; NG_ﾗﾝﾌﾟ
;□_ﾕｰｻﾞｰｱﾗｰﾑ表示 / User alarm display
LD B020                  ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
@SET CR3310              ; PLUG-RB-ERR
LDB B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
@RES CR3310              ; PLUG-RB-ERR
LD B021                  ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
@SET CR3311              ; SHUTT-RB-ERR
LDB B021                 ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
@RES CR3311              ; SHUTT-RB-ERR
LD B022                  ; 画像検査  機器_異常
@SET CR3312              ; CAMERA-ERR
LDB B022                 ; 画像検査  機器_異常
@RES CR3312              ; CAMERA-ERR
;<h1/>□_異常検出 / ALARM
LDP X108F                ; ﾌﾟﾗｸﾞ圧入 RB_ｱﾗｰﾑ
AND M64                  ; 運転準備
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0520                ; ﾌﾟﾗｸﾞ圧入  RB_異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
ORL
OUT B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0520                 ; ﾌﾟﾗｸﾞ圧入  RB_異常BZ停止
AND B020                 ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
OUT B0520                ; ﾌﾟﾗｸﾞ圧入  RB_異常BZ停止
LDP X109F                ; 画像ｼｬｯﾀｰ RB_ｱﾗｰﾑ
AND M64                  ; 運転準備
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0521                ; 画像ｼｬｯﾀｰ  RB_異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B021                 ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
ORL
OUT B021                 ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0521                 ; 画像ｼｬｯﾀｰ  RB_異常BZ停止
AND B021                 ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
OUT B0521                ; 画像ｼｬｯﾀｰ  RB_異常BZ停止
LD B0A1F                 ; 画像検査(ｴﾗｰ)
AND M64                  ; 運転準備
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0522                ; 画像検査  機器_異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B022                 ; 画像検査  機器_異常
ORL
OUT B022                 ; 画像検査  機器_異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0522                 ; 画像検査  機器_異常BZ停止
AND B022                 ; 画像検査  機器_異常
OUT B0522                ; 画像検査  機器_異常BZ停止
LD CR2003                ; 常時OFF
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0523                ; ﾕﾆｯﾄ1_異常4BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B023                 ; ﾕﾆｯﾄ1_異常4
ORL
OUT B023                 ; ﾕﾆｯﾄ1_異常4
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0523                 ; ﾕﾆｯﾄ1_異常4BZ停止
AND B023                 ; ﾕﾆｯﾄ1_異常4
OUT B0523                ; ﾕﾆｯﾄ1_異常4BZ停止
LD CR2003                ; 常時OFF
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0524                ; ﾕﾆｯﾄ1_異常5BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B024                 ; ﾕﾆｯﾄ1_異常5
ORL
OUT B024                 ; ﾕﾆｯﾄ1_異常5
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0524                 ; ﾕﾆｯﾄ1_異常5BZ停止
AND B024                 ; ﾕﾆｯﾄ1_異常5
OUT B0524                ; ﾕﾆｯﾄ1_異常5BZ停止
LD CR2003                ; 常時OFF
LD M998                  ; ﾌﾞｻﾞｰ停止のみｷｬﾝｾﾙ
ANB B0525                ; ﾕﾆｯﾄ1_異常6BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B025                 ; ﾕﾆｯﾄ1_異常6
ORL
OUT B025                 ; ﾕﾆｯﾄ1_異常6
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0525                 ; ﾕﾆｯﾄ1_異常6BZ停止
AND B025                 ; ﾕﾆｯﾄ1_異常6
OUT B0525                ; ﾕﾆｯﾄ1_異常6BZ停止
;__異常ﾌﾞｻﾞｰまとめ / BUZZER ON
LD B020                  ; ﾌﾟﾗｸﾞ圧入 ﾛﾎﾞｯﾄ_異常
ANB B0520                ; ﾌﾟﾗｸﾞ圧入  RB_異常BZ停止
LD B021                  ; 画像ｼｬｯﾀｰ ﾛﾎﾞｯﾄ_異常
ANB B0521                ; 画像ｼｬｯﾀｰ  RB_異常BZ停止
ORL
LD B022                  ; 画像検査  機器_異常
ANB B0522                ; 画像検査  機器_異常BZ停止
ORL
LD B023                  ; ﾕﾆｯﾄ1_異常4
ANB B0523                ; ﾕﾆｯﾄ1_異常4BZ停止
ORL
LD B024                  ; ﾕﾆｯﾄ1_異常5
ANB B0524                ; ﾕﾆｯﾄ1_異常5BZ停止
ORL
LD B025                  ; ﾕﾆｯﾄ1_異常6
ANB B0525                ; ﾕﾆｯﾄ1_異常6BZ停止
ORL
OUT M2393                ; ﾕﾆｯﾄ1異常BZ補助
END
ENDH
