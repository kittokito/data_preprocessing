DEVICE:128
;MODULE:ﾒｲﾝ
;MODULE_TYPE:0
;<h1/>ｽｲｯﾁ類入力受け
LD X100                  ; 起動
OUT M100                 ; 起動PB
LD X101                  ; 原点復帰
OUT M101                 ; 原点復帰PB
LD X102                  ; 異常/ﾘｾｯﾄ
OUT M102                 ; ﾌﾞｻﾞｰ停止PB
OUT M103                 ; 異常ﾘｾｯﾄPB
;<h1/>品種切替
LD X110                  ; ｻﾑﾛｰﾀﾘ1入力1
OUT D20.0
LD X111                  ; ｻﾑﾛｰﾀﾘ1入力2
OUT D20.1
LD X112                  ; ｻﾑﾛｰﾀﾘ1入力3
OUT D20.2
LD X113                  ; ｻﾑﾛｰﾀﾘ1入力4
OUT D20.3
LD X114                  ; ｻﾑﾛｰﾀﾘ2入力1
OUT D22.0
LD X115                  ; ｻﾑﾛｰﾀﾘ2入力2
OUT D22.1
LD X116                  ; ｻﾑﾛｰﾀﾘ2入力3
OUT D22.2
LD X117                  ; ｻﾑﾛｰﾀﾘ2入力4
OUT D22.3
LD X118                  ; ｻﾑﾛｰﾀﾘ3入力1
OUT D23.0
LD X119                  ; ｻﾑﾛｰﾀﾘ3入力2
OUT D23.1
LD X11A                  ; ｻﾑﾛｰﾀﾘ3入力3
OUT D23.2
LD X11B                  ; ｻﾑﾛｰﾀﾘ3入力4
OUT D23.3
LD X11C                  ; ｻﾑﾛｰﾀﾘ4入力1
OUT D24.0
LD X11D                  ; ｻﾑﾛｰﾀﾘ4入力2
OUT D24.1
LD X11E                  ; ｻﾑﾛｰﾀﾘ4入力3
OUT D24.2
LD X11F                  ; ｻﾑﾛｰﾀﾘ4入力4
OUT D24.3
LD CR2002                ; 常時ON
CAL* D22 K10 D25         ; 選択品種番号(変換前二桁目)  選択品種番号(二桁目)
CAL* D23 K100 D27        ; 選択品種番号(変換前三桁目)  選択品種番号(三桁目)
CAL* D24 K1000 D29       ; 選択品種番号(変換前四桁目)  選択品種番号(四桁目)
CAL+.D D25 D27 D31       ; 選択品種番号(二桁目)  選択品種番号(三桁目)  選択品種番号(32bit)
CAL+.D D31 D29 D31       ; 選択品種番号(32bit)  選択品種番号(四桁目)  選択品種番号(32bit)
MPS
LDA.D D31                ; 選択品種番号(32bit)
CON
STA D33                  ; 選択品種番号(16bit)
MPP
CAL+ D33 D20 D10         ; 選択品種番号(16bit)  選択品種番号(一桁目)  選択品種番号
;<h1/>PLC正常確認
LD CR3905                ; 重度ｴﾗｰ発生中
OR CR3906                ; 軽度ｴﾗｰ発生中
INV
OUT M0                   ; PLC正常
LDB M0                   ; PLC正常
OUT M200                 ; PLC異常
;異常ﾘｾｯﾄ
LDB M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND M200                 ; PLC異常
OUT CR3909               ; 現在のｴﾗｰｸﾘｱ
;<h1/>異常まとめ
LDB M200                 ; PLC異常
ANB M4500                ; ｼﾘﾝﾀﾞｰｾﾝｻ異常
ANB M5500                ; RB1異常/警告
ANB M5501                ; RB1_位置決め異常
INV
OUT M3000                ; 異常発生中
;<h1/>自動/手動
LD X10A                  ; 自動ﾓｰﾄﾞ選択
ANB X10B                 ; 教示(ﾃｨｰﾁﾝｸﾞ)ﾓｰﾄﾞ選択
OUT M500                 ; 自動ﾓｰﾄﾞ
LDB X10A                 ; 自動ﾓｰﾄﾞ選択
AND X10B                 ; 教示(ﾃｨｰﾁﾝｸﾞ)ﾓｰﾄﾞ選択
OUT M501                 ; 手動(ﾃｨｰﾁﾝｸﾞ)ﾓｰﾄﾞ
;<h1/>安全回路系
LDB X108                 ; 運転準備OFF
AND X10C                 ; 安全回路正常
TMR K150 K5              ; 運転準備入
LD T150                  ; 運転準備入
OUT M1000                ; 運転準備入
;<h1/>原点
LD M1000                 ; 運転準備入
AND T0                   ; ｼﾘﾝﾀﾞｰ上昇端
AND M5050                ; RB1_待機位置
OUT M1010                ; 原点
;<h1/>原点復帰
LDP M101                 ; 原点復帰PB
ANB M1010                ; 原点
ANB M2000                ; 原点復帰中
ANB M5001                ; RB1_移動中
OR M2000                 ; 原点復帰中
AND T2                   ; ﾄﾞｱﾛｯｸ下降端
ANB M5202                ; RB1_手動ﾓｰﾄﾞ
AND M1000                ; 運転準備入
ANB M4001                ; 自動運転中
ANB M2010                ; 原点復帰完了
AND M5100                ; RB1_起動可
MC
OUT M2000                ; 原点復帰中
LD M2000                 ; 原点復帰中
ANB M2002                ; 原点復帰処理1_完了
OUT M2001                ; 原点復帰出力_ｼﾘﾝﾀﾞｰ上昇
LD M2000                 ; 原点復帰中
AND T0                   ; ｼﾘﾝﾀﾞｰ上昇端
OR M2002                 ; 原点復帰処理1_完了
OUT M2002                ; 原点復帰処理1_完了
LD M2002                 ; 原点復帰処理1_完了
ANB M2004                ; 原点復帰処理2_完了
OUT M2003                ; 原点復帰出力_RB1_待機位置移動
LD M2002                 ; 原点復帰処理1_完了
AND M5050                ; RB1_待機位置
OR M2004                 ; 原点復帰処理2_完了
OUT M2004                ; 原点復帰処理2_完了
LD M2004                 ; 原点復帰処理2_完了
OUT M2010                ; 原点復帰完了
MCR
;<h1/>ｽｲｯﾁ類出力
;ﾌﾞｻﾞｰ補助回路
LDP M102                 ; ﾌﾞｻﾞｰ停止PB
OR M300                  ; ﾌﾞｻﾞｰ停止
AND M3000                ; 異常発生中
OUT M300                 ; ﾌﾞｻﾞｰ停止
TMR K20 K3               ; ﾌﾞｻﾞｰ停止後
LD M103                  ; 異常ﾘｾｯﾄPB
AND M300                 ; ﾌﾞｻﾞｰ停止
AND T20                  ; ﾌﾞｻﾞｰ停止後
OUT M110                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LD M4000                 ; 自動起動可
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M4001                 ; 自動運転中
OUT Y150                 ; 起動ﾗﾝﾌﾟ
LD M2000                 ; 原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LD M1010                 ; 原点
ANB M2000                ; 原点復帰中
ORL
OUT Y151                 ; 原点復帰ﾗﾝﾌﾟ
LD M3000                 ; 異常発生中
OUT Y152                 ; 異常/ﾘｾｯﾄﾗﾝﾌﾟ
LD M3000                 ; 異常発生中
ANB M300                 ; ﾌﾞｻﾞｰ停止
OUT Y153                 ; ﾌﾞｻﾞｰ
END
ENDH
