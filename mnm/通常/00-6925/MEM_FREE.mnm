DEVICE:52
;MODULE:MEM_FREE
;MODULE_TYPE:1
;SCRIPT_TYPE:
;★★★　MEMFREE メモリーカードの空き容量を確認する　★★★
;■ 引数説明
;  P0		: ﾒﾓﾘｰｶｰﾄﾞの指定空き容量				[DMxxx,EMxxx等]
;  P1		: ﾒﾓﾘｰｶｰﾄﾞの現在空き容量				[DMxxx,EMxxx等]
;  P2		: エラーフラグ
;　P3		: エラー情報							[DMxxx,EMxxx等]
;■ 機能
;　・ﾒﾓﾘｰｶｰﾄﾞの空き容量が、P0 で指定されたエリアの容量以上あるか確認する。
;  ・P1で指定されたエリアに、ﾒﾓﾘｰｶｰﾄﾞの現在の空き容量を格納する。
;　・エラーが発生した場合は、エラーフラグをセットし処理を終了する。
;　
;■ エラーフラグ(P2)をセットする条件
;  ・空き容量が、P0 での指定量より小さい場合。
;  ・ﾒﾓﾘｰｶｰﾄﾞ命令が正しく実行されなかった場合。
;
;  ・P3にエラー情報を格納する。
;  　( 1〜9  : ﾒﾓﾘｰｶｰﾄﾞ命令ｴﾗｰｺｰﾄﾞ )
;  　( 10 	: 指定の空き容量なし )
;  　  
;<<　初期化処理　>>
LD @CR2008
RES P2                   ; ｴﾗｰﾘﾚｰ
CON
DW #0 P3                 ; ｴﾗｰ情報
CON
ZRES @DM0 @DM1           ; ﾒﾓﾘｰｶｰﾄﾞ空き容量
CON
ZRES @MR000 @MR015
;
;'処理開始
;SET( @MR0 )
;
;
LDF @CR2008
NCJ #1000
;'処理開始
;
;SET( @MR0 )
;
LD CR2002                ; 常時ON
SET @MR000
LABEL #1000
;
;<<　メモリ空き容量取得　>>
;@DM0　　：ﾒﾓﾘｰｶｰﾄﾞの現在空き容量を格納するｴﾘｱ
;
;@MR10	：命令完了 通知ﾋﾞｯﾄ
;@MR11	：ｴﾗｰﾋﾞｯﾄ ON=ｴﾗｰ OFF=ｴﾗｰ無し
LD @MR000
ANB CR3214               ; ﾒﾓﾘｶｰﾄﾞ命令実行中
ANB @MR010               ; MFREE 通知ﾋﾞｯﾄ
MFREE @DM0 @MR010        ; ﾒﾓﾘｰｶｰﾄﾞ空き容量  MFREE 通知ﾋﾞｯﾄ
LD @MR000
AND @MR010               ; MFREE 通知ﾋﾞｯﾄ
TMH @0 #3
CON
AND @T0
DIFU @MR005
LD @MR005
MPS
ANB @MR011               ; MEMFREE ｴﾗｰ
MPS
AND>=.D @DM0 P0          ; ﾒﾓﾘｰｶｰﾄﾞ空き容量  空き容量
RES @MR000
CON
RES P2                   ; ｴﾗｰﾘﾚｰ
CON
MOV.D @DM0 P1            ; ﾒﾓﾘｰｶｰﾄﾞ空き容量  現在空き容量
MEND
MPP
AND<.D @DM0 P0           ; ﾒﾓﾘｰｶｰﾄﾞ空き容量  空き容量
RES @MR000
CON
SET P2                   ; ｴﾗｰﾘﾚｰ
CON
DW #10 P3                ; ｴﾗｰ情報
MEND
MPP
LD @MR011                ; MEMFREE ｴﾗｰ
AND<> CM2390 #0          ; ﾒﾓﾘｶｰﾄﾞ命令用ｴﾗｰｺｰﾄﾞ
ANL
RES @MR000
CON
SET P2                   ; ｴﾗｰﾘﾚｰ
CON
MOV CM2390 P3            ; ﾒﾓﾘｶｰﾄﾞ命令用ｴﾗｰｺｰﾄﾞ  ｴﾗｰ情報
MEND
END
ENDH
