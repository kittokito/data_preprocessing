DEVICE:53
;MODULE:MAIN
;MODULE_TYPE:0
;□□　　ダミー接点　　□□
LD CR2003                ; 常時OFF
OUT L999
LD CR2003                ; 常時OFF
OUT M0                   ; 常時OFF
LD CR2003                ; 常時OFF
OUT M990                 ; 能拡_ST3有効(ON:有効)
OUT M991                 ; 能拡_ST4有効(ON:有効)
LD CR3905                ; 重度ｴﾗｰ発生中
INV
OUT M1                   ; CPU正常
LD CR2008                ; 運転開始時1ｽｷｬﾝON
OUT M2                   ; 1ｽｷｬﾝON
LD B101F                 ; PC_OFFLINE_MODE
OUT M3                   ; OFFLINE_MODE
LD CR2002                ; 常時ON
ANB T2                   ; 0.3Secｸﾛｯｸ
TMH K1 K30               ; 0.3Secｸﾛｯｸ
LD T1                    ; 0.3Secｸﾛｯｸ
TMH K2 K30               ; 0.3Secｸﾛｯｸ
LD CR2002                ; 常時ON
ANB T5                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
TMH K4 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
LD T4                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
TMH K5 K25               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
LD CR2002                ; 常時ON
ANB T7                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
TMH K6 K40               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
LD T6                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
TMH K7 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
LD CR2002                ; 常時ON
ANB T9                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
TMR K8 K10               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
LD T8                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
TMR K9 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
LD CR2002                ; 常時ON
ANB T37                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
TMR K36 K50              ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
LD T36                   ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
TMR K37 K5               ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
LD CR2002                ; 常時ON
ANB T39                  ; ﾜｰｸ投入可音程ﾀｲﾏ1
TMH K38 K30              ; ﾜｰｸ投入可音程ﾀｲﾏ
LD T38                   ; ﾜｰｸ投入可音程ﾀｲﾏ
TMH K39 K100             ; ﾜｰｸ投入可音程ﾀｲﾏ1
;<h1/>__□ 安全関連入力受け
;安全系の入力は一旦ここで受ける。
LD X03                   ; 圧空元圧正常
OUT M120                 ; 圧空正常
LD X01                   ; 非常停止正常
OUT M121                 ; 非常停止正常
LD CR2002                ; 常時ON
OUT M122                 ; 外部非常停止正常
LD X02                   ; 安全扉正常
ANB B0320                ; 800)安全扉開
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
OUT M123                 ; 安全扉正常
LD X04                   ; 書込み部ﾗｲﾄｶｰﾃﾝOUT1
OUT M124                 ; ｴﾘｱｾﾝｻ正常
LD X02                   ; 安全扉正常
ANB B0320                ; 800)安全扉開
AND X06                  ; 給材安全扉(X006)
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
TMR K60 K5               ; 投入部扉確認ﾀｲﾏ
LD X06                   ; 給材安全扉(X006)
TMR K61 K5               ; ｺﾝﾍﾞﾔ給材安全扉確認ﾀｲﾏ
LDB X06                  ; 給材安全扉(X006)
TMR K64 K5               ; ｺﾝﾍﾞﾔ給材安全扉開きﾀｲﾏ
LD X08                   ; (X008)
TMR K62 K5               ; ST3-4部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LD X0A                   ; (X010)
TMR K63 K5               ; 装置背面ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
LDB M120                 ; 圧空正常
ANB M121                 ; 非常停止正常
ANB M122                 ; 外部非常停止正常
ANB M123                 ; 安全扉正常
ANB M124                 ; ｴﾘｱｾﾝｻ正常
ANB M125
ANB M126
ANB M127
ANB M128
OUB M129                 ; 安全入力OK
;<h1/>__□ スイッチ変換
;操作系の入力は一旦ここで受ける。
LD B1016                 ; DRY_RUN_PB
OUT M7                   ; 空運転PB
LD B1001                 ; 自動/手動PB
OUT M10                  ; 自動/手動PB
;今回は運転準備PB無し装置電源入で常時ON
LD X00                   ; 装置電源入
OUT M12                  ; 運転準備入PB
LD CR2003                ; 常時OFF
OUT M13                  ; 運転準備切PB
LD B1010                 ; ﾌﾞｻﾞｰ停止SW
OUT M15                  ; ﾌﾞｻﾞｰ停止PB
LD B1011                 ; ｴﾗｰﾘｾｯﾄPB
OUT M16                  ; ﾘｾｯﾄPB
LD B1000                 ; 原点復帰PB
OUT M17                  ; 原点復帰PB
LD B1002                 ; ｽﾀｰﾄPB
OUT M18                  ; 起動PB
LD B1003                 ; ｻｲｸﾙ停止PB
TMR K15 K50              ; 強制停止
OUT M19                  ; 停止PB
LD B1004                 ; 段取りPB
OUT M22                  ; 段取りPB
;用途に応じて使用する
LD CR2003                ; 常時OFF
MPS
TMR K16 K20              ; 終了PB_ONﾃﾞｨﾚｲ
MPP
AND T16                  ; 終了PB_ONﾃﾞｨﾚｲ
OUT M20                  ; 終了PB
LD CR2003                ; 常時OFF
MPS
TMR K17 K10              ; 払出PB_ONﾃﾞｨﾚｲ
MPP
AND T17                  ; 払出PB_ONﾃﾞｨﾚｲ
OUT M21                  ; 払出PB
;<h1/>__□ 各モード選択
;自動・手動モード
LDP M10                  ; 自動/手動PB
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M30                  ; 自動ﾓｰﾄﾞ
MEP
LD M30                   ; 自動ﾓｰﾄﾞ
ANPB M31                 ; 手動ﾓｰﾄﾞ
ORL
ANB M51                  ; 即停止異常(緊急停止)
OUT M30                  ; 自動ﾓｰﾄﾞ
LDP M10                  ; 自動/手動PB
ANB M31                  ; 手動ﾓｰﾄﾞ
ANB M41                  ; 全ﾕﾆｯﾄ起動中
MEP
OR T51                   ; 手動ﾓｰﾄﾞ移行ﾀｲﾏ
OR CR2008                ; 運転開始時1ｽｷｬﾝON
LD M31                   ; 手動ﾓｰﾄﾞ
ANPB M30                 ; 自動ﾓｰﾄﾞ
ORL
ANB M71                  ; 自動運転開始BZ
OUT M31                  ; 手動ﾓｰﾄﾞ
LDB M40                  ; 全ﾕﾆｯﾄ原点
ANB M71                  ; 自動運転開始BZ
ORB M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M41                  ; 全ﾕﾆｯﾄ起動中
TMR K51 K10              ; 手動ﾓｰﾄﾞ移行ﾀｲﾏ
;この手動操作可条件はメインのみの条件
;ユニット毎に必要な条件は各ユニットへ追加する
LD M120                  ; 圧空正常
AND M121                 ; 非常停止正常
AND M122                 ; 外部非常停止正常
LD M123                  ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
OR M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ANL
OUT M32                  ; ﾒｲﾝ手動操作可能条件
LD M40                   ; 全ﾕﾆｯﾄ原点
OUT M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
;空運転､払出モードは必要に応じて使用
LD M7                    ; 空運転PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K34 K50              ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LDB M35                  ; 空運転ﾓｰﾄﾞ
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANP T34                  ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LD M35                   ; 空運転ﾓｰﾄﾞ
ANPB T34                 ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
ORL
OUT M35                  ; 空運転ﾓｰﾄﾞ
LDP M19                  ; 停止PB
OR M13309                ; PC→自動運転停止ﾌﾗｸﾞ
ORP M1404                ; ID異常時ｻｲｸﾙ停止選択
OR M59                   ; 払出停止異常まとめ
ANB M130                 ; 一時停止
ANB M36                  ; 払出ﾓｰﾄﾞ
LD M36                   ; 払出ﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M71                  ; 自動運転開始BZ
OUT M36                  ; 払出ﾓｰﾄﾞ
;ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD B1015                 ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙPB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K35 K50              ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
LDP T35                  ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LDPB T35                 ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
AND M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M41                  ; 全ﾕﾆｯﾄ起動中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
OUT M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
;エリアセンサー無効化
LD B1010                 ; ﾌﾞｻﾞｰ停止SW
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K41 K30              ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
LDP T41                  ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
ANB M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
LDPB T41                 ; ｴﾘｱｾﾝｻｰ解除ﾃﾞｨﾚｲﾀｲﾏｰ
AND M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ORL
ANB M41                  ; 全ﾕﾆｯﾄ起動中
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
ANB B1001                ; 自動/手動PB
ANB B1002                ; ｽﾀｰﾄPB
ANPB B1011               ; ｴﾗｰﾘｾｯﾄPB
ANB M51                  ; 即停止異常(緊急停止)
OUT M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
LD M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B151C                ; ｴﾘｱｾﾝｻｰ無効
;段取りﾓｰﾄﾞ
LDP M22                  ; 段取りPB
AND CR2003               ; 常時OFF
ANB M38                  ; 段取りﾓｰﾄﾞ
LD M38                   ; 段取りﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M41                  ; 全ﾕﾆｯﾄ起動中
OUT M38                  ; 段取りﾓｰﾄﾞ
;上流装置オフラインモード
LDP B101E                ; 上流連結運転SW
ANB L0                   ; 上流装置連結運転(0：ｽﾙ/1：ｼﾅｲ)
LD L0                    ; 上流装置連結運転(0：ｽﾙ/1：ｼﾅｲ)
ANPB B101E               ; 上流連結運転SW
ORL
OUT L0                   ; 上流装置連結運転(0：ｽﾙ/1：ｼﾅｲ)
;<h1/>__□ 装置状態集約
;■原点
LD M1480                 ; ﾕﾆｯﾄ1原点
AND M1980                ; 搬送ST1_原点
AND M3980                ; ﾕﾆｯﾄ6原点
AND M4480                ; ﾌﾗｯｼｭST1ﾕﾆｯﾄ7原点
AND M5480                ; ﾌﾗｯｼｭST2U9原点
LD M6480                 ; ﾌﾗｯｼｭST3U11原点
ORB M990                 ; 能拡_ST3有効(ON:有効)
ANL
LD M7480                 ; ﾌﾗｯｼｭST4U13原点
ORB M991                 ; 能拡_ST4有効(ON:有効)
ANL
OUT M40                  ; 全ﾕﾆｯﾄ原点
;■起動中
LDB M73                  ; 各工程起動指令
ANB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M4485                ; ﾕﾆｯﾄ7起動中
ANB M5485                ; U9起動中
ANB M6485                ; U11起動中
ANB M7485                ; U13起動中
INV
OUT M41                  ; 全ﾕﾆｯﾄ起動中
;■原点復帰中
LDB M1484                ; ﾕﾆｯﾄ1原点復帰中
ANB M1984                ; ﾕﾆｯﾄ2原点復帰中
ANB M3984                ; ﾕﾆｯﾄ6原点復帰中
ANB M4484                ; ﾕﾆｯﾄ7原点復帰中
ANB M5484                ; U9原点復帰中
ANB M6484                ; U11原点復帰中
ANB M7484                ; U13原点復帰中
INV
OUT M42                  ; 全ﾕﾆｯﾄ原点復帰中
;■ﾛﾎﾞｯﾄ準備完了
LD M1490                 ; ﾕﾆｯﾄ1機器準備完了
AND M1990                ; ﾕﾆｯﾄ2機器準備完了
AND M3990                ; ﾕﾆｯﾄ6機器準備完了
AND M4490                ; ﾕﾆｯﾄ7機器準備完了
AND M5490                ; U9機器準備完了
LD M6490                 ; U11機器準備完了
ORB M990                 ; 能拡_ST3有効(ON:有効)
ANL
LD M7490                 ; U13機器準備完了
ORB M991                 ; 能拡_ST4有効(ON:有効)
ANL
OUT M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
;■運転準備
LD CR2007                ; 運転開始時1ｽｷｬﾝOFF
TMR K0 K20               ; 電源投入0Nﾃﾞｨﾚｲ
TMR K11 K50              ; タッチパネル_ロゴ表示終了
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
AND M12                  ; 運転準備入PB
OR M5                    ; 運転準備補助
LDB M13                  ; 運転準備切PB
OR M41                   ; 全ﾕﾆｯﾄ起動中
ANL
ANB M51                  ; 即停止異常(緊急停止)
OUT M5                   ; 運転準備補助
LD M5                    ; 運転準備補助
MPS
TMR K10 K20              ; 運転準備ﾃﾞｨﾚｲ
MPP
AND T10                  ; 運転準備ﾃﾞｨﾚｲ
OUT M50                  ; 運転準備
LD M50                   ; 運転準備
TMR K70 K10              ; Ready On 1s
CON
TMR K71 K20              ; Ready On 2s
CON
TMR K72 K30              ; Ready On 3s
CON
TMR K73 K40              ; Ready On 4s
CON
TMR K74 K50              ; Ready On 5s
CON
TMR K75 K60              ; Ready On 6s
CON
TMR K76 K70              ; Ready On 7s
CON
TMR K77 K80              ; Ready On 8s
CON
TMR K78 K90              ; Ready On 9s
;<h1/>__□ 異常検出まとめ
;■即停止異常(全ﾕﾆｯﾄ即停止)
LDB B01                  ; 001)非常停止異常
ANB B02                  ; 002)圧空低下異常
ANB B03                  ; 003)安全扉開異常
ANB B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ANB B05                  ; 005)外部非常停止
ANB B06                  ; 006)PLC異常
ANB B07                  ; 007)ﾘﾝｸ異常
ANB M58                  ; 非常停止異常まとめ
ANB B0B                  ; 011)PC通信異常
ANB B0C                  ; 012)PCｴﾗｰ
ANB B017                 ; 023)EMG_STOP(ﾊﾝﾃﾞｨ)
ANB B018                 ; 024)EMG_STOP(操作ﾊﾟﾈﾙ)
ANB B019                 ; 025)EMG_STOP(ST1-2)
ANB B01A                 ; 026)EMG_STOP(REAR)
ANB B01B                 ; 027)EMG_STOP(OKconveyor)
ANB B01C                 ; 028)EMG_STOP(NGconveyor)
ANB B01D                 ; 029)
INV
OUT M51                  ; 即停止異常(緊急停止)
;■ﾕﾆｯﾄ異常検出(該当ﾕﾆｯﾄ以外ｻｲｸﾙ停止)
LDB M1486                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
ANB M1986                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ANB M3986                ; ﾕﾆｯﾄ6ｻｲｸﾙ停止異常
ANB M4486                ; ﾕﾆｯﾄ7ｻｲｸﾙ停止異常
ANB M5486                ; U9ｻｲｸﾙ停止異常
ANB M6486                ; U11ｻｲｸﾙ停止異常
ANB M7486                ; U13ｻｲｸﾙ停止異常
INV
OUT M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
;■ﾕﾆｯﾄ毎各種警報検出(ﾜｰｸ不足･ｵﾍﾟｺｰﾙ等)
LDB M1488                ; ﾕﾆｯﾄ1警報補助
ANB M1988                ; ﾕﾆｯﾄ2警報補助
ANB M3988                ; ﾕﾆｯﾄ6警報補助
ANB M4488                ; ﾕﾆｯﾄ7警報補助
ANB M5488                ; U9警報補助
ANB M6488                ; U11警報補助
ANB M7488                ; U13警報補助
INV
OUT M53                  ; 警報(ﾜｰｸ不足等)
LDB M1498
ANB M1998
ANB M3998                ; ﾕﾆｯﾄ6警報BZ補助
ANB M4498
ANB M5498
ANB M6498
ANB M7498
INV
OUT M63                  ; 警報(ﾜｰｸ不足等)ﾌﾞｻﾞｰ
;■ﾕﾆｯﾄ毎異常ﾌﾞｻﾞｰ検出
LDB M200                 ; ﾒｲﾝ異常BZ　まとめ
ANB M1489                ; ﾕﾆｯﾄ1異常BZ補助
ANB M1989                ; ﾕﾆｯﾄ2異常BZ補助
ANB M3489                ; ﾕﾆｯﾄ5異常BZ補助
ANB M3989                ; ﾕﾆｯﾄ6異常BZ補助
ANB M4489                ; ﾕﾆｯﾄ7異常BZ補助
ANB M5489                ; U9異常BZ補助
ANB M6489                ; U11異常BZ補助
ANB M7489                ; U13異常BZ補助
ANB M13489               ; ﾕﾆｯﾄ25異常BZ補助
INV
OUT M54                  ; 異常ﾌﾞｻﾞｰ
;□□　　自動運転停止指令(ｻｲｸﾙ停止)　　□□
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR T15                   ; 強制停止
LDB L999
AND B046                 ; 070)ﾊﾝﾄﾞ傾き異常
ORL
OR M57                   ; 自動起動停止指令(払出停止)
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M41                  ; 全ﾕﾆｯﾄ起動中
OUT M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
;□□　　払出完了運転停止指令(ｻｲｸﾙ停止)　　□□
LD M36                   ; 払出ﾓｰﾄﾞ
AND M1493                ; ﾕﾆｯﾄ1ﾜｰｸ無し
AND M1993                ; ﾕﾆｯﾄ2ﾜｰｸ無し(払出時)
AND M3993                ; ﾕﾆｯﾄ6ﾜｰｸ無し(払出時)
AND M4493                ; ﾕﾆｯﾄ7ﾜｰｸ無し
AND M5493                ; U9ﾜｰｸ無し
LD M6493                 ; U11ﾜｰｸ無し
ORB M990                 ; 能拡_ST3有効(ON:有効)
ANL
LD M7493                 ; U13ﾜｰｸ無し
ORB M991                 ; 能拡_ST4有効(ON:有効)
ANL
ANB M1485                ; ﾕﾆｯﾄ1起動中
ANB M1985                ; ﾕﾆｯﾄ2起動中
ANB M3985                ; ﾕﾆｯﾄ6起動中
ANB M4485                ; ﾕﾆｯﾄ7起動中
ANB M5485                ; U9起動中
ANB M6485                ; U11起動中
ANB M7485                ; U13起動中
OUT M57                  ; 自動起動停止指令(払出停止)
;緊急停止異常まとめ
LDB M1481                ; ﾕﾆｯﾄ1即停止異常
ANB M1981                ; ﾕﾆｯﾄ2即停止異常
ANB M3981                ; ﾕﾆｯﾄ6即停止異常
ANB M4481                ; ﾕﾆｯﾄ7即停止異常
ANB M5481                ; U9即停止異常
ANB M6481                ; U11即停止異常
ANB M7481                ; U13即停止異常
INV
OUT M58                  ; 非常停止異常まとめ
;払出停止異常まとめ
LDB M1487                ; ﾕﾆｯﾄ1払出停止異常
ANB M1987                ; ﾕﾆｯﾄ2払出停止異常
ANB M3987                ; ﾕﾆｯﾄ6払出停止異常
ANB M4487                ; ﾕﾆｯﾄ7払出停止異常
ANB M5487                ; U9払出停止異常
ANB M6487                ; U11払出停止異常
ANB M7487                ; U13払出停止異常
INV
OUT M59                  ; 払出停止異常まとめ
;■各ﾕﾆｯﾄｴﾗｰ検出
LDB B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
ANB B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
ANB M1491                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M1991                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M3991                ; ﾕﾆｯﾄ6ｴﾗｰ発生
ANB M4491                ; ﾕﾆｯﾄ7ｴﾗｰ発生
ANB M5491                ; U9ｴﾗｰ発生
ANB M6491                ; U11ｴﾗｰ発生
ANB M7491                ; U13ｴﾗｰ発生
INV
OUT M60                  ; ｴﾗｰ発生
;<h1/>__□ 一時停止処理
;アラーム発生時に使用
LD M73                   ; 各工程起動指令
OR M41                   ; 全ﾕﾆｯﾄ起動中
ANB M149                 ; 一時停止処理終了
ANB M133                 ; 一時停止　ｷｬﾝｾﾙ
MC
LDP B1012                ; 一時停止PB
LD CR2003                ; 常時OFF
ANP M60                  ; ｴﾗｰ発生
ORL
OR M130                  ; 一時停止
OUT M130                 ; 一時停止
LD M1492                 ; ﾕﾆｯﾄ1動作無し確認
AND M1992                ; ﾕﾆｯﾄ2動作無し確認
AND M3992                ; ﾕﾆｯﾄ6動作無し確認
AND M4492                ; ﾕﾆｯﾄ7動作無し確認
AND M5492                ; U9動作無し確認
LD M6492                 ; U11動作無し確認
ORB M990                 ; 能拡_ST3有効(ON:有効)
ANL
LD M7492                 ; U13動作無し確認
ORB M991                 ; 能拡_ST4有効(ON:有効)
ANL
OUB M131                 ; PACK中
LD M130                  ; 一時停止
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OR M133                  ; 一時停止　ｷｬﾝｾﾙ
MPS
LDB M133                 ; 一時停止　ｷｬﾝｾﾙ
ANB M131                 ; PACK中
ANL
OUT M132                 ; 全ﾕﾆｯﾄ停止OK
MPP
LDB M132                 ; 全ﾕﾆｯﾄ停止OK
AND M131                 ; PACK中
ANP B1002                ; ｽﾀｰﾄPB
ANL
OUT M133                 ; 一時停止　ｷｬﾝｾﾙ
LD M132                  ; 全ﾕﾆｯﾄ停止OK
OR M140                  ; ﾄﾞｱﾛｯｸ開
ANB M141                 ; ﾄﾞｱ閉確認
OUT M140                 ; ﾄﾞｱﾛｯｸ開
LD M140                  ; ﾄﾞｱﾛｯｸ開
AND M123                 ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
ANP B1002                ; ｽﾀｰﾄPB
OR M141                  ; ﾄﾞｱ閉確認
OUT M141                 ; ﾄﾞｱ閉確認
LD M141                  ; ﾄﾞｱ閉確認
AND M123                 ; 安全扉正常
AND M124                 ; ｴﾘｱｾﾝｻ正常
OR M148                  ; 状態復帰完了
OUT M148                 ; 状態復帰完了
LD M148                  ; 状態復帰完了
OR M149                  ; 一時停止処理終了
OUT M149                 ; 一時停止処理終了
MCR
;<h1/>__□ 自動起動回路
LDB L700                 ; ﾌﾗｯｼｭst1_有効状態
ANB L900                 ; ﾌﾗｯｼｭst2_有効状態
ANB L1100                ; ﾌﾗｯｼｭst3_有効状態
ANB L1300                ; ﾌﾗｯｼｭst4_有効状態
INV
AND M30                  ; 自動ﾓｰﾄﾞ
AND M33                  ; 自動ﾓｰﾄﾞ切替・起動可条件
AND M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M50                  ; 運転準備
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M51                  ; 即停止異常(緊急停止)
LDB W00.8                ; PC自動抑制中
OR M3                    ; OFFLINE_MODE
ANL
OUT M70                  ; 自動起動条件
LDP M18                  ; 起動PB
AND M70                  ; 自動起動条件
OR M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB M71                  ; 自動運転開始BZ
ANB M76                  ; PCより起動不可ON
AND M50                  ; 運転準備
MPS
OUT M75                  ; 自動運転開始PCﾁｪｯｸ開始
MPP
LDB M3                   ; OFFLINE_MODE
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANL
OUT W0100.6              ; 起動確認
LD M75                   ; 自動運転開始PCﾁｪｯｸ開始
ANB W00.6                ; 起動可能
AND W00.5                ; 起動確認完了
ANB M3                   ; OFFLINE_MODE
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT M76                  ; PCより起動不可ON
LD W00.6                 ; 起動可能
AND W00.5                ; 起動確認完了
OR M3                    ; OFFLINE_MODE
OR M35                   ; 空運転ﾓｰﾄﾞ
AND M75                  ; 自動運転開始PCﾁｪｯｸ開始
OUT M77                  ; PCより起動可能ON
LD M77                   ; PCより起動可能ON
OR M71                   ; 自動運転開始BZ
ANB M51                  ; 即停止異常(緊急停止)
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M71                  ; 自動運転開始BZ
INV
MOV K0 D200              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
;起動時ﾌﾞｻﾞｰ
LD M71                   ; 自動運転開始BZ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
MPS
ANB T21                  ; 自動起動ﾃﾞｨﾚｲ
TMR K20 K10              ; 起動前BZ_ONﾀｲﾏ
MPP
AND T20                  ; 起動前BZ_ONﾀｲﾏ
TMR K21 K5               ; 自動起動ﾃﾞｨﾚｲ
@INC D200                ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
LD= D200 K3              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M72                  ; 自動運転開始
LD M72                   ; 自動運転開始
OUT M73                  ; 各工程起動指令
;<h1/>__□ 原点復帰回路
LDB M72                  ; 自動運転開始
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
AND M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
AND M50                  ; 運転準備
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
AND M31                  ; 手動ﾓｰﾄﾞ
ANB M1734                ; 搬送RB動作(ﾕﾆｯﾄ1-3)起動中
OUT M80                  ; 原点復帰可能条件
LD M17                   ; 原点復帰PB
AND M80                  ; 原点復帰可能条件
OR M81                   ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
ANB T15                  ; 強制停止
OUT M81                  ; 原点復帰動作中
MPS
TMR K22 K5               ; 原点復帰開始ﾃﾞｨﾚｲ
MPP
LD X02                   ; 安全扉正常
AND X04                  ; 書込み部ﾗｲﾄｶｰﾃﾝOUT1
AND X06                  ; 給材安全扉(X006)
AND CR2003               ; 常時OFF
ANL
TMR K23 K600             ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LDP T22                  ; 原点復帰開始ﾃﾞｨﾚｲ
OR M82                   ; 原点復帰開始
AND M81                  ; 原点復帰動作中
ANB M84                  ; 原点復帰起動確認
OUT M82                  ; 原点復帰開始
LD M82                   ; 原点復帰開始
AND M1484                ; ﾕﾆｯﾄ1原点復帰中
AND M1984                ; ﾕﾆｯﾄ2原点復帰中
AND M3984                ; ﾕﾆｯﾄ6原点復帰中
AND M4484                ; ﾕﾆｯﾄ7原点復帰中
AND M5484                ; U9原点復帰中
LD M6484                 ; U11原点復帰中
ORB M990                 ; 能拡_ST3有効(ON:有効)
ANL
LD M7484                 ; U13原点復帰中
ORB M991                 ; 能拡_ST4有効(ON:有効)
ANL
OUT M83                  ; 原点復帰全起動確認補助
LD M82                   ; 原点復帰開始
AND M83                  ; 原点復帰全起動確認補助
OR M84                   ; 原点復帰起動確認
AND M81                  ; 原点復帰動作中
ANB M85                  ; 原点復帰完了
OUT M84                  ; 原点復帰起動確認
LD M84                   ; 原点復帰起動確認
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
OR M85                   ; 原点復帰完了
AND M81                  ; 原点復帰動作中
ANB M86                  ; 装置全原点確認
OUT M85                  ; 原点復帰完了
LD M85                   ; 原点復帰完了
AND M40                  ; 全ﾕﾆｯﾄ原点
OR M86                   ; 装置全原点確認
AND M81                  ; 原点復帰動作中
OUT M86                  ; 装置全原点確認
;未使用
LD M54                   ; 異常ﾌﾞｻﾞｰ
MPS
ANB T30                  ; 異常ﾌﾞｻﾞｰ自動停止
OUT M100                 ; 異常ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K30 K100             ; 異常ﾌﾞｻﾞｰ自動停止
LD M53                   ; 警報(ﾜｰｸ不足等)
MPS
ANB T31                  ; 警報ﾌﾞｻﾞｰ自動停止
OUT M101                 ; 警報ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K31 K100             ; 警報ﾌﾞｻﾞｰ自動停止
LDP M15                  ; ﾌﾞｻﾞｰ停止PB
OUT M110                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LDP M16                  ; ﾘｾｯﾄPB
OUT M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
;<h1/>__□ その他補助回路
;ﾕﾆｯﾄ別動作停止、ﾕﾆｯﾄﾊﾟｽ設定､生産ｶｳﾝﾀ等装置固有の回路がある場合はここ。
;RCX240_CC-LINK初期通信処理
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
ANB R8400                ; ｴﾗｰ発生
AND R8401                ; ﾕﾆｯﾄﾚﾃﾞｨ
OUT M500                 ; CC-LINK_UNIT_OK
;□□　　出力回路　　□□
LD X00                   ; 装置電源入
OUT Y50                  ; 電源入表示
LD X05                   ; 検査部ﾗｲﾄｶｰﾃﾝOUT2
INV
AND M16                  ; ﾘｾｯﾄPB
OUT Y51                  ; ﾗｲﾄｶｰﾃﾝ
;<h1/>__□ ﾌﾞｻﾞｰ補助回路
;　　ブザー回路
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M51                   ; 即停止異常(緊急停止)
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANL
LDB M54                  ; 異常ﾌﾞｻﾞｰ
AND M4414                ; ｾｯﾄ不可ﾌﾞｻﾞｰ(ｴﾘｱｾﾝｻ検出中)
ORL
OUT M502                 ; 異常停止_連続ブザー
LD M54                   ; 異常ﾌﾞｻﾞｰ
LD M59                   ; 払出停止異常まとめ
OR M60                   ; ｴﾗｰ発生
ANL
LDB B0327                ; 807)OKｺﾝﾍﾞﾔ部ﾜｰｸ除去要求
ANB B032D                ; 813)NGｺﾝﾍﾞﾔ部ﾜｰｸ除去要求
INV
ORL
OUT M503                 ; エラー発生_ブザー_0.5
LD M63                   ; 警報(ﾜｰｸ不足等)ﾌﾞｻﾞｰ
ANB M502                 ; 異常停止_連続ブザー
ANB M503                 ; エラー発生_ブザー_0.5
OUT M504                 ; 警報_ブザー_1.0
LD M54                   ; 異常ﾌﾞｻﾞｰ
ANB M502                 ; 異常停止_連続ブザー
ANB M503                 ; エラー発生_ブザー_0.5
ANB M504                 ; 警報_ブザー_1.0
OUT M505                 ; その他_ブザー_1.0
LDB M54                  ; 異常ﾌﾞｻﾞｰ
LD M30                   ; 自動ﾓｰﾄﾞ
OR M42                   ; 全ﾕﾆｯﾄ原点復帰中
ANL
LD B0320                 ; 800)安全扉開
ANB B0820                ; 800)BzSTOP
OR B032E                 ; 814)NGｺﾝﾍﾞﾔカバー開
OR B032B                 ; 811)投入部ｶﾊﾞｰ開
OR B032C                 ; 812)ｴﾘｱｾﾝｻ遮光
ANL
ANB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OUT M510                 ; 自動時_安全ドア開_ブザー_1.0
LDB M54                  ; 異常ﾌﾞｻﾞｰ
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
OR M39                   ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
ANL
OUT M511                 ; IL解除中_ブザー_10.0
LD M71                   ; 自動運転開始BZ
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M520                 ; 自動運転開始_ブザー_1.O
LD M502                  ; 異常停止_連続ブザー
LD M503                  ; エラー発生_ブザー_0.5
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
LD M504                  ; 警報_ブザー_1.0
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
LD M505                  ; その他_ブザー_1.0
AND T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
ORL
LD M510                  ; 自動時_安全ドア開_ブザー_1.0
AND T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
ORL
LD M511                  ; IL解除中_ブザー_10.0
AND T36                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
ORL
OR M520                  ; 自動運転開始_ブザー_1.O
OUT Y328                 ; ﾒｲﾝﾊﾟﾈﾙﾌﾞｻﾞｰ
OUT Y31C                 ; ｼｸﾞﾅﾙﾀﾜｰﾌﾞｻﾞｰ
OUT Y329                 ; 背面ﾊﾟﾈﾙﾌﾞｻﾞｰ
LD CR2003                ; 常時OFF
TMR K45 K600             ; 投入要求ﾌﾞｻﾞｰ自動停止
;<h1/>__□ ｼｸﾞﾅﾙﾀﾜｰ
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
INV
LD M60                   ; ｴﾗｰ発生
OR M59                   ; 払出停止異常まとめ
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
OUT Y31D                 ; ｼｸﾞﾅﾙﾀﾜｰ赤表示
OUT W0100.8              ; ﾀﾜｰﾗｲﾄ赤
LD M53                   ; 警報(ﾜｰｸ不足等)
OR B0320                 ; 800)安全扉開
LD B032C                 ; 812)ｴﾘｱｾﾝｻ遮光
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y31E                 ; ｼｸﾞﾅﾙﾀﾜｰ黄表示
OUT W0100.9              ; ﾀﾜｰﾗｲﾄ黄
LD M71                   ; 自動運転開始BZ
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
AND> K3 D200             ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OR M73                   ; 各工程起動指令
OR M41                   ; 全ﾕﾆｯﾄ起動中
LD M42                   ; 全ﾕﾆｯﾄ原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y31F                 ; ｼｸﾞﾅﾙﾀﾜｰ緑表示
OUT W0100.10             ; ﾀﾜｰﾗｲﾄ緑
;<h1/>____▼_ﾌﾗｯｼｭﾍﾞｰｽ電源
LD W04.8                 ; ﾗｲﾀﾍﾞｰｽ#1電源OFF
OUT Y320                 ; ST1 ｺﾝﾄﾛｰﾗﾘｾｯﾄ
LD W04.9                 ; ﾗｲﾀﾍﾞｰｽ#2電源OFF
OUT Y322                 ; ST3 ｺﾝﾄﾛｰﾗﾘｾｯﾄ
;<h1/>__□ 画面切替管理
;□□　　ﾀｯﾁﾊﾟﾈﾙ表示　　□□
LDB M0                   ; 常時OFF
MPS
@MOV K0 D58              ; TP表示切り替え
MPP
AND T11                  ; タッチパネル_ロゴ表示終了
@MOV K1 D58              ; TP表示切り替え
LD T11                   ; タッチパネル_ロゴ表示終了
LD M81                   ; 原点復帰動作中
OR M30                   ; 自動ﾓｰﾄﾞ
ANL
OR M2340                 ; RB_原点復帰不可位置
@MOV K1 D58              ; TP表示切り替え
;ﾐﾆﾀｯﾁﾊﾟﾈﾙ
LDB M0                   ; 常時OFF
MPS
ANB T11                  ; タッチパネル_ロゴ表示終了
@MOV K0 D108             ; ミニパネル画面切替NO.
MPP
AND T11                  ; タッチパネル_ロゴ表示終了
@MOV K1 D108             ; ミニパネル画面切替NO.
LD T11                   ; タッチパネル_ロゴ表示終了
LD M81                   ; 原点復帰動作中
OR M30                   ; 自動ﾓｰﾄﾞ
ANL
@MOV K1 D108             ; ミニパネル画面切替NO.
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
INV
MEP
MPS
MOV K5 D108              ; ミニパネル画面切替NO.
MPP
LD<> D100 D820           ; ミニパネル表示中ﾍﾟｰｼﾞNO.  異常時画面記憶
AND<> D100 K5            ; ミニパネル表示中ﾍﾟｰｼﾞNO.
ANL
MOV D100 D820            ; ミニパネル表示中ﾍﾟｰｼﾞNO.  異常時画面記憶
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
MEP
MOV D820 D108            ; 異常時画面記憶  ミニパネル画面切替NO.
LD M40                   ; 全ﾕﾆｯﾄ原点
ANB M81                  ; 原点復帰動作中
LD M81                   ; 原点復帰動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1500                ; 原点復帰TP-PL
LD M30                   ; 自動ﾓｰﾄﾞ
OUT B1501                ; 自動/手動TP-PL
LD M30                   ; 自動ﾓｰﾄﾞ
LD M41                   ; 全ﾕﾆｯﾄ起動中
OR M71                   ; 自動運転開始BZ
ANL
OUT B1502                ; 起動TP-PL
LDB M41                  ; 全ﾕﾆｯﾄ起動中
LD M36                   ; 払出ﾓｰﾄﾞ
OR M56                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M41                  ; 全ﾕﾆｯﾄ起動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1503                ; 停止TP-PL
LDB M54                  ; 異常ﾌﾞｻﾞｰ
ANB M63                  ; 警報(ﾜｰｸ不足等)ﾌﾞｻﾞｰ
LDB B0320                ; 800)安全扉開
OR B0820                 ; 800)BzSTOP
ANL
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1510                ; ﾌﾞｻﾞｰ停止PL
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M53                  ; 警報(ﾜｰｸ不足等)
ANB M59                  ; 払出停止異常まとめ
ANB M60                  ; ｴﾗｰ発生
ANB B0320                ; 800)安全扉開
ANB M13300               ; PCﾌﾞｻﾞｰ要求(PC_Busy)
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1511                ; ｴﾗｰﾘｾｯﾄPL
LD M130                  ; 一時停止
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M132                  ; 全ﾕﾆｯﾄ停止OK
OUT B1512                ; 一時停止PL
LD M37                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1515                ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙ中PL
LD M35                   ; 空運転ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1516                ; DRY_RUN_PL
LD M38                   ; 段取りﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1517                ; 段取り_PL
LD L0                    ; 上流装置連結運転(0：ｽﾙ/1：ｼﾅｲ)
OUT B151E                ; 上流装置ｵﾌﾗｲﾝﾓｰﾄﾞPL
LD M3                    ; OFFLINE_MODE
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B151D                ; OFFLINEモード
;<h1/>____ メッセージ表示
LDB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M57                  ; 自動起動停止指令(払出停止)
ANB M59                  ; 払出停止異常まとめ
MPS
ANB M43                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
OUT B0384                ; 900)ｲﾆｼｬﾗｲｽﾞ中表示
MRD
LDB M90                  ; ｲﾆｼｬﾗｲｽﾞ中表示
ANB M81                  ; 原点復帰動作中
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANB B036                 ; 054)
ANB B066                 ; 102)
ANB B0384                ; 900)ｲﾆｼｬﾗｲｽﾞ中表示
ANB B0320                ; 800)安全扉開
ANB B032C                ; 812)ｴﾘｱｾﾝｻ遮光
ANB B032E                ; 814)NGｺﾝﾍﾞﾔカバー開
ANL
OUT B0385                ; 901)原点復帰要求
MPP
AND M81                  ; 原点復帰動作中
OUT B0386                ; 902)原点復帰中
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0886                 ; 902)BzSTOP
AND B0386                ; 902)原点復帰中
OUT B0886                ; 902)BzSTOP
LDP B0384                ; 900)ｲﾆｼｬﾗｲｽﾞ中表示
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV H0384 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LDP B0385                ; 901)原点復帰要求
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV H0385 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LDP B0386                ; 902)原点復帰中
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV H0386 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
;<h1/>__□ 主幹異常
;ﾒｲﾝ回路に関連する異常はここにまとめる。
LDB M121                 ; 非常停止正常
LD CR2003                ; 常時OFF
ANB B0501                ; 001)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01                  ; 001)非常停止異常
ORL
OUT B01                  ; 001)非常停止異常
LDP B01                  ; 001)非常停止異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1001 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0501                 ; 001)BzSTOP
AND B01                  ; 001)非常停止異常
OUT B0501                ; 001)BzSTOP
LD M50                   ; 運転準備
ANB M120                 ; 圧空正常
LD CR2003                ; 常時OFF
ANB B0502                ; 002)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02                  ; 002)圧空低下異常
ORL
MPS
AND T80                  ; 圧空低下確認
OUT B02                  ; 002)圧空低下異常
MPP
TMR K80 K50              ; 圧空低下確認
LDP B02                  ; 002)圧空低下異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1002 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0502                 ; 002)BzSTOP
AND B02                  ; 002)圧空低下異常
OUT B0502                ; 002)BzSTOP
LDB M37                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ANB M123                 ; 安全扉正常
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B0503                ; 003)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B03                  ; 003)安全扉開異常
ORL
OUT B03                  ; 003)安全扉開異常
LDP B03                  ; 003)安全扉開異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1003 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0503                 ; 003)BzSTOP
AND B03                  ; 003)安全扉開異常
OUT B0503                ; 003)BzSTOP
LD T23                   ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LD CR2003                ; 常時OFF
ANB B0504                ; 004)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ORL
OUT B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
LDP B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1004 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0504                 ; 004)BzSTOP
AND B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OUT B0504                ; 004)BzSTOP
LD M50                   ; 運転準備
ANB M122                 ; 外部非常停止正常
LD CR2003                ; 常時OFF
ANB B0505                ; 005)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B05                  ; 005)外部非常停止
ORL
OUT B05                  ; 005)外部非常停止
LDP B05                  ; 005)外部非常停止
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1005 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0505                 ; 005)BzSTOP
AND B05                  ; 005)外部非常停止
OUT B0505                ; 005)BzSTOP
LD CR3905                ; 重度ｴﾗｰ発生中
LD CR2003                ; 常時OFF
ANB B0506                ; 006)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B06                  ; 006)PLC異常
ORL
OUT B06                  ; 006)PLC異常
LDP B06                  ; 006)PLC異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1006 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0506                 ; 006)BzSTOP
AND B06                  ; 006)PLC異常
OUT B0506                ; 006)BzSTOP
LD M5                    ; 運転準備補助
AND R8400                ; ｴﾗｰ発生
ANB R9200                ; 他局通信状態
ANB R9201                ; 他局WDTｴﾗｰ状態
ANB R9202                ; 他局ﾋｭｰｽﾞ断状態
LDB R9200                ; 他局通信状態
ANB R9201                ; 他局WDTｴﾗｰ状態
ANB R9202                ; 他局ﾋｭｰｽﾞ断状態
INV
AND M5                   ; 運転準備補助
AND R8401                ; ﾕﾆｯﾄﾚﾃﾞｨ
ORL
LD CR2003                ; 常時OFF
ANB B0507                ; 007)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B07                  ; 007)ﾘﾝｸ異常
ORL
OUT B07                  ; 007)ﾘﾝｸ異常
LDP B07                  ; 007)ﾘﾝｸ異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1007 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0507                 ; 007)BzSTOP
AND B07                  ; 007)ﾘﾝｸ異常
OUT B0507                ; 007)BzSTOP
LD CR2306                ; ﾊﾞｯｸｱｯﾌﾟ電池異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LD CR2003                ; 常時OFF
ANB B0508                ; 008)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
ORL
MPS
TMR K50 K18000           ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
MPP
AND T50                  ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
OUT B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
LDP B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K8 D13500            ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0508                 ; 008)BzSTOP
AND B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
OUT B0508                ; 008)BzSTOP
LDP M10                  ; 自動/手動PB
ANB M40                  ; 全ﾕﾆｯﾄ原点
ANB M41                  ; 全ﾕﾆｯﾄ起動中
ANB M71                  ; 自動運転開始BZ
LD CR2003                ; 常時OFF
ANB B050A                ; 010)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
ORL
OUT B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
LDP B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K10 D13500           ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B050A                 ; 010)BzSTOP
AND B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
OUT B050A                ; 010)BzSTOP
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
ANB M300                 ; PC通信正常
LD CR2003                ; 常時OFF
ANB B050B                ; 011)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0B                  ; 011)PC通信異常
ORL
OUT B0B                  ; 011)PC通信異常
LDP B0B                  ; 011)PC通信異常
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1011 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B050B                 ; 011)BzSTOP
AND B0B                  ; 011)PC通信異常
OUT B050B                ; 011)BzSTOP
LD W00.1                 ; PC Error
LD CR2003                ; 常時OFF
ANB B050C                ; 012)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0C                  ; 012)PCｴﾗｰ
ORL
ANB M3                   ; OFFLINE_MODE
OUT B0C                  ; 012)PCｴﾗｰ
LDP B0C                  ; 012)PCｴﾗｰ
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1012 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B050C                 ; 012)BzSTOP
AND B0C                  ; 012)PCｴﾗｰ
OUT B050C                ; 012)BzSTOP
LDB X302                 ; 非常停止1正常(ﾊﾝﾃﾞｨﾊﾟﾈﾙ)
LD CR2003                ; 常時OFF
ANB B0517                ; 023)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B017                 ; 023)EMG_STOP(ﾊﾝﾃﾞｨ)
ORL
OUT B017                 ; 023)EMG_STOP(ﾊﾝﾃﾞｨ)
LDP B017                 ; 023)EMG_STOP(ﾊﾝﾃﾞｨ)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1023 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0517                 ; 023)BzSTOP
AND B017                 ; 023)EMG_STOP(ﾊﾝﾃﾞｨ)
OUT B0517                ; 023)BzSTOP
LDB X303                 ; 非常停止2正常(ﾒｲﾝ操作ﾊﾟﾈﾙ)
LD CR2003                ; 常時OFF
ANB B0518                ; 024)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B018                 ; 024)EMG_STOP(操作ﾊﾟﾈﾙ)
ORL
OUT B018                 ; 024)EMG_STOP(操作ﾊﾟﾈﾙ)
LDP B018                 ; 024)EMG_STOP(操作ﾊﾟﾈﾙ)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1024 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0518                 ; 024)BzSTOP
AND B018                 ; 024)EMG_STOP(操作ﾊﾟﾈﾙ)
OUT B0518                ; 024)BzSTOP
LDB X304                 ; 非常停止3正常(ST1-2間)
LD CR2003                ; 常時OFF
ANB B0519                ; 025)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B019                 ; 025)EMG_STOP(ST1-2)
ORL
OUT B019                 ; 025)EMG_STOP(ST1-2)
LDP B019                 ; 025)EMG_STOP(ST1-2)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1025 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0519                 ; 025)BzSTOP
AND B019                 ; 025)EMG_STOP(ST1-2)
OUT B0519                ; 025)BzSTOP
LDB X305                 ; 非常停止4正常(装置背面)
LD CR2003                ; 常時OFF
ANB B051A                ; 026)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01A                 ; 026)EMG_STOP(REAR)
ORL
OUT B01A                 ; 026)EMG_STOP(REAR)
LDP B01A                 ; 026)EMG_STOP(REAR)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1026 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051A                 ; 026)BzSTOP
AND B01A                 ; 026)EMG_STOP(REAR)
OUT B051A                ; 026)BzSTOP
LDB X306                 ; 非常停止5正常(OKｺﾝﾍﾞﾔ部)
LD CR2003                ; 常時OFF
ANB B051B                ; 027)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01B                 ; 027)EMG_STOP(OKconveyor)
ORL
OUT B01B                 ; 027)EMG_STOP(OKconveyor)
LDP B01B                 ; 027)EMG_STOP(OKconveyor)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1027 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051B                 ; 027)BzSTOP
AND B01B                 ; 027)EMG_STOP(OKconveyor)
OUT B051B                ; 027)BzSTOP
LDB X307                 ; 非常停止6正常(NGｺﾝﾍﾞﾔ部)
LD CR2003                ; 常時OFF
ANB B051C                ; 028)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01C                 ; 028)EMG_STOP(NGconveyor)
ORL
OUT B01C                 ; 028)EMG_STOP(NGconveyor)
LDP B01C                 ; 028)EMG_STOP(NGconveyor)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1028 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051C                 ; 028)BzSTOP
AND B01C                 ; 028)EMG_STOP(NGconveyor)
OUT B051C                ; 028)BzSTOP
LDB X308                 ; /
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B051D                ; 029)BzSTOP
ORB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01D                 ; 029)
ORL
OUT B01D                 ; 029)
LDP B01D                 ; 029)
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K1029 D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B051D                 ; 029)BzSTOP
AND B01D                 ; 029)
OUT B051D                ; 029)BzSTOP
;<h1/>__□警報
LD X02                   ; 安全扉正常
INV
LD M30                   ; 自動ﾓｰﾄﾞ
OR M42                   ; 全ﾕﾆｯﾄ原点復帰中
ANL
LDB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0320                ; 800)安全扉開
ORL
ANB M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
OUT B0320                ; 800)安全扉開
LDP B0320                ; 800)安全扉開
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K800 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
ORP X02                  ; 安全扉正常
OR B0820                 ; 800)BzSTOP
AND B0320                ; 800)安全扉開
ANFB X02                 ; 安全扉正常
OUT B0820                ; 800)BzSTOP
LDB T61                  ; ｺﾝﾍﾞﾔ給材安全扉確認ﾀｲﾏ
LD M30                   ; 自動ﾓｰﾄﾞ
OR M42                   ; 全ﾕﾆｯﾄ原点復帰中
ANL
ANB M39                  ; ｴﾘｱｾﾝｻｰ解除ﾓｰﾄﾞ
OUT B032B                ; 811)投入部ｶﾊﾞｰ開
LDP B032B                ; 811)投入部ｶﾊﾞｰ開
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV K810 D13500          ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
LD M124                  ; ｴﾘｱｾﾝｻ正常
INV
OUT B032C                ; 812)ｴﾘｱｾﾝｻ遮光
LDP B032C                ; 812)ｴﾘｱｾﾝｻ遮光
AND< D13510 K64          ; FIFO_DATA_VALUE
MOV H032C D13500         ; ALM_WRITE_CODE(複合)
FIFOW D13500 D13510      ; ALM_WRITE_CODE(複合)  FIFO_DATA_VALUE
;異常ﾌﾞｻﾞｰまとめ
LDB B01                  ; 001)非常停止異常
OR B0501                 ; 001)BzSTOP
LDB B02                  ; 002)圧空低下異常
OR B0502                 ; 002)BzSTOP
ANL
LDB B03                  ; 003)安全扉開異常
OR B0503                 ; 003)BzSTOP
ANL
LDB B04                  ; 004)原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OR B0504                 ; 004)BzSTOP
ANL
LDB B05                  ; 005)外部非常停止
OR B0505                 ; 005)BzSTOP
ANL
LDB B06                  ; 006)PLC異常
OR B0506                 ; 006)BzSTOP
ANL
LDB B07                  ; 007)ﾘﾝｸ異常
OR B0507                 ; 007)BzSTOP
ANL
LDB B08                  ; 008)PLCﾊﾞｯﾃﾘ低下
OR B0508                 ; 008)BzSTOP
ANL
LDB B0A                  ; 010)原点未完ﾓｰﾄﾞ切替不可異常
OR B050A                 ; 010)BzSTOP
ANL
LDB B0B                  ; 011)PC通信異常
OR B050B                 ; 011)BzSTOP
ANL
LDB B0C                  ; 012)PCｴﾗｰ
OR B050C                 ; 012)BzSTOP
ANL
LDB B0D                  ; 013)
OR B050D                 ; 013)BzSTOP
ANL
LDB B0D                  ; 013)
OR B050D                 ; 013)BzSTOP
ANL
LDB B0E                  ; 014)
OR B050E                 ; 014)BzSTOP
ANL
LDB B0386                ; 902)原点復帰中
OR CR2002                ; 常時ON
OR B0886                 ; 902)BzSTOP
ANL
LDB B013                 ; 019)
OR B0513                 ; 019)BzSTOP
ANL
LDB B014                 ; 020)
OR B0514                 ; 020)BzSTOP
ANL
LDB B015                 ; 021)
OR B0515                 ; 021)BzSTOP
ANL
LDB B016                 ; 022)
OR B0516                 ; 022)BzSTOP
ANL
LDB M13300               ; PCﾌﾞｻﾞｰ要求(PC_Busy)
OR M13301                ; ﾌﾞｻﾞｰ停止要求
ANL
INV
OUT M200                 ; ﾒｲﾝ異常BZ　まとめ
;<h1/>__□ﾁｬｯｸｽｲｯﾁﾎﾞｯｸｽﾓｰﾄﾞ切替
;（ﾁｬｯｸ開閉のほかにTPで選択した動作ができるようにする）
LDB L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
ANB L31                  ; ﾁｬｯｸﾘﾘｰｽSW:ST2選択
ANB L32                  ; ﾁｬｯｸﾘﾘｰｽSW:ST3選択
ANB L33                  ; ﾁｬｯｸﾘﾘｰｽSW:ST4選択
INV
OUT M160                 ; ﾁｬｯｸSWﾎﾞｯｸｽﾓｰﾄﾞ選択中
LDB M71                  ; 自動運転開始BZ
ANB M30                  ; 自動ﾓｰﾄﾞ
ANB M81                  ; 原点復帰動作中
ANB B0324                ; 804)搬送ﾜｰｸ除去要求
INV
ZRES L30 L33             ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択  ﾁｬｯｸﾘﾘｰｽSW:ST4選択
LDP L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
LDA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
CON
ANDA K1
CON
STA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
LDP L31                  ; ﾁｬｯｸﾘﾘｰｽSW:ST2選択
LDA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
CON
ANDA K2
CON
STA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
LDP L32                  ; ﾁｬｯｸﾘﾘｰｽSW:ST3選択
LDA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
CON
ANDA K4
CON
STA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
LDP L33                  ; ﾁｬｯｸﾘﾘｰｽSW:ST4選択
LDA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
CON
ANDA K8
CON
STA L30                  ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
LD L30                   ; ﾁｬｯｸﾘﾘｰｽSW:ST1選択
MPS
AND X1165                ; ﾁｬｯｸ1_開きSW
OUT M161                 ; ﾁｬｯｸSWﾓｰﾄﾞ1_ST1Buﾎﾞｰﾄﾞ上昇SW
MPP
AND X116D                ; ﾁｬｯｸ2_開きSW
OUT M162                 ; ﾁｬｯｸSWﾓｰﾄﾞ1_ST1Buﾎﾞｰﾄﾞ下降SW
LD L31                   ; ﾁｬｯｸﾘﾘｰｽSW:ST2選択
MPS
AND X1165                ; ﾁｬｯｸ1_開きSW
OUT M163                 ; ﾁｬｯｸSWﾓｰﾄﾞ2_ST2Buﾎﾞｰﾄﾞ上昇SW
MPP
AND X116D                ; ﾁｬｯｸ2_開きSW
OUT M164                 ; ﾁｬｯｸSWﾓｰﾄﾞ2_ST2Buﾎﾞｰﾄﾞ下降SW
LD L32                   ; ﾁｬｯｸﾘﾘｰｽSW:ST3選択
MPS
AND X1165                ; ﾁｬｯｸ1_開きSW
OUT M165                 ; ﾁｬｯｸSWﾓｰﾄﾞ3_ST3Buﾎﾞｰﾄﾞ上昇SW
MPP
AND X116D                ; ﾁｬｯｸ2_開きSW
OUT M166                 ; ﾁｬｯｸSWﾓｰﾄﾞ3_ST3Buﾎﾞｰﾄﾞ下降SW
LD L33                   ; ﾁｬｯｸﾘﾘｰｽSW:ST4選択
MPS
AND X1165                ; ﾁｬｯｸ1_開きSW
OUT M167                 ; ﾁｬｯｸSWﾓｰﾄﾞ4_ST4Buﾎﾞｰﾄﾞ上昇SW
MPP
AND X116D                ; ﾁｬｯｸ2_開きSW
OUT M168                 ; ﾁｬｯｸSWﾓｰﾄﾞ4_ST4Buﾎﾞｰﾄﾞ下降SW
;□ｻｲｸﾙﾀｲﾑ計測
LD M1521                 ; U2-1_S21_
OUT M210                 ; ｻｲｸﾙﾀｲﾑ計測開始
LD M71                   ; 自動運転開始BZ
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC E0                  ; ｻｲｸﾙﾀｲﾑ計測時間
LDP M210                 ; ｻｲｸﾙﾀｲﾑ計測開始
BMOV E0 E1 K99           ; ｻｲｸﾙﾀｲﾑ計測時間  ｻｲｸﾙﾀｲﾑ
MOV K0 E0                ; ｻｲｸﾙﾀｲﾑ計測時間
MPS
AND> K100 E110           ; ｻｲｸﾙﾀｲﾑ計測回数
INC E110                 ; ｻｲｸﾙﾀｲﾑ計測回数
MPP
AVG E1 E110              ; ｻｲｸﾙﾀｲﾑ  ｻｲｸﾙﾀｲﾑ計測回数
CON
STA E112
LD B1002                 ; ｽﾀｰﾄPB
AND B1003                ; ｻｲｸﾙ停止PB
FMOV K0 E0 K120          ; ｻｲｸﾙﾀｲﾑ計測時間
END
ENDH
