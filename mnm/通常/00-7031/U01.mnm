DEVICE:128
;MODULE:U01
;MODULE_TYPE:0
;  ▽_入力受け
LD M1042                 ; U1C1_S042
OR M1600                 ; ST1 給材完了
AND X101                 ; INDEX 旋回戻端
AND M74                  ; 自動運転中
OUT M1600                ; ST1 給材完了
LD M1052                 ; U1C1_S052
OR M1601                 ; ST2 給材完了
AND X100                 ; INDEX 旋回端
AND M74                  ; 自動運転中
OUT M1601                ; ST2 給材完了
;
;<h1/>□_装置状態・動作条件
;  ▽_原点
LD M1800                 ; Cycle1原点
LD M1820                 ; Cycle2原点
AND M1840                ; Cycle3原点
AND M1860                ; Cycle4原点
AND M1880                ; Cycle5原点
OR CR2002                ; 常時ON
ANL
OUT M1980                ; ﾕﾆｯﾄ1原点
;  ▽_即停止異常
LDB M1801                ; Cycle1即停止異常
ANB M1821                ; Cycle2即停止異常
ANB M1841                ; Cycle3即停止異常
ANB M1861                ; Cycle4即停止異常
ANB M1881                ; Cycle5即停止異常
INV
OUT M1981                ; ﾕﾆｯﾄ1即停止異常
;  ▽_原点復帰OFF
LDB M50                  ; 運転準備
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
INV
ORL
LDB T15                  ; 強制停止ONﾀｲﾏ
ANB M3995                ; ﾕﾆｯﾄ3原点復帰中断
INV
ORL
OUT M1983                ; ﾕﾆｯﾄ1_原点復帰起動OFF
;  ▽_原点復帰中
LD M82                   ; 原点復帰開始
ANB M1984                ; ﾕﾆｯﾄ1原点復帰中
OR M1984                 ; ﾕﾆｯﾄ1原点復帰中
ANB M1983                ; ﾕﾆｯﾄ1_原点復帰起動OFF
ANB M1969                ; U1org_StepEnd
OUT M1984                ; ﾕﾆｯﾄ1原点復帰中
;  ▽_サイクル起動中
LDB M1804                ; Cycle1起動中
ANB M1824                ; Cycle2起動中
ANB M1844                ; Cycle3起動中
ANB M1864                ; Cycle4起動中
ANB M1884                ; Cycle5起動中
OUB M1985                ; ﾕﾆｯﾄ1起動中
;  ▽_サイクル停止異常
LDB M1805                ; Cycle1ｻｲｸﾙ停止異常
ANB M1825                ; Cycle2ｻｲｸﾙ停止異常
ANB M1845                ; Cycle3ｻｲｸﾙ停止異常
ANB M1865                ; Cycle4ｻｲｸﾙ停止異常
ANB M1885                ; Cycle5ｻｲｸﾙ停止異常
OUB M1986                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
;  ▽_払い出し後に停止する異常
LDB CR2003               ; 常時OFF
OUB M1987                ; ﾕﾆｯﾄ1払出後停止異常
;  ▽_警報ﾌﾞｻﾞｰ
LDB B030                 ; 048)_ﾜｰｸ給材要求
ANB B032                 ; 050)_ﾜｰｸ除材要求
ANB B034                 ; 052)_INDEX投入部ｹｰｽ検出OFF
ANB B035                 ; 053)_INDEX投入部基板検出OFF
ANB B036                 ; 054)_INDEX投入部ﾌﾀ検出OFF
OUB M1988                ; ﾕﾆｯﾄ1警報補助
;  ▽_機器準備完了
LD CR2002                ; 常時ON
OUT M1990                ; ﾕﾆｯﾄ1機器準備完了
;  ▽_一時停止異常
LDB M1806                ; Cycle1ｴﾗｰ発生(一時停止処理)
ANB M1826                ; Cycle2ｴﾗｰ発生(一時停止処理)
ANB M1846                ; Cycle3ｴﾗｰ発生(一時停止処理)
ANB M1866                ; Cycle4ｴﾗｰ発生(一時停止処理)
ANB M1886                ; Cycle5ｴﾗｰ発生(一時停止処理)
OUB M1991                ; ﾕﾆｯﾄ1ｴﾗｰ発生
;  ▽_ﾕﾆｯﾄ停止中ﾌﾗｸﾞ
LD M1809                 ; Cycle1動作無し完了
LD M1827                 ; Cycle2動作無し
AND M1847                ; Cycle3動作無し
AND M1867                ; Cycle4動作無し
AND M1887                ; Cycle5動作無し
OR CR2002                ; 常時ON
ANL
OUT M1992                ; ﾕﾆｯﾄ1動作無し確認
;  ▽_ﾕﾆｯﾄ1ﾜｰｸ無確認
LDB X139                 ; NG BOX ﾜｰｸ検出
OUT M1993                ; ﾕﾆｯﾄ1ﾜｰｸ無し
LD M1902                 ; U1org_S002
OR M1994                 ; ﾕﾆｯﾄ1原点復帰時NGBOXﾜｰｸ有り
AND M1984                ; ﾕﾆｯﾄ1原点復帰中
OUT M1994                ; ﾕﾆｯﾄ1原点復帰時NGBOXﾜｰｸ有り
;<h1/>□_自動回路
;<h1/>▽_Cycle1
LDB X139                 ; NG BOX ﾜｰｸ検出
OUT M1800                ; Cycle1原点
LDB CR2003               ; 常時OFF
INV
OUT M1801                ; Cycle1即停止異常
LD M50                   ; 運転準備
ANB M1803                ; Cycle1起動OFF
ANB M1804                ; Cycle1起動中
ANB M130                 ; 一時停止
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M1808                ; U1_Cycle1払出完了
OUT M1802                ; Cycle1起動条件
LDB M50                  ; 運転準備
OR M1986                 ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
OR M51                   ; 即停止異常(緊急停止)
OR T15                   ; 強制停止ONﾀｲﾏ
OUT M1803                ; Cycle1起動OFF
LD M73                   ; 各工程起動指令
AND M1802                ; Cycle1起動条件
OR M1804                 ; Cycle1起動中
AND M50                  ; 運転準備
ANB M1803                ; Cycle1起動OFF
ANB M1199                ; U1C1_S199__動作終了
OUT M1804                ; Cycle1起動中
LDB CR2003               ; 常時OFF
INV
OUT M1805                ; Cycle1ｻｲｸﾙ停止異常
LDB B020                 ; 032)NGBOXﾜｰｸ残
ANB B037                 ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
INV
OUT M1806                ; Cycle1ｴﾗｰ発生(一時停止処理)
LD CR2002                ; 常時ON
OUT M1895                ; UT1 ｼﾘﾝﾀﾞ動作中無し
LDB M1000                ; U1C1_S000
ANB M1010                ; U1C1_S010
ANB M1011                ; U1C1_S011
ANB M1041                ; U1C1_S041
ANB M1051                ; U1C1_S051
ANB M1190                ; U1C1_S190
INV
AND M130                 ; 一時停止
OUT M1807                ; Cycle1動作無し
LD M1807                 ; Cycle1動作無し
AND M1895                ; UT1 ｼﾘﾝﾀﾞ動作中無し
ORB M1804                ; Cycle1起動中
OR M1808                 ; U1_Cycle1払出完了
OR M1809                 ; Cycle1動作無し完了
AND M130                 ; 一時停止
OUT M1809                ; Cycle1動作無し完了
;ｻｲｸﾙﾀｲﾑ計測
;  ▽_Cycle1ｽﾃｯﾌﾟ回路
LD CR2002                ; 常時ON
ZRES M1000 M1199         ; U1C1_S000  U1C1_S199__動作終了
LDB M1804                ; Cycle1起動中
MOV K0 D1000             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1804                 ; Cycle1起動中
MOV D1000 Z1             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
SET M1000:Z1             ; U1C1_S000
;<h1/>▽_Cycle1動作回路
LD M1000                 ; U1C1_S000
ANB M130                 ; 一時停止
MOV K10 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S010 INDEXから指令確認
;UT1→UT2:旋回可要求
LD M1010                 ; U1C1_S010
MPS
ANB M130                 ; 一時停止
MPS
LD M2500                 ; U2→U1&U3 旋回可要求受付
OR M2502                 ; UT2→UT1:除材要求(旋回戻側)
OR M2503                 ; UT2→UT1:給材要求(旋回戻側)
OR M2512                 ; UT2→UT1:除材要求(旋回側)
OR M2513                 ; UT2→UT1:給材要求(旋回側)
ANL
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LDB M2500                ; U2→U1&U3 旋回可要求受付
ANB M2985                ; ﾕﾆｯﾄ2起動中
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR M2808                 ; U2_Cycle1払出完了
ANL
ANL
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1500                ; UT1→UT2:旋回可要求
;UT2→UT1:給材要求(旋回戻側):S40へ or 除材要求(旋回戻側):S70へ
;UT2→UT1:給材要求(旋回側):S50へ or 除材要求(旋回側):S80へ
LD M1011                 ; U1C1_S011
ANB M130                 ; 一時停止
MPS
LDB M2502                ; UT2→UT1:除材要求(旋回戻側)
ANB M2512                ; UT2→UT1:除材要求(旋回側)
ANL
MPS
AND M2503                ; UT2→UT1:給材要求(旋回戻側)
MOV K40 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND M2513                ; UT2→UT1:給材要求(旋回側)
MOV K50 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
AND M2502                ; UT2→UT1:除材要求(旋回戻側)
MOV K70 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
AND M2512                ; UT2→UT1:除材要求(旋回側)
MOV K80 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
AND M3503                ; UT3→UT2:旋回要求
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LDB M2985                ; ﾕﾆｯﾄ2起動中
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR M36                   ; 払出ﾓｰﾄﾞ
ANL
ANB M2502                ; UT2→UT1:除材要求(旋回戻側)
ANB M2512                ; UT2→UT1:除材要求(旋回側)
ANL
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S040 給材動作(旋回戻側)
LD M1040                 ; U1C1_S040
MPS
ANB M2503                ; UT2→UT1:給材要求(旋回戻側)
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
@FMOV K0 D20200 K50      ; UT2-1
MPP
OUT M1503                ; UT1→UT2:給材要求受付(旋回戻側)
;UT1→OP:給材要求
;投入部のｹｰｽ、基板、ﾌﾀ確認
LD M1041                 ; U1C1_S041
ANB M130                 ; 一時停止
MPS
LD X110                  ; INDEX投入部ｹｰｽ検出
LD X111                  ; INDEX投入部基板検出
OR M38                   ; 基板無ﾓｰﾄﾞ
ANL
AND X112                 ; INDEX投入部ﾌﾀ検出
OR M35                   ; 空運転ﾓｰﾄﾞ
AND M301                 ; INDEXﾊﾟﾚｯﾄID OK
ANB B037                 ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
ANL
MPS
ANP X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MOV K1 D20201            ; UT2-1_ﾜｰｸ状態(0-10)
MPP
TMR K66 K100             ; 基板投入ｽｲｯﾁ入力待ち(旋回戻)
MRD
OUT M1506                ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
MRD
AND M2661                ; INDEX 旋回要求
MOV K10 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LD B07B                  ; 123)ﾌﾟﾚｽ判定NG
AND M16                  ; ﾘｾｯﾄPB
AND M176                 ; 嵌合NG時   刻印無し
ANL
MOV K100 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LD B07C                  ; 124) BCR読取結果NG
OR B084                  ; 132)_搬送RB_ﾁｬｯｸ異常(50)
AND M16                  ; ﾘｾｯﾄPB
ANL
MOV K110 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LD M36                   ; 払出ﾓｰﾄﾞ
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANL
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1042                 ; U1C1_S042
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S050 給材動作(旋回側)
LD M1050                 ; U1C1_S050
MPS
ANB M2513                ; UT2→UT1:給材要求(旋回側)
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
@FMOV K0 D20250 K50      ; UT2-2
MPP
OUT M1513                ; UT1→UT2:給材要求受付(旋回側)
;UT1→OP:給材要求
;投入部のｹｰｽ、基板、ﾌﾀ確認
LD M1051                 ; U1C1_S051
ANB M130                 ; 一時停止
MPS
LD X110                  ; INDEX投入部ｹｰｽ検出
LD X111                  ; INDEX投入部基板検出
OR M38                   ; 基板無ﾓｰﾄﾞ
ANL
AND X112                 ; INDEX投入部ﾌﾀ検出
OR M35                   ; 空運転ﾓｰﾄﾞ
AND M301                 ; INDEXﾊﾟﾚｯﾄID OK
ANB B037                 ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
ANL
MPS
ANP X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MOV K1 D20251            ; UT2-2_ﾜｰｸ状態(0-10)
MPP
TMR K67 K100             ; 基板投入ｽｲｯﾁ入力待ち(旋回)
MRD
OUT M1516                ; UT1→OP:ﾜｰｸ給材要求(旋回側)
MRD
AND M2671                ; INDEX 旋回戻要求
MOV K10 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LD B07B                  ; 123)ﾌﾟﾚｽ判定NG
AND M16                  ; ﾘｾｯﾄPB
AND M176                 ; 嵌合NG時   刻印無し
ANL
MOV K100 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LD B07C                  ; 124) BCR読取結果NG
OR B084                  ; 132)_搬送RB_ﾁｬｯｸ異常(50)
AND M16                  ; ﾘｾｯﾄPB
ANL
MOV K110 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LD M36                   ; 払出ﾓｰﾄﾞ
OR M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANL
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1052                 ; U1C1_S052
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S070 除材動作(旋回戻側)
LD M1070                 ; U1C1_S070
MPS
ANB M2502                ; UT2→UT1:除材要求(旋回戻側)
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1502                ; UT1→UT2:除材要求受付(旋回戻側)
;UT1→OP:除材要求
;NG BOX ﾜｰｸ検出確認
LD M1071                 ; U1C1_S071
MPS
LD X139                  ; NG BOX ﾜｰｸ検出
ANB X110                 ; INDEX投入部ｹｰｽ検出
ANB X111                 ; INDEX投入部基板検出
ANB X112                 ; INDEX投入部ﾌﾀ検出
LD M124                  ; 基板投入ｴﾘｱｾﾝｻ正常
ANP X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ORP B1013                ; NGﾘｾｯﾄPB
ANL
ANL
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
OUT M1540                ; ｹｰｽ除材完了ﾌﾞｻﾞｰ起動
MRD
AND M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1507                ; UT1→OP:ﾜｰｸ除材要求(旋回戻側)
;UT2-1 ﾃﾞｰﾀｸﾘｱ
LD M1072                 ; U1C1_S072
MPS
LD W01.8                 ; 投入部製品取り出し完了
OR M3                    ; OFFLINE_MODE
ANL
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
ANB M3                   ; OFFLINE_MODE
OUT M1520                ; UT1→PC:投入部製品取出要求
MPP
@FMOV K0 D20200 K50      ; UT2-1
LD M1073                 ; U1C1_S073
ANB X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S080 除材動作(旋回側)
LD M1080                 ; U1C1_S080
MPS
ANB M2512                ; UT2→UT1:除材要求(旋回側)
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1512                ; UT1→UT2:除材要求受付(旋回側)
;UT1→OP:除材要求
;NG BOX ﾜｰｸ検出確認
LD M1081                 ; U1C1_S081
MPS
LD X139                  ; NG BOX ﾜｰｸ検出
ANB X110                 ; INDEX投入部ｹｰｽ検出
ANB X111                 ; INDEX投入部基板検出
ANB X112                 ; INDEX投入部ﾌﾀ検出
LD M124                  ; 基板投入ｴﾘｱｾﾝｻ正常
ANP X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ORP B1013                ; NGﾘｾｯﾄPB
ANL
ANL
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
OUT M1541                ; ｹｰｽ除材完了ﾌﾞｻﾞｰ起動
MRD
AND M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1517                ; UT1→OP:ﾜｰｸ除材要求(旋回側)
;UT2-2 ﾃﾞｰﾀｸﾘｱ
LD M1082                 ; U1C1_S082
MPS
LD W01.8                 ; 投入部製品取り出し完了
OR M3                    ; OFFLINE_MODE
ANL
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
ANB M3                   ; OFFLINE_MODE
OUT M1521                ; UT1→PC:投入部製品取出要求
MPP
@FMOV K0 D20250 K50      ; UT2-2
LD M1083                 ; U1C1_S083
ANB X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
MOV K190 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S100 ﾌﾟﾚｽNG品排出動作
LD M1100                 ; U1C1_S100
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1101                 ; U1C1_S101
ANB X110                 ; INDEX投入部ｹｰｽ検出
ANB X111                 ; INDEX投入部基板検出
ANB X112                 ; INDEX投入部ﾌﾀ検出
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
ANB B07A                 ; 122) INDEX投入部ﾜｰｸ除去要求
AND X105                 ; 基板傾き補正ﾁｬｯｸ 上昇端
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1102                 ; U1C1_S102
MPS
AND M2661                ; INDEX 旋回要求
MOV K80 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
AND M2671                ; INDEX 旋回戻要求
MOV K70 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1523                ; UT1→UT2:旋回要求
;<h1/>▽_S110 INDEX組付部ﾜｰｸ排出動作
LD M1110                 ; U1C1_S110
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1111                 ; U1C1_S111
ANB X110                 ; INDEX投入部ｹｰｽ検出
ANB X111                 ; INDEX投入部基板検出
ANB X112                 ; INDEX投入部ﾌﾀ検出
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
ANB B07A                 ; 122) INDEX投入部ﾜｰｸ除去要求
AND X105                 ; 基板傾き補正ﾁｬｯｸ 上昇端
INC D1000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M1112                 ; U1C1_S112
MPS
AND M2010                ; U2C1_S010
MOV K10 D1000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M1524                ; UT1→UT2:旋回要求
;<h1/>  ▽_ｻｲｸﾙ終了
LD M1190                 ; U1C1_S190
AND M2808                ; U2_Cycle1払出完了
OR M1803                 ; Cycle1起動OFF
OR M1808                 ; U1_Cycle1払出完了
AND M36                  ; 払出ﾓｰﾄﾞ
OUT M1808                ; U1_Cycle1払出完了
LD M1190                 ; U1C1_S190
ANB M130                 ; 一時停止
MOV K199 D1000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>□_原点復帰回路
LD CR2002                ; 常時ON
ZRES M1900 M1969         ; U1org_S000  U1org_StepEnd
LDB M1984                ; ﾕﾆｯﾄ1原点復帰中
MOV K0 D1010             ; 原点復帰ｽﾃｯﾌﾟ
LD M1984                 ; ﾕﾆｯﾄ1原点復帰中
MOV D1010 Z1             ; 原点復帰ｽﾃｯﾌﾟ
SET M1900:Z1             ; U1org_S000
;  ▽_原点復帰開始
LD M1900                 ; U1org_S000
INC D1010                ; 原点復帰ｽﾃｯﾌﾟ
;NGBOXﾜｰｸ確認
LD M1901                 ; U1org_S001 
MPS
ANB X139                 ; NG BOX ﾜｰｸ検出
MOV K31 D1010            ; 原点復帰ｽﾃｯﾌﾟ
MPP
AND X139                 ; NG BOX ﾜｰｸ検出
INC D1010                ; 原点復帰ｽﾃｯﾌﾟ
LD M1902                 ; U1org_S002
AND M1994                ; ﾕﾆｯﾄ1原点復帰時NGBOXﾜｰｸ有り
MOV K31 D1010            ; 原点復帰ｽﾃｯﾌﾟ
;終了
LD M1931                 ; U1org_S031
AND M1980                ; ﾕﾆｯﾄ1原点
ANB M82                  ; 原点復帰開始
MOV K69 D1010            ; 原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M1984                ; ﾕﾆｯﾄ1原点復帰中
ANB M1985                ; ﾕﾆｯﾄ1起動中
MC
LDP B10A2                ; UNIT2-1-3 TP-PB
LD M1701                 ; MUN_照明ON指令
ANB M1703                ; MUN_照明OFF指令
ORL
OUT M1701                ; MUN_照明ON指令
LDP B10A3                ; UNIT2-1-4 TP-PB
LD M1703                 ; MUN_照明OFF指令
ANB M1701                ; MUN_照明ON指令
ORL
OUT M1703                ; MUN_照明OFF指令
MCR
;
;<h1/>□_出力回路
;<h1/>____▼_SOL
LDB M74                  ; 自動運転中
ANB M1701                ; MUN_照明ON指令
INV
LD Y5F                   ; 投入部照明ON
ANB M1703                ; MUN_照明OFF指令
ANFB M74                 ; 自動運転中
ORL
AND L29                  ; KTR仕様
OUT Y5F                  ; 投入部照明ON
LD M50                   ; 運転準備
AND M123                 ; 安全扉正常
ANB M51                  ; 即停止異常(緊急停止)
MC
;<h1/>____▼_ﾗﾝﾌﾟ出力
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
ANB T66                  ; 基板投入ｽｲｯﾁ入力待ち(旋回戻)
LD M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
ANB T67                  ; 基板投入ｽｲｯﾁ入力待ち(旋回)
ORL
LD CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
LD T66                   ; 基板投入ｽｲｯﾁ入力待ち(旋回戻)
OR T67                   ; 基板投入ｽｲｯﾁ入力待ち(旋回)
OR M1507                 ; UT1→OP:ﾜｰｸ除材要求(旋回戻側)
OR M1517                 ; UT1→OP:ﾜｰｸ除材要求(旋回側)
OR B07A                  ; 122) INDEX投入部ﾜｰｸ除去要求
ANL
ORL
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
OUT Y30C                 ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ 緑点灯
LDB T105                 ; INDEX旋回確認ﾀｲﾏ
ANB T106                 ; INDEX旋回戻確認ﾀｲﾏ
OUT M1545                ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ赤点滅
LD M71                   ; 自動運転開始BZ
ANB M1506                ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
ANB M1507                ; UT1→OP:ﾜｰｸ除材要求(旋回戻側)
ANB M1516                ; UT1→OP:ﾜｰｸ給材要求(旋回側)
ANB M1517                ; UT1→OP:ﾜｰｸ除材要求(旋回側)
ANB B07A                 ; 122) INDEX投入部ﾜｰｸ除去要求
OUT M1546                ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ赤点灯
LDB M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
LD T105                  ; INDEX旋回確認ﾀｲﾏ
OR T106                  ; INDEX旋回戻確認ﾀｲﾏ
LD M1545                 ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ赤点滅
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
LD M124                  ; 基板投入ｴﾘｱｾﾝｻ正常
LD M1546                 ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ赤点灯
LD M31                   ; 手動ﾓｰﾄﾞ
ANB T105                 ; INDEX旋回確認ﾀｲﾏ
ANB T106                 ; INDEX旋回戻確認ﾀｲﾏ
ORL
ANL
ORL
OUT Y30D                 ; ｹｰｽ･基板投入口 ﾗｲﾄｶｰﾃﾝ 赤点灯
LDB X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ANB M1600                ; ST1 給材完了
ANB M1601                ; ST2 給材完了
INV
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
OR M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
AND M301                 ; INDEXﾊﾟﾚｯﾄID OK
AND X110                 ; INDEX投入部ｹｰｽ検出
LD X111                  ; INDEX投入部基板検出
OR M38                   ; 基板無ﾓｰﾄﾞ
ANL
AND X112                 ; INDEX投入部ﾌﾀ検出
AND M124                 ; 基板投入ｴﾘｱｾﾝｻ正常
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT Y317                 ; ｹｰｽ･基板投入 ｽｲｯﾁﾗﾝﾌﾟ
LD Y5F                   ; 投入部照明ON
OUT B15A2
LDB Y5F                  ; 投入部照明ON
OUT B15A3
;ｽｲｯﾁ入力ﾌﾞｻﾞｰ
LDP M1042                ; U1C1_S042
ORP M1052                ; U1C1_S052
OR M1533                 ; ｹｰｽ投入完了記憶
SHOT K20 M1533           ; ｹｰｽ投入完了記憶
FLIK K5 K5 M1532         ; ｹｰｽ投入完了ﾌﾞｻﾞｰ
LDP M1540                ; ｹｰｽ除材完了ﾌﾞｻﾞｰ起動
ORP M1541                ; ｹｰｽ除材完了ﾌﾞｻﾞｰ起動
OR M1535                 ; ｹｰｽ除材完了記憶
SHOT K20 M1535           ; ｹｰｽ除材完了記憶
FLIK K5 K5 M1534         ; ｹｰｽ除材完了ﾌﾞｻﾞｰ
MCR
;<h1/>____▼_Tp出力
;<h1/>____▼_PC出力
LDB M1520                ; UT1→PC:投入部製品取出要求
ANB M1521                ; UT1→PC:投入部製品取出要求
INV
OUT W0101.8              ; 投入部製品取り出し要求
;<h1/>□_異常回路
;原点復帰時NGBOXﾜｰｸ残
LD M2912                 ; Org_S12
AND M1994                ; ﾕﾆｯﾄ1原点復帰時NGBOXﾜｰｸ有り
LD M85                   ; 原点復帰完了
AND X139                 ; NG BOX ﾜｰｸ検出
ORL
LD B020                  ; 032)NGBOXﾜｰｸ残
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B020                 ; 032)NGBOXﾜｰｸ残
LDP B020                 ; 032)NGBOXﾜｰｸ残
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0020 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B021                  ; 033)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B021                 ; 033)
LDP B021                 ; 033)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0021 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B022                  ; 034)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B022                 ; 034)
LDP B022                 ; 034)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0022 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B023                  ; 035)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B023                 ; 035)
LDP B023                 ; 035)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0023 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B024                  ; 036)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B024                 ; 036)
LDP B024                 ; 036)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0024 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;<h1/>□_警報回路
;ﾜｰｸ給材要求
LD T66                   ; 基板投入ｽｲｯﾁ入力待ち(旋回戻)
OR T67                   ; 基板投入ｽｲｯﾁ入力待ち(旋回)
OUT B030                 ; 048)_ﾜｰｸ給材要求
LDP B030                 ; 048)_ﾜｰｸ給材要求
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0030 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
OUT B031                 ; 049)_
LDP B031                 ; 049)_
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0031 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾜｰｸ除材要求
LD M1507                 ; UT1→OP:ﾜｰｸ除材要求(旋回戻側)
OR M1517                 ; UT1→OP:ﾜｰｸ除材要求(旋回側)
OUT B032                 ; 050)_ﾜｰｸ除材要求
LDP B032                 ; 050)_ﾜｰｸ除材要求
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0032 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B033                  ; 051)_
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B033                 ; 051)_
LDP B033                 ; 051)_
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0033 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;INDEX投入部ｹｰｽ検出OFF
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
OR M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
AND X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ANB X110                 ; INDEX投入部ｹｰｽ検出
LD B034                  ; 052)_INDEX投入部ｹｰｽ検出OFF
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB M1042                ; U1C1_S042
ANB M1052                ; U1C1_S052
ORL
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT B034                 ; 052)_INDEX投入部ｹｰｽ検出OFF
LDP B034                 ; 052)_INDEX投入部ｹｰｽ検出OFF
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0034 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;INDEX投入部基板検出OFF
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
OR M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
AND X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ANB X111                 ; INDEX投入部基板検出
LD B035                  ; 053)_INDEX投入部基板検出OFF
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB M1042                ; U1C1_S042
ANB M1052                ; U1C1_S052
ORL
ANB M35                  ; 空運転ﾓｰﾄﾞ
ANB M38                  ; 基板無ﾓｰﾄﾞ
OUT B035                 ; 053)_INDEX投入部基板検出OFF
LDP B035                 ; 053)_INDEX投入部基板検出OFF
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0035 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;INDEX投入部ﾌﾀ検出OFF
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
OR M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
AND X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ANB X112                 ; INDEX投入部ﾌﾀ検出
LD B036                  ; 054)_INDEX投入部ﾌﾀ検出OFF
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB M1042                ; U1C1_S042
ANB M1052                ; U1C1_S052
ORL
ANB M35                  ; 空運転ﾓｰﾄﾞ
OUT B036                 ; 054)_INDEX投入部ﾌﾀ検出OFF
LDP B036                 ; 054)_INDEX投入部ﾌﾀ検出OFF
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0036 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;INDEX投入部ﾊﾟﾚｯﾄID不一致
LD M1506                 ; UT1→OP:ﾜｰｸ給材要求(旋回戻側)
OR M1516                 ; UT1→OP:ﾜｰｸ給材要求(旋回側)
AND X137                 ; ｹｰｽ･基板投入ｽｲｯﾁ入力
ANB M301                 ; INDEXﾊﾟﾚｯﾄID OK
LD M73                   ; 各工程起動指令
LD T105                  ; INDEX旋回確認ﾀｲﾏ
OR T106                  ; INDEX旋回戻確認ﾀｲﾏ
ANL
ANB M301                 ; INDEXﾊﾟﾚｯﾄID OK
AND T48                  ; INDEXﾊﾟﾚｯﾄID確認ﾃﾞｨﾚｲ
ORL
LD M2912                 ; Org_S12
AND M2996                ; ﾕﾆｯﾄ2原点復帰時ﾊﾟﾚｯﾄID異常
ORL
LD B037                  ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ANB M1042                ; U1C1_S042
ANB M1052                ; U1C1_S052
ORL
OUT B037                 ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
LDP B037                 ; 055)_INDEX投入部ﾊﾟﾚｯﾄID不一致
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H0037 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾌﾞｻﾞｰ発生回路
LD<>.D B020 D1012        ; 032)NGBOXﾜｰｸ残  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
AND<>.D B020 K0          ; 032)NGBOXﾜｰｸ残
OUT M1989                ; ﾕﾆｯﾄ1異常BZ補助
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
MOV.D B020 D1012         ; 032)NGBOXﾜｰｸ残  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
LD=.D B020 K0            ; 032)NGBOXﾜｰｸ残
@MOV.D K0 D1012          ; ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
END
ENDH
