DEVICE:128
;MODULE:IAI_CON_MOVE
;MODULE_TYPE:1
;SCRIPT_TYPE:
;SupportMacro
;
;-----------------------------------------
;Macro Name 	: 	IAI_CON_MOVE
;Macro Type	:	Self Holding
;Function	:	IAI CON系ﾎﾟｼﾞｼｮﾝ移動ﾏｸﾛ
;-----------------------------------------
;引数：
;	P0  =	移動ﾎﾟｼﾞｼｮﾝNo.格納		    DM番号で指定	P0 = Leading Address of Point data storage
;	P1	=	RB入力先頭番地				DM番号で指定	P1 = Input Leading Address for RB
;	P2  =	RB出力先頭番地				DM番号で指定	P2 = Output Leading Address for RB
;	P3  =   完了フラグ
;	P4  =   完了コード									P4 = Macro Complete Code
;			#20 	= 正常終了								#20 = No Error
;			#21 	= RB応答なし							#21 = Non Responce
;			#22 	= ｽﾃｰﾀｽ異常								#22 = Status Abnormal
;			#23 	= ｱﾗｰﾑ発生
;	P5  =   非常停止ﾌﾗｸﾞ                            	P5 = Emergency Stop Flag
;	V1  =   ﾀｲﾑｱｳﾄ設定                              	V1 = Time out Value
;機能：
;	ACON/PCON/SCON用ﾎﾟｼﾞｼｮﾝ移動指令ﾏｸﾛ/ Robot Move Macro for Position Move Mode
;初期化　/ Initialize
LD @CR2008
DW K0 P4                 ; C_CODE
CON
ZRES @M2 @M15            ; 移動ﾎﾟｼﾞｼｮﾝNo設定
CON
SET @M0
;RBｽﾃｰﾀｽﾓﾆﾀｰ / Status Monitoring
BLD P1:#3 K3             ; RB_IN
OUT @M14                 ; ｱﾗｰﾑ発生
BLD P1:#3 K1             ; RB_IN
BAND P1:#3 K4            ; RB_IN
BANB P1:#3 K8            ; RB_IN
BAND P1:#3 K14           ; RB_IN
BANB P1:#3 K15           ; RB_IN
OUT @M1                  ; RB移動許可
;ｱﾗｰﾑ発生
LDP @M14                 ; ｱﾗｰﾑ発生
DW K23 P4                ; C_CODE
CON
ZRES @M0 @M15
CON
SET P3                   ; END_FLAG
;RB状態ﾁｪｯｸ /　Robot Status Check
STG @M0
MPS
LD @M1                   ; RB移動許可
BANB P1:#3 K2            ; RB_IN
ANL
JMP @M2                  ; 移動ﾎﾟｼﾞｼｮﾝNo設定
MPP
ANB @M1                  ; RB移動許可
TMR @0 V1                ; TimeOut
CON
AND @T0
ANB CR2003               ; 常時OFF
DW K22 P4                ; C_CODE
CON
JMP @M15
;P2.U:2 = P0.U   '移動ﾎﾟｼﾞｼｮﾝNo.
;SET(@MR003)     'ﾃﾞｰﾀ格納 完了ﾌﾗｸﾞ
LD @M2                   ; 移動ﾎﾟｼﾞｼｮﾝNo設定
NCJ K1000
;P2.U:2 = P0.U   '移動ﾎﾟｼﾞｼｮﾝNo.
;
LD CR2002                ; 常時ON
MOV P0 P2:#2             ; POSNO_DATA  RB_OUT
;SET(@MR003)     'ﾃﾞｰﾀ格納 完了ﾌﾗｸﾞ
LD CR2002                ; 常時ON
SET @M3                  ; ﾃﾞｰﾀ格納 完了
LABEL K1000
;
STG @M2                  ; 移動ﾎﾟｼﾞｼｮﾝNo設定
AND @M3                  ; ﾃﾞｰﾀ格納 完了
TMS @1 K5
CON
AND @T1
BSET P2:#3 K0            ; RB_OUT
CON
RES @M3                  ; ﾃﾞｰﾀ格納 完了
CON
MPS
BAND P1:#3 K0            ; RB_IN
JMP @M4                  ; 移動開始1
MPP
BANB P1:#3 K0            ; RB_IN
JMP @M5                  ; 移動開始2
STG @M4                  ; 移動開始1
MPS
BANB P1:#3 K0            ; RB_IN
BRES P2:#3 K0            ; RB_OUT
CON
JMP @M6
MPP
BAND P1:#3 K0            ; RB_IN
TMR @2 V1                ; TimeOut
CON
AND @T2
ANB CR2003               ; 常時OFF
BRES P2:#3 K0            ; RB_OUT
CON
DW K21 P4                ; C_CODE
CON
JMP @M15
STG @M5                  ; 移動開始2
MPS
BAND P1:#3 K2            ; RB_IN
BRES P2:#3 K0            ; RB_OUT
CON
JMP @M6
MPP
BAND P1:#3 K0            ; RB_IN
TMR @5 V1                ; TimeOut
CON
AND @T5
ANB CR2003               ; 常時OFF
BRES P2:#3 K0            ; RB_OUT
CON
DW K21 P4                ; C_CODE
CON
JMP @M15
STG @M6
MPS
BLDB P1:#3 K2            ; RB_IN
BAND P1:#3 K0            ; RB_IN
ANL
TMH @4 K10
CON
AND @T4
JMP @M7
MPP
BANB P1:#3 K0            ; RB_IN
TMR @3 V1                ; TimeOut
CON
AND @T3
ANB CR2003               ; 常時OFF
BRES P2:#3 K0            ; RB_OUT
CON
DW K21 P4                ; C_CODE
CON
JMP @M15
STG @M7
DW K20 P4                ; C_CODE
CON
JMP @M15
STG @M15
SET P3                   ; END_FLAG
CON
ENDS
;終了処理
LDB P5                   ; EMG_FLAG
ZRES @M0 @M15
BRES P2:#3 K0            ; RB_OUT
LD P3                    ; END_FLAG
ORB P5                   ; EMG_FLAG
MEND
END
ENDH
