DEVICE:128
;MODULE:U05
;MODULE_TYPE:0
;  ▽_入力受け
LD Y52                   ; 集塵機 POWER ON
AND Y58                  ; 集塵機 運転
ANB M171                 ; 集塵機無効ﾓｰﾄﾞ
TMR K251 K300            ; 集塵機起動待ち
LD X05                   ; 集塵機差圧計OUT1
OR M171                  ; 集塵機無効ﾓｰﾄﾞ
TMR K252 K10             ; 集塵機差圧計OUT1
;<h1/>□_装置状態・動作条件
;  ▽_原点
LD M5800                 ; Cycle1原点
AND M5820                ; Cycle2原点
LD M5840                 ; Cycle3原点
AND M5860                ; Cycle4原点
AND M5880                ; Cycle5原点
OR CR2002                ; 常時ON
ANL
OUT M5980                ; ﾕﾆｯﾄ5原点
;  ▽_即停止異常
LDB M5801                ; Cycle1即停止異常
ANB M5821                ; Cycle2即停止異常
ANB M5841                ; Cycle3即停止異常
ANB M5861                ; Cycle4即停止異常
ANB M5881                ; Cycle5即停止異常
INV
OUT M5981                ; ﾕﾆｯﾄ5即停止異常
;  ▽_原点復帰OFF
LDB M50                  ; 運転準備
LDB M51                  ; 即停止異常(緊急停止)
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M60                  ; ｴﾗｰ発生
INV
ORL
LDB T15                  ; 強制停止ONﾀｲﾏ
ANB M3995                ; ﾕﾆｯﾄ3原点復帰中断
INV
ORL
OUT M5983                ; ﾕﾆｯﾄ5_原点復帰起動OFF
;  ▽_原点復帰中
LD M82                   ; 原点復帰開始
ANB M5984                ; ﾕﾆｯﾄ5原点復帰中
OR M5984                 ; ﾕﾆｯﾄ5原点復帰中
ANB M5983                ; ﾕﾆｯﾄ5_原点復帰起動OFF
ANB M5969                ; U5org_StepEnd
OUT M5984                ; ﾕﾆｯﾄ5原点復帰中
;  ▽_サイクル起動中
LDB M5804                ; Cycle1起動中
ANB M5824                ; Cycle2起動中
ANB M5844                ; Cycle3起動中
ANB M5864                ; Cycle4起動中
ANB M5884                ; Cycle5起動中
OUB M5985                ; ﾕﾆｯﾄ5起動中
;  ▽_サイクル停止異常
LDB M5805                ; Cycle1ｻｲｸﾙ停止異常
ANB M5825                ; Cycle2ｻｲｸﾙ停止異常
ANB M5845                ; Cycle3ｻｲｸﾙ停止異常
ANB M5865                ; Cycle4ｻｲｸﾙ停止異常
ANB M5885                ; Cycle5ｻｲｸﾙ停止異常
OUB M5986                ; ﾕﾆｯﾄ5ｻｲｸﾙ停止異常
;  ▽_払い出し後に停止する異常
LDB B0A1                 ; 161)_ｸﾘｰﾝｴｱﾕﾆｯﾄｱﾗｰﾑ
OUB M5987                ; ﾕﾆｯﾄ5払出後停止異常
;  ▽_警報ﾌﾞｻﾞｰ
LDB B0A2                 ; 162)_ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
ANB B0A5                 ; 165)_集塵機 警報出力
ANB B0A6                 ; 166)_ﾚｰｻﾞｰﾏｰｶ警報
OUB M5988                ; ﾕﾆｯﾄ5警報補助
;  ▽_機器準備完了
LD CR2002                ; 常時ON
OUT M5990                ; ﾕﾆｯﾄ5機器準備完了
;  ▽_一時停止異常
LDB M5806                ; Cycle1ｴﾗｰ発生(一時停止処理)
ANB M5826                ; Cycle2ｴﾗｰ発生(一時停止処理)
ANB M5846                ; Cycle3ｴﾗｰ発生(一時停止処理)
ANB M5866                ; Cycle4ｴﾗｰ発生(一時停止処理)
ANB M5886                ; Cycle5ｴﾗｰ発生(一時停止処理)
OUB M5991                ; ﾕﾆｯﾄ5ｴﾗｰ発生
;  ▽_ﾕﾆｯﾄ停止中ﾌﾗｸﾞ
LD M5809                 ; Cycle1動作無し完了
AND M5827                ; Cycle2動作無し
LD M5847                 ; Cycle3動作無し
AND M5867                ; Cycle4動作無し
AND M5887                ; Cycle5動作無し
OR CR2002                ; 常時ON
ANL
OUT M5992                ; ﾕﾆｯﾄ5動作無し確認
;  ▽_ﾕﾆｯﾄ1ﾜｰｸ無確認
LD CR2002                ; 常時ON
OUT M5993                ; ﾕﾆｯﾄ5ﾜｰｸ無し
LD M5902                 ; U5org_S002
OR M5994                 ; ﾕﾆｯﾄ5原点復帰時ｶﾊﾞｰID異常
AND M5984                ; ﾕﾆｯﾄ5原点復帰中
OUT M5994                ; ﾕﾆｯﾄ5原点復帰時ｶﾊﾞｰID異常
LD M5904                 ; U5org_S004
OR M5995                 ; ﾕﾆｯﾄ5原点復帰時保護ｶﾞﾗｽ異常
AND M5984                ; ﾕﾆｯﾄ5原点復帰中
OUT M5995                ; ﾕﾆｯﾄ5原点復帰時保護ｶﾞﾗｽ異常
;<h1/>□_自動回路
;<h1/>▽_Cycle1
LDB Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
AND M302                 ; 刻印部ｶﾊﾞｰID OK
AND X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
OUT M5800                ; Cycle1原点
LDB B0A0                 ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
ANB B0A4                 ; 164)_集塵機 運転中信号OFF異常
INV
OUT M5801                ; Cycle1即停止異常
LD M50                   ; 運転準備
ANB M5803                ; Cycle1起動OFF
ANB M5804                ; Cycle1起動中
ANB M130                 ; 一時停止
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M5808                ; U5_Cycle1払出完了
OUT M5802                ; Cycle1起動条件
LDB M50                  ; 運転準備
OR M5986                 ; ﾕﾆｯﾄ5ｻｲｸﾙ停止異常
OR M51                   ; 即停止異常(緊急停止)
OR T15                   ; 強制停止ONﾀｲﾏ
OUT M5803                ; Cycle1起動OFF
LD M73                   ; 各工程起動指令
AND M5802                ; Cycle1起動条件
OR M5804                 ; Cycle1起動中
AND M50                  ; 運転準備
ANB M5803                ; Cycle1起動OFF
ANB M5199                ; U5C1_S199__動作終了
OUT M5804                ; Cycle1起動中
LDB CR2003               ; 常時OFF
INV
OUT M5805                ; Cycle1ｻｲｸﾙ停止異常
LDB B0A3                 ; 163)_集塵機吸引圧力異常
ANB B0A7                 ; 167)刻印部ｶﾊﾞｰID異常
ANB B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
ANB B0AA                 ; 170)ﾚｰｻﾞｰ刻印不良
INV
OUT M5806                ; Cycle1ｴﾗｰ発生(一時停止処理)
LD CR2002                ; 常時ON
OUT M5895                ; UT5 ｼﾘﾝﾀﾞ動作中無し
LDB M5000                ; U5C1_S000
ANB M5011                ; U5C1_S011
ANB M5012                ; U5C1_S012
ANB M5014                ; U5C1_S014
ANB M5110                ; U5C1_S110
ANB M5195                ; U5C1_S195
ANB M5024                ; U5C1_S024
INV
AND M130                 ; 一時停止
OUT M5807                ; Cycle1動作無し
LD M5807                 ; Cycle1動作無し
AND M5895                ; UT5 ｼﾘﾝﾀﾞ動作中無し
ORB M5804                ; Cycle1起動中
OR M5808                 ; U5_Cycle1払出完了
OR M5809                 ; Cycle1動作無し完了
AND M130                 ; 一時停止
OUT M5809                ; Cycle1動作無し完了
;ｻｲｸﾙﾀｲﾑ計測
LD M5804                 ; Cycle1起動中
AND>= D5000 K14          ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
AND<= D5000 K21          ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
AND CR2004               ; 10msｸﾛｯｸﾊﾟﾙｽ
@INC D5900               ; ｻｲｸﾙﾀｲﾑ
LDP M5804                ; Cycle1起動中
BMOV D5900 D5901 K10     ; ｻｲｸﾙﾀｲﾑ  ｻｲｸﾙﾀｲﾑ Log1
DW K0 D5900              ; ｻｲｸﾙﾀｲﾑ
;  ▽_Cycle1ｽﾃｯﾌﾟ回路
LD CR2002                ; 常時ON
ZRES M5000 M5199         ; U5C1_S000  U5C1_S199__動作終了
LDB M5804                ; Cycle1起動中
MOV K0 D5000             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M5804                 ; Cycle1起動中
MOV D5000 Z1             ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
SET M5000:Z1             ; U5C1_S000
;<h1/>▽_Cycle1動作回路
;集塵機ON
LD M5000                 ; U5C1_S000
ANB M130                 ; 一時停止
MPS
LD Y52                   ; 集塵機 POWER ON
AND Y58                  ; 集塵機 運転
LD T252                  ; 集塵機差圧計OUT1
OR M35                   ; 空運転ﾓｰﾄﾞ
OR M171                  ; 集塵機無効ﾓｰﾄﾞ
ANL
OR M171                  ; 集塵機無効ﾓｰﾄﾞ
ANL
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5761                ; 集塵機 運転 ON要求
LD M5001                 ; U5C1_S001
MOV K10 D5000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S010 ﾜｰｸ 刻印
LD M5010                 ; U5C1_S010
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;搬送RB 刻印位置移動
LD M5011                 ; U5C1_S011
MPS
ANB M130                 ; 一時停止
MPS
AND M4490                ; UT4→UT5:刻印位置移動完了
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MRD
LDB M4985                ; ﾕﾆｯﾄ4起動中
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LD M4808                 ; U4_Cycle1払出完了
AND M36                  ; 払出ﾓｰﾄﾞ
ORL
ANL
ANL
MOV K190 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
ANB Y58                  ; 集塵機 運転
OUT M5762                ; 集塵機 運転 ON要求
MPP
LD X102                  ; 搬送RBﾁｬｯｸ 閉端
AND X11C                 ; 搬送RBﾁｬｯｸ 在荷確認
LD Y302                  ; 搬送RBﾁｬｯｸ 閉
AND M35                  ; 空運転ﾓｰﾄﾞ
ORL
AND= D20301 K12          ; UT4_ﾜｰｸ状態(0-10)
AND= D20302 K1           ; UT4_BCROK/NG(1:OK 2:NG)
LD= D20303 K1            ; UT4_ﾌﾟﾚｽOK/NG(1:OK 2:NG)
OR CR2002                ; 常時ON
ANL
ANL
OUT M5615                ; U5→U4 刻印位置移動要求
LD M5012                 ; U5C1_S012
ANB M130                 ; 一時停止
ANB M4490                ; UT4→UT5:刻印位置移動完了
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;刻印ﾜｰｸ確認
LD M5013                 ; U5C1_S013
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;刻印開始
LD M5014                 ; U5C1_S014
OR M5580                 ; W1刻印要求
AND M5804                ; Cycle1起動中
ANB M5585                ; W1刻印完了確認
OUT M5580                ; W1刻印要求
LD M5014                 ; U5C1_S014
ANB M130                 ; 一時停止
AND M5595                ; W1刻印中
MOV K20 D5000            ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;刻印完了待ち
LD M5020                 ; U5C1_S020
AND M5590                ; W1刻印完了
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M5021                 ; U5C1_S021
MPS
LDB M5580                ; W1刻印要求
ANB M5590                ; W1刻印完了
ANB M5595                ; W1刻印中
ANL
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5585                ; W1刻印完了確認
LD M5022                 ; U5C1_S022
MPS
AND= D20304 K1           ; UT4_印字OK/NG(1:OK 2:NG)
MOV K110 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
AND= D20304 K2           ; UT4_印字OK/NG(1:OK 2:NG)
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;◇ﾚｰｻﾞｰ刻印異常 / Laser marking NG
LD M5023                 ; U5C1_S023
AND B0AA                 ; 170)ﾚｰｻﾞｰ刻印不良
INC D5000                ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
LD M5024                 ; U5C1_S024
ANB M130                 ; 一時停止
ANB B0AA                 ; 170)ﾚｰｻﾞｰ刻印不良
MOV K110 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_S110 排出要求
LD M5110                 ; U5C1_S110
MPS
LDB M130                 ; 一時停止
LD M4492                 ; UT4→UT5:OK1移動完了
OR M4493                 ; UT4→UT5:OK2移動完了
OR M4494                 ; UT4→UT5:OK3移動完了
OR M4495                 ; UT4→UT5:NG1移動完了
OR M4496                 ; UT4→UT5:NG2移動完了
ANL
ANL
MOV K190 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5619                ; U5→U4 排出移動要求
;<h1/>▽_S0190 終了処理
LD M5190                 ; U5C1_S190
MOV K195 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_ｻｲｸﾙ終了
LD M5195                 ; U5C1_S195
AND M4808                ; U4_Cycle1払出完了
OR M5803                 ; Cycle1起動OFF
OR M5808                 ; U5_Cycle1払出完了
AND M36                  ; 払出ﾓｰﾄﾞ
OUT M5808                ; U5_Cycle1払出完了
LD M5195                 ; U5C1_S195
ANB M130                 ; 一時停止
MOV K199 D5000           ; Cycle1ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>▽_Cycle2
LD CR2002                ; 常時ON
OUT M5820                ; Cycle2原点
LDB CR2003               ; 常時OFF
INV
OUT M5821                ; Cycle2即停止異常
LD M50                   ; 運転準備
ANB M5823                ; Cycle2起動OFF
ANB M5824                ; Cycle2起動中
ANB M130                 ; 一時停止
ANB M56                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M5808                ; U5_Cycle1払出完了
OUT M5822                ; Cycle2起動条件
LDB M50                  ; 運転準備
OR M5986                 ; ﾕﾆｯﾄ5ｻｲｸﾙ停止異常
OR M51                   ; 即停止異常(緊急停止)
OR T15                   ; 強制停止ONﾀｲﾏ
OUT M5823                ; Cycle2起動OFF
LD M73                   ; 各工程起動指令
AND M5822                ; Cycle2起動条件
OR M5824                 ; Cycle2起動中
ANB M5823                ; Cycle2起動OFF
ANB M5299                ; U5C2_S099__動作終了
OUT M5824                ; Cycle2起動中
LDB B0A0                 ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
INV
OUT M5825                ; Cycle2ｻｲｸﾙ停止異常
LDB B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
INV
OUT M5826                ; Cycle2ｴﾗｰ発生(一時停止処理)
LDB M5200                ; U5C2_S000
ANB M5213                ; U5C2_S013
ANB M5290                ; U5C2_S090
INV
ORB M5824                ; Cycle2起動中
AND M130                 ; 一時停止
OUT M5827                ; Cycle2動作無し
LD M5827                 ; Cycle2動作無し
ORB M5824                ; Cycle2起動中
OR M5828                 ; Cycle2動作無し完了
AND M130                 ; 一時停止
OUT M5828                ; Cycle2動作無し完了
;  ▽_Cycle2ｽﾃｯﾌﾟ回路
LD CR2002                ; 常時ON
ZRES M5200 M5299         ; U5C2_S000  U5C2_S099__動作終了
LDB M5824                ; Cycle2起動中
MOV K0 D5001             ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
LD M5824                 ; Cycle2起動中
MOV D5001 Z1             ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
SET M5200:Z1             ; U5C2_S000
;<h1/>▽_Cycle2動作回路
;Mode check
LD M5200                 ; U5C2_S000
ANB M130                 ; 一時停止
MPS
AND M5580                ; W1刻印要求
MOV K10 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LDB M5580                ; W1刻印要求
LD M5808                 ; U5_Cycle1払出完了
LD M52                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M5804                ; Cycle1起動中
ORL
ANL
ANL
MOV K90 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
LD>= D5001 K10           ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
AND<= D5001 K29          ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
OUT M5595                ; W1刻印中
;<h1/>▽_S010 刻印
;刻印ﾜｰｸﾁｪｯｸ
LD M5210                 ; U5C2_S010
AND W1011.14             ; BOARD_PP_RB_ﾜｰｸ刻印完了(36)
MPS
LD<> D20301 K12          ; UT4_ﾜｰｸ状態(0-10)
OR<> D20302 K1           ; UT4_BCROK/NG(1:OK 2:NG)
LD<> D20303 K1           ; UT4_ﾌﾟﾚｽOK/NG(1:OK 2:NG)
AND CR2003               ; 常時OFF
ORL
ANL
MOV K26 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
LD= D20301 K12           ; UT4_ﾜｰｸ状態(0-10)
AND= D20302 K1           ; UT4_BCROK/NG(1:OK 2:NG)
LD= D20303 K1            ; UT4_ﾌﾟﾚｽOK/NG(1:OK 2:NG)
OR CR2002                ; 常時ON
ANL
ANL
INC D5001                ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
;ﾚｰｻﾞｰ保護ｶﾞﾗｽ装着確認
LD M5211                 ; U5C2_S011
MPS
LD X133                  ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
LD T252                  ; 集塵機差圧計OUT1
OR M35                   ; 空運転ﾓｰﾄﾞ
OR M171                  ; 集塵機無効ﾓｰﾄﾞ
ANL
LD X132                  ; ﾚｰｻﾞｰﾏｰｶ ﾄﾘｶﾞﾚﾃﾞｨ
OR M170                  ; LASER MARKER無効ﾓｰﾄﾞ
ANL
AND M302                 ; 刻印部ｶﾊﾞｰID OK
ANL
MOV K20 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
ANB X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
INC D5001                ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
;◇ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF
LD M5212                 ; U5C2_S012
AND B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
INC D5001                ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
LD M5213                 ; U5C2_S013
ANB M130                 ; 一時停止
ANB B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
MOV K11 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
;刻印部ｴｱﾌﾞﾛｰ / Air blow ON
LD M5220                 ; U5C2_S020
MPS
AND Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
INC D5001                ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5661                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
;刻印
;ﾜｰｸ状態=13:印字完了
LD M5221                 ; U5C2_S021
MPS
LDB M3                   ; OFFLINE_MODE
ANB M170                 ; LASER MARKER無効ﾓｰﾄﾞ
ANL
MPS
OUT W0104.0              ; ﾚｰｻﾞｰ印字要求
MPP
AND W04.0                ; ﾚｰｻﾞｰ印字完了
MOV K26 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MOV K13 D20301           ; UT4_ﾜｰｸ状態(0-10)
MPS
AND W04.1                ; ﾚｰｻﾞｰ印字結果(ON=OK)
DW K1 D20304             ; UT4_印字OK/NG(1:OK 2:NG)
MPP
ANB W04.1                ; ﾚｰｻﾞｰ印字結果(ON=OK)
DW K2 D20304             ; UT4_印字OK/NG(1:OK 2:NG)
MPP
LD M3                    ; OFFLINE_MODE
OR M170                  ; LASER MARKER無効ﾓｰﾄﾞ
ANL
MPS
TMR K260 K30             ; ﾚｰｻﾞｰ刻印模擬時間
MPP
AND T260                 ; ﾚｰｻﾞｰ刻印模擬時間
MOV K26 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
DW K1 D20304             ; UT4_印字OK/NG(1:OK 2:NG)
MOV K13 D20301           ; UT4_ﾜｰｸ状態(0-10)
;刻印部ｴｱﾌﾞﾛｰ・照明OFF
LD M5226                 ; U5C2_S026
MPS
ANB Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
INC D5001                ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5671                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
;End
LD M5227                 ; U5C2_S027
MPS
ANB M5580                ; W1刻印要求
MOV K0 D5001             ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
MPP
OUT M5590                ; W1刻印完了
;<h1/>▽_ｻｲｸﾙ終了
LD M5290                 ; U5C2_S090
ANB M130                 ; 一時停止
MOV K99 D5001            ; Cycle2ｽﾃｯﾌﾟｶｳﾝﾀ
;<h1/>■原点復帰回路
LD CR2002                ; 常時ON
ZRES M5900 M5969         ; U5org_S000  U5org_StepEnd
LDB M5984                ; ﾕﾆｯﾄ5原点復帰中
MOV K0 D5010             ; 原点復帰ｽﾃｯﾌﾟ
LD M5984                 ; ﾕﾆｯﾄ5原点復帰中
MOV D5010 Z1             ; 原点復帰ｽﾃｯﾌﾟ
SET M5900:Z1             ; U5org_S000
;  ▽_原点復帰開始
LD M5900                 ; U5org_S000
MPS
LDB Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
ANB Y5B                  ; ﾚｰｻﾞｰ刻印部照明ON
ANL
INC D5010                ; 原点復帰ｽﾃｯﾌﾟ
MRD
OUT M5674                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
MPP
OUT M5654                ; ﾚｰｻﾞｰ刻印部照明 OFF要求
;ﾚｰｻﾞｰ刻印部ｶﾊﾞｰID確認
LD M5901                 ; U5org_S001
MPS
AND M302                 ; 刻印部ｶﾊﾞｰID OK
MOV K3 D5010             ; 原点復帰ｽﾃｯﾌﾟ
MPP
ANB M302                 ; 刻印部ｶﾊﾞｰID OK
INC D5010                ; 原点復帰ｽﾃｯﾌﾟ
LD M5902                 ; U5org_S002
AND M5994                ; ﾕﾆｯﾄ5原点復帰時ｶﾊﾞｰID異常
INC D5010                ; 原点復帰ｽﾃｯﾌﾟ
LD M5903                 ; U5org_S003 
MPS
AND X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
MOV K31 D5010            ; 原点復帰ｽﾃｯﾌﾟ
MPP
ANB X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
INC D5010                ; 原点復帰ｽﾃｯﾌﾟ
LD M5904                 ; U5org_S004
AND M5995                ; ﾕﾆｯﾄ5原点復帰時保護ｶﾞﾗｽ異常
MOV K68 D5010            ; 原点復帰ｽﾃｯﾌﾟ
;終了
LD M5931                 ; U5org_S031
AND M5980                ; ﾕﾆｯﾄ5原点
ANB M82                  ; 原点復帰開始
MOV K69 D5010            ; 原点復帰ｽﾃｯﾌﾟ
;<h1/>□_手動回路
LD M31                   ; 手動ﾓｰﾄﾞ
AND M32                  ; ﾒｲﾝ手動操作可能条件
ANB M5984                ; ﾕﾆｯﾄ5原点復帰中
ANB M5985                ; ﾕﾆｯﾄ5起動中
MC
;	ﾚｰｻﾞｰ刻印部照明
LD CR2002                ; 常時ON
OUT M5704                ; ﾚｰｻﾞｰ刻印部照明 ON条件
LDP B1102                ; UNIT5-1-3 TP-PB
ANB B1103                ; UNIT5-1-4 TP-PB
AND M5704                ; ﾚｰｻﾞｰ刻印部照明 ON条件
LD M5705                 ; MUN_ﾚｰｻﾞｰ刻印部照明 ON指令
ANB M5707                ; MUN_ﾚｰｻﾞｰ刻印部照明 OFF指令
ORL
OUT M5705                ; MUN_ﾚｰｻﾞｰ刻印部照明 ON指令
LD CR2002                ; 常時ON
OUT M5706                ; ﾚｰｻﾞｰ刻印部照明 OFF条件
LDP B1103                ; UNIT5-1-4 TP-PB
ANB B1102                ; UNIT5-1-3 TP-PB
AND M5706                ; ﾚｰｻﾞｰ刻印部照明 OFF条件
LD M5707                 ; MUN_ﾚｰｻﾞｰ刻印部照明 OFF指令
ANB M5705                ; MUN_ﾚｰｻﾞｰ刻印部照明 ON指令
ORL
OUT M5707                ; MUN_ﾚｰｻﾞｰ刻印部照明 OFF指令
;	ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
LD CR2002                ; 常時ON
OUT M5708                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON条件
LDP B1106                ; UNIT5-1-7 TP-PB
ANB B1107                ; UNIT5-1-8 TP-PB
AND M5708                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON条件
LD M5709                 ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON指令
ANB M5711                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF指令
ORL
OUT M5709                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON指令
LD CR2002                ; 常時ON
ANB M5500                ; 手動捺印ﾓｰﾄﾞ
OUT M5710                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF条件
LDP B1107                ; UNIT5-1-8 TP-PB
ANB B1106                ; UNIT5-1-7 TP-PB
AND M5710                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF条件
LD M5711                 ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF指令
ANB M5709                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON指令
ORL
OUT M5711                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF指令
;	集塵機電源
LD CR2002                ; 常時ON
ANB M171                 ; 集塵機無効ﾓｰﾄﾞ
OUT M5716                ; 集塵機 POWER ON条件
LDP B1108                ; UNIT5-1-9 TP-PB
ANB B1109                ; UNIT5-1-10 TP-PB
AND M5716                ; 集塵機 POWER ON条件
LD M5717                 ; MUN_集塵機 POWER ON指令
ANB M5719                ; MUN_集塵機 POWER OFF指令
ORL
OUT M5717                ; MUN_集塵機 POWER ON指令
LD CR2002                ; 常時ON
OUT M5718                ; 集塵機 POWER OFF条件
LDP B1109                ; UNIT5-1-10 TP-PB
ANB B1108                ; UNIT5-1-9 TP-PB
AND M5718                ; 集塵機 POWER OFF条件
LD M5719                 ; MUN_集塵機 POWER OFF指令
ANB M5717                ; MUN_集塵機 POWER ON指令
ORL
OUT M5719                ; MUN_集塵機 POWER OFF指令
;	集塵機運転
LD CR2002                ; 常時ON
ANB M171                 ; 集塵機無効ﾓｰﾄﾞ
OUT M5720                ; 集塵機 運転 ON条件
LDP B110A                ; UNIT5-1-11 TP-PB
ANB B110B                ; UNIT5-1-12 TP-PB
AND M5720                ; 集塵機 運転 ON条件
LD M5721                 ; MUN_集塵機 運転 ON指令
ANB M5723                ; MUN_集塵機 運転 OFF指令
ANB B0A4                 ; 164)_集塵機 運転中信号OFF異常
ORL
OUT M5721                ; MUN_集塵機 運転 ON指令
LD CR2002                ; 常時ON
OUT M5722                ; 集塵機 運転 OFF条件
LDP B110B                ; UNIT5-1-12 TP-PB
ANB B110A                ; UNIT5-1-11 TP-PB
AND M5722                ; 集塵機 運転 OFF条件
LD M5723                 ; MUN_集塵機 運転 OFF指令
ANB M5721                ; MUN_集塵機 運転 ON指令
ORL
OUT M5723                ; MUN_集塵機 運転 OFF指令
;<h1/>□_手動捺印ﾓｰﾄﾞ
;動作許可信号
LD X105                  ; 基板傾き補正ﾁｬｯｸ 上昇端
ANB X110                 ; INDEX投入部ｹｰｽ検出
AND X111                 ; INDEX投入部基板検出
AND X112                 ; INDEX投入部ﾌﾀ検出
LDB X120                 ; NG置台 ﾜｰｸ検出1（手前側）
ORB X121                 ; NG置台 ﾜｰｸ検出2（奥側）
ANL
OUT M5560                ; 手動捺印 ﾜｰｸOK
LD X132                  ; ﾚｰｻﾞｰﾏｰｶ ﾄﾘｶﾞﾚﾃﾞｨ
AND X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
OUT M5561                ; 手動捺印 ﾚｰｻﾞｰﾏｰｶOK
LD M50                   ; 運転準備
AND M31                  ; 手動ﾓｰﾄﾞ
AND M5990                ; ﾕﾆｯﾄ5機器準備完了
AND M123                 ; 安全扉正常
AND Y51                  ; 安全ﾄﾞｱﾛｯｸ
ANB M41                  ; ﾕﾆｯﾄ_起動中
ANB M42                  ; 全ﾕﾆｯﾄ原点復帰中
ANB M52                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M51                  ; 即停止異常(緊急停止)
AND M5802                ; Cycle1起動条件
LD T105                  ; INDEX旋回確認ﾀｲﾏ
OR T106                  ; INDEX旋回戻確認ﾀｲﾏ
ANL
AND M5560                ; 手動捺印 ﾜｰｸOK
AND M5561                ; 手動捺印 ﾚｰｻﾞｰﾏｰｶOK
AND W102F.10             ; BOARD_BUILT-IN_RB_PORG
AND W1014.4              ; BOARD_PP_RB_ｴﾘｱ5_取出部待機(104)
OR M5500                 ; 手動捺印ﾓｰﾄﾞ
ANB M5525                ; 手動捺印完了
AND M50                  ; 運転準備
ANB B084                 ; 132)_搬送RB_ﾁｬｯｸ異常(50)
OFDL K100 M5501          ; 手動捺印可能
LD M5501                 ; 手動捺印可能
ANB M3                   ; OFFLINE_MODE
OUT W0104.8              ; 手動捺印可能
;要求有り
LD M5501                 ; 手動捺印可能
LDB M3                   ; OFFLINE_MODE
AND W04.9                ; 手動捺印準備要求
LD M3                    ; OFFLINE_MODE
AND B1104                ; UNIT5-1-5 TP-PB
ORL
ANL
OR M5500                 ; 手動捺印ﾓｰﾄﾞ
ANB M5525                ; 手動捺印完了
AND M55                  ; 異常なし
ANB B084                 ; 132)_搬送RB_ﾁｬｯｸ異常(50)
OUT M5500                ; 手動捺印ﾓｰﾄﾞ
MPS
@MOV K15 D58             ; Tp_切替ﾍﾟｰｼﾞNo.
MPP
ANB M3                   ; OFFLINE_MODE
@MOV W024 D20003         ; 搬送ﾛﾎﾞｯﾄNo.  Type No.入力
LDF M5500                ; 手動捺印ﾓｰﾄﾞ
MOV K1 D58               ; Tp_切替ﾍﾟｰｼﾞNo.
LD M5500                 ; 手動捺印ﾓｰﾄﾞ
MC
;ID切り替え / ID change
LDB M3                   ; OFFLINE_MODE
AND= D20000 W024         ; Type No.  搬送ﾛﾎﾞｯﾄNo.
OR M3                    ; OFFLINE_MODE
OR M5510                 ; ID切り替え完了
MPS
TMH K266 K10             ; 動作delay
MPP
AND T266                 ; 動作delay
OUT M5510                ; ID切り替え完了
;☆INDEX旋回 / INDEX turn
LD M5510                 ; ID切り替え完了
AND T105                 ; INDEX旋回確認ﾀｲﾏ
AND M2702                ; INDEX 旋回戻条件
OR M5511                 ; INDEX 旋回戻し
ANB M5512                ; INDEX 旋回
MPS
OUT M5511                ; INDEX 旋回戻し
MPP
ANB M5513                ; INDEX 動作完了
OUT M5503                ; U5→U2:INDEX旋回戻要求
;☆INDEX旋回戻し / INDEX return
LD M5510                 ; ID切り替え完了
AND T106                 ; INDEX旋回戻確認ﾀｲﾏ
AND M2700                ; INDEX 旋回条件
OR M5512                 ; INDEX 旋回
ANB M5511                ; INDEX 旋回戻し
MPS
OUT M5512                ; INDEX 旋回
MPP
ANB M5513                ; INDEX 動作完了
OUT M5504                ; U5→U2:INDEX旋回要求
;
LD M5511                 ; INDEX 旋回戻し
AND T106                 ; INDEX旋回戻確認ﾀｲﾏ
LD M5512                 ; INDEX 旋回
AND T105                 ; INDEX旋回確認ﾀｲﾏ
ORL
OR M5513                 ; INDEX 動作完了
MPS
OUT M5513                ; INDEX 動作完了
MPP
ANB M5514                ; 基板ﾊﾝﾄﾞ下降完了
OUT M5502                ; U5→U3 基板ﾊﾝﾄﾞ下降要求
;基板ﾊﾝﾄﾞ下降 / Bpard-hand down
LD M5513                 ; INDEX 動作完了
AND X104                 ; 基板傾き補正ﾁｬｯｸ 下降端
OR M5514                 ; 基板ﾊﾝﾄﾞ下降完了
MPS
OUT M5514                ; 基板ﾊﾝﾄﾞ下降完了
MPP
ANB M5515                ; ﾜｰｸ取出完了
OUT M5611                ; U5→U4 ﾜｰｸ取出移動要求
;ﾜｰｸ取出1ｻｲｸﾙ / Case pickup
LD M5514                 ; 基板ﾊﾝﾄﾞ下降完了
AND W1011.13             ; BOARD_PP_RB_ﾜｰｸ取出完了(35)
AND X11C                 ; 搬送RBﾁｬｯｸ 在荷確認
OR M5515                 ; ﾜｰｸ取出完了
MPS
OUT M5515                ; ﾜｰｸ取出完了
MPP
ANB M5516                ; ﾜｰｸ刻印位置移動完了
OUT M5505                ; U5→U3 基板ﾊﾝﾄﾞ上昇要求
ONDL K5 M5612            ; U5→U4 刻印位置移動要求
;刻印位置移動 / Move to Marking point 
LD M5515                 ; ﾜｰｸ取出完了
AND W1011.14             ; BOARD_PP_RB_ﾜｰｸ刻印完了(36)
AND X105                 ; 基板傾き補正ﾁｬｯｸ 上昇端
OR M5516                 ; ﾜｰｸ刻印位置移動完了
MPS
OUT M5516                ; ﾜｰｸ刻印位置移動完了
MPP
ANB M5517                ; ﾏｰｷﾝｸﾞｴｱﾌﾞﾛｰON完了
OUT M5662                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
;ｴｱﾌﾞﾛｰON / Air blow ON
LD M5516                 ; ﾜｰｸ刻印位置移動完了
AND Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
OR M5517                 ; ﾏｰｷﾝｸﾞｴｱﾌﾞﾛｰON完了
OUT M5517                ; ﾏｰｷﾝｸﾞｴｱﾌﾞﾛｰON完了
TMR K267 K10             ; ｴｱﾌﾞﾛｰONﾃﾞｨﾚｲ
;照明・集塵機吸引圧確認 / Marking room LED & Dust collector suction pressure check
LD M5517                 ; ﾏｰｷﾝｸﾞｴｱﾌﾞﾛｰON完了
AND T267                 ; ｴｱﾌﾞﾛｰONﾃﾞｨﾚｲ
AND Y5B                  ; ﾚｰｻﾞｰ刻印部照明ON
AND Y52                  ; 集塵機 POWER ON
AND X122                 ; 集塵機 運転中信号
LD T252                  ; 集塵機差圧計OUT1
OR M171                  ; 集塵機無効ﾓｰﾄﾞ
ANL
OR M5518                 ; ﾏｰｷﾝｸﾞ準備完了
MPS
OUT M5518                ; ﾏｰｷﾝｸﾞ準備完了
MPP
ANB M3                   ; OFFLINE_MODE
OUT W0104.9              ; 手動捺印準備完了
;印字完了待ち / Marking 
LD M5518                 ; ﾏｰｷﾝｸﾞ準備完了
AND M3                   ; OFFLINE_MODE
TMR K265 K30             ; 手動捺印模擬ﾀｲﾏ
LD M5518                 ; ﾏｰｷﾝｸﾞ準備完了
LDB M3                   ; OFFLINE_MODE
ANB W04.9                ; 手動捺印準備要求
LD M3                    ; OFFLINE_MODE
AND T265                 ; 手動捺印模擬ﾀｲﾏ
ORL
ANL
OR M5519                 ; ﾏｰｷﾝｸﾞ完了
MPS
OUT M5519                ; ﾏｰｷﾝｸﾞ完了
MPP
ANB M5520                ; NG置台1排出要求
OUT M5672                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
;☆NG置台1排出 / Eject to NG-station1
LD M5519                 ; ﾏｰｷﾝｸﾞ完了
ANB X120                 ; NG置台 ﾜｰｸ検出1（手前側）
OR M5520                 ; NG置台1排出要求
ANB M5521                ; NG置台2排出要求
ANB M5522                ; OK1上空移動要求
MPS
OUT M5520                ; NG置台1排出要求
MPP
ANB M5523                ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
OUT M5613                ; U5→U4 NG1位置移動要求
;☆NG置台2排出 / Eject to NG-station2
LD M5519                 ; ﾏｰｷﾝｸﾞ完了
AND X120                 ; NG置台 ﾜｰｸ検出1（手前側）
ANB X121                 ; NG置台 ﾜｰｸ検出2（奥側）
OR M5521                 ; NG置台2排出要求
ANB M5520                ; NG置台1排出要求
ANB M5522                ; OK1上空移動要求
MPS
OUT M5521                ; NG置台2排出要求
MPP
ANB M5523                ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
OUT M5614                ; U5→U4 NG2位置移動要求
;☆OK1上空排出 / Eject to Upside of OK1
LD M5519                 ; ﾏｰｷﾝｸﾞ完了
ANB X120                 ; NG置台 ﾜｰｸ検出1（手前側）
ANB X121                 ; NG置台 ﾜｰｸ検出2（奥側）
OR M5522                 ; OK1上空移動要求
ANB M5520                ; NG置台1排出要求
ANB M5521                ; NG置台2排出要求
MPS
OUT M5522                ; OK1上空移動要求
MPP
ANB M5523                ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
OUT M5616                ; U5→U4 OK1上空移動要求
;
LD M5520                 ; NG置台1排出要求
AND W1012.3              ; BOARD_PP_RB_NG1排出完了(43)
LD M5521                 ; NG置台2排出要求
AND W1012.4              ; BOARD_PP_RB_NG2排出完了(44)
ORL
LD M5522                 ; OK1上空移動要求
AND W1012.12             ; BOARD_PP_RB_OK1上空完了(54)
ORL
OR M5523                 ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
OUT M5523                ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
OUT M4654                ; 搬送RBﾁｬｯｸ 開要求
TMR K253 K10             ; 集塵機OFFﾃﾞｨﾚｲ
;	搬送RB ﾁｬｯｸ開き / Chuck open
LD M5523                 ; ﾏｰｷﾝｸﾞﾜｰｸ排出完了
AND X103                 ; 搬送RBﾁｬｯｸ　開端
OR M5524                 ; ﾁｬｯｸ開き完了
OUT M5524                ; ﾁｬｯｸ開き完了
;終了 / end
LD M5524                 ; ﾁｬｯｸ開き完了
AND T253                 ; 集塵機OFFﾃﾞｨﾚｲ
OUT M5525                ; 手動捺印完了
MCR
;
MCR
;<h1/>○  Subﾙｰﾁﾝ  ○
;<h1/>□_出力回路
;<h1/>____▼_SOL
;ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
LDB M5709                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON指令
ANB M5661                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
ANB M5662                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
ANB M5663                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
ANB M5664                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON要求
INV
LD M5660                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON補助
ANB M5670                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF補助
ORL
AND M50                  ; 運転準備
LD M123                  ; 安全扉正常
OR M73                   ; 各工程起動指令
ANL
OUT M5660                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON補助
LD M5660                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ ON補助
OUT Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
LDB M5711                ; MUN_ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF指令
ANB M5671                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
ANB M5672                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
ANB M5673                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
ANB M5674                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF要求
ANFB M5759               ; 集塵機 運転 ON補助_A
INV
OUT M5670                ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ OFF補助
;<h1/>____▼_POWER出力
;ﾚｰｻﾞｰ刻印部照明ON
LDB M5705                ; MUN_ﾚｰｻﾞｰ刻印部照明 ON指令
ANB M5641                ; ﾚｰｻﾞｰ刻印部照明 ON要求
ANB M5642                ; ﾚｰｻﾞｰ刻印部照明 ON要求
ANB M5643                ; ﾚｰｻﾞｰ刻印部照明 ON要求
ANB M5644                ; ﾚｰｻﾞｰ刻印部照明 ON要求
INV
LD M5640                 ; ﾚｰｻﾞｰ刻印部照明 ON補助
ANB M5650                ; ﾚｰｻﾞｰ刻印部照明 OFF補助
ORL
AND M50                  ; 運転準備
OUT M5640                ; ﾚｰｻﾞｰ刻印部照明 ON補助
LD M5640                 ; ﾚｰｻﾞｰ刻印部照明 ON補助
OR M73                   ; 各工程起動指令
OR M5500                 ; 手動捺印ﾓｰﾄﾞ
OUT Y5B                  ; ﾚｰｻﾞｰ刻印部照明ON
LDB M5707                ; MUN_ﾚｰｻﾞｰ刻印部照明 OFF指令
ANB M5651                ; ﾚｰｻﾞｰ刻印部照明 OFF要求
ANB M5652                ; ﾚｰｻﾞｰ刻印部照明 OFF要求
ANB M5653                ; ﾚｰｻﾞｰ刻印部照明 OFF要求
ANB M5654                ; ﾚｰｻﾞｰ刻印部照明 OFF要求
INV
OUT M5650                ; ﾚｰｻﾞｰ刻印部照明 OFF補助
;集塵機 POWER
LDB M5717                ; MUN_集塵機 POWER ON指令
ANB M5741                ; 集塵機 POWER ON要求
ANB M5742                ; 集塵機 POWER ON要求
ANB M5743                ; 集塵機 POWER ON要求
ANB M5744                ; 集塵機 POWER ON要求
ANB Y58                  ; 集塵機 運転
INV
LD M5740                 ; 集塵機 POWER ON補助
ANB M5750                ; 集塵機 POWER OFF補助
ORL
OUT M5740                ; 集塵機 POWER ON補助
LD M5740                 ; 集塵機 POWER ON補助
OR T73                   ; 運転準備ONﾃﾞｨﾚｲ_3s
LDB M171                 ; 集塵機無効ﾓｰﾄﾞ
OR L29                   ; KTR仕様
ANL
OUT Y52                  ; 集塵機 POWER ON
LDB M5719                ; MUN_集塵機 POWER OFF指令
ANB M5751                ; 集塵機 POWER OFF要求
ANB M5752                ; 集塵機 POWER OFF要求
ANB M5753                ; 集塵機 POWER OFF要求
ANB M5754                ; 集塵機 POWER OFF要求
INV
OUT M5750                ; 集塵機 POWER OFF補助
;集塵機 運転
LDB M5500                ; 手動捺印ﾓｰﾄﾞ
INV
OFDL K650 M5759          ; 集塵機 運転 ON補助_A
LDB M5721                ; MUN_集塵機 運転 ON指令
ANB M5761                ; 集塵機 運転 ON要求
ANB M5762                ; 集塵機 運転 ON要求
ANB M5763                ; 集塵機 運転 ON要求
ANB M5764                ; 集塵機 運転 ON要求
INV
OUT M5760                ; 集塵機 運転 ON補助_B
LDB M5759                ; 集塵機 運転 ON補助_A
ANB M5760                ; 集塵機 運転 ON補助_B
INV
LD Y58                   ; 集塵機 運転
AND M74                  ; 自動運転中
ORL
ANB M5770                ; 集塵機 運転 OFF補助
ANB M171                 ; 集塵機無効ﾓｰﾄﾞ
OUT Y58                  ; 集塵機 運転
LDB M5723                ; MUN_集塵機 運転 OFF指令
ANB M5771                ; 集塵機 運転 OFF要求
ANB M5772                ; 集塵機 運転 OFF要求
ANB M5773                ; 集塵機 運転 OFF要求
ANB M5774                ; 集塵機 運転 OFF要求
INV
OUT M5770                ; 集塵機 運転 OFF補助
;ｸﾘｰﾝｴｱﾕﾆｯﾄ 電源ON
LD M50                   ; 運転準備
ANB M172                 ; ｸﾘｰﾝｴｱﾕﾆｯﾄ無効ﾓｰﾄﾞ
OUT Y5A                  ; ｸﾘｰﾝｴｱﾕﾆｯﾄ 電源ON
;<h1/>____▼_MD-X1500
LD T0                    ; PLC_RUN_ONﾀｲﾏ
OR Y53                   ; ﾚｰｻﾞｰﾏｰｶ POWER ON
LDB M170                 ; LASER MARKER無効ﾓｰﾄﾞ
OR CR2002                ; 常時ON
ANL
OUT Y53                  ; ﾚｰｻﾞｰﾏｰｶ POWER ON
LDB X00                  ; 非常停止正常
ORB M123                 ; 安全扉正常
OR M170                  ; LASER MARKER無効ﾓｰﾄﾞ
OUT Y310                 ; ﾚｰｻﾞｰﾏｰｶ ﾄﾘｶﾞﾛｯｸ
LD X130                  ; ﾚｰｻﾞｰﾏｰｶ ｱﾗｰﾑ
LD B0A0                  ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
ANP B1011                ; ｴﾗｰﾘｾｯﾄPB
ORB M5639                ; MD-X起動時ｴﾗｰ解除記憶
ANL
SHOT K100 Y311           ; ﾚｰｻﾞｰﾏｰｶ ｱﾗｰﾑ解除
LD Y311                  ; ﾚｰｻﾞｰﾏｰｶ ｱﾗｰﾑ解除
ANB X130                 ; ﾚｰｻﾞｰﾏｰｶ ｱﾗｰﾑ
OR M5639                 ; MD-X起動時ｴﾗｰ解除記憶
AND Y53                  ; ﾚｰｻﾞｰﾏｰｶ POWER ON
OUT M5639                ; MD-X起動時ｴﾗｰ解除記憶
;<h1/>____▼_Tp出力
LD Y53                   ; ﾚｰｻﾞｰﾏｰｶ POWER ON
OUT B1600
LDB Y53                  ; ﾚｰｻﾞｰﾏｰｶ POWER ON
OUT B1601
LD Y5B                   ; ﾚｰｻﾞｰ刻印部照明ON
OUT B1602
LDB Y5B                  ; ﾚｰｻﾞｰ刻印部照明ON
OUT B1603
LD M5500                 ; 手動捺印ﾓｰﾄﾞ
OUT B1604
LD Y308                  ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
OUT B1606
LDB Y308                 ; ﾚｰｻﾞｰ刻印部ｴｱﾌﾞﾛｰ
OUT B1607
LD Y52                   ; 集塵機 POWER ON
OUT B1608
LDB Y52                  ; 集塵機 POWER ON
OUT B1609
LD Y58                   ; 集塵機 運転
OUT B160A
LDB Y58                  ; 集塵機 運転
OUT B160B
;<h1/>□_異常回路
;B0A0 ﾚｰｻﾞｰﾏｰｶ異常
LD X130                  ; ﾚｰｻﾞｰﾏｰｶ ｱﾗｰﾑ
AND T79                  ; 運転準備ONﾃﾞｨﾚｲ_20s
AND M50                  ; 運転準備
LD B0A0                  ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K257 K10             ; ﾚｰｻﾞｰﾏｰｶ異常ﾃﾞｨﾚｲ
MPP
AND T257                 ; ﾚｰｻﾞｰﾏｰｶ異常ﾃﾞｨﾚｲ
OUT B0A0                 ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
LDP B0A0                 ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A0 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;B0A1 ｸﾘｰﾝｴｱﾕﾆｯﾄｱﾗｰﾑ
LDB X08                  ; ｸﾘｰﾝｴｱﾕﾆｯﾄ ｱﾗｰﾑ1
LD B0A1                  ; 161)_ｸﾘｰﾝｴｱﾕﾆｯﾄｱﾗｰﾑ
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A1                 ; 161)_ｸﾘｰﾝｴｱﾕﾆｯﾄｱﾗｰﾑ
LDP B0A1                 ; 161)_ｸﾘｰﾝｴｱﾕﾆｯﾄｱﾗｰﾑ
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A1 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;B0A2 ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
;	30分後に再度警報
LDB X09                  ; ｸﾘｰﾝｴｱﾕﾆｯﾄ ｱﾗｰﾑ2
ANB B0A2                 ; 162)_ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
TMR K258 K18000          ; ｲｵﾅｲｻﾞｸﾘｰﾆﾝｸﾞ予告ﾃﾞｨﾚｲ
LDB X09                  ; ｸﾘｰﾝｴｱﾕﾆｯﾄ ｱﾗｰﾑ2
OR T258                  ; ｲｵﾅｲｻﾞｸﾘｰﾆﾝｸﾞ予告ﾃﾞｨﾚｲ
MEP
LD B0A2                  ; 162)_ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A2                 ; 162)_ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
LDP B0A2                 ; 162)_ｲｵﾅｲｻﾞﾕﾆｯﾄｸﾘｰﾆﾝｸﾞ予告
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A2 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;集塵機吸引圧力異常
LD T251                  ; 集塵機起動待ち
ANB T252                 ; 集塵機差圧計OUT1
TMR K255 K50             ; 集塵機吸引圧力異常ﾃﾞｨﾚｲ
LD T255                  ; 集塵機吸引圧力異常ﾃﾞｨﾚｲ
ANB M35                  ; 空運転ﾓｰﾄﾞ
LD B0A3                  ; 163)_集塵機吸引圧力異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A3                 ; 163)_集塵機吸引圧力異常
LDP B0A3                 ; 163)_集塵機吸引圧力異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A3 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;集塵機 運転中信号OFF異常
LD Y52                   ; 集塵機 POWER ON
AND Y58                  ; 集塵機 運転
ANB X122                 ; 集塵機 運転中信号
ANB M171                 ; 集塵機無効ﾓｰﾄﾞ
TMR K256 K80             ; 集塵機 運転中OFFﾃﾞｨﾚｲ
LD T256                  ; 集塵機 運転中OFFﾃﾞｨﾚｲ
LD B0A4                  ; 164)_集塵機 運転中信号OFF異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A4                 ; 164)_集塵機 運転中信号OFF異常
LDP B0A4                 ; 164)_集塵機 運転中信号OFF異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A4 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;集塵機警報出力
LD X123                  ; 集塵機 警報出力
LD B0A5                  ; 165)_集塵機 警報出力
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A5                 ; 165)_集塵機 警報出力
LDP B0A5                 ; 165)_集塵機 警報出力
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A5 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾚｰｻﾞｰﾏｰｶ警報
LD X131                  ; ﾚｰｻﾞｰﾏｰｶ 警報
LD B0A6                  ; 166)_ﾚｰｻﾞｰﾏｰｶ警報
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A6                 ; 166)_ﾚｰｻﾞｰﾏｰｶ警報
LDP B0A6                 ; 166)_ﾚｰｻﾞｰﾏｰｶ警報
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A6 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;刻印部ｶﾊﾞｰID異常
LD M73                   ; 各工程起動指令
ANB M302                 ; 刻印部ｶﾊﾞｰID OK
LD M2912                 ; Org_S12
AND M5994                ; ﾕﾆｯﾄ5原点復帰時ｶﾊﾞｰID異常
ORL
LD B0A7                  ; 167)刻印部ｶﾊﾞｰID異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
MPS
TMR K259 K10             ; 刻印部ｶﾊﾞｰID異常ﾀｲﾏ
MPP
AND T259                 ; 刻印部ｶﾊﾞｰID異常ﾀｲﾏ
OUT B0A7                 ; 167)刻印部ｶﾊﾞｰID異常
LDP B0A7                 ; 167)刻印部ｶﾊﾞｰID異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A7 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD CR2003                ; 常時OFF
LD B0A8                  ; 168)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A8                 ; 168)
LDP B0A8                 ; 168)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A8 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
LDB M5212                ; U5C2_S012
INV
LD M73                   ; 各工程起動指令
ANB M130                 ; 一時停止
ANB X133                 ; ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出
ORL
LD M2912                 ; Org_S12
AND M5995                ; ﾕﾆｯﾄ5原点復帰時保護ｶﾞﾗｽ異常
ORL
LD B0A9                  ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
LDP B0A9                 ; 169)ﾚｰｻﾞｰ保護ｶﾞﾗｽ検出OFF異常
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00A9 D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;
LD M5023                 ; U5C1_S023
LD B0AA                  ; 170)ﾚｰｻﾞｰ刻印不良
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0AA                 ; 170)ﾚｰｻﾞｰ刻印不良
LDP B0AA                 ; 170)ﾚｰｻﾞｰ刻印不良
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00AA D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;<h1/>□_警報回路
;
LD CR2003                ; 常時OFF
LD B0BA                  ; 186)
ANB M111                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
ORL
OUT B0BA                 ; 186)
LDP B0BA                 ; 186)
AND< D13510 K64          ; 発生ｴﾗｰｺｰﾄﾞ格納先
MOV H00BA D13500         ; ｴﾗｰｺｰﾄﾞ
FIFOW D13500 D13510      ; ｴﾗｰｺｰﾄﾞ  発生ｴﾗｰｺｰﾄﾞ格納先
;ﾌﾞｻﾞｰ発生回路
LD<>.D B0A0 D5012        ; 160)_ﾚｰｻﾞｰﾏｰｶ異常  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
AND<>.D B0A0 K0          ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
OUT M5989                ; ﾕﾆｯﾄ5異常BZ補助
LD M110                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR M111                  ; 異常ﾘｾｯﾄﾊﾟﾙｽ
MOV.D B0A0 D5012         ; 160)_ﾚｰｻﾞｰﾏｰｶ異常  ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
LD=.D B0A0 K0            ; 160)_ﾚｰｻﾞｰﾏｰｶ異常
@MOV.D K0 D5012          ; ﾌﾞｻﾞｰSTOP監視ｴﾘｱ
END
ENDH
