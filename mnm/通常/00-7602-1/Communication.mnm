DEVICE:133
;MODULE:Communication
;MODULE_TYPE:0
;<h1/>□_PC operation check
LD W00.0                 ; PC動作中
TMR K300 K30             ; PC normal check timer
LDB W00.0                ; PC動作中
TMR K301 K30             ; PC normal check timer
LDB T300                 ; PC normal check timer
ANB T301                 ; PC normal check timer
OR M3                    ; OFFLINE
OUT M300                 ; PC通信正常
;Request from HOST
LD X05                   ; ｸﾗﾝﾌﾟ動作点
OR M3300                 ; ﾌﾟﾛｰﾌﾞ上昇開始
ANB M3310                ; 位置決め解除完了
ANB W00.4                ; ｽﾃｰｼﾞ有効/無効
MC
LD W00.8                 ; ﾌﾟﾛｰﾌﾞﾋﾟﾝ上昇要求(PC→PLC)
OR M3300                 ; ﾌﾟﾛｰﾌﾞ上昇開始
MPS
OUT M3300                ; ﾌﾟﾛｰﾌﾞ上昇開始
MPP
ANB M3302                ; 位置決め完了
OUT M3301                ; 位置決めﾛｯｸ指令
LD M3300                 ; ﾌﾟﾛｰﾌﾞ上昇開始
AND T120                 ; ｸﾗﾝﾌﾟﾛｯｸ確認ﾀｲﾏ
OR M3302                 ; 位置決め完了
MPS
OUT M3302                ; 位置決め完了
MPP
ANB M3305                ; ﾌﾟﾛｰﾌﾞ上昇完了
OUT M3304                ; ﾌﾟﾛｰﾌﾞ上昇指令
LD M3302                 ; 位置決め完了
AND T122                 ; ﾌﾟﾛｰﾌﾞ上昇端(GND接続側)ONﾀｲﾏ
OR M3305                 ; ﾌﾟﾛｰﾌﾞ上昇完了
OUT M3305                ; ﾌﾟﾛｰﾌﾞ上昇完了
LD M3305                 ; ﾌﾟﾛｰﾌﾞ上昇完了
ANB W00.8                ; ﾌﾟﾛｰﾌﾞﾋﾟﾝ上昇要求(PC→PLC)
OR M3306                 ; 要求解除
MPS
OUT M3306                ; 要求解除
MPP
ANB M3307                ; ﾌﾟﾛｰﾌﾞ下降指令
OUT M3307                ; ﾌﾟﾛｰﾌﾞ下降指令
LD M3306                 ; 要求解除
AND T123                 ; ﾌﾟﾛｰﾌﾞ下降端(ONﾀｲﾏ)
OR M3308                 ; ﾌﾟﾛｰﾌﾞ下降完了
MPS
OUT M3308                ; ﾌﾟﾛｰﾌﾞ下降完了
MPP
ANB M3310                ; 位置決め解除完了
OUT M3309                ; 位置決め解除指令
LD M3308                 ; ﾌﾟﾛｰﾌﾞ下降完了
AND T121                 ; ｸﾗﾝﾌﾟ解除確認ﾀｲﾏ
OUT M3310                ; 位置決め解除完了
MCR
;<h1/>□_Err code transmission (for error log)
LD CR2008                ; 運転開始時1ｽｷｬﾝON
FMOV K0 D900 K32         ; 異常内容
DW K0 D900               ; 異常内容
CON
DW K30 D901              ; 格納異常数
LD<> D902 K0             ; ｴﾗｰｺｰﾄﾞ最終
ANB M3020                ; ERROR LOG START
AND= D3000 K0            ; Comm_Log_step
ANB W00.2                ; ｴﾗｰﾛｸﾞ完了
OR M3020                 ; ERROR LOG START
ANB M3015                ; ｴﾗｰﾛｸﾞ完了
OUT M3020                ; ERROR LOG START
LD CR2002                ; 常時ON
FMOV K0 M3000 K1         ; 異常ﾛｸﾞ通信中
LDB M3020                ; ERROR LOG START
MOV K0 D3000             ; Comm_Log_step
LD M3020                 ; ERROR LOG START
MOV D3000 Z2             ; Comm_Log_step
OUT M3000:Z2             ; 異常ﾛｸﾞ通信中
LD M3000                 ; 異常ﾛｸﾞ通信中
INC D3000                ; Comm_Log_step
LD M3001                 ; 異常内容読出し
@FIFOR D900 W021         ; 異常内容  ｴﾗｰｺｰﾄﾞ(ﾛｸﾞ保存用)
INC D3000                ; Comm_Log_step
LD M3002                 ; ｴﾗｰﾛｸﾞ読込み要求ON
AND M300                 ; PC通信正常
MPS
OUT W010.2               ; ｴﾗｰﾛｸﾞ要求
MPP
LD W00.2                 ; ｴﾗｰﾛｸﾞ完了
OR M3                    ; OFFLINE
ANL
INC D3000                ; Comm_Log_step
LD M3003                 ; ﾃﾞｰﾀｸﾘｱｰ
LD M300                  ; PC通信正常
ANB W00.2                ; ｴﾗｰﾛｸﾞ完了
ORB M300                 ; PC通信正常
OR M3                    ; OFFLINE
ANL
INC D3000                ; Comm_Log_step
LD M3004                 ; ｴﾗｰﾛｸﾞ完了確認
MOV K15 D3000            ; Comm_Log_step
;<h1/>□_Err code transmission (for display)
LD M300                  ; PC通信正常
ANB M3120                ; 異常発生Bitｻｰﾁ開始ON
AND= D3100 K0            ; Comm_display_step
OR M3120                 ; 異常発生Bitｻｰﾁ開始ON
ANB M3115                ; 異常発生Bitｻｰﾁ終了
OUT M3120                ; 異常発生Bitｻｰﾁ開始ON
LD CR2002                ; 常時ON
FMOV K0 M3100 K1         ; 異常発生Bitｻｰﾁ開始
LDB M3120                ; 異常発生Bitｻｰﾁ開始ON
MOV K0 D3100             ; Comm_display_step
LD M3120                 ; 異常発生Bitｻｰﾁ開始ON
MOV D3100 Z2             ; Comm_display_step
OUT M3100:Z2             ; 異常発生Bitｻｰﾁ開始
LD M3100                 ; 異常発生Bitｻｰﾁ開始
INC D3100                ; Comm_display_step
LD M3101                 ; ｻｰﾁ番号ﾘｾｯﾄ
MOV K0 Z6
INC D3100                ; Comm_display_step
LD M3102                 ; ﾙｰﾌﾟ開始/ｴﾗｰBitON確認
MPS
AND B00:Z6
INC D3100                ; Comm_display_step
MPP
ANB B00:Z6
MOV K5 D3100             ; Comm_display_step
LD M3103                 ; ｴﾗｰｺｰﾄﾞｾｯﾄ
MOV Z6 W020              ; ｴﾗｰｺｰﾄﾞ(表示用)
INC D3100                ; Comm_display_step
LD M3104                 ; 表示中
MPS
TMR K304 K20             ; display time
MPP
AND T304                 ; display time
INC D3100                ; Comm_display_step
LD M3105                 ; 最終Bit確認
MPS
AND>= Z6 K64
MOV K7 D3100             ; Comm_display_step
MPP
AND< Z6 K64
INC D3100                ; Comm_display_step
LD M3106                 ; ｻｰﾁ番号ｶｳﾝﾄｱｯﾌﾟﾙｰﾌﾟ開始へ
@INC Z6
MOV K2 D3100             ; Comm_display_step
LD M3107                 ; 異常発生Bitｻｰﾁ終了
MOV K15 D3100            ; Comm_display_step
;ｴﾗｰ発生判定ﾌﾗｸﾞ追加(ｴﾗｰ発生中はPCに0を送らない対策)
;Error determination flag added
;(measures during the error that does not send a 0 to the PC)
LD CR2002                ; 常時ON
WSUM B00 K4
CON
STA D3110                ; ｴﾗｰ発生判定(0で無し)
LDB M3104                ; 表示中
MPS
TMR K305 K25             ; display 無し check
MPP
LD T305                  ; display 無し check
AND= D3110 K0            ; ｴﾗｰ発生判定(0で無し)
ANL
@MOV K0 W020             ; ｴﾗｰｺｰﾄﾞ(表示用)
END
ENDH
