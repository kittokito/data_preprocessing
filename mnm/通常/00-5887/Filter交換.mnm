DEVICE:51
;MODULE:Filter交換
;MODULE_TYPE:0
;Filter画面が立上がった場合はTP装置状態をクリアーする
;
LDB MR3004               ; 一連動作中
ANP MR10006              ; Filter交換画表示
OUT MR10200              ; Filter画面立上り
LD MR10200               ; Filter画面立上り
MOV #11 DM2              ; TP装置状態
;検査画面が表示されてる状態で開始PBを押した瞬間に装置状態を「接続確認中」にする
;TP装置状態  接続確認中
LD MR5001                ; 流路1検査中
OR MR5101                ; 流路2検査中
AND MR10006              ; Filter交換画表示
MPS
TMR #51 #2               ; Filter交換開始
MPP
AND T51                  ; Filter交換開始
SET MR16000              ; Filter交換
LDP MR16000              ; Filter交換
ORP MR7200               ; 一連操作流路1接続確認
MOV #0 DM2               ; TP装置状態
;元圧空0.05MPa調整  B719で終了
LDP MR16000              ; Filter交換
OR MR15900               ; 電空0.05MPa
AND MR3004               ; 一連動作中
ANB MR16001              ; 接続確認①
OUT MR15900              ; 電空0.05MPa
;バルブ接続確認①（加圧）
;入口流路1閉、圧空1開、入口流路2閉、圧空2開、元圧空1開、出口流路1閉、元圧空2開、出口流路2閉にして確認する
LD MR15900               ; 電空0.05MPa
AND B0719                ; TPSW 調整終了
OR MR16001               ; 接続確認①
AND MR3004               ; 一連動作中
ANB MR16002              ; 接続確認①OK
OUT MR16001              ; 接続確認①
TMR #120 #20             ; 接続確認①判定
;接続確認①判定は常にOK
LD MR16001               ; 接続確認①
ANP T120                 ; 接続確認①判定
MPS
AND CR2002               ; 常時ON
OUT MR16002              ; 接続確認①OK
MPP
ANB CR2002               ; 常時ON
OUT MR17002              ; 接続確認①NG
;バルブ接続確認②圧力センサーで圧力上昇の有無を検出し、良否判定を行う
;入口流量1閉、圧空1開、入口流路2閉、圧空2開、元圧空1閉、出口流路1閉、元圧空2閉、出口流路2閉にして確認する
;
LDP MR16002              ; 接続確認①OK
OR MR16003               ; 接続確認②
AND MR3004               ; 一連動作中
ANB MR16004              ; 接続確認②OK
ANB MR17004              ; 接続確認②NG
OUT MR16003              ; 接続確認②
TMR #121 #50             ; 接続確認②判定
;接続判定：  ＊判定条件  OK：MPa以上  TP表示  接続OK  NG：TP表示  接続NG）    
LD MR16003               ; 接続確認②
ANP T121                 ; 接続確認②判定
MPS
LD MR5804                ; 流路1ﾜｰｸﾁｭｰﾌﾞ適正圧力
ORB MR5001               ; 流路1検査中
LD MR5805                ; 流路2ﾜｰｸﾁｭｰﾌﾞ適正圧力
ORB MR5101               ; 流路2検査中
ANL
ANL
OUT MR16004              ; 接続確認②OK
MPP
LDB MR5804               ; 流路1ﾜｰｸﾁｭｰﾌﾞ適正圧力
AND MR5001               ; 流路1検査中
LDB MR5805               ; 流路2ﾜｰｸﾁｭｰﾌﾞ適正圧力
AND MR5101               ; 流路2検査中
ORL
ANL
OUT MR17004              ; 接続確認②NG
;TP表示  接続OK
LDP MR16004              ; 接続確認②OK
MOV #1 DM2               ; TP装置状態
;TP表示  接続NG
LDP MR17004              ; 接続確認②NG
MOV #6 DM2               ; TP装置状態
;接続NGでもバルブ接続確認③を実行する
;バルブ接続確認③エアー解放
;入口流量1閉、圧空1開、入口流路2閉、圧空2開、元圧空1閉、出口流路1開、元圧空2閉、出口流路2開して確認する
;
LDP MR16004              ; 接続確認②OK
ORP MR17004              ; 接続確認②NG
OR MR16005               ; 接続確認③
AND MR3004               ; 一連動作中
ANB MR16006              ; 接続確認③OK
ANB MR17006              ; 接続確認③NG
OUT MR16005              ; 接続確認③
TMR #122 #10             ; 接続判定③
LD MR16005               ; 接続確認③
ANP T122                 ; 接続判定③
LDA DM2                  ; TP装置状態
CON
CMP #1
CON
MPS
AND CR2010               ; 演算結果がｾﾞﾛ
OUT MR16006              ; 接続確認③OK
MPP
ANB CR2010               ; 演算結果がｾﾞﾛ
OUT MR17006              ; 接続確認③NG
;接続確認③NGでﾀﾜｰﾗﾝﾌﾟ赤、ﾌﾞｻﾞｰ早い
LDP MR17006              ; 接続確認③NG
AND MR3004               ; 一連動作中
OUT MR17007              ; 接続確認③NG
;TP表示  排液中
LDP MR16006              ; 接続確認③OK
MOV #4 DM2               ; TP装置状態
LD MR7305                ; 一連操作　F排液
TMR #55 #2               ; 一連操作　F排液開始
LDP MR16006              ; 接続確認③OK
ORP T55                  ; 一連操作　F排液開始
OR MR18000               ; Filter排液
ANB MR18002              ; バルブ排液②
AND MR3004               ; 一連動作中
OUT MR18000              ; Filter排液
TMR #140 #1              ; TP　排液0.1MP表示
LD T140                  ; TP　排液0.1MP表示
AND B0719                ; TPSW 調整終了
OR MR18001               ; Filter排液
AND MR3004               ; 一連動作中
ANB MR18002              ; バルブ排液②
OUT MR18001              ; Filter排液
TMR #141 #300            ; バルブ排液②遅延
LDP T141                 ; バルブ排液②遅延
LD B1016                 ; TP流路1&2液抜き選択ﾗﾝﾌﾟ
AND MR10002              ; 検査画面表示
ORL
OR MR18002               ; バルブ排液②
ANB MR18003              ; バルブ待機状態0.005MPa
AND MR3004               ; 一連動作中
MPS
OUT MR18002              ; バルブ排液②
MPP
AND CR2003               ; 常時OFF
TMR #142 #100            ; バルブ待機状態遅延
;TP表示  Filter排液終了
LDP T142                 ; バルブ待機状態遅延
MOV #12 DM2              ; TP装置状態
LDP T142                 ; バルブ待機状態遅延
OR MR18003               ; バルブ待機状態0.005MPa
ANB MR18004              ; fliter排液終了ゆっくり3秒
AND MR3004               ; 一連動作中
OUT MR18003              ; バルブ待機状態0.005MPa
;TP表示  排液終了
;
LD MR18003               ; バルブ待機状態0.005MPa
AND B0719                ; TPSW 調整終了
OR MR18004               ; fliter排液終了ゆっくり3秒
ANB MR18005              ; 液循環開始
AND MR3004               ; 一連動作中
OUT MR18004              ; fliter排液終了ゆっくり3秒
@MOV #13 DM2             ; TP装置状態
TMR #177 #10             ; 準備OK　液循環開始？
;準備OK？液循環開始
LD T177                  ; 準備OK　液循環開始？
AND B0720                ; 液循環開始
OUT MR18005              ; 液循環開始
;Filter排液の終了  B721未使用
;TP表示  排液終了
LD MR18004               ; fliter排液終了ゆっくり3秒
AND B0721                ; Filter排液終了
OR MR18006               ; Filter排液終了
AND MR3004               ; 一連動作中
AND CR2003               ; 常時OFF
OUT MR18006              ; Filter排液終了
MOV #13 DM2              ; TP装置状態
;バルブ：液循環  流量、IN側温度を確認した後、OUT側温度の値で良否判定を行う
;入口流量1開、圧空1閉、入口流路2開、圧空2閉、元圧空1閉、出口流路1開、元圧空2閉、出口流路2開して確認する
;TP表示：液循環中
;
LDP MR18005              ; 液循環開始
MOV #2 DM2               ; TP装置状態
LDP MR18005              ; 液循環開始
OR MR16007               ; 液循環
AND MR3004               ; 一連動作中
ANB MR16008              ; 液循環終了
OUT MR16007              ; 液循環
TMR #123 #1              ; 液循環
LD MR16007               ; 液循環
ANP T123                 ; 液循環
OUT MR16008              ; 液循環終了
;チラー：液送り
;
LDP MR16008              ; 液循環終了
OR MR16009               ; ﾁﾗｰ液送り
AND MR3004               ; 一連動作中
ANB MR16010              ; ﾁﾗｰ送り終了
OUT MR16009              ; ﾁﾗｰ液送り
TMR #124 #600            ; ﾁﾗｰ送り終了
;ﾁﾗｰ送り終了
LD MR16009               ; ﾁﾗｰ液送り
ANP T124                 ; ﾁﾗｰ送り終了
OUT MR16010              ; ﾁﾗｰ送り終了
;液循環フラグ
LD MR16007               ; 液循環
OR MR16009               ; ﾁﾗｰ液送り
OUT MR16102              ; Filter交換時液循環
;排液1  0.05MPa   チラー：液送り停止  タワーランプは黄
;TP表示  排液中
LDP MR16010              ; ﾁﾗｰ送り終了
OR MR16104               ; 排液開始0.05MPa
MPS
LD MR3004                ; 一連動作中
ANB MR16106              ; 排液1　0.2MPa
ANL
OUT MR16104              ; 排液開始0.05MPa
MPP
AND CR2002               ; 常時ON
@MOV #4 DM2              ; TP装置状態
;排液1  0.05MPa調整終了  5秒後に0.2MPa変更
;TP表示  排液中
LD MR16104               ; 排液開始0.05MPa
AND B0719                ; TPSW 調整終了
AND MR3004               ; 一連動作中
OUT MR16105              ; 排液①0.05Mpa
TMR #127 #50             ; 電空0.2MPa変更
;排液1  電空0.2MPa調整終了  10秒後に0.05MPaに変更
;TP表示  排液中
LD T127                  ; 電空0.2MPa変更
AND B0719                ; TPSW 調整終了
OR MR16106               ; 排液1　0.2MPa
AND MR3004               ; 一連動作中
ANB MR16107              ; 排液2　0.05MPa
OUT MR16106              ; 排液1　0.2MPa
TMR #128 #100            ; 空圧0.05MPa変更
;排液2  電空0.05MPa調整終了  5秒後に0.005MPaに変更
;TP表示  排液終了0.005MPa
LD T128                  ; 空圧0.05MPa変更
AND B0719                ; TPSW 調整終了
OR MR16107               ; 排液2　0.05MPa
AND MR3004               ; 一連動作中
ANB MR16108              ; 排液2　0.005MPa
OUT MR16107              ; 排液2　0.05MPa
TMR #129 #50             ; 空圧0.005MPa変更
;排液2終了  電空0.005MPa
;TP表示  Filter交換終了
LD T129                  ; 空圧0.005MPa変更
AND B0719                ; TPSW 調整終了
OR MR16108               ; 排液2　0.005MPa
MPS
AND MR3004               ; 一連動作中
OUT MR16108              ; 排液2　0.005MPa
TMR #130 #5              ; Filter交換終了
MPP
@MOV #10 DM2             ; TP装置状態
;検査終了（待機状態にする）
LDP T130                 ; Filter交換終了
AND MR3004               ; 一連動作中
MPS
AND MR7305               ; 一連操作　F排液
OUT MR16109              ; Filter交換終了
MPP
RES MR16000              ; Filter交換
;排液ﾌﾗｸﾞ
LD MR16105               ; 排液①0.05Mpa
OR MR16106               ; 排液1　0.2MPa
OR MR16107               ; 排液2　0.05MPa
OUT MR16110              ; Filter交換排液ﾌﾗｸﾞ
;液循環PBの表示
LD T177                  ; 準備OK　液循環開始？
OUT MR8003               ; 液循環PBの表示
END
ENDH
