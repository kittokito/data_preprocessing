DEVICE:51
;MODULE:塗装出力インターフェース
;MODULE_TYPE:0
LD CR2002                ; 常時ON
TMR #130 #30             ; 塗装ﾛｰﾗ回転遅延
LDB R30800               ; 塗装自動選択SS
AND R30804               ; 塗装ローラON　SW
OR R30800                ; 塗装自動選択SS
AND T130                 ; 塗装ﾛｰﾗ回転遅延
OUT R31600               ; 塗装ローラモータ　ON
LD R31909                ; X軸ティーチング完了
OR R31601                ; 塗装送り　データ書込中
AND R30908               ; 塗装送り　データ書込
OUT R31601               ; 塗装送り　データ書込中
LD R46106                ; 塗装押え上昇（Man）
OR R46308                ; 塗装押え上昇（Man&Org）
OR R47200                ; 塗装押さえ上昇（Auto）
OUT R31602               ; 塗装押え上下　上昇
LD R46112                ; 塗装ﾊﾝﾄﾞ１上昇（Man）
OR R46303                ; 塗装ﾊﾝﾄﾞ１．２上昇（Org）
OR R47103                ; 塗装ﾊﾝﾄﾞ1上昇（Auto）
ANB R31606               ; 塗装ハンド上下　下降１
OUT R31604               ; 塗装ハンド上下　上昇１
LD R46110                ; 塗装ﾊﾝﾄﾞ２上昇（Man）
OR R46303                ; 塗装ﾊﾝﾄﾞ１．２上昇（Org）
OR R47104                ; 塗装ﾊﾝﾄﾞ2上昇（Auto）
ANB R31607               ; 塗装ハンド上下　下降２
OUT R31605               ; 塗装ハンド上下　上昇２
LD R46114                ; 塗装ﾊﾝﾄﾞ１下降（Man）
OR R47105                ; 塗装ﾊﾝﾄﾞ1下降（Auto）
ANB R31604               ; 塗装ハンド上下　上昇１
OUT R31606               ; 塗装ハンド上下　下降１
LD R46200                ; 塗装ﾊﾝﾄﾞ２下降（Man）
OR R47106                ; 塗装ﾊﾝﾄﾞ2下降（Auto）
ANB R31605               ; 塗装ハンド上下　上昇２
OUT R31607               ; 塗装ハンド上下　下降２
LD R46204                ; 塗装ﾊﾝﾄﾞ閉（Man）
OR R47107                ; 塗装ﾊﾝﾄﾞ閉（Auto）
ANB R31609               ; 塗装ハンド　開
OUT R31608               ; 塗装ハンド　閉
LD R46202                ; 塗装ﾊﾝﾄﾞ開（Man）
OR R46302                ; 塗装ﾊﾝﾄﾞ開（Org）
OR R47108                ; 塗装ﾊﾝﾄﾞ開（Auto）
ANB R31608               ; 塗装ハンド　閉
OUT R31609               ; 塗装ハンド　開
LD CR2002                ; 常時ON
ANB T131                 ; １分クロック
TMR #131 #600            ; １分クロック
LD R48600                ; 液交換ﾘｾｯﾄPB ON記憶
AND T44                  ; 塗装液面センサ
MPS
AND T131                 ; １分クロック
@INC.S DM6100            ; 液交換時間ｶｳﾝﾄReg
MPP
AND>=.L DM6100 +240      ; 液交換時間ｶｳﾝﾄReg
OUT R48500               ; 液交換時間タイアップ
LD R48500                ; 液交換時間タイアップ
OR R48501                ; 液交換ﾀｲﾑｱｯﾌﾟ警報<#１０>
ANB R48503               ; 液交換警報リセット
OUT R48501               ; 液交換ﾀｲﾑｱｯﾌﾟ警報<#１０>
DIFU R48502              ; 液交換ﾀｲﾑｱｯﾌﾟ警報ﾌﾞｻﾞｰON PLS
LD R48501                ; 液交換ﾀｲﾑｱｯﾌﾟ警報<#１０>
AND R30909               ; 液交換リセットPB
LD R30003                ; リセットPB(メイン)
ANB R48600               ; 液交換ﾘｾｯﾄPB ON記憶
ORL
MOV #0 DM6100            ; 液交換時間ｶｳﾝﾄReg
OUT R48503               ; 液交換警報リセット
LD R48501                ; 液交換ﾀｲﾑｱｯﾌﾟ警報<#１０>
LD CR2006                ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB R48600               ; 液交換ﾘｾｯﾄPB ON記憶
AND R48602               ; 液交換ﾀｲﾑｱｯﾌﾟ警報PLS
ORL
OUT R31610               ; 液交換　表示
LD R30909                ; 液交換リセットPB
AND R48601               ; リセットPB(メイン)　ON記憶
SET R48600               ; 液交換ﾘｾｯﾄPB ON記憶
LD R30003                ; リセットPB(メイン)
SET R48601               ; リセットPB(メイン)　ON記憶
LD R48501                ; 液交換ﾀｲﾑｱｯﾌﾟ警報<#１０>
DIFU R48602              ; 液交換ﾀｲﾑｱｯﾌﾟ警報PLS
LD R48602                ; 液交換ﾀｲﾑｱｯﾌﾟ警報PLS
LD R48600                ; 液交換ﾘｾｯﾄPB ON記憶
AND R48601               ; リセットPB(メイン)　ON記憶
ANB R30800               ; 塗装自動選択SS
AND R30802               ; 塗装起動ＰＢ
ORL
RES R48600               ; 液交換ﾘｾｯﾄPB ON記憶
RES R48601               ; リセットPB(メイン)　ON記憶
LD R31115                ; 非常停止（OFFで非常停止）<#1>
OUT R31611               ; 非常停止
LD R46504                ; 塗装一時停止受付
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT R31612               ; 塗装一時停止中表示
LD R41501                ; 塗装原位置
OUT R31613               ; 塗装原位置表示
LD R46800                ; 塗装サイクル運転中
ANB R46400               ; 塗装サイクル停止受付
LD R46400                ; 塗装サイクル停止受付
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT R31614               ; 塗装運転中表示
LD T5                    ; 電源ON後７秒
ANB R31114               ; 15B　アラーム
ORB R72015               ; 塗装アラーム無し
OR R72013                ; NC原点復帰未完了
OR R72014                ; 原点復帰時ﾊﾟﾚｯﾄ取除き要求
OUT R31615               ; 塗装ｱﾗｰﾑ表示
;<h1/>塗装列ロボット出力I/F
LD R48001                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（１）
OR R48003                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（3）
OR R48005                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（5）
OR R48007                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（7）
LD R46800                ; 塗装サイクル運転中
OR R46104                ; 塗装列切換開始PLS Rob 0.2Sec(Man
OR R46312                ; 塗装列ﾛﾎﾞｯﾄP1へ移動（Org）
ANL
OUT R31700               ; ５A 指令ポジション１
LD R48002                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（2）
OR R48003                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（3）
OR R48006                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（6）
OR R48007                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（7）
LD R46800                ; 塗装サイクル運転中
OR R46104                ; 塗装列切換開始PLS Rob 0.2Sec(Man
ANL
OUT R31701               ; ６A 指令ポジション２
LD R48004                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（4）
OR R48005                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（5）
OR R48006                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（6）
OR R48007                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（7）
LD R46800                ; 塗装サイクル運転中
OR R46104                ; 塗装列切換開始PLS Rob 0.2Sec(Man
ANL
OUT R31702               ; ７A 指令ポジション４
LD R48008                ; 塗装ﾊﾟﾚｯﾄ列ﾎﾟｲﾝﾀ（8）
LD R46800                ; 塗装サイクル運転中
OR R46104                ; 塗装列切換開始PLS Rob 0.2Sec(Man
ANL
OUT R31703               ; ８A 指令ポジション８
LD CR2003                ; 常時OFF
OUT R31704               ; ９A 指令ポジション１６
LD CR2003                ; 常時OFF
OUT R31705               ; １０A 指令ポジション３２
LDB R31115               ; 非常停止（OFFで非常停止）<#1>
LD R41507                ; 塗装部アラーム無し
AND R30800               ; 塗装自動選択SS
ORL
OUT R31706               ; １４A ブレーキ解除
LD CR2003                ; 常時OFF
OUT R31707               ; １５B 運転モード
LD R46208                ; 塗装列ﾛﾎﾞｯﾄ原点復帰0.2SecPLS(Org
OR R46305                ; 塗装列切換ﾛﾎﾞｯﾄ起動（Org）
OUT R31708               ; １６A 原点復帰
LD R31115                ; 非常停止（OFFで非常停止）<#1>
OUT R31709               ; １７A 一時停止
LDP T126                 ; 塗装列ﾛﾎﾞｯﾄP1へ移動Dly
OR R10400                ; 18A　ｽﾀｰﾄ
ANB T128                 ; 18A　ｽﾀｰﾄ切
OUT R10400               ; 18A　ｽﾀｰﾄ
TMR #128 #3              ; 18A　ｽﾀｰﾄ切
LD R46104                ; 塗装列切換開始PLS Rob 0.2Sec(Man
OR R10400                ; 18A　ｽﾀｰﾄ
OR R46810                ; 塗装開始NC待機位置へ移動（Auto）
OR T143                  ; 塗装8回終了ﾊﾝﾄﾞ2上昇列ﾛﾎﾞｯﾄ復帰
ANB R46504               ; 塗装一時停止受付
OUT R31710               ; １８A スタート
LD= DM100 #3             ; 異常コードｾｯﾄRag
AND T0                   ; ブザーOFF確認
AND R30003               ; リセットPB(メイン)
ORP T6                   ; 塗装列切換ﾘｾｯﾄ遅延
OR R31711                ; １９A アラームリセット
ANB T129                 ; １９A アラームリセット時間
OUT R31711               ; １９A アラームリセット
TMR #129 #1              ; １９A アラームリセット時間
LD R31115                ; 非常停止（OFFで非常停止）<#1>
AND T7                   ; 塗装列切換ｻｰﾎﾞON遅延
OUT R31712               ; ２０A サーボオン
;<h1/>塗装NC出力I/F
LD CR2002                ; 常時ON
AND R31115               ; 非常停止（OFFで非常停止）<#1>
OUT R32400               ; 塗装NC動作許可
LD R31800                ; 塗装NC動作可能
OUT R10000               ; 塗装NC運転準備OK
LD R20000                ; NCエラークリア命令
AND R31806               ; 塗装NCエラー中
OUT R32406               ; 塗装NCエラークリア
;<h1/>原点復帰
LDP R20001               ; 原点復帰開始命令
AND R31800               ; 塗装NC動作可能
ANB R31801               ; 塗装NC移動中
SET R32408               ; 塗装NC原点復帰
LD R31808                ; 塗装NC原点復帰中
ORB R31115               ; 非常停止（OFFで非常停止）<#1>
RES R32408               ; 塗装NC原点復帰
LD R31805                ; 塗装NC位置決め完了
OUT R10205               ; 塗装NC位置決め動作完了
LDF R31808               ; 塗装NC原点復帰中
ORF R31809               ; X軸ホームポジション移動中
ORF R46914               ; 塗装NC送り原点へ送り（Auto）
OR R47002                ; 塗装ｻｲｸﾙ終了・列ﾛﾎﾞｯﾄP1ｾｯﾄ
ORF R49006               ; NC受渡位置（移動指令No.6）
OR R10207                ; 塗装NC原点停止フラグ
AND R10205               ; 塗装NC位置決め動作完了
OUT R10207               ; 塗装NC原点停止フラグ
LD CR2002                ; 常時ON
MPS
LD>=.L DM5710 DM50       ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ1比較用L側(固定)
AND<=.L DM5710 DM52      ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ1比較用H側(固定)
ANL
OUT R10208               ; 塗装NCゾーン１
MRD
LD>=.L DM5710 DM54       ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ2比較用L側(固定)
AND<=.L DM5710 DM56      ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ2比較用H側(固定)
ANL
OUT R10209               ; 塗装NCゾーン２
MPP
LD>=.L DM5710 DM58       ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ3比較用L側(固定)
AND<=.L DM5710 DM60      ; 現在位置ﾃﾞｰﾀ（符号付き2W）  ｿﾞｰﾝ3比較用H側(固定)
ANL
OUT R10210               ; 塗装NCゾーン３
;<h1/>運転開始
;
;　　ﾎﾟｲﾝﾄNo.1:受取位置
;　　ﾎﾟｲﾝﾄNo.2:待機位置(No.5と同じ位置)
;　　ﾎﾟｲﾝﾄNo.3:塗装開始位置
;　　ﾎﾟｲﾝﾄNo.4:塗装完了位置
;　　ﾎﾟｲﾝﾄNo.5:待機位置(No.2と同じ位置)
;　　ﾎﾟｲﾝﾄNo.6:受渡位置
LDP R10101               ; 塗装NC起動
MPS
LD R49000                ; NC原点位置（移動指令No.0）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #1 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49001                ; NC受取位置（移動指令No.1）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #2 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49002                ; NC待機位置（移動指令No.2）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #3 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49003                ; NC塗装開始位置（移動指令No.3）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #4 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49004                ; NC塗装終了位置（移動指令No.4）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #5 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49005                ; NC待機位置（移動指令No.5）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #6 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R49006                ; NC受渡位置（移動指令No.6）
AND R31800               ; 塗装NC動作可能
ANB R30800               ; 塗装自動選択SS
ANL
DW #7 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46802                ; 塗装NCﾊﾟﾚｯﾄ受取へ押え下降(Autol)
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #1 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46810                ; 塗装開始NC待機位置へ移動（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #2 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46813                ; 塗装NC塗装開始位置へ移動（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #3 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46900                ; 塗装NC塗装終了位置へ移動（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #4 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46904                ; 塗装NC待機位置へ移動（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #5 DM10024            ; 塗装NC開始読出ポイントNo.
MRD
LD R46909                ; 塗装終了NC受渡し位置へ（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #6 DM10024            ; 塗装NC開始読出ポイントNo.
MPP
LD R46914                ; 塗装NC送り原点へ送り（Auto）
AND R31800               ; 塗装NC動作可能
AND R30800               ; 塗装自動選択SS
ANL
DW #7 DM10024            ; 塗装NC開始読出ポイントNo.
LDP R10101               ; 塗装NC起動
AND R31800               ; 塗装NC動作可能
SET R32403               ; 塗装NC運転開始
LD R31810                ; X軸運転開始要求受理
ORB R31115               ; 非常停止（OFFで非常停止）<#1>
RES R32403               ; 塗装NC運転開始
LD R46011                ; NCｽﾃｯﾌﾟ起動指令（Man）
OR R46802                ; 塗装NCﾊﾟﾚｯﾄ受取へ押え下降(Autol)
OR R46810                ; 塗装開始NC待機位置へ移動（Auto）
OR R46813                ; 塗装NC塗装開始位置へ移動（Auto）
OR R46900                ; 塗装NC塗装終了位置へ移動（Auto）
OR R46904                ; 塗装NC待機位置へ移動（Auto）
OR R46909                ; 塗装終了NC受渡し位置へ（Auto）
OR R46914                ; 塗装NC送り原点へ送り（Auto）
OR R10101                ; 塗装NC起動
ANB R46504               ; 塗装一時停止受付
ANB R31810               ; X軸運転開始要求受理
ANB R31801               ; 塗装NC移動中
OUT R10101               ; 塗装NC起動
LD R46206                ; 塗装NC原点復帰指令0.2SecPLS(Org)
OR R46304                ; 塗装NC原点復帰、ﾊﾝﾄﾞ復帰後（Org
OUT R20001               ; 原点復帰開始命令
;<h1/>塗装NCJog
;
LD R46101                ; 塗装NC Jog前進指令（Man）
LDB R31806               ; 塗装NCエラー中
OR CR2002                ; 常時ON
ANL
OUT R32511               ; JOGX軸CW
LD R46103                ; 塗装NC Jog後退指令（Man）
LDB R31806               ; 塗装NCエラー中
OR CR2002                ; 常時ON
ANL
OUT R32512               ; JOGX軸CCW
LD R30806                ; 塗装塗送高速SS
OUT R32513               ; JOGX軸高速ON
;<h1/>塗装NCエラーﾘｾｯﾄ
LD R31814                ; X軸CWリミットスイッチ状態
AND R46103               ; 塗装NC Jog後退指令（Man）
LD R31815                ; X軸CCWリミットスイッチ状態
AND R46101               ; 塗装NC Jog前進指令（Man）
ORL
ANB T145                 ; 塗装NCｴﾗｰﾘｾｯﾄ解除
OUT R48700               ; 塗装NCｴﾗｰﾘｾｯﾄ
LD R48700                ; 塗装NCｴﾗｰﾘｾｯﾄ
AND R31801               ; 塗装NC移動中
OR R48701                ; 塗装ｴﾗｰﾘｾｯﾄ完了
LD R46101                ; 塗装NC Jog前進指令（Man）
OR R46103                ; 塗装NC Jog後退指令（Man）
ANL
OUT R48701               ; 塗装ｴﾗｰﾘｾｯﾄ完了
TMR #145 #5              ; 塗装NCｴﾗｰﾘｾｯﾄ解除
LD R48700                ; 塗装NCｴﾗｰﾘｾｯﾄ
OUT R20000               ; NCエラークリア命令
;<h1/>塗装NC減速停止
LD R41001                ; 塗装自動、手動SS切換ON PLS
OR R41007                ; 塗装自動、手動SS切換確認OFF PLS
OR R20006                ; 塗装NC減速停止命令
ANB T147                 ; 塗装NC減速停止命令時間
OUT R20006               ; 塗装NC減速停止命令
TMR #147 #5              ; 塗装NC減速停止命令時間
LDB R31115               ; 非常停止（OFFで非常停止）<#1>
LDB R41507               ; 塗装部アラーム無し
AND R30800               ; 塗装自動選択SS
ORL
OUT R20002               ; 塗装NC減速停止命令
LD R20002                ; 塗装NC減速停止命令
AND R31801               ; 塗装NC移動中
OR R20005                ; ﾃﾞｰﾀ書込時，塗装NC減速停止命令
OR R20006                ; 塗装NC減速停止命令
OUT R32402               ; 塗装NC減速停止
END
ENDH
