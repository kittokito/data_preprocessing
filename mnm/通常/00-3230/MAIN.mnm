DEVICE:53
;MODULE:MAIN
;MODULE_TYPE:0
;□□　　ダミー接点　　□□
LD CR2002                ; 常時ON
OUT M43                  ; 後で修正(藤坂)
LD CR2003                ; 常時OFF
OUT M45                  ; 修正箇所140130（腰原）
LD CR2002                ; 常時ON
OUT M119                 ; ﾋｰﾀｰﾚｽ仕様温調異常無視
LD CR2003                ; 常時OFF
OUT M0                   ; 常時OFF
LD CR3905                ; 重度ｴﾗｰ発生中
INV
OUT M1                   ; CPU正常
LD CR2008                ; 運転開始時1ｽｷｬﾝON
OUT M2                   ; 1ｽｷｬﾝON
LD CR2002                ; 常時ON
ANB T2                   ; 0.3Secｸﾛｯｸ
TMH K1 K30               ; 0.3Secｸﾛｯｸ
LD T1                    ; 0.3Secｸﾛｯｸ
TMH K2 K30               ; 0.3Secｸﾛｯｸ
LD CR2002                ; 常時ON
ANB T5                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
TMH K4 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
LD T4                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
TMH K5 K25               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1-1
LD CR2002                ; 常時ON
ANB T7                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
TMH K6 K40               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
LD T6                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
TMH K7 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2-1
LD CR2002                ; 常時ON
ANB T9                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
TMR K8 K10               ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
LD T8                    ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
TMR K9 K5                ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3-1
LD CR2002                ; 常時ON
ANB T37                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
TMR K36 K100             ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
LD T36                   ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
TMR K37 K5               ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ1
LD CR2002                ; 常時ON
ANB T39                  ; ﾜｰｸ投入可音程ﾀｲﾏ1
TMH K38 K30              ; ﾜｰｸ投入可音程ﾀｲﾏ
LD T38                   ; ﾜｰｸ投入可音程ﾀｲﾏ
TMH K39 K100             ; ﾜｰｸ投入可音程ﾀｲﾏ1
LD X04                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT1
TMR K60 K2               ; 投入部ﾗｲﾄｶｰﾃﾝ確認ﾀｲﾏ
;□□　安全関連入力受け　□□
;安全系の入力は一旦ここで受ける。
LD X03                   ; 圧空元圧正常
OUT M128                 ; 圧空正常
LD X01                   ; 非常停止正常
OUT M129                 ; 非常停止正常
LD CR2002                ; 常時ON
OUT M130                 ; 外部非常停止正常
LD X02                   ; 安全扉正常
OUT M131                 ; 安全扉正常
LD X04                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT1
AND X06                  ; 排出部ﾄﾞｱ閉
AND X08                  ; 予備
OR CR2002                ; 常時ON
OUT M132                 ; ｴﾘｱｾﾝｻ正常
;□□　スイッチ変換　□□
;操作系の入力は一旦ここで受ける。
LD B1016                 ; DRY_RUN_PB
OUT M7                   ; 空運転PB
LD B1001                 ; 自動/手動PB
OUT M10                  ; 自動/手動PB
;今回は運転準備PB無し装置電源入で常時ON
LD X00                   ; 装置電源入
OUT M12                  ; 運転準備入PB
LD CR2003                ; 常時OFF
OUT M13                  ; 運転準備切PB
LD B1010                 ; ﾌﾞｻﾞｰ停止PB
OUT M15                  ; ﾌﾞｻﾞｰ停止PB
LD B1011                 ; ｴﾗｰﾘｾｯﾄPB
OUT M16                  ; ﾘｾｯﾄPB
LD B1000                 ; 原点復帰PB
OUT M17                  ; 原点復帰PB
LD B1002                 ; ｽﾀｰﾄPB
OUT M18                  ; 起動PB
LD B1003                 ; ｻｲｸﾙ停止PB
TMR K15 K50              ; 強制停止
OUT M19                  ; 停止PB
;用途に応じて使用する
LD CR2003                ; 常時OFF
MPS
TMR K16 K20              ; 終了PB_ONﾃﾞｨﾚｲ
MPP
AND T16                  ; 終了PB_ONﾃﾞｨﾚｲ
OUT M20                  ; 終了PB
LD CR2003                ; 常時OFF
MPS
TMR K17 K10              ; 払出PB_ONﾃﾞｨﾚｲ
MPP
AND T17                  ; 払出PB_ONﾃﾞｨﾚｲ
OUT M21                  ; 払出PB
;<h1/>□□　　各モード選択　　□□
;自動・手動モード
LDP M10                  ; 自動/手動PB
AND M35                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M32                  ; 自動ﾓｰﾄﾞ
MEP
LD M32                   ; 自動ﾓｰﾄﾞ
ANPB M33                 ; 手動ﾓｰﾄﾞ
ORL
OUT M32                  ; 自動ﾓｰﾄﾞ
LDP M10                  ; 自動/手動PB
ANB M33                  ; 手動ﾓｰﾄﾞ
ANB M49                  ; 全ﾕﾆｯﾄ起動中
MEP
OR T51
OR M66                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR M65                   ; 即停止異常(緊急停止)
OR CR2008                ; 運転開始時1ｽｷｬﾝON
LD M33                   ; 手動ﾓｰﾄﾞ
ANPB M32                 ; 自動ﾓｰﾄﾞ
ORL
ANB M81                  ; 自動運転開始BZ
OUT M33                  ; 手動ﾓｰﾄﾞ
LDB M48                  ; 全ﾕﾆｯﾄ原点
ANB M81                  ; 自動運転開始BZ
ORB M35                  ; 自動ﾓｰﾄﾞ切替・起動可条件
ANB M49                  ; 全ﾕﾆｯﾄ起動中
TMR K51 K10
;この手動操作可条件はメインのみの条件
;ユニット毎に必要な条件は各ユニットへ追加する
LD M128                  ; 圧空正常
AND M129                 ; 非常停止正常
AND M130                 ; 外部非常停止正常
LD M131                  ; 安全扉正常
AND M132                 ; ｴﾘｱｾﾝｻ正常
OR M39                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ANL
OUT M34                  ; ﾒｲﾝ手動操作可能条件
LD X1148                 ; ｺｰﾃｨﾝｸﾞ材温調完了
ANB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
OR= D15 K1               ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
LD X1191                 ; ｺｰﾃｨﾝｸﾞ溶剤液面検出
OR= D2110 K1             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
ANL
AND M48                  ; 全ﾕﾆｯﾄ原点
AND M144                 ; 供給治具No._OK
AND M145                 ; 集塵治具No._OK
AND M146                 ; 排出治具No._OK
OUT M35                  ; 自動ﾓｰﾄﾞ切替・起動可条件
;<h1/>空運転､払出モードは必要に応じて使用
LD M7                    ; 空運転PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K34 K50              ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LDB M37                  ; 空運転ﾓｰﾄﾞ
ANB M49                  ; 全ﾕﾆｯﾄ起動中
ANP T34                  ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
LD M37                   ; 空運転ﾓｰﾄﾞ
ANPB T34                 ; 空運転ﾓｰﾄﾞONﾃﾞｨﾚｲ
ORL
OUT M37                  ; 空運転ﾓｰﾄﾞ
LDP M19                  ; 停止PB
OR M73                   ; 払出停止異常まとめ
ANB M160                 ; 一時停止
ANB M38                  ; 払出ﾓｰﾄﾞ
LD M38                   ; 払出ﾓｰﾄﾞ
ANPB M18                 ; 起動PB
ORL
AND M49                  ; 全ﾕﾆｯﾄ起動中
OUT M38                  ; 払出ﾓｰﾄﾞ
;ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD B1015                 ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙPB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K35 K50              ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
LDP T35                  ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
ANB M39                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LDPB T35                 ; ｲﾝﾀｰﾛｯｸ解除ONﾃﾞｨﾚｲ
AND M39                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M49                  ; 全ﾕﾆｯﾄ起動中
AND M33                  ; 手動ﾓｰﾄﾞ
ANB M50                  ; 全ﾕﾆｯﾄ原点復帰中
ANB B1000                ; 原点復帰PB
OUT M39                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
;□□　　装置状態集約　　□□
;■装置原点
LD M2384                 ; ﾕﾆｯﾄ1原点
AND M3184                ; ﾕﾆｯﾄ2原点
AND M3984                ; ﾕﾆｯﾄ3原点
AND M4784                ; ﾕﾆｯﾄ4原点
OUT M48                  ; 全ﾕﾆｯﾄ原点
;■起動中
LDB M2389                ; ﾕﾆｯﾄ1起動中
ANB M3189                ; ﾕﾆｯﾄ2起動中
ANB M3989                ; ﾕﾆｯﾄ3起動中
ANB M4789                ; ﾕﾆｯﾄ4起動中
INV
OUT M49                  ; 全ﾕﾆｯﾄ起動中
;■原点復帰中
LDB M2388                ; ﾕﾆｯﾄ1原点復帰中
ANB M3188                ; ﾕﾆｯﾄ2原点復帰中
ANB M3988                ; ﾕﾆｯﾄ3原点復帰中
ANB M4788                ; ﾕﾆｯﾄ4原点復帰中
INV
OUT M50                  ; 全ﾕﾆｯﾄ原点復帰中
;■ﾛﾎﾞｯﾄ準備完了
LD X108C                 ; 塗布ﾉｽﾞﾙ上下RB_ｻｰﾎﾞON(SVON)
AND X110C                ; 給材搬送PP　RB_ｻｰﾎﾞON(SVON)
AND X112C                ; 集塵ﾉｽﾞﾙ前後RB_ｻｰﾎﾞON(SVON)
AND X1003                ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
AND X1004                ; ﾛﾎﾞｯﾄ初期化完了
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
AND X1006                ; ﾛﾎﾞｯﾄ外部ﾓｰﾄﾞ
ANB X1008                ; ﾛﾎﾞｯﾄ警告
OUT M53                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
;■運転準備
LD CR2007                ; 運転開始時1ｽｷｬﾝOFF
TMR K0 K20               ; 電源投入0Nﾃﾞｨﾚｲ
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
AND M12                  ; 運転準備入PB
OR M5                    ; 運転準備補助
LDB M13                  ; 運転準備切PB
OR M49                   ; 全ﾕﾆｯﾄ起動中
ANL
ANB M65                  ; 即停止異常(緊急停止)
OUT M5                   ; 運転準備補助
LD M5                    ; 運転準備補助
MPS
TMR K10 K10              ; 運転準備ﾃﾞｨﾚｲ
MPP
AND T10                  ; 運転準備ﾃﾞｨﾚｲ
OUT M64                  ; 運転準備
;□□　　異常検出まとめ　　□□
;■即停止異常(全ﾕﾆｯﾄ即停止)
LDB B00                  ; 非常停止異常
ANB B01                  ; 圧空低下異常
ANB B02                  ; 安全扉開異常
ANB B03                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ANB B04                  ; 外部非常停止
ANB B05                  ; PLC異常
ANB B06                  ; ﾘﾝｸ異常
ANB M72                  ; 非常停止異常まとめ
INV
OUT M65                  ; 即停止異常(緊急停止)
;■ﾕﾆｯﾄ異常検出(該当ﾕﾆｯﾄ以外ｻｲｸﾙ停止)
LDB M2390                ; ﾕﾆｯﾄ1ｻｲｸﾙ停止異常
ANB M3190                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ANB M3990                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
ANB M4790                ; ﾕﾆｯﾄ4ｻｲｸﾙ停止異常
ANB M90                  ; ﾉｽﾞﾙ圧異常ｻｲｸﾙ停止指令
INV
OUT M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LDB B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
ANB B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
ANB B051                 ; ﾉｽﾞﾙIN側圧力上限異常
ANB B052                 ; ﾉｽﾞﾙIN側圧力下限異常
OUB M90                  ; ﾉｽﾞﾙ圧異常ｻｲｸﾙ停止指令
;■ﾕﾆｯﾄ毎各種警報検出(ﾜｰｸ不足･ｵﾍﾟｺｰﾙ等)
LDB M2392                ; ﾕﾆｯﾄ1警報補助
ANB M3192                ; ﾕﾆｯﾄ2警報補助
ANB M3992                ; ﾕﾆｯﾄ3警報補助
ANB M4792                ; ﾕﾆｯﾄ4警報補助
INV
OUT M67                  ; 警報(ﾜｰｸ不足等)
;■ﾕﾆｯﾄ毎異常ﾌﾞｻﾞｰ検出
LDB M159                 ; ﾒｲﾝ異常BZ　まとめ
ANB M2393                ; ﾕﾆｯﾄ1異常BZ補助
ANB M3193                ; ﾕﾆｯﾄ2異常BZ補助
ANB M3993                ; ﾕﾆｯﾄ3異常BZ補助
ANB M4793                ; ﾕﾆｯﾄ4異常BZ補助
INV
OUT M68                  ; 異常ﾌﾞｻﾞｰ
;□□　　自動運転停止指令(ｻｲｸﾙ停止)　　□□
LD M66                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
OR M90                   ; ﾉｽﾞﾙ圧異常ｻｲｸﾙ停止指令
OR T15                   ; 強制停止
OR M71                   ; 自動起動停止指令(払出停止)
OR M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M49                  ; 全ﾕﾆｯﾄ起動中
OUT M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
;□□　　払出完了運転停止指令(ｻｲｸﾙ停止)　　□□
LD M38                   ; 払出ﾓｰﾄﾞ
AND M49                  ; 全ﾕﾆｯﾄ起動中
AND M1920                ; UNIT1払出処理ﾜｰｸ無
AND M3520                ; UNIT3払出処理ﾜｰｸ無
AND M2398                ; ﾕﾆｯﾄ1動作無し確認
AND M3198                ; ﾕﾆｯﾄ2動作無し確認
AND M3998                ; ﾕﾆｯﾄ3動作無し確認
AND M4798                ; ﾕﾆｯﾄ4動作無し確認
OUT M71                  ; 自動起動停止指令(払出停止)
;緊急停止異常まとめ
LDB M2385                ; ﾕﾆｯﾄ1非常停止異常
ANB M3185                ; ﾕﾆｯﾄ2非常停止停止異常
ANB M3985                ; ﾕﾆｯﾄ3非常停止異常
ANB M4785                ; ﾕﾆｯﾄ4非常停止異常
INV
OUT M72                  ; 非常停止異常まとめ
;払出停止異常まとめ
LDB M2391                ; ﾕﾆｯﾄ1払出停止異常
ANB M3191                ; ﾕﾆｯﾄ2払出停止異常
ANB M3991                ; ﾕﾆｯﾄ3払出停止異常
ANB M4791                ; ﾕﾆｯﾄ4払出停止異常
INV
OUT M73                  ; 払出停止異常まとめ
;■各ﾕﾆｯﾄｴﾗｰ検出
LDB B07                  ; PLCﾊﾞｯﾃﾘ低下
ANB B09                  ; 原点未完ﾓｰﾄﾞ切替不可異常
ANB M2397                ; ﾕﾆｯﾄ1ｴﾗｰ発生
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M3997                ; ﾕﾆｯﾄ3ｴﾗｰ発生
ANB M4797                ; ﾕﾆｯﾄ4ｴﾗｰ発生
INV
OUT M77                  ; ｴﾗｰ発生
;選択治具番号と治具ID照合
LD D111.0                ; 治具選択No.1
AND X117C                ; 給材PP給材部治具確認1
LDB D111.0               ; 治具選択No.1
ANB X117C                ; 給材PP給材部治具確認1
ORL
LD D111.1                ; 治具選択No.2
AND X117D                ; 給材PP給材部治具確認2
LDB D111.1               ; 治具選択No.2
ANB X117D                ; 給材PP給材部治具確認2
ORL
ANL
LD D111.2                ; 治具選択No.4
AND X117E                ; 給材PP給材部治具確認3
LDB D111.2               ; 治具選択No.4
ANB X117E                ; 給材PP給材部治具確認3
ORL
ANL
OUT M144                 ; 供給治具No._OK
LD D111.0                ; 治具選択No.1
AND X1270                ; 給材PP集塵部治具確認1
LDB D111.0               ; 治具選択No.1
ANB X1270                ; 給材PP集塵部治具確認1
ORL
LD D111.1                ; 治具選択No.2
AND X1271                ; 給材PP集塵部治具確認2
LDB D111.1               ; 治具選択No.2
ANB X1271                ; 給材PP集塵部治具確認2
ORL
ANL
LD D111.2                ; 治具選択No.4
AND X1272                ; 給材PP集塵部治具確認3
LDB D111.2               ; 治具選択No.4
ANB X1272                ; 給材PP集塵部治具確認3
ORL
ANL
OUT M145                 ; 集塵治具No._OK
LD D111.0                ; 治具選択No.1
AND X1273                ; 給材PP取出部治具確認1
LDB D111.0               ; 治具選択No.1
ANB X1273                ; 給材PP取出部治具確認1
ORL
LD D111.1                ; 治具選択No.2
AND X1274                ; 給材PP取出部治具確認2
LDB D111.1               ; 治具選択No.2
ANB X1274                ; 給材PP取出部治具確認2
ORL
ANL
LD D111.2                ; 治具選択No.4
AND X1275                ; 給材PP取出部治具確認3
LDB D111.2               ; 治具選択No.4
ANB X1275                ; 給材PP取出部治具確認3
ORL
ANL
OUT M146                 ; 排出治具No._OK
;機種別ﾛﾎﾞｯﾄPOS先頭番号演算
;機種別に５POSのﾛﾎﾞｯﾄﾃﾞｰﾀを使用
;適用ﾛﾎﾞｯﾄは給材PP_RB
LD CR2002                ; 常時ON
CAL- D100 K1 D102        ; TP機種設定No.  機種別RB_POS基準
CAL* D102 K5 D104        ; 機種別RB_POS基準  機種別RB_POS先頭番号
;電源OFF処理
;乾燥炉冷却後に主電源OFF
LDF X0A                  ; 電源PB
AND T0                   ; 電源投入0Nﾃﾞｨﾚｲ
ANB M49                  ; 全ﾕﾆｯﾄ起動中
OR M192                  ; 電源OFF処理開始
OUT M192                 ; 電源OFF処理開始
;一時停止処理
;アラーム発生時に使用
LD M83                   ; 各工程起動指令
OR M49                   ; 全ﾕﾆｯﾄ起動中
ANB M179                 ; 一時停止処理終了
ANB M163                 ; 一時停止　ｷｬﾝｾﾙ
MC
LDP B1012                ; 一時停止PB
OR M160                  ; 一時停止
OUT M160                 ; 一時停止
LD M2398                 ; ﾕﾆｯﾄ1動作無し確認
AND M3198                ; ﾕﾆｯﾄ2動作無し確認
AND M3998                ; ﾕﾆｯﾄ3動作無し確認
AND M4798                ; ﾕﾆｯﾄ4動作無し確認
OUB M161                 ; PACK中
LD M160                  ; 一時停止
OR M162                  ; 全ﾕﾆｯﾄ停止OK
OR M163                  ; 一時停止　ｷｬﾝｾﾙ
MPS
LDB M163                 ; 一時停止　ｷｬﾝｾﾙ
ANB M161                 ; PACK中
ANL
OUT M162                 ; 全ﾕﾆｯﾄ停止OK
MPP
LDB M162                 ; 全ﾕﾆｯﾄ停止OK
AND M161                 ; PACK中
ANP B1002                ; ｽﾀｰﾄPB
ANL
OUT M163                 ; 一時停止　ｷｬﾝｾﾙ
LD M162                  ; 全ﾕﾆｯﾄ停止OK
OR M170                  ; ﾄﾞｱﾛｯｸ開
ANB M171                 ; ﾄﾞｱ閉確認
OUT M170                 ; ﾄﾞｱﾛｯｸ開
LD M170                  ; ﾄﾞｱﾛｯｸ開
AND X02                  ; 安全扉正常
ANP B1002                ; ｽﾀｰﾄPB
OR M171                  ; ﾄﾞｱ閉確認
OUT M171                 ; ﾄﾞｱ閉確認
LD M171                  ; ﾄﾞｱ閉確認
AND X02                  ; 安全扉正常
AND Y51                  ; 安全扉ﾛｯｸ
OR M178                  ; 状態復帰完了
OUT M178                 ; 状態復帰完了
LD M178                  ; 状態復帰完了
OR M179                  ; 一時停止処理終了
OUT M179                 ; 一時停止処理終了
MCR
;<h1/>□□　　自動起動回路　　□□
LD M32                   ; 自動ﾓｰﾄﾞ
AND M35                  ; 自動ﾓｰﾄﾞ切替・起動可条件
AND M48                  ; 全ﾕﾆｯﾄ原点
ANB M49                  ; 全ﾕﾆｯﾄ起動中
ANB M50                  ; 全ﾕﾆｯﾄ原点復帰中
AND M64                  ; 運転準備
ANB M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M2396                ; ﾕﾆｯﾄ1機器準備完了
AND M3196                ; ﾕﾆｯﾄ2機器準備完了
AND M3996                ; ﾕﾆｯﾄ3機器準備完了
AND M4796                ; ﾕﾆｯﾄ4機器準備完了
OUT M80                  ; 自動起動条件
LD M18                   ; 起動PB
AND M80                  ; 自動起動条件
OR M81                   ; 自動運転開始BZ
ANB M65                  ; 即停止異常(緊急停止)
ANB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
OUT M81                  ; 自動運転開始BZ
INV
MOV K0 D10               ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
;起動時ﾌﾞｻﾞｰ
LD M81                   ; 自動運転開始BZ
AND> K3 D10              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
MPS
ANB T21                  ; 自動起動ﾃﾞｨﾚｲ
TMR K20 K10              ; 起動前BZ_ONﾀｲﾏ
MPP
AND T20                  ; 起動前BZ_ONﾀｲﾏ
TMR K21 K5               ; 自動起動ﾃﾞｨﾚｲ
@CAL+ D10 K1 D10         ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ  起動時ﾌﾞｻﾞｰｶｳﾝﾄ
LD= D10 K3               ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
OUT M82                  ; 自動運転開始
LD M82                   ; 自動運転開始
OUT M83                  ; 各工程起動指令
;<h1/>□□　　原点復帰回路　　□□
LDB M82                  ; 自動運転開始
ANB M49                  ; 全ﾕﾆｯﾄ起動中
ANB M50                  ; 全ﾕﾆｯﾄ原点復帰中
AND M64                  ; 運転準備
ANB M65                  ; 即停止異常(緊急停止)
ANB M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M77                  ; ｴﾗｰ発生
AND M33                  ; 手動ﾓｰﾄﾞ
AND M2396                ; ﾕﾆｯﾄ1機器準備完了
AND M3196                ; ﾕﾆｯﾄ2機器準備完了
AND M3996                ; ﾕﾆｯﾄ3機器準備完了
AND M4796                ; ﾕﾆｯﾄ4機器準備完了
OUT M96                  ; 原点復帰可能条件
LD M17                   ; 原点復帰PB
AND M96                  ; 原点復帰可能条件
OR M97                   ; 原点復帰動作中
ANB M102                 ; 装置全原点確認
ANB M65                  ; 即停止異常(緊急停止)
ANB M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M77                  ; ｴﾗｰ発生
ANB T15                  ; 強制停止
OUT M97                  ; 原点復帰動作中
MPS
TMR K22 K5               ; 原点復帰開始ﾃﾞｨﾚｲ
MPP
AND CR2003               ; 常時OFF
TMR K23 K300             ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LDP T22                  ; 原点復帰開始ﾃﾞｨﾚｲ
OR M98                   ; 原点復帰開始
AND M97                  ; 原点復帰動作中
ANB M100                 ; 原点復帰起動確認
OUT M98                  ; 原点復帰開始
LD M98                   ; 原点復帰開始
AND M2388                ; ﾕﾆｯﾄ1原点復帰中
AND M3188                ; ﾕﾆｯﾄ2原点復帰中
AND M3988                ; ﾕﾆｯﾄ3原点復帰中
AND M4788                ; ﾕﾆｯﾄ4原点復帰中
OUT M99                  ; 原点復帰全起動確認補助
LD M98                   ; 原点復帰開始
AND M99                  ; 原点復帰全起動確認補助
OR M100                  ; 原点復帰起動確認
AND M97                  ; 原点復帰動作中
ANB M101                 ; 原点復帰完了
OUT M100                 ; 原点復帰起動確認
LD M100                  ; 原点復帰起動確認
ANB M50                  ; 全ﾕﾆｯﾄ原点復帰中
OR M101                  ; 原点復帰完了
AND M97                  ; 原点復帰動作中
ANB M102                 ; 装置全原点確認
OUT M101                 ; 原点復帰完了
LD M101                  ; 原点復帰完了
AND M48                  ; 全ﾕﾆｯﾄ原点
OR M102                  ; 装置全原点確認
AND M97                  ; 原点復帰動作中
OUT M102                 ; 装置全原点確認
;□□　　ﾌﾞｻﾞｰ補助回路　　□□
LD M68                   ; 異常ﾌﾞｻﾞｰ
MPS
ANB T30                  ; 異常ﾌﾞｻﾞｰ自動停止
OUT M112                 ; 異常ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K30 K100             ; 異常ﾌﾞｻﾞｰ自動停止
LD M67                   ; 警報(ﾜｰｸ不足等)
MPS
ANB T31                  ; 警報ﾌﾞｻﾞｰ自動停止
OUT M113                 ; 警報ﾌﾞｻﾞｰON
MPP
AND CR2003               ; 常時OFF
TMR K31 K100             ; 警報ﾌﾞｻﾞｰ自動停止
LDP M15                  ; ﾌﾞｻﾞｰ停止PB
OUT M122                 ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LDP M16                  ; ﾘｾｯﾄPB
OUT M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
;□□　　その他補助回路　　□□
;ﾕﾆｯﾄ別動作停止、ﾕﾆｯﾄﾊﾟｽ設定､生産ｶｳﾝﾀ等装置固有の回路がある場合はここ。
LDB M49                  ; 全ﾕﾆｯﾄ起動中
MPS
AND B1019                ; 温度起動条件ON_TP-PB
@MOV K0 D15              ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
MPP
AND B101A                ; 温度起動条件OFF_TP-PB
@MOV K1 D15              ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
;機種・治具選択
LDB M49                  ; 全ﾕﾆｯﾄ起動中
AND B102A                ; ﾌﾟﾛｸﾞﾗﾑNo.設定OK_TP-PB
@MOV D100 D110           ; TP機種設定No.  機種選択No.
@MOV D100 D111           ; TP機種設定No.  治具選択No.
@MOV D100 D112           ; TP機種設定No.  入力機種No.
LD= D100 D110            ; TP機種設定No.  機種選択No.
AND= D100 D111           ; TP機種設定No.  治具選択No.
AND>= D110 K1            ; 機種選択No.
AND<= D110 K10           ; 機種選択No.
OUT M208                 ; 機種選択OK
;TP機種記憶
;Type change memory
LD= D12000 K300          ; 現在表示中画面ﾍﾟｰｼﾞ
@MOV D100 D112           ; TP機種設定No.  入力機種No.
LD<> D12000 K300         ; 現在表示中画面ﾍﾟｰｼﾞ
@MOV D112 D100           ; 入力機種No.  TP機種設定No.
;□□　　出力回路　　□□
LD X00                   ; 装置電源入
OUT Y50                  ; 電源入表示
LD X00                   ; 装置電源入
LD M32                   ; 自動ﾓｰﾄﾞ
LD CR2003                ; 常時OFF
ANB M33                  ; 手動ﾓｰﾄﾞ
ORL
ANL
ANB M170                 ; ﾄﾞｱﾛｯｸ開
OUT Y51                  ; 安全扉ﾛｯｸ
LD M192                  ; 電源OFF処理開始
OUT Y52                  ; 主電源切
LD X05                   ; 投入部ﾗｲﾄｶｰﾃﾝOUT2
LD X07                   ; 排出部ﾗｲﾄｶｰﾃﾝOUT2
AND X09                  ; 予備
OR CR2002                ; 常時ON
ANL
INV
AND M16                  ; ﾘｾｯﾄPB
OUT Y53                  ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ
LD M67                   ; 警報(ﾜｰｸ不足等)
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ANB M68                  ; 異常ﾌﾞｻﾞｰ
LD M68                   ; 異常ﾌﾞｻﾞｰ
LD M65                   ; 即停止異常(緊急停止)
OR M66                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
LD M77                   ; ｴﾗｰ発生
OR M73                   ; 払出停止異常まとめ
AND T4                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ1
ORL
ANL
ORL
LDB M68                  ; 異常ﾌﾞｻﾞｰ
LD B08                   ; 安全扉開
ANB T8                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ3
ANB M39                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
LD M39                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND T36                  ; ｲﾝﾀｰﾛｯｸ解除音程ﾀｲﾏ
ORL
ANL
ORL
LD M1605                 ; 供給部ﾜｰｸ浮き確認NG
AND T6                   ; ﾌﾞｻﾞｰ音程ﾀｲﾏ2
ORL
LD M1843                 ; ﾁｬｯｸ確認OK
LD M1601                 ; 供給部ﾜｰｸ無し供給要求ON
OR M1602                 ; ﾜｰｸ供給開始確認
ANL
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
ANB T38                  ; ﾜｰｸ投入可音程ﾀｲﾏ
ANB X1182                ; 給材PP投入ﾜｰｸ確認
ANB T45                  ; 投入要求ﾌﾞｻﾞｰ自動停止
ORL
LD M81                   ; 自動運転開始BZ
ANB T20                  ; 起動前BZ_ONﾀｲﾏ
AND> K3 D10              ; 起動時ﾌﾞｻﾞｰｶｳﾝﾄ
ORL
OUT Y2254                ; ﾒｲﾝﾊﾟﾈﾙﾌﾞｻﾞｰ
LD M1601                 ; 供給部ﾜｰｸ無し供給要求ON
OR M1602                 ; ﾜｰｸ供給開始確認
TMR K45 K600             ; 投入要求ﾌﾞｻﾞｰ自動停止
;□□　　画面切替管理　　□□
;※画面切替管理は別ﾓｼﾞｭｰﾙを作成しても良い。
;□□　　ﾀｯﾁﾊﾟﾈﾙ表示　　□□
LD M48                   ; 全ﾕﾆｯﾄ原点
LD M97                   ; 原点復帰動作中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1500                ; 原点復帰TP-PL
LD M32                   ; 自動ﾓｰﾄﾞ
OUT B1501                ; 自動/手動TP-PL
LD M32                   ; 自動ﾓｰﾄﾞ
AND M49                  ; 全ﾕﾆｯﾄ起動中
OUT B1502                ; 起動TP-PL
LDB M49                  ; 全ﾕﾆｯﾄ起動中
LD M38                   ; 払出ﾓｰﾄﾞ
OR M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M49                  ; 全ﾕﾆｯﾄ起動中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B1503                ; 停止TP-PL
LDB M67                  ; 警報(ﾜｰｸ不足等)
ANB M68                  ; 異常ﾌﾞｻﾞｰ
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1510                ; ﾌﾞｻﾞｰ停止PL
LDB M65                  ; 即停止異常(緊急停止)
ANB M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M73                  ; 払出停止異常まとめ
ANB M77                  ; ｴﾗｰ発生
INV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1511                ; ｴﾗｰﾘｾｯﾄPL
LD M160                  ; 一時停止
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR M162                  ; 全ﾕﾆｯﾄ停止OK
OUT B1512                ; 一時停止PL
LD M39                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1515                ; ｲﾝﾀｰﾛｯｸｷｬﾝｾﾙ中PL
LD M37                   ; 空運転ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1516                ; DRY_RUN_PL
LD= D2110 K1             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B1517                ; COATING_OFF_PL
LD M5321
OR M2925                 ; ｺｰﾃｨﾝｸﾞ材温調停止
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
AND M32                  ; 自動ﾓｰﾄﾞ
OUT B1518                ; 温調OFF_TP-PL
LD= D15 K0               ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
OUT B1519                ; 温度起動条件ON_TP-PL
LD= D15 K1               ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
OUT B151A                ; 温度起動条件OFF_TP-PL
LD M3978                 ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B151B                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞTP-PL
LD= D1112 K1             ; 集塵機運転有無(0_有/1_無)
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT B151C                ; 集塵機OFFﾓｰﾄﾞtp-pl
;Daily check display Interlock
LDB M2944                ; 塗布幅確認移動開始
ANB M2954                ; 塗布幅確認終了移動開始
INV
OUT M26                  ; Display change Interlock
;機種選択OK表示
LD M208                  ; 機種選択OK
OUT B152A                ; ﾌﾟﾛｸﾞﾗﾑNo.設定OK_TP-PL
;ｺｰﾃｨﾝｸﾞ材加温ｽﾃｰﾀｽ表示
LDB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
AND X1148                ; ｺｰﾃｨﾝｸﾞ材温調完了
MOV K2 D121              ; ｺｰﾃｨﾝｸﾞ材加温ｽﾃｰﾀｽ
LDB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
ANB X1148                ; ｺｰﾃｨﾝｸﾞ材温調完了
MOV K1 D121              ; ｺｰﾃｨﾝｸﾞ材加温ｽﾃｰﾀｽ
LD Y2252                 ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
MOV K0 D121              ; ｺｰﾃｨﾝｸﾞ材加温ｽﾃｰﾀｽ
;メッセージ表示
LDB M49                  ; 全ﾕﾆｯﾄ起動中
ANB M65                  ; 即停止異常(緊急停止)
ANB M66                  ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
ANB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ANB M71                  ; 自動起動停止指令(払出停止)
ANB M73                  ; 払出停止異常まとめ
MPS
ANB M53                  ; ｲﾆｼｬﾗｲｽﾞ完了ﾌﾗｸﾞ
OUT B010                 ; ｲﾆｼｬﾗｲｽﾞ中表示
MRD
LDB B010                 ; ｲﾆｼｬﾗｲｽﾞ中表示
ANB M97                  ; 原点復帰動作中
ANB M48                  ; 全ﾕﾆｯﾄ原点
ANB B036                 ; 供給部ﾜｰｸ有原復帰不可異常
ANB B055                 ; 供給ｽﾗｲﾀﾞﾜｰｸ有原点復帰不可異常
ANB B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
AND M96                  ; 原点復帰可能条件
AND X04                  ; 投入部ﾗｲﾄｶｰﾃﾝOUT1
ANL
OUT B011                 ; 原点復帰要求
MPP
AND M97                  ; 原点復帰動作中
OUT B012                 ; 原点復帰中
;□□　　主幹異常　　□□
;ﾒｲﾝ回路に関連する異常はここにまとめる。
LDB M129                 ; 非常停止正常
LD CR2003                ; 常時OFF
ANB B0500                ; 非常停止異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B00                  ; 非常停止異常
ORL
OUT B00                  ; 非常停止異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0500                 ; 非常停止異常BZ停止
AND B00                  ; 非常停止異常
OUT B0500                ; 非常停止異常BZ停止
LD M64                   ; 運転準備
ANB M128                 ; 圧空正常
LD CR2003                ; 常時OFF
ANB B0501                ; 圧空低下異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B01                  ; 圧空低下異常
ORL
MPS
AND T80                  ; 圧空低下確認
OUT B01                  ; 圧空低下異常
MPP
TMR K80 K50              ; 圧空低下確認
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0501                 ; 圧空低下異常BZ停止
AND B01                  ; 圧空低下異常
OUT B0501                ; 圧空低下異常BZ停止
LD Y51                   ; 安全扉ﾛｯｸ
LD M2398                 ; ﾕﾆｯﾄ1動作無し確認
AND M3198                ; ﾕﾆｯﾄ2動作無し確認
AND M3998                ; ﾕﾆｯﾄ3動作無し確認
AND M4798                ; ﾕﾆｯﾄ4動作無し確認
INV
ANB M39                  ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
ORL
ANB M131                 ; 安全扉正常
LD CR2003                ; 常時OFF
ANB B0502                ; 安全扉開異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B02                  ; 安全扉開異常
ORL
OUT B02                  ; 安全扉開異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0502                 ; 安全扉開異常BZ停止
AND B02                  ; 安全扉開異常
OUT B0502                ; 安全扉開異常BZ停止
LD T23                   ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
LD CR2003                ; 常時OFF
ANB B0503                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B03                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
ORL
OUT B03                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0503                 ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
AND B03                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OUT B0503                ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
LD M64                   ; 運転準備
ANB M130                 ; 外部非常停止正常
LD CR2003                ; 常時OFF
ANB B0504                ; 外部非常停止BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04                  ; 外部非常停止
ORL
OUT B04                  ; 外部非常停止
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0504                 ; 外部非常停止BZ停止
AND B04                  ; 外部非常停止
OUT B0504                ; 外部非常停止BZ停止
LD CR3905                ; 重度ｴﾗｰ発生中
LD CR2003                ; 常時OFF
ANB B0505                ; PLC異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B05                  ; PLC異常
ORL
OUT B05                  ; PLC異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0505                 ; PLC異常BZ停止
AND B05                  ; PLC異常
OUT B0505                ; PLC異常BZ停止
LD R8400                 ; ｴﾗｰ発生
LD CR2003                ; 常時OFF
ANB B0506                ; ﾘﾝｸ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B06                  ; ﾘﾝｸ異常
ORL
OUT B06                  ; ﾘﾝｸ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0506                 ; ﾘﾝｸ異常BZ停止
AND B06                  ; ﾘﾝｸ異常
OUT B0506                ; ﾘﾝｸ異常BZ停止
LD CR2306                ; ﾊﾞｯｸｱｯﾌﾟ電池異常
ANB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
LD CR2003                ; 常時OFF
ANB B0507                ; PLCﾊﾞｯﾃﾘ低下BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B07                  ; PLCﾊﾞｯﾃﾘ低下
ORL
MPS
TMR K50 K18000           ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
MPP
AND T50                  ; CPUﾊﾞｯﾃﾘ異常ﾀｲﾏ
OUT B07                  ; PLCﾊﾞｯﾃﾘ低下
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0507                 ; PLCﾊﾞｯﾃﾘ低下BZ停止
AND B07                  ; PLCﾊﾞｯﾃﾘ低下
OUT B0507                ; PLCﾊﾞｯﾃﾘ低下BZ停止
LDB X02                  ; 安全扉正常
OUT B08                  ; 安全扉開
LDP M10                  ; 自動/手動PB
ANB M48                  ; 全ﾕﾆｯﾄ原点
ANB M49                  ; 全ﾕﾆｯﾄ起動中
ANB M81                  ; 自動運転開始BZ
LD CR2003                ; 常時OFF
ANB B0509                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B09                  ; 原点未完ﾓｰﾄﾞ切替不可異常
ORL
OUT B09                  ; 原点未完ﾓｰﾄﾞ切替不可異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0509                 ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
AND B09                  ; 原点未完ﾓｰﾄﾞ切替不可異常
OUT B0509                ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
;異常ﾌﾞｻﾞｰまとめ
LDB B00                  ; 非常停止異常
OR B0500                 ; 非常停止異常BZ停止
LDB B01                  ; 圧空低下異常
OR B0501                 ; 圧空低下異常BZ停止
ANL
LDB B02                  ; 安全扉開異常
OR B0502                 ; 安全扉開異常BZ停止
ANL
LDB B03                  ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰ
OR B0503                 ; 原点復帰ｻｲｸﾙﾀｲﾑｵｰﾊﾞｰBZ停止
ANL
LDB B04                  ; 外部非常停止
OR B0504                 ; 外部非常停止BZ停止
ANL
LDB B05                  ; PLC異常
OR B0505                 ; PLC異常BZ停止
ANL
LDB B06                  ; ﾘﾝｸ異常
OR B0506                 ; ﾘﾝｸ異常BZ停止
ANL
LDB B07                  ; PLCﾊﾞｯﾃﾘ低下
OR B0507                 ; PLCﾊﾞｯﾃﾘ低下BZ停止
ANL
LDB B09                  ; 原点未完ﾓｰﾄﾞ切替不可異常
OR B0509                 ; 原点未完ﾓｰﾄﾞ切替不可異常BZ停止
ANL
LDB B0A
OR B050A
ANL
INV
OUT M159                 ; ﾒｲﾝ異常BZ　まとめ
;□□ Panel
LD CR2008                ; 運転開始時1ｽｷｬﾝON
MOV K1 D12008            ; 切り替え画面No.
LD= D12000 K1            ; 現在表示中画面ﾍﾟｰｼﾞ
TMR K85 K30              ; ﾒｲﾝ画面表示ﾃﾞｨﾚｲ
LD T85                   ; ﾒｲﾝ画面表示ﾃﾞｨﾚｲ
MOV K0 D12008            ; 切り替え画面No.
END
ENDH
