DEVICE:53
;MODULE:UNIT3_Coarting_RB
;MODULE_TYPE:0
;□□　　入力受け　　□□
LD X1160                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
LD X1030                 ; (816)塗布ﾛﾎﾞｯﾄﾁｬｯｸ閉要求
AND M37                  ; 空運転ﾓｰﾄﾞ
ORL
TMR K301 K3              ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
LD X1161                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
TMR K302 K3              ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
LD CR2003                ; 常時OFF
TMR K303 K3
LD CR2003                ; 常時OFF
TMR K304 K3
;ロボット速度変更モード
;ティーチングチェック時に速度が変更出来るようにする。
;
LD B1017                 ; ROBOT_TEST_TP-PB
AND B1011                ; ｴﾗｰﾘｾｯﾄPB
TMR K325 K50             ; ﾛﾎﾞｯﾄ速度変更ﾓｰﾄONﾃﾞｨﾚｲﾞ
LDB M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
ANB M49                  ; 全ﾕﾆｯﾄ起動中
ANP T325                 ; ﾛﾎﾞｯﾄ速度変更ﾓｰﾄONﾃﾞｨﾚｲﾞ
LD M3978                 ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
ANPB T325                ; ﾛﾎﾞｯﾄ速度変更ﾓｰﾄONﾃﾞｨﾚｲﾞ
ORL
OUT M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
;□ 装置状態・動作条件 □
LD X1161                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
AND X1003                ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
AND X1004                ; ﾛﾎﾞｯﾄ初期化完了
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
AND X1006                ; ﾛﾎﾞｯﾄ外部ﾓｰﾄﾞ
ANB X1008                ; ﾛﾎﾞｯﾄ警告
AND X102A                ; (810)塗布ﾛﾎﾞｯﾄ原点位置
OUT M3984                ; ﾕﾆｯﾄ3原点
LD M3984                 ; ﾕﾆｯﾄ3原点
ANB M3200                ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ開始
ANB M160                 ; 一時停止
ANB B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
ANB B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
ANB B051                 ; ﾉｽﾞﾙIN側圧力上限異常
ANB B052                 ; ﾉｽﾞﾙIN側圧力下限異常
OUT M3986                ; ﾕﾆｯﾄ3起動条件
LDB M64                  ; 運転準備
LD M77                   ; ｴﾗｰ発生
OR M66                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M3988                ; ﾕﾆｯﾄ3原点復帰中
ORL
OR M3990                 ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3987                ; ﾕﾆｯﾄ3起動OFF
LD M98                   ; 原点復帰開始
ANB M3988                ; ﾕﾆｯﾄ3原点復帰中
OR M3988                 ; ﾕﾆｯﾄ3原点復帰中
ANB M3987                ; ﾕﾆｯﾄ3起動OFF
ANB M3679                ; ﾕﾆｯﾄ3原点復帰完了確認
OUT M3988                ; ﾕﾆｯﾄ3原点復帰中
LD M83                   ; 各工程起動指令
ANB M3520                ; UNIT3払出処理ﾜｰｸ無
AND M1766                ; ﾜｰｸ供給完了取出可
LD M4001                 ; 乾燥炉CV基板収納可
LD M4006                 ; 乾燥炉CVﾋﾟｯﾁ送り回転
AND M46                  ; 0205森山
ORL
LD M4007                 ; 乾燥炉CV通過確認
AND M46                  ; 0205森山
ORL
ANL
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
AND CR2003               ; 常時OFF
ORL
AND M3986                ; ﾕﾆｯﾄ3起動条件
OR M3989                 ; ﾕﾆｯﾄ3起動中
ANB M3987                ; ﾕﾆｯﾄ3起動OFF
ANB M3263                ; ﾕﾆｯﾄ3動作完了
OUT M3989                ; ﾕﾆｯﾄ3起動中
LDB B061                 ; 塗布RBﾊﾞｯﾃﾘ切れ警告
OR B0561                 ; ｺｰﾃｨﾝｸﾞRBﾊﾞｯﾃﾘ切れ警告BZ停止
INV
OUT M3992                ; ﾕﾆｯﾄ3警報補助
LDB X1160                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
AND X1003                ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
AND X1004                ; ﾛﾎﾞｯﾄ初期化完了
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
AND X1006                ; ﾛﾎﾞｯﾄ外部ﾓｰﾄﾞ
ANB X1008                ; ﾛﾎﾞｯﾄ警告
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
ANB M3697                ; 塗布RB1ｻｲｸﾙ開始
OUT M3996                ; ﾕﾆｯﾄ3機器準備完了
;□非常停止フラグ
LDB CR2003               ; 常時OFF
INV
OUT M3985                ; ﾕﾆｯﾄ3非常停止異常
;□エラー発生　一時停止フラグ
LDB B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
ANB B067                 ; 塗布RB準備未完異常
INV
OUT M3997                ; ﾕﾆｯﾄ3ｴﾗｰ発生
;□サイクル停止異常フラグ
LDB B060                 ; 塗布RB異常
ANB B063                 ; 塗布RB基板空振異常
ANB B064                 ; 基板収納時ﾗｯｸ浮き異常
ANB B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
ANB B068                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ異常
ANB B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
INV
OUT M3990                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
;□払出停止異常フラグ
LDB CR2003               ; 常時OFF
INV
OUT M3991                ; ﾕﾆｯﾄ3払出停止異常
;□　動作中ユニット無しフラグ
LD T310                  ; ﾕﾆｯﾄ3動作無し判定ﾀｲﾏ
LDB X1001                ; ﾛﾎﾞｯﾄ運転中
AND M33                  ; 手動ﾓｰﾄﾞ
ORL
OUT M3998                ; ﾕﾆｯﾄ3動作無し確認
;動作停止判定
LD= D3000 D3005          ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ3動作中判定
ORB M3989                ; ﾕﾆｯﾄ3起動中
LDB X1001                ; ﾛﾎﾞｯﾄ運転中
OR M160                  ; 一時停止
ANL
LDB X1029                ; (809)塗布ﾛﾎﾞｯﾄ動作中
ORB X1001                ; ﾛﾎﾞｯﾄ運転中
ANL
TMR K310 K20             ; ﾕﾆｯﾄ3動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D3000 D3005          ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ3動作中判定
;□ステップ起動用フラグ
LD M32                   ; 自動ﾓｰﾄﾞ
LD M33                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M3968                ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ動作
;払出しﾜｰｸ無し確認
LDB M3989                ; ﾕﾆｯﾄ3起動中
AND M1920                ; UNIT1払出処理ﾜｰｸ無
AND X1161                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
AND M38                  ; 払出ﾓｰﾄﾞ
OUT M3520                ; UNIT3払出処理ﾜｰｸ無
;□□□□□□□□□□□□□□□
;□　　 　UNIT3自動回路  　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
FMOV K0 M3200 K10        ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ開始
LDB M3989                ; ﾕﾆｯﾄ3起動中
MOV K0 D3000             ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
LD M3989                 ; ﾕﾆｯﾄ3起動中
MOV D3000 Z1             ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
OUT M3200:Z1             ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ開始
;自動運転ステップ開始
LD M3200                 ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ開始
ANB Y2246                ; 乾燥炉搬送CV送回転
AND Y54                  ; 排出部ﾄﾞｱﾛｯｸON
ANB X123F                ; 乾燥炉CV取出側ﾜｰｸ確認
ANB X123D                ; 乾燥炉CV投入側ﾜｰｸ確認
ANB M160                 ; 一時停止
LD= D2028 K3             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K4             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANL
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
@MOV K1 D3050            ; 自動ﾗｯｸ収納指令番号
;ｺｰﾃｨﾝｸﾞ動作開始起動
LD M3201                 ; 塗布開始可確認
ANB M160                 ; 一時停止
@MOV K2 D3020            ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K1 D3022            ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
@MOV D110 D3030          ; 機種選択No.  ﾛﾎﾞｯﾄ機種番号(1-64)
@MOV D3050 D3031         ; 自動ﾗｯｸ収納指令番号  ﾛﾎﾞｯﾄﾗｯｸ収納番号(1-10)
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ起動､起動確認
LD M3202                 ; 塗布開始可確認RB起動指令
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄﾗｯｸ収納確認
LD M3203                 ; 塗布RB起動確認
AND X1025                ; (805)塗布ﾛﾎﾞｯﾄﾗｯｸ収納完了
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
LD M3204                 ; 塗布RB収納完了ON確認
ANB X1025                ; (805)塗布ﾛﾎﾞｯﾄﾗｯｸ収納完了
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;乾燥炉CVへ収納可ON
LD M3205                 ; 塗布RB収納完了OFF確認
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
LD M3206                 ; 乾燥炉CVへ収納完了
ANB M4001                ; 乾燥炉CV基板収納可
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ1ｻｲｸﾙ終了確認
LD M3207                 ; ﾗｯｸ収納可OFF確認
AND X1026                ; (806)塗布ﾛﾎﾞｯﾄ塗布1ｻｲｸﾙ完了
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
LD M3208                 ; 塗布RB動作完了確認
ANB X1026                ; (806)塗布ﾛﾎﾞｯﾄ塗布1ｻｲｸﾙ完了
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ運転終了確認
LD M3209                 ; 塗布RB運転中OFF確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D3000               ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
@INC D132                ; 塗布完了数
;ｻｲｸﾙ停止処理
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
LD M3200                 ; ﾕﾆｯﾄ3ｽﾃｯﾌﾟ開始
LD M3203                 ; 塗布RB起動確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
LD M3190                 ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
ORB M4076                ; 乾燥炉_CV起動中
ANL
ORL
ANL
MOV K62 D3000            ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;終了
LD M3262                 ; ﾛﾎﾞｯﾄｻｲｸﾙ停止処理
OR M3210                 ; ﾛﾎﾞｯﾄｻｲｸﾙ終了確認
@MOV K63 D3000           ; ﾕﾆｯﾄ3自動運転ｽﾃｯﾌﾟ
;ﾗｯｸ収納番号変更処理
;ﾀｯﾁﾊﾟﾈﾙより変更可能
LD B10CF                 ; ﾗｯｸ収納番号変更TP-PB
ANB M3989                ; ﾕﾆｯﾄ3起動中
@MOV D3055 D3050         ; ﾗｯｸ収納番号  自動ﾗｯｸ収納指令番号
;□□□□□□□□□□□□□□□
;□　　 UNIT3原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
FMOV K0 M3600 K5         ; ﾕﾆｯﾄ3原点復帰開始
LDB M3988                ; ﾕﾆｯﾄ3原点復帰中
MOV K0 D3010             ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
LD M3988                 ; ﾕﾆｯﾄ3原点復帰中
MOV D3010 Z1             ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
OUT M3600:Z1             ; ﾕﾆｯﾄ3原点復帰開始
;原点復帰ｻｲｸﾙ監視
LD M3988                 ; ﾕﾆｯﾄ3原点復帰中
AND M3984                ; ﾕﾆｯﾄ3原点
TMR K300 K200            ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
LD M3600                 ; ﾕﾆｯﾄ3原点復帰開始
@MOV K2 D3020            ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K50 D3022           ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
@INC D3010               ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ起動､起動確認
LD M3601                 ; 塗布RB起動指令
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D3010               ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ原点確認
LD M3602                 ; 塗布RB起動確認
AND X102A                ; (810)塗布ﾛﾎﾞｯﾄ原点位置
@INC D3010               ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
;ﾛﾎﾞｯﾄ運転終了確認
LD M3603                 ; 塗布RB原点完了確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
@INC D3010               ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
LD M3604                 ; 塗布RB運転中OFF確認ｸﾗﾝﾌﾟ開
AND X1161                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
@INC D3010               ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M3605                 ; 塗布RB原点完了
AND M3984                ; ﾕﾆｯﾄ3原点
@MOV K78 D3010           ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
LD M3678                 ; ﾕﾆｯﾄ3原点復帰完了
ANB M98                  ; 原点復帰開始
@MOV K79 D3010           ; ﾕﾆｯﾄ3原点復帰ｽﾃｯﾌﾟ
;□□□□□□□□□□□□□□□
;□　　 　UNIT3手動回路  　　□
;□□□□□□□□□□□□□□□
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
ANB M3989                ; ﾕﾆｯﾄ3起動中
MC
;ﾁｬｯｸ開閉
LDB CR2003               ; 常時OFF
ANB M3697                ; 塗布RB1ｻｲｸﾙ開始
OUT M3690                ; 塗布RBﾁｬｯｸ閉可
LD B10C0                 ; 塗布RBｸﾗﾝﾌﾟ閉TP-PB
AND M3690                ; 塗布RBﾁｬｯｸ閉可
OR M3691                 ; 塗布RBﾁｬｯｸ閉
ANPB M3693               ; 塗布RBﾁｬｯｸ開
OUT M3691                ; 塗布RBﾁｬｯｸ閉
LDB CR2003               ; 常時OFF
ANB M3697                ; 塗布RB1ｻｲｸﾙ開始
OUT M3692                ; 塗布RBﾁｬｯｸ開可
LD B10C1                 ; 塗布RBｸﾗﾝﾌﾟ開TP-PB
AND M3692                ; 塗布RBﾁｬｯｸ開可
OR M3693                 ; 塗布RBﾁｬｯｸ開
ANPB M3691               ; 塗布RBﾁｬｯｸ閉
OUT M3693                ; 塗布RBﾁｬｯｸ開
;塗布動作1ｻｲｸﾙ起動
;塗布条件確認、ティーチング位置の確認で使用
LD M131                  ; 安全扉正常
AND X1187                ; 給材PP取出ﾜｰｸ浮き確認1
AND X1188                ; 給材PP取出ﾜｰｸ浮き確認2
AND X1186                ; 給材PP取出ﾜｰｸ確認
AND M2177                ; 搬送PP待機位置
AND= D2028 K3            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
AND X1276                ; 乾燥炉CVｽﾄｯﾊﾟﾛｯｸ端
AND X123E                ; 乾燥炉CVﾋﾟｯﾁｾﾝｻ確認
ANB X123D                ; 乾燥炉CV投入側ﾜｰｸ確認
AND M3984                ; ﾕﾆｯﾄ3原点
OUT M3696                ; 塗布RB1ｻｲｸﾙ動作可
LD B10C3                 ; 塗布RB1ｻｲｸﾙTP-PB
AND M3696                ; 塗布RB1ｻｲｸﾙ動作可
OR M3697                 ; 塗布RB1ｻｲｸﾙ開始
ANB M3702                ; 塗布RB手動1ｻｲｸﾙ終了
ANB M3987                ; ﾕﾆｯﾄ3起動OFF
OUT M3697                ; 塗布RB1ｻｲｸﾙ開始
LDP M3697                ; 塗布RB1ｻｲｸﾙ開始
OR M3698                 ; 塗布RB1起動指令
AND M3697                ; 塗布RB1ｻｲｸﾙ開始
ANB M3699                ; 塗布RB起動確認
@MOV K2 D3020            ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K1 D3022            ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
@MOV D110 D3030          ; 機種選択No.  ﾛﾎﾞｯﾄ機種番号(1-64)
@MOV K1 D3031            ; ﾛﾎﾞｯﾄﾗｯｸ収納番号(1-10)
OUT M3698                ; 塗布RB1起動指令
;ﾛﾎﾞｯﾄ起動､起動確認
LD M3698                 ; 塗布RB1起動指令
AND X1001                ; ﾛﾎﾞｯﾄ運転中
OR M3699                 ; 塗布RB起動確認
AND M3697                ; 塗布RB1ｻｲｸﾙ開始
ANB M3700                ; 塗布RB収納完了ON確認
OUT M3699                ; 塗布RB起動確認
;ﾛﾎﾞｯﾄﾗｯｸ収納確認
LD M3699                 ; 塗布RB起動確認
AND X1025                ; (805)塗布ﾛﾎﾞｯﾄﾗｯｸ収納完了
OR M3700                 ; 塗布RB収納完了ON確認
AND M3697                ; 塗布RB1ｻｲｸﾙ開始
ANB M3701                ; 塗布RB収納完了OFF確認
OUT M3700                ; 塗布RB収納完了ON確認
LD M3700                 ; 塗布RB収納完了ON確認
ANB X1025                ; (805)塗布ﾛﾎﾞｯﾄﾗｯｸ収納完了
OR M3701                 ; 塗布RB収納完了OFF確認
AND M3697                ; 塗布RB1ｻｲｸﾙ開始
ANB M3702                ; 塗布RB手動1ｻｲｸﾙ終了
OUT M3701                ; 塗布RB収納完了OFF確認
;ﾛﾎﾞｯﾄ1ｻｲｸﾙ終了確認
LD M3701                 ; 塗布RB収納完了OFF確認
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
OR M3702                 ; 塗布RB手動1ｻｲｸﾙ終了
AND M3697                ; 塗布RB1ｻｲｸﾙ開始
OUT M3702                ; 塗布RB手動1ｻｲｸﾙ終了
MCR
;ﾛﾎﾞｯﾄ補助回路
;ﾛﾎﾞｯﾄ初期準備
LDB M3965                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
AND M64                  ; 運転準備
AND X1004                ; ﾛﾎﾞｯﾄ初期化完了
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
MEP
LD M32                   ; 自動ﾓｰﾄﾞ
ANB X1003                ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
MEP
ORL
OR M3952                 ; 塗布RB運転準備
LDB M3954                ; 塗布RBｻｰﾎﾞON､CAL完了
ANB M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
LDB M3956                ; 塗布RB外部ﾓｰﾄﾞ完了
AND M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
ORL
ANL
ANB M3962                ; 塗布RB初期準備
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
TMR K321 K2              ; 塗布RB初期化処理開始ﾃﾞｨﾚｲ
OUT M3952                ; 塗布RB運転準備
LD M3952                 ; 塗布RB運転準備
AND T321                 ; 塗布RB初期化処理開始ﾃﾞｨﾚｲ
MEP
OR M3953                 ; 塗布RBｻｰﾎﾞON､CAL要求
ANB M3954                ; 塗布RBｻｰﾎﾞON､CAL完了
AND M3952                ; 塗布RB運転準備
MPS
ANB M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
@MOV K131 D3020          ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
MRD
AND M3978                ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
@MOV K1 D3020            ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
MRD
@MOV K7 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
MPP
OUT M3953                ; 塗布RBｻｰﾎﾞON､CAL要求
LD M3953                 ; 塗布RBｻｰﾎﾞON､CAL要求
AND X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
MEP
OR M3954                 ; 塗布RBｻｰﾎﾞON､CAL完了
ANB M3955                ; 塗布RB外部ﾓｰﾄﾞ要求
AND M3952                ; 塗布RB運転準備
OUT M3954                ; 塗布RBｻｰﾎﾞON､CAL完了
;調整時速度をペンダントから行う場合(ﾓｰﾀｰON､CAL実行)+(外部ﾓｰﾄﾞ切替)
;時に使用
LD M3978                 ; ﾛﾎﾞｯﾄ速度変更可能ﾓｰﾄﾞ
AND M3954                ; 塗布RBｻｰﾎﾞON､CAL完了
ANB X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
MEP
OR M3955                 ; 塗布RB外部ﾓｰﾄﾞ要求
ANB M3956                ; 塗布RB外部ﾓｰﾄﾞ完了
AND M3952                ; 塗布RB運転準備
@MOV K128 D3020          ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K7 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
OUT M3955                ; 塗布RB外部ﾓｰﾄﾞ要求
LD M3955                 ; 塗布RB外部ﾓｰﾄﾞ要求
AND X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
OR M3956                 ; 塗布RB外部ﾓｰﾄﾞ完了
AND M3952                ; 塗布RB運転準備
OUT M3956                ; 塗布RB外部ﾓｰﾄﾞ完了
;自動運転時ﾌﾟﾛｸﾞﾗﾑ強制停止
LD T15                   ; 強制停止
OR M3957                 ; 塗布RB停止指令
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@MOV K64 D3020           ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K1 D3022            ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
OUT M3957                ; 塗布RB停止指令
;自動運転時ｱﾗｰﾑ発生ｽﾃｯﾌﾟ停止
LD CR2003                ; 常時OFF
OR M3959                 ; 塗布RBｽﾃｯﾌﾟ停止
AND M3989                ; ﾕﾆｯﾄ3起動中
AND X1001                ; ﾛﾎﾞｯﾄ運転中
@MOV K16 D3020           ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K1 D3022            ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
OUT M3959                ; 塗布RBｽﾃｯﾌﾟ停止
;自動運転時ｱﾗｰﾑ解除ｽﾃｯﾌﾟ続行機動
LD CR2003                ; 常時OFF
MEP
OR M3960                 ; 塗布RBｽﾃｯﾌﾟ起動
AND M3989                ; ﾕﾆｯﾄ3起動中
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
@MOV K2 D3020            ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
@MOV K1 D3022            ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
OUT M3960                ; 塗布RBｽﾃｯﾌﾟ起動
;ﾛﾎﾞｯﾄｱﾗｰﾑﾘｾｯﾄ
LD M16                   ; ﾘｾｯﾄPB
OR M3964                 ; 塗布RBｱﾗｰﾑﾘｾｯﾄ記憶
LD X1002                 ; ﾛﾎﾞｯﾄ異常
OR X1008                 ; ﾛﾎﾞｯﾄ警告
ANL
OUT M3964                ; 塗布RBｱﾗｰﾑﾘｾｯﾄ記憶
LD M3964                 ; 塗布RBｱﾗｰﾑﾘｾｯﾄ記憶
ANB T323                 ; ｱﾗｰﾑﾘｾｯﾄﾘﾄﾗｲ
@MOV K1000 D3021         ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
TMR K323 K10             ; ｱﾗｰﾑﾘｾｯﾄﾘﾄﾗｲ
OUT M3958                ; 塗布RBｱﾗｰﾑﾘｾｯﾄ
;扉開時ﾛﾎﾞｯﾄ警告解除
LDP M131                 ; 安全扉正常
OR M3961                 ; 塗布RB扉開時警告解除処理
AND X1008                ; ﾛﾎﾞｯﾄ警告
ANB B00                  ; 非常停止異常
ANB T322                 ; 塗布RB扉開時警告解除ﾀｲﾏ
@MOV K1000 D3021         ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
TMR K322 K10             ; 塗布RB扉開時警告解除ﾀｲﾏ
OUT M3961                ; 塗布RB扉開時警告解除処理
;ﾌﾟﾛｸﾞﾗﾑﾘｾｯﾄ
LDF M3964                ; 塗布RBｱﾗｰﾑﾘｾｯﾄ記憶
OR M3965                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
ANB M3969                ; ﾘｾｯﾄ完了
OUT M3965                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
LD M3965                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
ANB X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
ANB X1002                ; ﾛﾎﾞｯﾄ異常
OR M3966                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ実行
AND M3965                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
MPS
ANB M3967                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ完了
OUT M3966                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ実行
MRD
@MOV K1 D3021            ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ
MRD
@MOV H0040 D3020         ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
MPP
@MOV.S -1 D3022          ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
LD M3966                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ実行
AND X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
OR M3967                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ完了
AND M3965                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
OUT M3967                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ完了
LD M3967                 ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ完了
ANB X100E                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ処理完了
OR M3969                 ; ﾘｾｯﾄ完了
AND M3965                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ予約
OUT M3969                ; ﾘｾｯﾄ完了
;□□　　出力回路　　□□
;ソレノイドバルブ出力
LDB CR2003               ; 常時OFF
ANB M3691                ; 塗布RBﾁｬｯｸ閉
INV
MEP
LD X1030                 ; (816)塗布ﾛﾎﾞｯﾄﾁｬｯｸ閉要求
MEP
ORL
LD Y2260                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
ANB Y2261                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
ORL
ANPB Y2261               ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
OUT Y2260                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
LDB M3604                ; 塗布RB運転中OFF確認ｸﾗﾝﾌﾟ開
ANB M3693                ; 塗布RBﾁｬｯｸ開
INV
MEP
LD X1031                 ; (817)塗布ﾛﾎﾞｯﾄﾁｬｯｸ開要求
MEP
ORL
LD Y2261                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
ANB Y2260                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
ORL
ANPB Y2260               ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
OUT Y2261                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
;ﾛﾎﾞｯﾄ出力
LD M64                   ; 運転準備
LD X02                   ; 安全扉正常
OR M162                  ; 全ﾕﾆｯﾄ停止OK
ANL
ANB M3990                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
OUT Y2240                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｲﾈｰﾌﾞﾙ
LD CR2002                ; 常時ON
OUT Y2000                ; ﾛﾎﾞｯﾄｽﾃｯﾌﾟ停止
LD M129                  ; 非常停止正常
LD M131                  ; 安全扉正常
OR M162                  ; 全ﾕﾆｯﾄ停止OK
ANL
ANB M3990                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
OUT Y2002                ; ﾛﾎﾞｯﾄ瞬時停止
LD CR2003                ; 常時OFF
OUT Y2004                ; ﾛﾎﾞｯﾄ割り込みｽｷｯﾌﾟ
;ﾛﾎﾞｯﾄｽﾄﾛｰﾌﾞ
LDB M3958                ; 塗布RBｱﾗｰﾑﾘｾｯﾄ
ANB M3961                ; 塗布RB扉開時警告解除処理
INV
OUT M3962                ; 塗布RB初期準備
LDB M3202                ; 塗布開始可確認RB起動指令
ANB M3601                ; 塗布RB起動指令
ANB M3698                ; 塗布RB1起動指令
ANB M3953                ; 塗布RBｻｰﾎﾞON､CAL要求
ANB M3955                ; 塗布RB外部ﾓｰﾄﾞ要求
ANB M3957                ; 塗布RB停止指令
ANB M3959                ; 塗布RBｽﾃｯﾌﾟ停止
ANB M3960                ; 塗布RBｽﾃｯﾌﾟ起動
ANB M3966                ; Prgﾘｾｯﾄｺﾏﾝﾄﾞ実行
INV
OUT M3963                ; 塗布RB起動処理
LDB M3962                ; 塗布RB初期準備
ANB M3963                ; 塗布RB起動処理
INV
MPS
TMR K320 K2              ; 塗布RBｺﾏﾝﾄﾞｽﾄﾛｰﾌﾞONﾃﾞｨﾚｲ
MPP
AND T320                 ; 塗布RBｺﾏﾝﾄﾞｽﾄﾛｰﾌﾞONﾃﾞｨﾚｲ
OUT Y2003                ; ﾛﾎﾞｯﾄｽﾄﾛｰﾌﾞ
;ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ出力
LD M3962                 ; 塗布RB初期準備
OR M3963                 ; 塗布RB起動処理
MPS
AND D3021.0              ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ0Bit
OUT Y2020                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ領域0bit
MRD
AND D3021.1              ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ1Bit
OUT Y2021                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ領域1bit
MRD
AND D3021.2              ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ2Bit
OUT Y2022                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ領域2bit
MPP
AND D3021.3              ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞﾃﾞｰﾀ3Bit
OUT Y2023                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ領域3bit
LDB M3963                ; 塗布RB起動処理
INV
MPS
LDA D3020                ; ﾛﾎﾞｯﾄ制御ﾃﾞｰﾀ1
CON
PSTA Y2008 K8            ; ﾛﾎﾞｯﾄﾃﾞｰﾀ領域1-0bit
MPP
LDA D3022                ; ﾛﾎﾞｯﾄﾌﾟﾛｸﾞﾗﾑ番号
CON
PSTA Y2010 K16           ; ﾛﾎﾞｯﾄﾃﾞｰﾀ領域2-0bit
LDB M3963                ; 塗布RB起動処理
MPS
LDA K0
CON
PSTA Y2008 K8            ; ﾛﾎﾞｯﾄﾃﾞｰﾀ領域1-0bit
MPP
LDA K0
CON
PSTA Y2010 K16           ; ﾛﾎﾞｯﾄﾃﾞｰﾀ領域2-0bit
LD CR2002                ; 常時ON
ANB M3962                ; 塗布RB初期準備
OUT Y2007                ; ﾛﾎﾞｯﾄｺﾏﾝﾄﾞ･ﾃﾞｰﾀ領域奇数ﾊﾟﾘﾃｨ
;動作機種番号転送
LD CR2002                ; 常時ON
MPS
AND D3030.0              ; ﾛﾎﾞｯﾄ機種番号1
OUT Y2028                ; (552)ﾛﾎﾞｯﾄ機種番号1
MRD
AND D3030.1              ; ﾛﾎﾞｯﾄ機種番号2
OUT Y2029                ; (553)ﾛﾎﾞｯﾄ機種番号2
MRD
AND D3030.2              ; ﾛﾎﾞｯﾄ機種番号4
OUT Y202A                ; (554)ﾛﾎﾞｯﾄ機種番号4
MPP
AND D3030.3              ; ﾛﾎﾞｯﾄ機種番号8
OUT Y202B                ; (555)ﾛﾎﾞｯﾄ機種番号8
;ﾗｯｸ収納番号転送
LD CR2002                ; 常時ON
MPS
AND D3031.0              ; ﾛﾎﾞｯﾄﾗｯｸ収納番号1
OUT Y202C                ; (556)ﾗｯｸ収納番号1
MRD
AND D3031.1              ; ﾛﾎﾞｯﾄﾗｯｸ収納番号2
OUT Y202D                ; (557)ﾗｯｸ収納番号2
MRD
AND D3031.2              ; ﾛﾎﾞｯﾄﾗｯｸ収納番号4
OUT Y202E                ; (558)ﾗｯｸ収納番号4
MPP
AND D3031.3              ; ﾛﾎﾞｯﾄﾗｯｸ収納番号8
OUT Y202F                ; (559)ﾗｯｸ収納番号8
;塗布ﾛﾎﾞｯﾄＩ/Ｆ信号
LDB B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
LDB M160                 ; 一時停止
LD X02                   ; 安全扉正常
AND Y51                  ; 安全扉ﾛｯｸ
ORL
ANL
MPS
AND T301                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
OUT Y203A                ; (570)塗布ﾛﾎﾞｯﾄﾁｬｯｸ閉端
MPP
AND T302                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
OUT Y203B                ; (571)塗布ﾛﾎﾞｯﾄﾁｬｯｸ開端
LDB B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
ANB M160                 ; 一時停止
MPS
LD M1766                 ; ﾜｰｸ供給完了取出可
OR M3697                 ; 塗布RB1ｻｲｸﾙ開始
LD M2177                 ; 搬送PP待機位置
OR M2178                 ; 搬送PP取出位置
OR M2180                 ; 給材搬送RB_待機移動中
OR M2181                 ; 給材搬送RB_取出移動中
ANL
LDB M37                  ; 空運転ﾓｰﾄﾞ
ANB M3699                ; 塗布RB起動確認
LDB X1021                ; (801)塗布ﾛﾎﾞｯﾄﾜｰｸ取出完了
AND M37                  ; 空運転ﾓｰﾄﾞ
ORL
ANL
ANL
OUT Y203C                ; (572)取出干渉無
MRD
LD M2413                 ; 塗布RBへ準備完了ON
OR M3697                 ; 塗布RB1ｻｲｸﾙ開始
LD= D2028 K3             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K4             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANL
ANB X1023                ; (803)塗布ﾛﾎﾞｯﾄ塗布完了
ANL
OUT Y203D                ; (573)塗布準備完干渉無
MPP
LD M4001                 ; 乾燥炉CV基板収納可
OR M3697                 ; 塗布RB1ｻｲｸﾙ開始
ANB X123D                ; 乾燥炉CV投入側ﾜｰｸ確認
AND X123E                ; 乾燥炉CVﾋﾟｯﾁｾﾝｻ確認
AND X1276                ; 乾燥炉CVｽﾄｯﾊﾟﾛｯｸ端
ANL
OUT Y203E                ; (574)ﾗｯｸ収納可干渉無
LD M3204                 ; 塗布RB収納完了ON確認
OR M3700                 ; 塗布RB収納完了ON確認
OUT Y203F                ; (575)ﾗｯｸ収納完了受信確認
LD M3208                 ; 塗布RB動作完了確認
OR M3701                 ; 塗布RB収納完了OFF確認
OUT Y2040                ; (576)塗布RBへ1ｻｲｸﾙ完了受信確認
LD M3603                 ; 塗布RB原点完了確認
OUT Y2041                ; (577)塗布RBへ原点完了受信確認
LD M3190                 ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
OR M4790                 ; ﾕﾆｯﾄ4ｻｲｸﾙ停止異常
OUT Y2045                ; (581)塗布停止異常有
;□□　　パネル表示　　□□
LD X1160                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
LD Y2260                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15C0                ; 塗布RBｸﾗﾝﾌﾟ閉TP-PL
LD X1161                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
LD Y2261                 ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT B15C1                ; 塗布RBｸﾗﾝﾌﾟ開TP-PL
LD CR2003                ; 常時OFF
OUT B15C2                ; 塗布RB原点復帰TP-PL
LD M3697                 ; 塗布RB1ｻｲｸﾙ開始
OUT B15C3                ; 塗布RB1ｻｲｸﾙTP-PL
LD CR2003                ; 常時OFF
OUT B15C4                ; UNIT3-1-5 TP-PL
LD CR2003                ; 常時OFF
OUT B15C5                ; UNIT3-1-6 TP-PL
LD CR2003                ; 常時OFF
OUT B15C6                ; UNIT3-1-7 TP-PL
LD CR2003                ; 常時OFF
OUT B15C7                ; UNIT3-1-8 TP-PL
LD= D3055 D3050          ; ﾗｯｸ収納番号  自動ﾗｯｸ収納指令番号
OUT B15CF                ; ﾗｯｸ番号変更完了TP-PL
;□□　　異常検出　　□□
LD X1002                 ; ﾛﾎﾞｯﾄ異常
AND M64                  ; 運転準備
LDB X1003                ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
AND M3203                ; 塗布RB起動確認
ORL
LD CR2003                ; 常時OFF
ANB B0560                ; ｺｰﾃｨﾝｸﾞRB異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B060                 ; 塗布RB異常
ORL
OUT B060                 ; 塗布RB異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0560                 ; ｺｰﾃｨﾝｸﾞRB異常BZ停止
AND B060                 ; 塗布RB異常
OUT B0560                ; ｺｰﾃｨﾝｸﾞRB異常BZ停止
LD X1007                 ; ﾛﾎﾞｯﾄﾊﾞｯﾃﾘ切れ警告
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0561                ; ｺｰﾃｨﾝｸﾞRBﾊﾞｯﾃﾘ切れ警告BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B061                 ; 塗布RBﾊﾞｯﾃﾘ切れ警告
ORL
OUT B061                 ; 塗布RBﾊﾞｯﾃﾘ切れ警告
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0561                 ; ｺｰﾃｨﾝｸﾞRBﾊﾞｯﾃﾘ切れ警告BZ停止
AND B061                 ; 塗布RBﾊﾞｯﾃﾘ切れ警告
OUT B0561                ; ｺｰﾃｨﾝｸﾞRBﾊﾞｯﾃﾘ切れ警告BZ停止
LDB X1160                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
AND Y2260                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ閉SV
AND M32                  ; 自動ﾓｰﾄﾞ
ANB M37                  ; 空運転ﾓｰﾄﾞ
LDB X1161                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
AND Y2261                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
ORL
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0562                ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
ORL
MPS
TMR K350 K50             ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS確認
MPP
AND T350                 ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS確認
OUT B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0562                 ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS異常BZ停止
AND B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
OUT B0562                ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS異常BZ停止
LD X1028                 ; (808)塗布ﾛﾎﾞｯﾄﾜｰｸ搬送中
ANB X1160                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ閉端
ANB Y2261                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄﾁｬｯｸ開SV
AND M3989                ; ﾕﾆｯﾄ3起動中
ANB M37                  ; 空運転ﾓｰﾄﾞ
LD CR2003                ; 常時OFF
ANB B0563                ; 塗布RB基板空振異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B063                 ; 塗布RB基板空振異常
ORL
OUT B063                 ; 塗布RB基板空振異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0563                 ; 塗布RB基板空振異常BZ停止
AND B063                 ; 塗布RB基板空振異常
OUT B0563                ; 塗布RB基板空振異常BZ停止
LD X1024                 ; (804)塗布ﾛﾎﾞｯﾄﾗｯｸ収納進入中
ANB X1276                ; 乾燥炉CVｽﾄｯﾊﾟﾛｯｸ端
LD M3989                 ; ﾕﾆｯﾄ3起動中
OR M3697                 ; 塗布RB1ｻｲｸﾙ開始
ANL
LD CR2003                ; 常時OFF
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B064                 ; 基板収納時ﾗｯｸ浮き異常
ORL
OUT B064                 ; 基板収納時ﾗｯｸ浮き異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0564                 ; 基板収納時ﾗｯｸ浮き異常BZ停止
AND B064                 ; 基板収納時ﾗｯｸ浮き異常
OUT B0564                ; 基板収納時ﾗｯｸ浮き異常BZ停止
LD X1020                 ; (800)塗布ﾛﾎﾞｯﾄﾜｰｸ取出進入中
ANB X110B                ; 給材搬送PP　RB_位置決完(INP)
LD X1022                 ; (802)塗布ﾛﾎﾞｯﾄ塗布進入中
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
ORL
AND CR2003               ; 常時OFF
LD CR2003                ; 常時OFF
ANB B0565                ; ﾛﾎﾞｯﾄ動作時干渉異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
ORL
OUT B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0565                 ; ﾛﾎﾞｯﾄ動作時干渉異常BZ停止
AND B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
OUT B0565                ; ﾛﾎﾞｯﾄ動作時干渉異常BZ停止
LDB M49                  ; 全ﾕﾆｯﾄ起動中
AND M17                  ; 原点復帰PB
ANB X1161                ; ｺｰﾃｨﾝｸﾞﾛﾎﾞｯﾄｸﾗﾝﾌﾟ開端
LD CR2003                ; 常時OFF
ANB B0566                ; 塗布RBﾜｰｸ有原復帰不可異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
ORL
OUT B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0566                 ; 塗布RBﾜｰｸ有原復帰不可異常BZ停止
AND B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
OUT B0566                ; 塗布RBﾜｰｸ有原復帰不可異常BZ停止
LD X1003                 ; ﾛﾎﾞｯﾄｻｰﾎﾞON中
AND X1004                ; ﾛﾎﾞｯﾄ初期化完了
AND X1005                ; ﾛﾎﾞｯﾄ自動ﾓｰﾄﾞ
AND X1006                ; ﾛﾎﾞｯﾄ外部ﾓｰﾄﾞ
ANB X1008                ; ﾛﾎﾞｯﾄ警告
INV
ANB M49                  ; 全ﾕﾆｯﾄ起動中
AND M17                  ; 原点復帰PB
LD CR2003                ; 常時OFF
ANB B0567                ; 塗布RB準備未完異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B067                 ; 塗布RB準備未完異常
ORL
OUT B067                 ; 塗布RB準備未完異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0567                 ; 塗布RB準備未完異常BZ停止
AND B067                 ; 塗布RB準備未完異常
OUT B0567                ; 塗布RB準備未完異常BZ停止
LD T300                  ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ
LD CR2003                ; 常時OFF
ANB B0568                ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B068                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B068                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0568                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳ異常BZ停止
AND B068                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ異常
OUT B0568                ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳ異常BZ停止
LD CR2003                ; 常時OFF
LD CR2003                ; 常時OFF
ANB B0569                ; UNIT3異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B069                 ; UNIT3異常予備
ORL
OUT B069                 ; UNIT3異常予備
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0569                 ; UNIT3異常BZ停止予備
AND B069                 ; UNIT3異常予備
OUT B0569                ; UNIT3異常BZ停止予備
LD CR2003                ; 常時OFF
LD CR2003                ; 常時OFF
ANB B056A                ; UNIT3異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B06A                 ; UNIT3異常予備
ORL
OUT B06A                 ; UNIT3異常予備
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B056A                 ; UNIT3異常BZ停止予備
AND B06A                 ; UNIT3異常予備
OUT B056A                ; UNIT3異常BZ停止予備
;異常ﾌﾞｻﾞｰまとめ
LDB B060                 ; 塗布RB異常
OR B0560                 ; ｺｰﾃｨﾝｸﾞRB異常BZ停止
LDB B062                 ; 塗布RBｸﾗﾝﾌﾟ開閉LS異常
OR B0562                 ; ｺｰﾃｨﾝｸﾞRBｸﾗﾝﾌﾟ開閉LS異常BZ停止
ANL
LDB B063                 ; 塗布RB基板空振異常
OR B0563                 ; 塗布RB基板空振異常BZ停止
ANL
LDB B064                 ; 基板収納時ﾗｯｸ浮き異常
OR B0564                 ; 基板収納時ﾗｯｸ浮き異常BZ停止
ANL
LDB B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
OR B0565                 ; ﾛﾎﾞｯﾄ動作時干渉異常BZ停止
ANL
LDB B066                 ; 塗布RBﾜｰｸ有原復帰不可異常
OR B0566                 ; 塗布RBﾜｰｸ有原復帰不可異常BZ停止
ANL
LDB B067                 ; 塗布RB準備未完異常
OR B0567                 ; 塗布RB準備未完異常BZ停止
ANL
LDB B068                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳﾄ異常
OR B0568                 ; ｺｰﾃｨﾝｸﾞRB原点復帰ﾀｲﾑｱｳ異常BZ停止
ANL
LDB B069                 ; UNIT3異常予備
OR B0569                 ; UNIT3異常BZ停止予備
ANL
LDB B06A                 ; UNIT3異常予備
OR B056A                 ; UNIT3異常BZ停止予備
ANL
INV
OUT M3993                ; ﾕﾆｯﾄ3異常BZ補助
END
ENDH
