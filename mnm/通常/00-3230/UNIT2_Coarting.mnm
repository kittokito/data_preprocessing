DEVICE:53
;MODULE:UNIT2_Coarting
;MODULE_TYPE:0
;SCRIPT_TYPE:
;□□　　入力受け　　□□
LDB X1191                ; ｺｰﾃｨﾝｸﾞ溶剤液面検出
TMR K201 K600            ; 溶剤ﾀﾝｸ液面低下
LD X1148                 ; ｺｰﾃｨﾝｸﾞ材温調完了
TMR K202 K50             ; ｺｰﾃｨﾝｸﾞ材温調完了ONﾀｲﾏ
LD CR2003                ; 常時OFF
TMR K203 K3
LD CR2003                ; 常時OFF
TMR K204 K3
LD CR2002                ; 常時ON
MPS
ANB W0105.0              ; DL-EP1[1]現在値無効状態[1]
MOV.L W0204 D200         ; DL-EP1[1]判定値(P.V.値)[1]  IG測定値
MPP
AND W0105.0              ; DL-EP1[1]現在値無効状態[1]
MOV.L +0 D200            ; IG測定値
;□ ﾗｯｸ供給ﾀｲﾏ設定
LDB M49                  ; 全ﾕﾆｯﾄ起動中
MPS
AND B10AE                ; 塗布ﾊﾞﾙﾌﾞ動作有TP-PB
@MOV K0 D2110            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
MPP
AND B10AF                ; 塗布ﾊﾞﾙﾌﾞ動作無TP-PB
@MOV K1 D2110            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
;■ロボット等機器入力受け
;ロボット、検査装置など状態、現在位置などをまとめる信号は一旦ここで受ける。
;塗布ﾉｽﾞﾙ上下ﾛﾎﾞｯﾄ現在値
LD X108A                 ; 塗布ﾉｽﾞﾙ上下RB_原点完了(SETON)
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
MPS
AND X1080                ; 塗布ﾉｽﾞﾙ上下RB_完了POS1
OUT D2028.0              ; 塗布ﾉｽﾞﾙ上下RB完了POS1
MRD
AND X1081                ; 塗布ﾉｽﾞﾙ上下RB_完了POS2
OUT D2028.1              ; 塗布ﾉｽﾞﾙ上下RB_完了POS2
MRD
AND X1082                ; 塗布ﾉｽﾞﾙ上下RB_完了POS4
OUT D2028.2              ; 塗布ﾉｽﾞﾙ上下RB_完了POS4
MRD
AND X1083                ; 塗布ﾉｽﾞﾙ上下RB_完了POS8
OUT D2028.3              ; 塗布ﾉｽﾞﾙ上下RB_完了POS8
MRD
AND X1084                ; 塗布ﾉｽﾞﾙ上下RB_完了POS16
OUT D2028.4              ; 塗布ﾉｽﾞﾙ上下RB_完了POS16
MRD
AND X1085                ; 塗布ﾉｽﾞﾙ上下RB_完了POS32
OUT D2028.5              ; 塗布ﾉｽﾞﾙ上下RB_完了POS32
MPP
INV
MOV K0 D2028             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
;□ 装置状態・動作条件 □
LD X1163                 ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
AND X1190                ; ｺｰﾃｨﾝｸﾞｺｯﾌﾟ検出
AND= D2028 K1            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
LDB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
OR= D15 K1               ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
ANL
OUT M3184                ; ﾕﾆｯﾄ2原点
LD M3184                 ; ﾕﾆｯﾄ2原点
ANB M2400                ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ開始
ANB M160                 ; 一時停止
OUT M3186                ; ﾕﾆｯﾄ2起動条件
LDB M64                  ; 運転準備
LD M77                   ; ｴﾗｰ発生
OR M66                   ; ｻｲｸﾙ停止異常(ｻｲｸﾙ停止)
AND M3188                ; ﾕﾆｯﾄ2原点復帰中
ORL
OR M3190                 ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
OR T15                   ; 強制停止
OUT M3187                ; ﾕﾆｯﾄ2起動OFF
LD M98                   ; 原点復帰開始
ANB M3188                ; ﾕﾆｯﾄ2原点復帰中
OR M3188                 ; ﾕﾆｯﾄ2原点復帰中
ANB M3187                ; ﾕﾆｯﾄ2起動OFF
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M2879                ; ﾕﾆｯﾄ2原点復帰完了確認
OUT M3188                ; ﾕﾆｯﾄ2原点復帰中
LD M83                   ; 各工程起動指令
LD M3989                 ; ﾕﾆｯﾄ3起動中
LD X1185                 ; 集塵部ﾜｰｸ確認
OR X1186                 ; 給材PP取出ﾜｰｸ確認
MEP
ORL
ANL
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
AND CR2003               ; 常時OFF
ORL
AND M3186                ; ﾕﾆｯﾄ2起動条件
OR M3189                 ; ﾕﾆｯﾄ2起動中
ANB M3187                ; ﾕﾆｯﾄ2起動OFF
ANB M2463                ; ﾕﾆｯﾄ2動作完了
OUT M3189                ; ﾕﾆｯﾄ2起動中
LDB B053                 ; ﾀﾝｸ温度上限異常
OR B0553                 ; UNIT2異常BZ停止予備
LDB B054                 ; ﾀﾝｸ温度下限異常
OR B0554                 ; UNIT2異常BZ停止予備
ANL
INV
OUT M3192                ; ﾕﾆｯﾄ2警報補助
LD X108C                 ; 塗布ﾉｽﾞﾙ上下RB_ｻｰﾎﾞON(SVON)
ANB B053                 ; ﾀﾝｸ温度上限異常
ANB B054                 ; ﾀﾝｸ温度下限異常
OUT M3196                ; ﾕﾆｯﾄ2機器準備完了
;□非常停止フラグ
LDB CR2003               ; 常時OFF
INV
OUT M3185                ; ﾕﾆｯﾄ2非常停止停止異常
;□エラー発生　一時停止フラグ
LDB B042                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常
ANB B043                 ; ｺｰﾄ材液面検出ｾﾝｻ異常
ANB B045                 ; 捨打ちｶｯﾌﾟ無し異常
ANB B04B                 ; ﾉｽﾞﾙ上下RB動作ﾀｲﾑｱｳﾄ異常
ANB B04C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異常
ANB B04D                 ; 材料不足ﾓｰﾄﾞ切替不可異常
INV
OUT M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
;□サイクル停止異常フラグ
LDB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
ANB B041                 ; ｺｰﾃｨﾝｸﾞ材温調過昇異常
ANB B044                 ; 塗布幅判定異常
ANB B047                 ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ異常
ANB B04E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常
ANB B056                 ; 電空ﾚｷﾞｭﾚｰﾀｰ異常
INV
OUT M3190                ; ﾕﾆｯﾄ2ｻｲｸﾙ停止異常
;□払出停止異常フラグ
LDB B046                 ; 溶剤ﾀﾝｸ液面低下異常
ANB B048                 ; ｺｰﾄ材液面低下補充要求
ANB B049                 ; ｺｰﾄ材液面低下異常
ANB B04A                 ; ｺｰﾄ材温度異常
INV
OUT M3191                ; ﾕﾆｯﾄ2払出停止異常
;□　動作中ユニット無しフラグ
LD T210                  ; ﾕﾆｯﾄ2動作無し判定ﾀｲﾏ
LD X108B                 ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND M33                  ; 手動ﾓｰﾄﾞ
ORL
OUT M3198                ; ﾕﾆｯﾄ2動作無し確認
;動作停止判定
LD= D2000 D2005          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ2動作中判定
ORB M3189                ; ﾕﾆｯﾄ2起動中
LD X108B                 ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR CR2002                ; 常時ON
ANL
TMR K210 K30             ; ﾕﾆｯﾄ2動作無し判定ﾀｲﾏ
LD CR2002                ; 常時ON
MOV D2000 D2005          ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ  ﾕﾆｯﾄ2動作中判定
;□ステップ起動用フラグ
LD M32                   ; 自動ﾓｰﾄﾞ
LD M33                   ; 手動ﾓｰﾄﾞ
AND CR2003               ; 常時OFF
ORL
OUT M3168                ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ動作
;□□□□□□□□□□□□□□□
;□　　 　UNIT2自動回路  　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
FMOV K0 M2400 K4         ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ開始
LDB M3189                ; ﾕﾆｯﾄ2起動中
MOV K0 D2000             ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M3189                 ; ﾕﾆｯﾄ2起動中
MOV D2000 Z1             ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
OUT M2400:Z1             ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ開始
;自動運転ステップ開始
LD M2400                 ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ開始
AND M3168                ; ﾕﾆｯﾄ2ｽﾃｯﾌﾟ動作
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;塗布ﾉｽﾞﾙ捨打位置(POS2)移動
LD M2401                 ; 塗布準備要求確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@MOV K2 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2402                 ; 塗布ﾉｽﾞﾙ捨打位置移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M2403                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K2            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;ﾄﾙｴﾝｶｯﾌﾟ前進
LD M2404                 ; ﾄﾙｴﾝｶｯﾌﾟ前進
AND X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
AND X1190                ; ｺｰﾃｨﾝｸﾞｺｯﾌﾟ検出
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;※塗布幅判定
;
;ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞが開いて0.8秒間(T220)捨打後に幅判定開始
;幅判定開始後1秒以上塗布幅確認GO信号が入力されないと(T221)がﾀｲﾑｱｯﾌﾟ後ﾘﾄﾗｲ。
;ﾊﾞﾙﾌﾞ動作無し又は空運転時は塗布幅判定無し
LD M2405                 ; 塗布ﾊﾞﾙﾌﾞ ON捨打開始
MPS
LD= D2110 K1             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
OR M37                   ; 空運転ﾓｰﾄﾞ
ANL
@MOV K10 D2000           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
LD= D2110 K0             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
ANB M37                  ; 空運転ﾓｰﾄﾞ
AND Y226C                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
ANL
MPS
TMR K220 K8              ; ｺｰﾃｨﾝｸﾞ幅確認捨打ﾀｲﾏ
MPP
AND T220                 ; ｺｰﾃｨﾝｸﾞ幅確認捨打ﾀｲﾏ
MPS
AND W010B.0              ; 塗布幅確認GO
@MOV K10 D2000           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@MOV K0 D2050            ; 塗布幅計測ﾘﾄﾗｲ回数
MPP
TMR K221 K10             ; ｺｰﾃｨﾝｸﾞ幅確認ﾀｲﾏ
;塗布幅NGの場合はﾘﾄﾗｲ
;ﾘﾄﾗｲしてもNGなら異常で自動運転停止
LD M2405                 ; 塗布ﾊﾞﾙﾌﾞ ON捨打開始
AND T221                 ; ｺｰﾃｨﾝｸﾞ幅確認ﾀｲﾏ
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@INC D2050               ; 塗布幅計測ﾘﾄﾗｲ回数
LD M2406                 ; 塗布幅NG､塗布ﾊﾞﾙﾌﾞOFF
MPS
AND<= D2050 K1           ; 塗布幅計測ﾘﾄﾗｲ回数
MPS
TMR K222 K10             ; 塗布ﾊﾞﾙﾌﾞOFF待機
MPP
AND T222                 ; 塗布ﾊﾞﾙﾌﾞOFF待機
@MOV K5 D2000            ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
MPP
AND> D2050 K1            ; 塗布幅計測ﾘﾄﾗｲ回数
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@MOV K0 D2050            ; 塗布幅計測ﾘﾄﾗｲ回数
;塗布ﾉｽﾞﾙ塗布位置(POS3)移動
LD M2410                 ; 塗布幅OK､塗布ﾊﾞﾙﾌﾞOFF
ANB Y226C                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@MOV K3 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2411                 ; 塗布ﾉｽﾞﾙ塗布位置移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M2412                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K3            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;塗布準備完了ON､塗布完了確認
LD M2413                 ; 塗布RBへ準備完了ON
ANB X1001                ; ﾛﾎﾞｯﾄ運転中
ANB M3989                ; ﾕﾆｯﾄ3起動中
ANB X1022                ; (802)塗布ﾛﾎﾞｯﾄ塗布進入中
CAL* D2100 K10 D2102     ; 待機ﾀｲﾏTP設定値  塗布待機ﾀｲﾏ
TMR K211 D2102           ; 待機時間経過原点戻ﾀｲﾏ  塗布待機ﾀｲﾏ
LD M2413                 ; 塗布RBへ準備完了ON
LD X1023                 ; (803)塗布ﾛﾎﾞｯﾄ塗布完了
ANB X1022                ; (802)塗布ﾛﾎﾞｯﾄ塗布進入中
ANB M2389                ; ﾕﾆｯﾄ1起動中
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
ANB M3989                ; ﾕﾆｯﾄ3起動中
ANB B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
ORL
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
OR M1920                 ; UNIT1払出処理ﾜｰｸ無
ANL
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
ANB M3989                ; ﾕﾆｯﾄ3起動中
AND T211                 ; 待機時間経過原点戻ﾀｲﾏ
ORL
ANL
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M2414                 ; 塗布RBより塗布完了確認
ANB Y226C                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
ANB X1023                ; (803)塗布ﾛﾎﾞｯﾄ塗布完了
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;ﾄﾙｴﾝｶｯﾌﾟ後退､ﾉｽﾞﾙ漬置位置移動
LD M2415                 ; ﾄﾙｴﾝｶｯﾌﾟ後退
AND X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M160                 ; 一時停止
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
@MOV K1 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2416                 ; 塗布ﾉｽﾞﾙ漬置位置移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M2417                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K1            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
@INC D2000               ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M2413                ; 塗布RBへ準備完了ON
ANB M3989                ; ﾕﾆｯﾄ3起動中
AND M3990                ; ﾕﾆｯﾄ3ｻｲｸﾙ停止異常
MOV K62 D2000            ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;ｻｲｸﾙ終了
LD M2462                 ; 塗布ｻｲｸﾙ停止処理
OR M2418                 ; 位置決完了確認
@MOV K63 D2000           ; ﾕﾆｯﾄ2自動運転ｽﾃｯﾌﾟ
;□□□□□□□□□□□□□□□
;□　　 UNIT2原点復帰回路　　□
;□□□□□□□□□□□□□□□
LD CR2002                ; 常時ON
FMOV K0 M2800 K5         ; ﾕﾆｯﾄ2原点復帰開始
LDB M3188                ; ﾕﾆｯﾄ2原点復帰中
MOV K0 D2010             ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
LD M3188                 ; ﾕﾆｯﾄ2原点復帰中
MOV D2010 Z1             ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
OUT M2800:Z1             ; ﾕﾆｯﾄ2原点復帰開始
;原点復帰ｻｲｸﾙ監視
LD M3188                 ; ﾕﾆｯﾄ2原点復帰中
AND M3184                ; ﾕﾆｯﾄ2原点
TMR K200 K200            ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ
;原点復帰ステップ開始
;ﾄﾙｴﾝｶｯﾌﾟ位置確認
LD M2800                 ; ﾕﾆｯﾄ2原点復帰開始
AND M3984                ; ﾕﾆｯﾄ3原点
MPS
AND X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
MPP
LDB X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
OR X1163                 ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
ANL
@MOV K4 D2010            ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
;塗布ﾉｽﾞﾙ待避上昇(幅ﾁｪｯｸ位置)
LD M2801                 ; ﾄﾙｴﾝｶｯﾌﾟ後退端
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
@MOV K2 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2802                 ; 塗布ﾉｽﾞﾙ捨打位置移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
LD M2803                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K2            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
;ﾄﾙｴﾝｶｯﾌﾟ後退､塗布ﾉｽﾞﾙ下降
LD M2804                 ; ﾄﾙｴﾝｶｯﾌﾟ後退
AND X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
@MOV K1 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2805                 ; 塗布ﾉｽﾞﾙ待機位置移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
LD M2806                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K1            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
@INC D2010               ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M2807                 ; 位置決完了確認
AND M3184                ; ﾕﾆｯﾄ2原点
@MOV K78 D2010           ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
LD M2878                 ; ﾕﾆｯﾄ2原点復帰完了
ANB M98                  ; 原点復帰開始
MOV K79 D2010            ; ﾕﾆｯﾄ2原点復帰ｽﾃｯﾌﾟ
;□□□□□□□□□□□□□□□
;□　　 　UNIT2手動回路  　　□
;□□□□□□□□□□□□□□□
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
ANB M3189                ; ﾕﾆｯﾄ2起動中
ANB M3188                ; ﾕﾆｯﾄ2原点復帰中
ANB M3697                ; 塗布RB1ｻｲｸﾙ開始
MC
;ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ
LD X1162                 ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
AND X1190                ; ｺｰﾃｨﾝｸﾞｺｯﾌﾟ検出
LD= D2028 K2             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
LD M39                   ; ｲﾝﾀｰﾛｯｸ解除ﾓｰﾄﾞ
AND= D2028 K3            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ORL
ANL
OUT M2890                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開可
LD B10A0                 ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開TP-PB
ORP B05F1                ; 2sec吐出
AND M2890                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開可
OR M2891                 ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
ANB T276                 ; 2sec吐出ﾀｲﾏ
ANFB B10A0               ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開TP-PB
OUT M2891                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
LD B05F1                 ; 2sec吐出
AND M2890                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開可
OR M2891                 ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
ANB T276                 ; 2sec吐出ﾀｲﾏ
TMR K276 K20             ; 2sec吐出ﾀｲﾏ
;ﾄﾙｴﾝｶｯﾌﾟ前進､後退
LD CR2002                ; 常時ON
LD= D2028 K2             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K3             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K4             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANL
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
OUT M2892                ; ﾄﾙｴﾝｶｯﾌﾟ前進可
LD B10A2                 ; ﾄﾙｴﾝｶｯﾌﾟ前進TP-PB
AND M2892                ; ﾄﾙｴﾝｶｯﾌﾟ前進可
OR M2893                 ; ﾄﾙｴﾝｶｯﾌﾟ前進
ANPB M2895               ; ﾄﾙｴﾝｶｯﾌﾟ後退
OUT M2893                ; ﾄﾙｴﾝｶｯﾌﾟ前進
LD CR2002                ; 常時ON
LD= D2028 K2             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K3             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR= D2028 K4             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
ANL
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
OUT M2894                ; ﾄﾙｴﾝｶｯﾌﾟ後退可
LD B10A3                 ; ﾄﾙｴﾝｶｯﾌﾟ後退TP-PB
AND M2894                ; ﾄﾙｴﾝｶｯﾌﾟ後退可
OR M2895                 ; ﾄﾙｴﾝｶｯﾌﾟ後退
ANPB M2893               ; ﾄﾙｴﾝｶｯﾌﾟ前進
OUT M2895                ; ﾄﾙｴﾝｶｯﾌﾟ後退
;塗布ﾉｽﾞﾙ待機移動(浸置位置)
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
AND X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND<> D2028 K1           ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2896                ; 塗布ﾉｽﾞﾙ待機移動可
LD B10A4                 ; 塗布ﾉｽﾞﾙ待機移動TP-PB
AND M2896                ; 塗布ﾉｽﾞﾙ待機移動可
OR M2897                 ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2900                ; 位置決完了確認
ANB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
MPS
OUT M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
MPP
ANB M2899                ; 起動確認
OUT M2898                ; 塗布ﾉｽﾞﾙ待機移動
@MOV K1 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2898                 ; 塗布ﾉｽﾞﾙ待機移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2899                 ; 起動確認
AND M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
OUT M2899                ; 起動確認
LD M2899                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K1            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2900                ; 位置決完了確認
;塗布ﾉｽﾞﾙ捨打移動
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND<> D2028 K2           ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2901                ; 塗布ﾉｽﾞﾙ捨打移動可
LD B10A5                 ; 塗布ﾉｽﾞﾙ捨打移動TP-PB
AND M2901                ; 塗布ﾉｽﾞﾙ捨打移動可
OR M2902                 ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2905                ; 位置決完了確認
ANB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
MPS
OUT M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
MPP
ANB M2904                ; 起動確認
OUT M2903                ; 塗布ﾉｽﾞﾙ捨打移動
@MOV K2 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2903                 ; 塗布ﾉｽﾞﾙ捨打移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2904                 ; 起動確認
AND M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
OUT M2904                ; 起動確認
LD M2904                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K2            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2905                ; 位置決完了確認
;塗布ﾉｽﾞﾙ塗布移動
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND<> D2028 K3           ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2906                ; 塗布ﾉｽﾞﾙ塗布移動可
LD B10A6                 ; 塗布ﾉｽﾞﾙ塗布移動TP-PB
AND M2906                ; 塗布ﾉｽﾞﾙ塗布移動可
OR M2907                 ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2910                ; 位置決完了確認
ANB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
MPS
OUT M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
MPP
ANB M2909                ; 起動確認
OUT M2908                ; 塗布ﾉｽﾞﾙ塗布移動
@MOV K3 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2908                 ; 塗布ﾉｽﾞﾙ塗布移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2909                 ; 起動確認
AND M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
OUT M2909                ; 起動確認
LD M2909                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K3            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2910                ; 位置決完了確認
;塗布ﾉｽﾞﾙ上昇移動
LD X102A                 ; (810)塗布ﾛﾎﾞｯﾄ原点位置
ANB M2897                ; 塗布ﾉｽﾞﾙ待機移動開始
ANB M2902                ; 塗布ﾉｽﾞﾙ捨打移動開始
ANB M2907                ; 塗布ﾉｽﾞﾙ塗布移動開始
ANB M2913                ; 塗布ﾉｽﾞﾙ上昇開始
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND<> D2028 K4           ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2912                ; 塗布ﾉｽﾞﾙ上昇可
LD B10A7                 ; 塗布ﾉｽﾞﾙ上昇移動TP-PB
AND M2912                ; 塗布ﾉｽﾞﾙ上昇可
OR M2913                 ; 塗布ﾉｽﾞﾙ上昇開始
ANB M2916                ; 位置決完了確認
ANB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
MPS
OUT M2913                ; 塗布ﾉｽﾞﾙ上昇開始
MPP
ANB M2915                ; 起動確認
OUT M2914                ; 塗布ﾉｽﾞﾙ上昇
@MOV K4 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2914                 ; 塗布ﾉｽﾞﾙ上昇
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2915                 ; 起動確認
AND M2913                ; 塗布ﾉｽﾞﾙ上昇開始
OUT M2915                ; 起動確認
LD M2915                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K4            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT M2916                ; 位置決完了確認
MCR
LD CR2002                ; 常時ON
LD M33                   ; 手動ﾓｰﾄﾞ
ANB M3188                ; ﾕﾆｯﾄ2原点復帰中
OR= D15 K1               ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
ANL
MC
;ｺｰﾃｨﾝｸﾞ材温調
LDB M49                  ; 全ﾕﾆｯﾄ起動中
OUT M2922                ; ｺｰﾃｨﾝｸﾞ材温調開始可
LD B10A8                 ; ｺｰﾃｨﾝｸﾞ材温調開始TP-PB
AND M2922                ; ｺｰﾃｨﾝｸﾞ材温調開始可
OR M2923                 ; ｺｰﾃｨﾝｸﾞ材温調開始
ANPB M2925               ; ｺｰﾃｨﾝｸﾞ材温調停止
OUT M2923                ; ｺｰﾃｨﾝｸﾞ材温調開始
LDB M49                  ; 全ﾕﾆｯﾄ起動中
OUT M2924                ; ｺｰﾃｨﾝｸﾞ材温調停止可
LD B10A9                 ; ｺｰﾃｨﾝｸﾞ材温調停止TP-PB
AND M2924                ; ｺｰﾃｨﾝｸﾞ材温調停止可
OR M2925                 ; ｺｰﾃｨﾝｸﾞ材温調停止
ANPB M2923               ; ｺｰﾃｨﾝｸﾞ材温調開始
OUT M2925                ; ｺｰﾃｨﾝｸﾞ材温調停止
MCR
;塗布幅確認移動動作
;手動で各部を動かすのが面倒なので連動動作する機能を追加
;ノズル捨打位置移動後カップ前進
;手動＋日常点検モードのみ有効
LD M33                   ; 手動ﾓｰﾄﾞ
ANB M3197                ; ﾕﾆｯﾄ2ｴﾗｰ発生
ANB M3187                ; ﾕﾆｯﾄ2起動OFF
AND= D12000 K10          ; 現在表示中画面ﾍﾟｰｼﾞ
MC
LDP B10B0                ; 塗布幅測定位置 TP-PB
AND M3184                ; ﾕﾆｯﾄ2原点
AND M3984                ; ﾕﾆｯﾄ3原点
OR M2944                 ; 塗布幅確認移動開始
ANB M2948                ; 動作完了
ANB M2954                ; 塗布幅確認終了移動開始
OUT M2944                ; 塗布幅確認移動開始
;塗布ﾉｽﾞﾙ捨打位置(POS2)移動
LD M2944                 ; 塗布幅確認移動開始
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2945                 ; 塗布ﾉｽﾞﾙ捨打移動
ANB M2946                ; 起動確認
AND M2944                ; 塗布幅確認移動開始
OUT M2945                ; 塗布ﾉｽﾞﾙ捨打移動
@MOV K2 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2945                 ; 塗布ﾉｽﾞﾙ捨打移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2946                 ; 起動確認
ANB M2947                ; ﾄﾙｴﾝｶｯﾌﾟ前進
AND M2944                ; 塗布幅確認移動開始
OUT M2946                ; 起動確認
;捨打カップ前進
LD M2946                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K2            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR M2947                 ; ﾄﾙｴﾝｶｯﾌﾟ前進
ANB M2948                ; 動作完了
AND M2944                ; 塗布幅確認移動開始
OUT M2947                ; ﾄﾙｴﾝｶｯﾌﾟ前進
LD M2947                 ; ﾄﾙｴﾝｶｯﾌﾟ前進
AND X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
OR M2948                 ; 動作完了
AND M2944                ; 塗布幅確認移動開始
OUT M2948                ; 動作完了
;ユニット２原点戻り
;塗布幅確認動作でユニットを原点に戻す際に使用
;手動＋日常点検モードのみ有効
LDP B10B1                ; 塗布ﾕﾆｯﾄ原点戻 TP-PB
AND M3984                ; ﾕﾆｯﾄ3原点
OR M2954                 ; 塗布幅確認終了移動開始
ANB M2958                ; 動作完了
ANB M2944                ; 塗布幅確認移動開始
OUT M2954                ; 塗布幅確認終了移動開始
;捨打カップ後退
LDP M2954                ; 塗布幅確認終了移動開始
OR M2955                 ; ﾄﾙｴﾝｶｯﾌﾟ後退
ANB M2956                ; 塗布ﾉｽﾞﾙ待機移動
AND M2954                ; 塗布幅確認終了移動開始
OUT M2955                ; ﾄﾙｴﾝｶｯﾌﾟ後退
;塗布ﾉｽﾞﾙ捨打位置(POS2)移動
LD M2955                 ; ﾄﾙｴﾝｶｯﾌﾟ後退
AND X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2956                 ; 塗布ﾉｽﾞﾙ待機移動
ANB M2957                ; 起動確認
AND M2954                ; 塗布幅確認終了移動開始
OUT M2956                ; 塗布ﾉｽﾞﾙ待機移動
@MOV K1 D2024            ; 塗布ﾉｽﾞﾙ上下RB_POS確定
LD M2956                 ; 塗布ﾉｽﾞﾙ待機移動
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
OR M2957                 ; 起動確認
ANB M2958                ; 動作完了
AND M2954                ; 塗布幅確認終了移動開始
OUT M2957                ; 起動確認
;捨打カップ前進
LD M2957                 ; 起動確認
AND X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND= D2028 K1            ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OR M2958                 ; 動作完了
AND M2954                ; 塗布幅確認終了移動開始
OUT M2958                ; 動作完了
MCR
;□□　　出力回路　　□□
;ソレノイドバルブ出力
LDB X1032                ; (818)塗布ﾛﾎﾞｯﾄ塗布開始要求
LDB M3697                ; 塗布RB1ｻｲｸﾙ開始
ANB M3989                ; ﾕﾆｯﾄ3起動中
ORL
ORB X1001                ; ﾛﾎﾞｯﾄ運転中
LDB M2405                ; 塗布ﾊﾞﾙﾌﾞ ON捨打開始
ORB M3189                ; ﾕﾆｯﾄ2起動中
ANL
LDB M2891                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
ORB M33                  ; 手動ﾓｰﾄﾞ
ANL
MPS
INV
AND= D2110 K0            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
ANB M37                  ; 空運転ﾓｰﾄﾞ
OUT Y226C                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
MPP
OUT Y226D                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ閉
LDB M2404                ; ﾄﾙｴﾝｶｯﾌﾟ前進
ANB M2893                ; ﾄﾙｴﾝｶｯﾌﾟ前進
ANB M2947                ; ﾄﾙｴﾝｶｯﾌﾟ前進
INV
LD Y2280                 ; ﾄﾙｴﾝｶｯﾌﾟ前進SV
ANB Y2281                ; ﾄﾙｴﾝｶｯﾌﾟ後退SV
ORL
OUT Y2280                ; ﾄﾙｴﾝｶｯﾌﾟ前進SV
LDB M2415                ; ﾄﾙｴﾝｶｯﾌﾟ後退
ANB M2804                ; ﾄﾙｴﾝｶｯﾌﾟ後退
ANB M2895                ; ﾄﾙｴﾝｶｯﾌﾟ後退
ANB M2955                ; ﾄﾙｴﾝｶｯﾌﾟ後退
INV
LD Y2281                 ; ﾄﾙｴﾝｶｯﾌﾟ後退SV
ANB Y2280                ; ﾄﾙｴﾝｶｯﾌﾟ前進SV
ORL
OUT Y2281                ; ﾄﾙｴﾝｶｯﾌﾟ後退SV
;コーティング材温調
;※温調のｲﾍﾞﾝﾄ信号は停止時ON･運転時OFF
LD T0                    ; 電源投入0Nﾃﾞｨﾚｲ
ANB X1149                ; ｺｰﾃｨﾝｸﾞ材温調異常(過昇)
OUT Y55                  ; ｺｰﾃｨﾝｸﾞ材加温ﾋｰﾀｰ電源ON
LD Y55                   ; ｺｰﾃｨﾝｸﾞ材加温ﾋｰﾀｰ電源ON
ANB M2925                ; ｺｰﾃｨﾝｸﾞ材温調停止
LD M32                   ; 自動ﾓｰﾄﾞ
OR M33                   ; 手動ﾓｰﾄﾞ
ANL
INV
OUT Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
LD CR2003                ; 常時OFF
OUT Y2253                ; ｺｰﾃｨﾝｸﾞ材加温温調EV2_ON
;ロボット出力
;■塗布ノズル上下 ロボット動作出力
LDB M2402                ; 塗布ﾉｽﾞﾙ捨打位置移動
ANB M2411                ; 塗布ﾉｽﾞﾙ塗布位置移動
ANB M2416                ; 塗布ﾉｽﾞﾙ漬置位置移動
ANB M2802                ; 塗布ﾉｽﾞﾙ捨打位置移動
ANB M2805                ; 塗布ﾉｽﾞﾙ待機位置移動
ANB M2945                ; 塗布ﾉｽﾞﾙ捨打移動
ANB M2956                ; 塗布ﾉｽﾞﾙ待機移動
INV
LDB M2898                ; 塗布ﾉｽﾞﾙ待機移動
ANB M2903                ; 塗布ﾉｽﾞﾙ捨打移動
ANB M2908                ; 塗布ﾉｽﾞﾙ塗布移動
ANB M2914                ; 塗布ﾉｽﾞﾙ上昇
INV
ORL
@MOV D2024 D2026         ; 塗布ﾉｽﾞﾙ上下RB_POS確定  塗布ﾉｽﾞﾙ上下RB_指令POS
MPS
TMR K290 K2              ; 塗布ﾉｽﾞﾙ上下RB_POS確定ﾃﾞｨﾚｲ
MPP
LD T290                  ; 塗布ﾉｽﾞﾙ上下RB_POS確定ﾃﾞｨﾚｲ
ANB X1022                ; (802)塗布ﾛﾎﾞｯﾄ塗布進入中
ANB B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
ANL
OUT Y208D                ; 塗布ﾉｽﾞﾙ上下RB_ｽﾀｰﾄ(DRIVE)
LD CR2002                ; 常時ON
MPS
AND D2026.0              ; 塗布ﾉｽﾞﾙ RB_指令POS1
OUT Y2080                ; 塗布ﾉｽﾞﾙ上下RB_指令POS1
MRD
AND D2026.1              ; 塗布ﾉｽﾞﾙ RB_指令POS2
OUT Y2081                ; 塗布ﾉｽﾞﾙ上下RB_指令POS2
MRD
AND D2026.2              ; 塗布ﾉｽﾞﾙ RB_指令POS4
OUT Y2082                ; 塗布ﾉｽﾞﾙ上下RB_指令POS4
MRD
AND D2026.3              ; 塗布ﾉｽﾞﾙ RB_指令POS8
OUT Y2083                ; 塗布ﾉｽﾞﾙ上下RB_指令POS8
MRD
AND D2026.4              ; 塗布ﾉｽﾞﾙ RB_指令POS16
OUT Y2084                ; 塗布ﾉｽﾞﾙ上下RB_指令POS16
MRD
AND D2026.5              ; 塗布ﾉｽﾞﾙ RB_指令POS32
OUT Y2085                ; 塗布ﾉｽﾞﾙ上下RB_指令POS32
MRD
AND D2026.6              ; 塗布ﾉｽﾞﾙ RB_指令POS64
OUT Y2086                ; 塗布ﾉｽﾞﾙ上下RB_指令POS64
MRD
AND D2026.7              ; 塗布ﾉｽﾞﾙ RB_指令POS128
OUT Y2087                ; 塗布ﾉｽﾞﾙ上下RB_指令POS128
MPP
AND D2026.8              ; 塗布ﾉｽﾞﾙ RB_指令POS256
OUT Y2088                ; 塗布ﾉｽﾞﾙ上下RB_指令POS256
LD CR2003                ; 常時OFF
OUT Y208B                ; 塗布ﾉｽﾞﾙ上下RB_原点復帰(SETON)
LDB X1022                ; (802)塗布ﾛﾎﾞｯﾄ塗布進入中
ANB B065                 ; ﾛﾎﾞｯﾄ動作時干渉異常
OUT Y208C                ; 塗布ﾉｽﾞﾙ上下RB_一時停止(HOLD)
LD M16                   ; ﾘｾｯﾄPB
ANB X108E                ; 塗布ﾉｽﾞﾙ上下RB_ｱﾗｰﾑ
OUT Y208E                ; 塗布ﾉｽﾞﾙ上下RB_ﾘｾｯﾄ(RESET)
LD M64                   ; 運転準備
OUT Y208F                ; 塗布ﾉｽﾞﾙ上下RB_ｻｰﾎﾞON(SVON)
;□□　　パネル表示　　□□
LD Y226C                 ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開
OUT B15A0                ; ｺｰﾃｨﾝｸﾞﾊﾞﾙﾌﾞ開TP-PL
LD CR2003                ; 常時OFF
OUT B15A1                ; UNIT2-1-2 TP-PL
LD X1162                 ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
OUT B15A2                ; ﾄﾙｴﾝｶｯﾌﾟ前進TP-PL
LD X1163                 ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
OUT B15A3                ; ﾄﾙｴﾝｶｯﾌﾟ後退TP-PL
LD= D2028 K1             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT B15A4                ; 塗布ﾉｽﾞﾙ待機移動TP-PL
LD= D2028 K2             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT B15A5                ; 塗布ﾉｽﾞﾙ捨打移動TP-PL
LD= D2028 K3             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT B15A6                ; 塗布ﾉｽﾞﾙ塗布移動TP-PL
LD= D2028 K4             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
OUT B15A7                ; 塗布ﾉｽﾞﾙ上昇移動TP-PL
LDB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
OUT B15A8                ; ｺｰﾃｨﾝｸﾞ材温調開始TP-PL
LD Y2252                 ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
OUT B15A9                ; ｺｰﾃｨﾝｸﾞ材温調停止TP-PL
LD= D2110 K0             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
OUT B15AE                ; 塗布ﾊﾞﾙﾌﾞ動作有TP-PL
LD= D2110 K1             ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
OUT B15AF                ; 塗布ﾊﾞﾙﾌﾞ動作無TP-PL
LD= D2028 K2             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
AND X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
OUT B15B0                ; 塗布幅測定位置 TP-PL
LD= D2028 K1             ; 塗布ﾉｽﾞﾙ上下RB_完了POS
AND X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
OUT B15B1                ; 塗布ﾕﾆｯﾄ原点戻 TP-PL
;□□　　異常検出　　□□
LDB X108E                ; 塗布ﾉｽﾞﾙ上下RB_ｱﾗｰﾑ
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0540                ; 塗布ﾉｽﾞﾙ上下RB異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
ORL
OUT B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0540                 ; 塗布ﾉｽﾞﾙ上下RB異常BZ停止
AND B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
OUT B0540                ; 塗布ﾉｽﾞﾙ上下RB異常BZ停止
LD X1149                 ; ｺｰﾃｨﾝｸﾞ材温調異常(過昇)
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0541                ; ｺｰﾄ材温調異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B041                 ; ｺｰﾃｨﾝｸﾞ材温調過昇異常
ORL
OUT B041                 ; ｺｰﾃｨﾝｸﾞ材温調過昇異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0541                 ; ｺｰﾄ材温調異常BZ停止
AND B041                 ; ｺｰﾃｨﾝｸﾞ材温調過昇異常
OUT B0541                ; ｺｰﾄ材温調異常BZ停止
LDB X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
AND Y2280                ; ﾄﾙｴﾝｶｯﾌﾟ前進SV
LDB X1163                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ後退端
AND Y2281                ; ﾄﾙｴﾝｶｯﾌﾟ後退SV
ORL
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0542                ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B042                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常
ORL
MPS
TMR K250 K50             ; ﾄﾙｴﾝｶｯﾌﾟ前後LS確認
MPP
AND T250                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS確認
OUT B042                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0542                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常BZ停止
AND B042                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常
OUT B0542                ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常BZ停止
LDB X1196                ; ｺｰﾃｨﾝｸﾞ液面検出ｱﾗｰﾑ
AND M64                  ; 運転準備
LD CR2003                ; 常時OFF
ANB B0543                ; ｺｰﾄ材液面検出ｾﾝｻ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B043                 ; ｺｰﾄ材液面検出ｾﾝｻ異常
ORL
OUT B043                 ; ｺｰﾄ材液面検出ｾﾝｻ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0543                 ; ｺｰﾄ材液面検出ｾﾝｻ異常BZ停止
AND B043                 ; ｺｰﾄ材液面検出ｾﾝｻ異常
OUT B0543                ; ｺｰﾄ材液面検出ｾﾝｻ異常BZ停止
LD M2407                 ; 塗布幅NG異常
AND= D2110 K0            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
ANB M37                  ; 空運転ﾓｰﾄﾞ
LD CR2003                ; 常時OFF
ANB B0544                ; 塗布幅判定異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B044                 ; 塗布幅判定異常
ORL
OUT B044                 ; 塗布幅判定異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0544                 ; 塗布幅判定異常BZ停止
AND B044                 ; 塗布幅判定異常
OUT B0544                ; 塗布幅判定異常BZ停止
LD M2404                 ; ﾄﾙｴﾝｶｯﾌﾟ前進
AND X1162                ; ｺｰﾃｨﾝｸﾞﾄﾙｴﾝｶｯﾌﾟ前進端
ANB X1190                ; ｺｰﾃｨﾝｸﾞｺｯﾌﾟ検出
LD CR2003                ; 常時OFF
ANB B0545                ; 捨打ちｶｯﾌﾟ無し異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B045                 ; 捨打ちｶｯﾌﾟ無し異常
ORL
OUT B045                 ; 捨打ちｶｯﾌﾟ無し異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0545                 ; 捨打ちｶｯﾌﾟ無し異常BZ停止
AND B045                 ; 捨打ちｶｯﾌﾟ無し異常
OUT B0545                ; 捨打ちｶｯﾌﾟ無し異常BZ停止
LD T201                  ; 溶剤ﾀﾝｸ液面低下
AND= D2110 K0            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
LD CR2003                ; 常時OFF
ANB B0546                ; 溶剤ﾀﾝｸ液面低下異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B046                 ; 溶剤ﾀﾝｸ液面低下異常
ORL
OUT B046                 ; 溶剤ﾀﾝｸ液面低下異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0546                 ; 溶剤ﾀﾝｸ液面低下異常BZ停止
AND B046                 ; 溶剤ﾀﾝｸ液面低下異常
OUT B0546                ; 溶剤ﾀﾝｸ液面低下異常BZ停止
LD T200                  ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ
LD CR2003                ; 常時OFF
ANB B0547                ; ｺｰﾃｨﾝｸﾞ原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B047                 ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ異常
ORL
OUT B047                 ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0547                 ; ｺｰﾃｨﾝｸﾞ原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
AND B047                 ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ異常
OUT B0547                ; ｺｰﾃｨﾝｸﾞ原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
LDB X1192                ; ｺｰﾃｨﾝｸﾞ液面検出OUT1
ANB B0548                ; ｺｰﾄ材液面低下補充要求BZ停止
ANB M37                  ; 空運転ﾓｰﾄﾞ
AND= D2110 K0            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
LD CR2003                ; 常時OFF
ANB B0548                ; ｺｰﾄ材液面低下補充要求BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B048                 ; ｺｰﾄ材液面低下補充要求
ORL
OUT B048                 ; ｺｰﾄ材液面低下補充要求
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0548                 ; ｺｰﾄ材液面低下補充要求BZ停止
AND B048                 ; ｺｰﾄ材液面低下補充要求
OUT B0548                ; ｺｰﾄ材液面低下補充要求BZ停止
LDB X1193                ; ｺｰﾃｨﾝｸﾞ液面検出OUT2
ANB M37                  ; 空運転ﾓｰﾄﾞ
AND= D2110 K0            ; 塗布ﾊﾞﾙﾌﾞ_0:有効_1無効
LD CR2003                ; 常時OFF
ANB B0549                ; ｺｰﾄ材液面低下異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B049                 ; ｺｰﾄ材液面低下異常
ORL
OUT B049                 ; ｺｰﾄ材液面低下異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0549                 ; ｺｰﾄ材液面低下異常BZ停止
AND B049                 ; ｺｰﾄ材液面低下異常
OUT B0549                ; ｺｰﾄ材液面低下異常BZ停止
;ｺｰﾃｨﾝｸﾞ材温度低下検出用
;温調完了時に記憶ﾌﾗｸﾞON
;ﾌﾗｸﾞON後に温調完了がOFFで異常
LD T202                  ; ｺｰﾃｨﾝｸﾞ材温調完了ONﾀｲﾏ
OR M3130                 ; ｺｰﾃｨﾝｸﾞ温調完了記憶
ANB Y2252                ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
OUT M3130                ; ｺｰﾃｨﾝｸﾞ温調完了記憶
LDB X1148                ; ｺｰﾃｨﾝｸﾞ材温調完了
AND M3130                ; ｺｰﾃｨﾝｸﾞ温調完了記憶
ANP M18                  ; 起動PB
LD CR2003                ; 常時OFF
ANB B054A                ; ｺｰﾄ材温度異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04A                 ; ｺｰﾄ材温度異常
ORL
OUT B04A                 ; ｺｰﾄ材温度異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054A                 ; ｺｰﾄ材温度異常BZ停止
AND B04A                 ; ｺｰﾄ材温度異常
OUT B054A                ; ｺｰﾄ材温度異常BZ停止
LD Y208D                 ; 塗布ﾉｽﾞﾙ上下RB_ｽﾀｰﾄ(DRIVE)
ANB X108B                ; 塗布ﾉｽﾞﾙ上下RB_位置決完(INP)
AND M3189                ; ﾕﾆｯﾄ2起動中
LD CR2003                ; 常時OFF
ANB B054B                ; ﾉｽﾞﾙ上下RBﾀｲﾑｱｳﾄ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04B                 ; ﾉｽﾞﾙ上下RB動作ﾀｲﾑｱｳﾄ異常
ORL
MPS
TMR K251 K30             ; ﾉｽﾞﾙ上下ﾀｲﾑｱｳﾄ確認
MPP
AND T251                 ; ﾉｽﾞﾙ上下ﾀｲﾑｱｳﾄ確認
OUT B04B                 ; ﾉｽﾞﾙ上下RB動作ﾀｲﾑｱｳﾄ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054B                 ; ﾉｽﾞﾙ上下RBﾀｲﾑｱｳﾄ異常BZ停止
AND B04B                 ; ﾉｽﾞﾙ上下RB動作ﾀｲﾑｱｳﾄ異常
OUT B054B                ; ﾉｽﾞﾙ上下RBﾀｲﾑｱｳﾄ異常BZ停止
LDB X1148                ; ｺｰﾃｨﾝｸﾞ材温調完了
OR Y2252                 ; ｺｰﾃｨﾝｸﾞ材加温温調EV1_ON
AND M33                  ; 手動ﾓｰﾄﾞ
ANP B1001                ; 自動/手動PB
LD CR2003                ; 常時OFF
ANB B054C                ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異BZ停
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異常
ORL
AND= D15 K0              ; 温度起動条件ﾌﾗｸﾞ_0:ON_1:OFF
OUT B04C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異BZ停
AND B04C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異常
OUT B054C                ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異BZ停
LDB X1192                ; ｺｰﾃｨﾝｸﾞ液面検出OUT1
ORB X1193                ; ｺｰﾃｨﾝｸﾞ液面検出OUT2
AND M33                  ; 手動ﾓｰﾄﾞ
ANB M37                  ; 空運転ﾓｰﾄﾞ
ANP B1001                ; 自動/手動PB
LD CR2003                ; 常時OFF
ANB B054D                ; 材料不足ﾓｰﾄﾞ切替不可異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04D                 ; 材料不足ﾓｰﾄﾞ切替不可異常
ORL
OUT B04D                 ; 材料不足ﾓｰﾄﾞ切替不可異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054D                 ; 材料不足ﾓｰﾄﾞ切替不可異常BZ停止
AND B04D                 ; 材料不足ﾓｰﾄﾞ切替不可異常
OUT B054D                ; 材料不足ﾓｰﾄﾞ切替不可異常BZ停止
LD CR2003                ; 常時OFF
LD CR2003                ; 常時OFF
ANB B054E                ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B04E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常
ORL
MPS
TMR K252 K150            ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ
MPP
AND T252                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ
OUT B04E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常BZ停止
AND B04E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常
OUT B054E                ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常BZ停止
LD X3043                 ; ch0 警報上限
TMR K260 K30             ; 監視時間
LD T260                  ; 監視時間
MEP
LD CR2003                ; 常時OFF
ANB B054F                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR M81                   ; 自動運転開始BZ
AND B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
ORL
OUT B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B054F                 ; UNIT2異常BZ停止予備
AND B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
OUT B054F                ; UNIT2異常BZ停止予備
LD X3044                 ; ch0 警報下限
TMR K261 K30             ; 監視時間
LD T261                  ; 監視時間
MEP
LD CR2003                ; 常時OFF
ANB B0550                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR M81                   ; 自動運転開始BZ
AND B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
ORL
OUT B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0550                 ; UNIT2異常BZ停止予備
AND B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
OUT B0550                ; UNIT2異常BZ停止予備
LD X304B                 ; ch1 警報上限
TMR K262 K30             ; 監視時間
LD T262                  ; 監視時間
MEP
LD CR2003                ; 常時OFF
ANB B0551                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR M81                   ; 自動運転開始BZ
AND B051                 ; ﾉｽﾞﾙIN側圧力上限異常
ORL
OUT B051                 ; ﾉｽﾞﾙIN側圧力上限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0551                 ; UNIT2異常BZ停止予備
AND B051                 ; ﾉｽﾞﾙIN側圧力上限異常
OUT B0551                ; UNIT2異常BZ停止予備
LD X304C                 ; ch1 警報下限
TMR K263 K30             ; 監視時間
LD T263                  ; 監視時間
MEP
LD CR2003                ; 常時OFF
ANB B0552                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
OR M81                   ; 自動運転開始BZ
AND B052                 ; ﾉｽﾞﾙIN側圧力下限異常
ORL
OUT B052                 ; ﾉｽﾞﾙIN側圧力下限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0552                 ; UNIT2異常BZ停止予備
AND B052                 ; ﾉｽﾞﾙIN側圧力下限異常
OUT B0552                ; UNIT2異常BZ停止予備
LD X3053                 ; ch2 警報上限
TMR K264 K30             ; 監視時間
LD T264                  ; 監視時間
LD CR2003                ; 常時OFF
ANB B0553                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B053                 ; ﾀﾝｸ温度上限異常
ORL
OUT B053                 ; ﾀﾝｸ温度上限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LD B0553                 ; UNIT2異常BZ停止予備
ANB T266                 ; ﾌﾞｻﾞｰ再鳴らす
ORL
AND B053                 ; ﾀﾝｸ温度上限異常
OUT B0553                ; UNIT2異常BZ停止予備
LD B0553                 ; UNIT2異常BZ停止予備
TMR K266 K3000           ; ﾌﾞｻﾞｰ再鳴らす
LD X3054                 ; ch2 警報下限
TMR K265 K30             ; 監視時間
LD T265                  ; 監視時間
LD CR2003                ; 常時OFF
ANB B0554                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B054                 ; ﾀﾝｸ温度下限異常
ORL
OUT B054                 ; ﾀﾝｸ温度下限異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
LD B0554                 ; UNIT2異常BZ停止予備
ANB T267                 ; ﾌﾞｻﾞｰ再鳴らす
ORL
AND B054                 ; ﾀﾝｸ温度下限異常
OUT B0554                ; UNIT2異常BZ停止予備
LD B0554                 ; UNIT2異常BZ停止予備
TMR K267 K3000           ; ﾌﾞｻﾞｰ再鳴らす
LD X133A                 ; ｴﾗｰ状態ﾌﾗｸﾞ_電空ﾚｷﾞｭﾚｰﾀｰ
LD CR2003                ; 常時OFF
ANB B0556                ; UNIT2異常BZ停止予備
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B056                 ; 電空ﾚｷﾞｭﾚｰﾀｰ異常
ORL
AND L10                  ; 電空ﾚｷﾞｭﾚｰﾀｰ(0無_1有)
OUT B056                 ; 電空ﾚｷﾞｭﾚｰﾀｰ異常
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B0556                 ; UNIT2異常BZ停止予備
AND B056                 ; 電空ﾚｷﾞｭﾚｰﾀｰ異常
OUT B0556                ; UNIT2異常BZ停止予備
;異常ﾌﾞｻﾞｰまとめ
LDB B040                 ; 塗布ﾉｽﾞﾙ上下RB異常
OR B0540                 ; 塗布ﾉｽﾞﾙ上下RB異常BZ停止
LDB B041                 ; ｺｰﾃｨﾝｸﾞ材温調過昇異常
OR B0541                 ; ｺｰﾄ材温調異常BZ停止
ANL
LDB B042                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常
OR B0542                 ; ﾄﾙｴﾝｶｯﾌﾟ前後LS異常BZ停止
ANL
LDB B043                 ; ｺｰﾄ材液面検出ｾﾝｻ異常
OR B0543                 ; ｺｰﾄ材液面検出ｾﾝｻ異常BZ停止
ANL
LDB B044                 ; 塗布幅判定異常
OR B0544                 ; 塗布幅判定異常BZ停止
ANL
LDB B045                 ; 捨打ちｶｯﾌﾟ無し異常
OR B0545                 ; 捨打ちｶｯﾌﾟ無し異常BZ停止
ANL
LDB B046                 ; 溶剤ﾀﾝｸ液面低下異常
OR B0546                 ; 溶剤ﾀﾝｸ液面低下異常BZ停止
ANL
LDB B047                 ; ｺｰﾃｨﾝｸﾞ部原点復帰ﾀｲﾑｱｳﾄ異常
OR B0547                 ; ｺｰﾃｨﾝｸﾞ原点復帰ﾀｲﾑｱｳﾄ異常BZ停止
ANL
LDB B048                 ; ｺｰﾄ材液面低下補充要求
OR B0548                 ; ｺｰﾄ材液面低下補充要求BZ停止
ANL
LDB B049                 ; ｺｰﾄ材液面低下異常
OR B0549                 ; ｺｰﾄ材液面低下異常BZ停止
ANL
LDB B04A                 ; ｺｰﾄ材温度異常
OR B054A                 ; ｺｰﾄ材温度異常BZ停止
ANL
LDB B04B                 ; ﾉｽﾞﾙ上下RB動作ﾀｲﾑｱｳﾄ異常
OR B054B                 ; ﾉｽﾞﾙ上下RBﾀｲﾑｱｳﾄ異常BZ停止
ANL
LDB B04C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異常
OR B054C                 ; ｺｰﾄ材温度未達ﾓｰﾄﾞ切替不可異BZ停
ANL
LDB B04D                 ; 材料不足ﾓｰﾄﾞ切替不可異常
OR B054D                 ; 材料不足ﾓｰﾄﾞ切替不可異常BZ停止
ANL
LDB B04E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常
OR B054E                 ; ﾉｽﾞﾙ上下RB原復ﾀｲﾑｱｳﾄ異常BZ停止
ANL
LDB B04F                 ; ﾉｽﾞﾙOUT側圧力上限異常
OR B054F                 ; UNIT2異常BZ停止予備
ANL
LDB B050                 ; ﾉｽﾞﾙOUT側圧力下限異常
OR B0550                 ; UNIT2異常BZ停止予備
ANL
LDB B051                 ; ﾉｽﾞﾙIN側圧力上限異常
OR B0551                 ; UNIT2異常BZ停止予備
ANL
LDB B052                 ; ﾉｽﾞﾙIN側圧力下限異常
OR B0552                 ; UNIT2異常BZ停止予備
ANL
LDB B053                 ; ﾀﾝｸ温度上限異常
OR B0553                 ; UNIT2異常BZ停止予備
ANL
LDB B054                 ; ﾀﾝｸ温度下限異常
OR B0554                 ; UNIT2異常BZ停止予備
ANL
LDB B056                 ; 電空ﾚｷﾞｭﾚｰﾀｰ異常
OR B0556                 ; UNIT2異常BZ停止予備
ANL
INV
OUT M3193                ; ﾕﾆｯﾄ2異常BZ補助
;<h1/>　　□電空ﾚｷﾞｭﾚｰﾀｰ
LDB X03                  ; 圧空元圧正常
@MOV K0 D11028           ; 設定圧力値
;'//// 電空ﾚｷﾞｭﾚｰﾀｰ圧力設定 ////
;D11028  = D2060.U * 10 * 4095 / 500 '設定値*4095（12BIT）*0.5MP
;D2062.U = D10028 *500 / 10 / 4095   '現在圧力表示
LD X03                   ; 圧空元圧正常
NCJ K1000
;'//// 電空ﾚｷﾞｭﾚｰﾀｰ圧力設定 ////
;
;D11028  = D2060.U * 10 * 4095 / 500 '設定値*4095（12BIT）*0.5MP
;
LD CR2002                ; 常時ON
LDA D2060                ; 電空ﾚｷﾞｭﾚｰﾀｰ設定値
CON
EXT
CON
MUL.L +10
CON
MUL.L +4095
CON
DIV.L +500
CON
STA D11028               ; 設定圧力値
;D2062.U = D10028 *500 / 10 / 4095   '現在圧力表示
LD CR2002                ; 常時ON
LDA D10028               ; 現在圧力値
CON
EXT
CON
MUL.L +500
CON
DIV.L +10
CON
EXT.L
CON
DIV.L +4095
CON
STA D2062
LABEL K1000
;
END
ENDH
