DEVICE:51
;MODULE:Resin
;MODULE_TYPE:0
;動作可能条件
;Operation status
LD T1204                 ; ｺｰﾃｨﾝｸﾞ材温度適温
OR M26                   ; ﾋｰﾀｰ無し
LD T1207                 ; ｺｰﾃｨﾝｸﾞ材液面検出OUT3 下限ﾚﾍﾞﾙ
AND T1208                ; ｺｰﾃｨﾝｸﾞ材液面検出OUT4 警報ﾚﾍﾞﾙ
AND T1202                ; 捨て打ちｶｯﾌﾟ検出
OR M25                   ; 吐出無し
ANL
AND T1201                ; 浸漬ｶｯﾌﾟ液面検出
OUT R3011                ; ｺｰﾃｨﾝｸﾞ部準備OK
;ﾋｰﾀｺﾝﾄﾛｰﾙ
;Heater control
LD T1000                 ; 装置電源入
ANB Y57                  ; ﾒｲﾝ ﾊﾟﾜｰ OFF
MPS
ANB B01                  ; 圧空低下異常
TMR K471 K10             ; 塗布剤ﾋｰﾀONﾃﾞｨﾚｰ
MPP
LD T471                  ; 塗布剤ﾋｰﾀONﾃﾞｨﾚｰ
AND T1003                ; 圧空元圧力正常
AND T1208                ; ｺｰﾃｨﾝｸﾞ材液面検出OUT4 警報ﾚﾍﾞﾙ
ANB M26                  ; ﾋｰﾀｰ無し
ANL
OUT R3010                ; 塗布剤ﾋｰﾀON
;*** Err333:Out of suitable Temp. of Resin
LDP M10                  ; 自動/手動PB
ANB M26                  ; ﾋｰﾀｰ無し
ANB X124                 ; ｺｰﾃｨﾝｸﾞ材温度適温
DW K333 D148             ; 警報No.
CON
SET M1933                ; Err333:ｺｰﾃｨﾝｸﾞ剤適温外
;圧力監視
;Resin pressure check
LD CR2002                ; 常時ON
MPS
LD>= D12201 E1001        ; 吐出圧   【0.000MPa】  吐出圧下限【0.000MPa】
AND<= D12201 E1000       ; 吐出圧   【0.000MPa】  吐出圧上限【0.000MPa】
ANL
OUT R3000                ; P1　正常
MPP
LDB M25                  ; 吐出無し
AND M82                  ; 自動運転開始
AND T1003                ; 圧空元圧力正常
ANL
MPS
AND< D12201 E1001        ; 吐出圧   【0.000MPa】  吐出圧下限【0.000MPa】
TMH K194 K20             ; ﾉｽﾞﾙIN側圧力下限異常
MPP
AND> D12201 E1000        ; 吐出圧   【0.000MPa】  吐出圧上限【0.000MPa】
TMH K193 K20             ; ﾉｽﾞﾙIN側圧力上限異常
LD CR2002                ; 常時ON
MPS
LD>= D12206 E1003        ; 背圧     【0.000MPa】  背圧　下限【0.000MPa】
AND<= D12206 E1002       ; 背圧     【0.000MPa】  背圧　上限【0.000MPa】
ANL
OUT R3002                ; P2　正常
MPP
LDB M25                  ; 吐出無し
AND M82                  ; 自動運転開始
AND T1003                ; 圧空元圧力正常
ANL
MPS
AND< D12206 E1003        ; 背圧     【0.000MPa】  背圧　下限【0.000MPa】
TMH K196 K20             ; ﾉｽﾞﾙOUT側圧力下限異常
MPP
AND> D12206 E1002        ; 背圧     【0.000MPa】  背圧　上限【0.000MPa】
TMH K195 K20             ; ﾉｽﾞﾙOUT側圧力上限異常
;循環ﾎﾟﾝﾌﾟ制御用
LDB M65                  ; 即停止異常(緊急停止)
ANB M66                  ; 主幹異常(ｻｲｸﾙ停止)
TMR K188 K600            ; 循環圧監視ﾃﾞｨﾚｲ
LD< D12201 K50           ; 吐出圧   【0.000MPa】
OR> D12201 K500          ; 吐出圧   【0.000MPa】
LD< D12206 K20           ; 背圧     【0.000MPa】
OR> D12206 K500          ; 背圧     【0.000MPa】
AND CR2003               ; 常時OFF
ORL
LD M64                   ; 運転準備
OR T188                  ; 循環圧監視ﾃﾞｨﾚｲ
OR M28                   ; 仮 NAT
ANL
AND Y21F                 ; 樹脂循環ﾎﾟﾝﾌﾟ駆動ｴｱON
TMR K189 K600            ; ﾎﾟﾝﾌﾟ駆動ｴｱOFF
;残量監視
;Resin Tank check
LD M64                   ; 運転準備
MPS
LD= D148 K334            ; 警報No.
OR M232                  ; ｺｰﾃｨﾝｸﾞ剤残量警告記憶
ANB T65                  ; ｺｰﾃｨﾝｸﾞ剤残量警報間隔
ANL
OUT M232                 ; ｺｰﾃｨﾝｸﾞ剤残量警告記憶
TMR K65 K6000            ; ｺｰﾃｨﾝｸﾞ剤残量警報間隔
MRD
LDB M25                  ; 吐出無し
ANB T1207                ; ｺｰﾃｨﾝｸﾞ材液面検出OUT3 下限ﾚﾍﾞﾙ
ANB M232                 ; ｺｰﾃｨﾝｸﾞ剤残量警告記憶
ANL
TMR K64 K10              ; ｺｰﾃｨﾝｸﾞ剤残量警報
MPP
LD T64                   ; ｺｰﾃｨﾝｸﾞ剤残量警報
ANB M71                  ; All Error PAUSE
ANL
DW K334 D148             ; 警報No.
CON
SET M1934                ; Err334:ｺｰﾃｨﾝｸﾞ剤残量警告
LD M64                   ; 運転準備
MPS
LDB M25                  ; 吐出無し
ANB T1208                ; ｺｰﾃｨﾝｸﾞ材液面検出OUT4 警報ﾚﾍﾞﾙ
ANL
TMR K66 K10              ; ｺｰﾃｨﾝｸﾞ剤残量異常
MPP
LD T66                   ; ｺｰﾃｨﾝｸﾞ剤残量異常
ANB M71                  ; All Error PAUSE
ANL
DW K335 D148             ; 警報No.
CON
SET M1935                ; Err335:ｺｰﾃｨﾝｸﾞ剤残量異常
CON
OUT M233                 ; ｺｰﾃｨﾝｸﾞ剤残量無
;ﾄﾙｴﾝ残量監視
;Toluen check
LD M64                   ; 運転準備
MPS
LD= D148 K338            ; 警報No.
OR M234                  ; ﾄﾙｴﾝ残少検出記憶
ANB T68                  ; ﾄﾙｴﾝ残少警報間隔
ANL
OUT M234                 ; ﾄﾙｴﾝ残少検出記憶
TMR K68 K1200            ; ﾄﾙｴﾝ残少警報間隔
MRD
LDB T1201                ; 浸漬ｶｯﾌﾟ液面検出
ANB M234                 ; ﾄﾙｴﾝ残少検出記憶
ANL
TMR K67 K10              ; ﾄﾙｴﾝ残少警報
MPP
LD T67                   ; ﾄﾙｴﾝ残少警報
ANB M71                  ; All Error PAUSE
ANL
DW K338 D148             ; 警報No.
CON
SET M1938                ; Err338:ﾄﾙｴﾝ残量警報
END
ENDH
