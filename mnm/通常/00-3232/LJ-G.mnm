DEVICE:53
;MODULE:LJ-G
;MODULE_TYPE:0
;**********************************************************************************************************************************
;◆KV-L20V初期設定
;　ポート１：9600.8.1.E
;　データの格納単位	D33515=0000H：バイト単位
;　モード設定		D33516=0002H：バッファクリアモード
;　受信ヘッダ設定	D33520=0000H：ヘッダなし
;　受信デリミタ設定	D33521=0D00H：CR
;　受信データ長設定	D33522=512　：５１２バイト
;
;　★ＫＶ−Ｌ２０Ｖユーザーズマニュアル１２章「無手順モードのプログラミング」参照
;
;**********************************************************************************************************************************
LD CR2008                ; 運転開始時1ｽｷｬﾝON
ORP M10900               ; SETTING TRG
MOV H0000 D33515         ; Port1 ﾃﾞｰﾀ格納単位設定
MOV H0002 D33516         ; Port1 ﾓｰﾄﾞ設定
MOV H0000 D33520         ; Port1 受信ﾍｯﾀﾞ設定
MOV H0D00 D33521         ; Port1 受信ﾃﾞﾘﾐﾀ設定
MOV K512 D33522          ; Port1 受信ﾃﾞｰﾀ長設定
SET Y810                 ; Port1 通信許可
MOV.F +2.5 D4440         ; 規格値_MIN_FLOAT
MOV.F +4 D4442           ; 規格値_MAX_FLOAT
;*********************************************************************************************************************************
;◆メモリカードフォルダ作成
;　
;*********************************************************************************************************************************
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES M10910               ; フォルダ作成完了通知
LD CR2008                ; 運転開始時1ｽｷｬﾝON
ORP M10901               ; AUTO START
OR M10920                ; フォルダ作成処理BUSY
ANB M10921               ; フォルダ作成処理END PLS
OUT M10920               ; フォルダ作成処理BUSY
RES M10912               ; フォルダ作成処理完了
LD M10920                ; フォルダ作成処理BUSY
ANB M10910               ; フォルダ作成完了通知
ANB CR3214               ; ﾒﾓﾘｶｰﾄﾞ命令実行中
MPS
LDA CM700                ; ｶﾚﾝﾀﾞﾀｲﾏ（年）
CON
TBCD
CON
ASC
CON
STA D10020               ; フォルダ名（年）
MRD
LDA CM701                ; ｶﾚﾝﾀﾞﾀｲﾏ（月）
CON
TBCD
CON
ASC
CON
STA D10021               ; フォルダ名（月）
MRD
LDA H0000
CON
STA D10022               ; フォルダ名終端コード
MPP
MMKDIR D10020 M10910     ; フォルダ名（年）  フォルダ作成完了通知
LD M10920                ; フォルダ作成処理BUSY
MPS
LD M10910                ; フォルダ作成完了通知
ANB M10911               ; フォルダ作成エラー通知
ANL
SET M10912               ; フォルダ作成処理完了
CON
RES M10910               ; フォルダ作成完了通知
MPP
LDB M10910               ; フォルダ作成完了通知
AND M10912               ; フォルダ作成処理完了
ANL
DIFU M10921              ; フォルダ作成処理END PLS
;*********************************************************************************************************************************
;◆メモリカード書込み設定
;　データ保存形式	D10000=8：ASCII文字列（CSVファイル）
;　書込みモード		D10001=0：追加モード
;　オプション設定	D10002=1：改行挿入、データ桁数可変
;
;*********************************************************************************************************************************　
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES M10913               ; データ書込み完了通知
LD CR2008                ; 運転開始時1ｽｷｬﾝON
ORP M10900               ; SETTING TRG
DW K8 D10000             ; データ保存形式
DW K0 D10001             ; 書込みモード
DW K1 D10002             ; オプション設定
;*********************************************************************************************************************************
;◆ＬＪ−Ｇスキャンプログラム
;　外部トリガによる断面１ポイント測定
;
;　※ＬＪ−Ｇへのトリガを２回かけないとデータが取得（出力）されないので、１回目のトリガＯＮ後
;　　１００ms（0.1sec）後に２回目のトリガをＯＮさせています。
;
;*********************************************************************************************************************************
LDB M39                  ; 日常点検ﾓｰﾄﾞ
ANB X10A                 ; ﾚｰｻﾞ変位ｾﾝｻ_BUSY
AND X10B                 ; ﾚｰｻﾞ変位ｾﾝｻ_READY_A
LD M39                   ; 日常点検ﾓｰﾄﾞ
AND X10A                 ; ﾚｰｻﾞ変位ｾﾝｻ_BUSY
ANB X10B                 ; ﾚｰｻﾞ変位ｾﾝｻ_READY_A
ORL
ANB M10961               ; データ受信処理BUSY
ANB M10970               ; データ書込み処理BUSY
ANB X802                 ; Port1 受信ﾃﾞｰﾀ読出要求
ANB CR3214               ; ﾒﾓﾘｶｰﾄﾞ命令実行中
OUT M10929               ; LJ-G 測定可能
LDB M4660                ; 塗布幅測定
ANB M4968                ; LJ-G測定要求(PCB)
ANB M4969                ; LJ-G測定要求(COVER)
INV
OUT M10930               ; SCAN TRG
@ZRES M10927 M10928      ; PCB側測定  Cover側測定
MPS
AND M4968                ; LJ-G測定要求(PCB)
@SET M10927              ; PCB側測定
MPP
AND M4969                ; LJ-G測定要求(COVER)
@SET M10928              ; Cover側測定
LD M10930                ; SCAN TRG
AND M10929               ; LJ-G 測定可能
OR M10931                ; LJ-G BUSY
AND X108                 ; ﾚｰｻﾞ変位ｾﾝｻ_ERROR
ANB M10940               ; LJ-G END PLS
OUT M10931               ; LJ-G BUSY
LDP M10931               ; LJ-G BUSY
OR M10932                ; LJ-G TRG ON1
ANPB M10934              ; LJ-G OUT WAIT
MPS
OUT M10932               ; LJ-G TRG ON1
MPP
LDB X10A                 ; ﾚｰｻﾞ変位ｾﾝｻ_BUSY
AND X10B                 ; ﾚｰｻﾞ変位ｾﾝｻ_READY_A
ANL
ONDL K10 M10933          ; LJ-G TRG ON2
LDP M10933               ; LJ-G TRG ON2
OR M10934                ; LJ-G OUT WAIT
AND X108                 ; ﾚｰｻﾞ変位ｾﾝｻ_ERROR
ANB M10937               ; LJ-G OUT WAIT END PLS
OUT M10934               ; LJ-G OUT WAIT
;☆☆☆　もしかして、こうか？
;☆☆☆　
LD M10934                ; LJ-G OUT WAIT
ANB X10A                 ; ﾚｰｻﾞ変位ｾﾝｻ_BUSY
AND X10B                 ; ﾚｰｻﾞ変位ｾﾝｻ_READY_A
MPS
AND X109                 ; ﾚｰｻﾞ変位ｾﾝｻ_GO
OUT M10935               ; LJ-G OK
MPP
ANB X109                 ; ﾚｰｻﾞ変位ｾﾝｻ_GO
ONDL K100 M10936         ; LJ-G NG
LD M10935                ; LJ-G OK
OR M10936                ; LJ-G NG
DIFU M10937              ; LJ-G OUT WAIT END PLS
LD M10931                ; LJ-G BUSY
AND M10937               ; LJ-G OUT WAIT END PLS
OR M10938                ; LJ-G RESET BUSY
ANB M10940               ; LJ-G END PLS
OUT M10938               ; LJ-G RESET BUSY
ONDL K100 M10939         ; LJ-G RESET ON
LD M10939                ; LJ-G RESET ON
DIFU M10940              ; LJ-G END PLS
LDP M10932               ; LJ-G TRG ON1
ORP M10933               ; LJ-G TRG ON2
SHOT K1 Y221             ; ﾚｰｻﾞ変位ｾﾝｻ_TRG_A
LD M4687                 ; LJ-G TIMING信号ON
OR M4689                 ; LJ-G TIMING信号ON
OUT Y222                 ; ﾚｰｻﾞ変位ｾﾝｻ_TIMING1
LD CR2008                ; 運転開始時1ｽｷｬﾝON
ORP M10939               ; LJ-G RESET ON
SHOT K1 Y224             ; ﾚｰｻﾞ変位ｾﾝｻ_RESET
LDP M4686                ; LJ-G RESET信号ON
ORP M4688                ; LJ-G RESET信号ON
SHOT K3 Y224             ; ﾚｰｻﾞ変位ｾﾝｻ_RESET
LD CR2002                ; 常時ON
MPS
LD M32                   ; 自動ﾓｰﾄﾞ
OR= E55 E54              ; Type No.  Test Type No.
ANL
MPS
ANB M39                  ; 日常点検ﾓｰﾄﾞ
MPS
AND>=.L D31290 +34000    ; 軸1 現在座標
MOV E55 D10050           ; Type No.  測定機種No.
MPP
AND<.L D31290 +34000     ; 軸1 現在座標
LDA E55                  ; Type No.
CON
SUB K4
CON
STA D10050               ; 測定機種No.
MPP
AND M39                  ; 日常点検ﾓｰﾄﾞ
MPS
AND>=.L D31290 +34000    ; 軸1 現在座標
LDA E55                  ; Type No.
CON
ADD K8
CON
STA E53                  ; Daily check Type No.
CON
MOV E53 D10050           ; Daily check Type No.  測定機種No.
MPP
AND<.L D31290 +34000     ; 軸1 現在座標
LDA E55                  ; Type No.
CON
ADD K4
CON
STA E53                  ; Daily check Type No.
CON
MOV E53 D10050           ; Daily check Type No.  測定機種No.
MRD
LDP M33                  ; 手動ﾓｰﾄﾞ
ORP M103                 ; 原点復帰OK
OR CR2008                ; 運転開始時1ｽｷｬﾝON
ANL
MOV E55 E54              ; Type No.  Test Type No.
MRD
AND M33                  ; 手動ﾓｰﾄﾞ
MPS
AND>=.L D31290 +34000    ; 軸1 現在座標
MOV E54 D10050           ; Test Type No.  測定機種No.
MPP
AND<.L D31290 +34000     ; 軸1 現在座標
LDA E54                  ; Test Type No.
CON
SUB K4
CON
STA D10050               ; 測定機種No.
MPP
PMOV D10050 K0 Y225 K0 K4; 測定機種No.  ﾚｰｻﾞ変位ｾﾝｻ_P1
;**********************************************************************************************************************************
;◆ＬＪ−Ｇ測定データ取得
;　ＫＶ−Ｌ２０Ｖからの受信データ読出要求
;　データ取得の日時（タイムスタンプ）を生成
;　測定データ生成（データ末尾にNULLを付加）
;　書込みデータ生成（タイムスタンプ＋測定データ）
;
;**********************************************************************************************************************************
LD M10937                ; LJ-G OUT WAIT END PLS
OR M10959                ; LJ-G ﾃﾞｰﾀ送信待ち
ANB M10961               ; データ受信処理BUSY
ANB B093                 ; LJ-Gﾃﾞｰﾀ未送信
OUT M10959               ; LJ-G ﾃﾞｰﾀ送信待ち
LD X800                  ; Port1 通信可能
AND X802                 ; Port1 受信ﾃﾞｰﾀ読出要求
AND M10959               ; LJ-G ﾃﾞｰﾀ送信待ち
DIFU M10960              ; データ受信処理TRG
LD M10960                ; データ受信処理TRG
OR M10961                ; データ受信処理BUSY
ANB M10964               ; データ受信処理END PLS
OUT M10961               ; データ受信処理BUSY
;　タイムスタンプ生成
;　yymmddhhmmss,NULL
;　
;　※NULLは文字列の終端を示すコードとして付加する必要があります。
;　
LD M10961                ; データ受信処理BUSY
MPS
LDA CM700                ; ｶﾚﾝﾀﾞﾀｲﾏ（年）
CON
TBCD
CON
ASC
CON
STA D10010               ; 現在値（年）
MRD
LDA CM701                ; ｶﾚﾝﾀﾞﾀｲﾏ（月）
CON
TBCD
CON
ASC
CON
STA D10011               ; 現在値（月）
MRD
LDA CM702                ; ｶﾚﾝﾀﾞﾀｲﾏ（日）
CON
TBCD
CON
ASC
CON
STA D10012               ; 現在値（日）
MRD
LDA CM703                ; ｶﾚﾝﾀﾞﾀｲﾏ（時）
CON
TBCD
CON
ASC
CON
STA D10013               ; 現在値（時）
MRD
LDA CM704                ; ｶﾚﾝﾀﾞﾀｲﾏ（分）
CON
TBCD
CON
ASC
CON
STA D10014               ; 現在値（分）
MRD
LDA CM705                ; ｶﾚﾝﾀﾞﾀｲﾏ（秒）
CON
TBCD
CON
ASC
CON
STA D10015               ; 現在値（秒）
MRD
AND CR2002               ; 常時ON
MPS
AND M10927               ; PCB側測定
@MOV H2C31 D10016        ; カンマ・NULL
MRD
AND M10928               ; Cover側測定
@MOV H2C32 D10016        ; カンマ・NULL
MPP
LDB M10927               ; PCB側測定
ANB M10928               ; Cover側測定
ANL
@MOV H2C30 D10016        ; カンマ・NULL
MRD
AND CR2002               ; 常時ON
MPS
AND= E55 K4              ; Type No.
@MOV H2C34 D10017
MRD
AND= E55 K5              ; Type No.
@MOV H2C35 D10017
MRD
AND= E55 K6              ; Type No.
@MOV H2C36 D10017
MPP
AND= E55 K7              ; Type No.
@MOV H2C37 D10017
MRD
@MOV H2C00 D10018
MPP
ONDL K1 M10962           ; タイムスタンプ生成END
;　測定データの最終文字にＮＵＬＬを付加
;　
;　※測定データ文字列への終端コードを付加する為、（データ長＋１）÷２のアドレスにＮＵＬＬを転送。
;　
;　　⇒データ取得毎に受信データをクリアすれば、測定データの末尾はＮＵＬＬになっているので、
;　　　わざわざＮＵＬＬを付加する必要はありません。
;　　　備忘録的に残しておきます。
;　
LD M10962                ; タイムスタンプ生成END
ADRSET D33259 D10040     ; Port1 受信ﾃﾞｰﾀ1  測定データ最終文字アドレス格納
MPS
LDA D33258               ; Port1 受信ﾃﾞｰﾀ長
CON
ADD K1
CON
STA D10042               ; 受信データ長+1
MRD
LDA D10042               ; 受信データ長+1
CON
DIV K2
CON
STA D10042               ; 受信データ長+1
MRD
ADRADD.S D10042 D10040   ; 受信データ長+1  測定データ最終文字アドレス格納
MRD
@MOV H0000 *D10040       ; 測定データ最終文字アドレス格納
MPP
ONDL K1 M10963           ; 測定データNULL付加END
;　書込みデータ生成
;　タイムスタンプ＋測定データの文字列を結合し、書込みデータアドレスに保存。
;　データ保存後、受信データ読出完了をセット。（データ受信処理完了）
;
LD M10963                ; 測定データNULL付加END
SADD D10010 D33259 D10100; 現在値（年）  Port1 受信ﾃﾞｰﾀ1  書込みデータ
SET Y812                 ; Port1 受信ﾃﾞｰﾀ読出完了
LD M10961                ; データ受信処理BUSY
ANB X802                 ; Port1 受信ﾃﾞｰﾀ読出要求
DIFU M10964              ; データ受信処理END PLS
RES Y812                 ; Port1 受信ﾃﾞｰﾀ読出完了
;**********************************************************************************************************************************
;◆メモリカード書込み
;　yymmddでファイル名を生成
;　フォルダパスを追加しファイルパスを生成　yymm\yymmdd
;　メモリカードにデータを書込み
;
;**********************************************************************************************************************************
LD M10964                ; データ受信処理END PLS
OR M10970                ; データ書込み処理BUSY
ANB M10971               ; データ書込み処理END PLS
OUT M10970               ; データ書込み処理BUSY
RES M10915               ; データ書込み処理完了
LD M10970                ; データ書込み処理BUSY
ANB M10913               ; データ書込み完了通知
ANB M10916               ; データ書込みｴﾗｰ処理
ANB CR3214               ; ﾒﾓﾘｶｰﾄﾞ命令実行中
MPS
LDA CM700                ; ｶﾚﾝﾀﾞﾀｲﾏ（年）
CON
TBCD
CON
ASC
CON
STA D10023               ; ファイル名（年）
MRD
LDA CM701                ; ｶﾚﾝﾀﾞﾀｲﾏ（月）
CON
TBCD
CON
ASC
CON
STA D10024               ; ファイル名（月）
MRD
LDA CM702                ; ｶﾚﾝﾀﾞﾀｲﾏ（日）
CON
TBCD
CON
ASC
CON
STA D10025               ; ファイル名（日）
MRD
LDA H0000
CON
STA D10026               ; ファイル名終端コード
MRD
SADD D10020 "\" D10029   ; フォルダ名（年）  フォルダパス
MRD
SADD D10029 D10023 D10030; フォルダパス  ファイル名（年）  ファイルパス
MPP
MWRIT D10030 D10100 K50 D10000 M10913;ファイルパス  書込みデータ  データ保存形式  データ書込み完了通知
LD M10970                ; データ書込み処理BUSY
MPS
AND M10913               ; データ書込み完了通知
MPS
ANB M10914               ; データ書込みエラー通知
SET M10915               ; データ書込み処理完了
CON
RES M10913               ; データ書込み完了通知
MPP
AND M10914               ; データ書込みエラー通知
RES M10913               ; データ書込み完了通知
CON
@SET M10916              ; データ書込みｴﾗｰ処理
MPP
LDB M10913               ; データ書込み完了通知
AND M10915               ; データ書込み処理完了
ANL
DIFU M10971              ; データ書込み処理END PLS
LD M10916                ; データ書込みｴﾗｰ処理
@SET M10980
STG M10980
ANB M71                  ; All Error PAUSE
RES M10914               ; データ書込みエラー通知
CON
DW K390 D148             ; 警報No.
CON
SET M4930                ; Err390:塗布幅ﾃﾞｰﾀ書込み異常
CON
JMP M10981
STG M10981
AND M71                  ; All Error PAUSE
JMP M10982
STG M10982
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
ANB CR3214               ; ﾒﾓﾘｶｰﾄﾞ命令実行中
RES M10913               ; データ書込み完了通知
CON
RES M10916               ; データ書込みｴﾗｰ処理
CON
ENDS
;**********************************************************************************************************************************
;◆受信データクリア
;　メモリカードにデータ書込み後、受信した測定データをクリアする
;
;**********************************************************************************************************************************
LD M10971                ; データ書込み処理END PLS
FMOV K0 D33258 K256      ; Port1 受信ﾃﾞｰﾀ長
END
ENDH
