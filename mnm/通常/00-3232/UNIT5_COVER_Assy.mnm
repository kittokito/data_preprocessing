DEVICE:53
;MODULE:UNIT5_COVER_Assy
;MODULE_TYPE:0
;SCRIPT_TYPE:
;
;UNIT5 COVER-Assy
;動作FLG
LD T1012                 ; ﾗｲﾄｶｰﾃﾝ3 OUT1
AND T1013                ; ﾗｲﾄｶｰﾃﾝ3 OUT2
OUT M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
LD T1012                 ; ﾗｲﾄｶｰﾃﾝ3 OUT1
AND T1013                ; ﾗｲﾄｶｰﾃﾝ3 OUT2
OUT M5951                ; ASSY部   遮光無し
LD X0C                   ; ﾗｲﾄｶｰﾃﾝ3 OUT1
AND X0D                  ; ﾗｲﾄｶｰﾃﾝ3 OUT2
TMH K554 K10             ; 遮光無し （即決）
LDB M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
OR M5850                 ; 取り出し作業確認
LD M5007                 ; ｴﾗｰ解除確認
OR M5154                 ; ｴﾗｰ解除確認
ANL
OUT M5850                ; 取り出し作業確認
LD CR2002                ; 常時ON
MPS
LD<= D5000 K10           ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
OR> D5000 K101           ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
ANL
DW K0 D5020              ; Coverﾊﾞｯﾌｧ数
MPP
LD> D5000 K10            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
AND<= D5000 K101         ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
ANL
DW K1 D5020              ; Coverﾊﾞｯﾌｧ数
LD<>.D D5210 K0          ; Case2 照合用ﾃﾞｰﾀ
OUT M5851                ; 読取りPCBﾃﾞｰﾀ有
LD Y209E                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANB Y209F                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 破壊ON
MPS
LD T1901                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
AND T1902                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
TMH K501 K2              ; 嵌合ﾍｯﾄﾞ吸着OK
MRD
LDB T1901                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
AND T1902                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
TMR K502 K30             ; 嵌合ﾍｯﾄﾞ吸着異常
MPP
LDB T1901                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
ANB T1902                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
TMH K503 K30             ; 嵌合ﾍｯﾄﾞ除材OK
LD CR2002                ; 常時ON
MPS
LDB T1012                ; ﾗｲﾄｶｰﾃﾝ3 OUT1
ORB T1013                ; ﾗｲﾄｶｰﾃﾝ3 OUT2
AND M5010                ; Cover投入依頼
ANL
OUT M5662                ; Cover嵌合ﾍｯﾄﾞ吸着ON
MPP
LD M5950                 ; ﾗｲﾄｶｰﾃﾝ  遮光無し
AND T2108                ; Source air pressure
AND T503                 ; 嵌合ﾍｯﾄﾞ除材OK
ANL
TMR K204 K30             ; Cover無し確認ﾃﾞｨﾚｲ
CON
AND T204                 ; Cover無し確認ﾃﾞｨﾚｲ
LDB M97                  ; 原点復帰動作中
ANB M81                  ; 自動運転開始BZ
LD M81                   ; 自動運転開始BZ
AND M5010                ; Cover投入依頼
ORL
ANL
OUT M5672                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
LD M5010                 ; Cover投入依頼
AND Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANP T501                 ; 嵌合ﾍｯﾄﾞ吸着OK
OR M5861                 ; 吸着OK
MPS
ANB T522                 ; 吸着OKﾌﾞｻﾞｰ時間2
OUT M5860                ; 吸着OKﾌﾞｻﾞｰ
MRD
ANB T521                 ; 吸着OKﾌﾞｻﾞｰ時間
OUT M5861                ; 吸着OK
MRD
TMH K521 K15             ; 吸着OKﾌﾞｻﾞｰ時間
MRD
ANB T523
TMH K522 K5              ; 吸着OKﾌﾞｻﾞｰ時間2
MPP
AND T522                 ; 吸着OKﾌﾞｻﾞｰ時間2
TMH K523 K5
LD M5510                 ; Z軸　原点OK
OR M5990                 ; 嵌合Z軸　原点復帰完了
ANB M103                 ; 原点復帰OK
OUT M5990                ; 嵌合Z軸　原点復帰完了
LD CR2002                ; 常時ON
MPS
DW K0 D5030              ; UT5 Cover status
MPP
LD Y209E                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
AND T501                 ; 嵌合ﾍｯﾄﾞ吸着OK
ANL
DW K2 D5030              ; UT5 Cover status
;■ロボット等機器入力受け
;ロボット、検査装置など状態、現在位置などをまとめる信号は一旦ここで受ける。
LD R4602                 ; RCA4 MOVE
BOR D32531 K2            ; RCA4 制御信号
ANB M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
LDB T1012                ; ﾗｲﾄｶｰﾃﾝ3 OUT1
ORB T1013                ; ﾗｲﾄｶｰﾃﾝ3 OUT2
ANL
BOUT D32531 K2           ; RCA4 制御信号
BLD D32531 K2            ; RCA4 制御信号
ANB M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
TMR K540 K30             ; RCA4一時停止超過
;<h1/>装置状態・動作条件
LD R3504                 ; COVER吸着領域
OR<=.L D126 +10000       ; RCA4_Position Data
AND T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
OUT M5900                ; ﾕﾆｯﾄ5原点
LDB B0A0                 ; ﾕﾆｯﾄ5_Jig番号異常
ANB B0A1                 ; RAC4ｱﾗｰﾑ_異常2
ANB B0A2                 ; ﾕﾆｯﾄ5_異常3
ANB B0A3                 ; ﾕﾆｯﾄ5_異常4
ANB B0A4                 ; ﾕﾆｯﾄ5_異常5
ANB B0A5                 ; ﾕﾆｯﾄ5_異常6
INV
OUT M5901                ; ﾕﾆｯﾄ5全異常
LD M5900                 ; ﾕﾆｯﾄ5原点
ANB M5000                ; ﾕﾆｯﾄ5ｽﾃｯﾌﾟ開始
ANB R4602                ; RCA4 MOVE
AND R4604                ; RCA4 SV
AND R4601                ; RCA4 HEND
ANB R4615                ; RCA4 EMGS
ANB R4603                ; RCA4 ALM
OUT M5902                ; ﾕﾆｯﾄ5起動条件
LDB M64                  ; 運転準備
OR M65                   ; 即停止異常(緊急停止)
OR M5901                 ; ﾕﾆｯﾄ5全異常
OR T15                   ; 強制停止
OUT M5903                ; ﾕﾆｯﾄ5起動OFF
LD M98                   ; 原点復帰開始
ANB M5904                ; ﾕﾆｯﾄ5原点復帰中
OR M5904                 ; ﾕﾆｯﾄ5原点復帰中
ANB M5903                ; ﾕﾆｯﾄ5起動OFF
ANB M5579                ; ﾕﾆｯﾄ5原点復帰完了確認
OUT M5904                ; ﾕﾆｯﾄ5原点復帰中
LD M83                   ; 各工程起動指令
AND M5902                ; ﾕﾆｯﾄ5起動条件
OR M5905                 ; ﾕﾆｯﾄ5起動中
ANB M5903                ; ﾕﾆｯﾄ5起動OFF
ANB M5399                ; ﾕﾆｯﾄ5動作完了
OUT M5905                ; ﾕﾆｯﾄ5起動中
LD CR2003                ; 常時OFF
OUT M5908                ; ﾕﾆｯﾄ5警報補助
;Pack処理
LDB M5802                ; COVER嵌合Z 指示移動中
ANB M5812                ; COVER嵌合Z 原点復帰中
INV
OUT M5910                ; UNIT5 Pack
;□エラー発生　一時停止フラグ
LD CR2002                ; 常時ON
MPS
PMOV.D M5920 K0 D5100 K0 K20;Err400:ﾜｰｸ残  UT5ｴﾗｰ判別
MPP
AND<> D5100 K0           ; UT5ｴﾗｰ判別
OUT M5912                ; UNIT5 ｴﾗｰ
LDB M5901                ; ﾕﾆｯﾄ5全異常
ANB M160                 ; ALL PAUSE
ANB M5908                ; ﾕﾆｯﾄ5警報補助
ANB M5912                ; UNIT5 ｴﾗｰ
LDB M32                  ; 自動ﾓｰﾄﾞ
ANB M33                  ; 手動ﾓｰﾄﾞ
LD M32                   ; 自動ﾓｰﾄﾞ
AND M5905                ; ﾕﾆｯﾄ5起動中
ORL
LD M33                   ; 手動ﾓｰﾄﾞ
AND M5904                ; ﾕﾆｯﾄ5原点復帰中
ORL
ANL
OUT M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
;RB interlock
LDB R4602                ; RCA4 MOVE
AND R4604                ; RCA4 SV
ANB R4603                ; RCA4 ALM
ANB R4615                ; RCA4 EMGS
ANB M5800                ; COVER嵌合Z 移動完了
ANB M5802                ; COVER嵌合Z 指示移動中
ANB M5804                ; COVER嵌合Z 移動開始
OUT M5959                ; Cover嵌合Z原点復帰可能
LD M5959                 ; Cover嵌合Z原点復帰可能
AND R4601                ; RCA4 HEND
LD R3111                 ; Case搬送Y2嵌合領域
OR CR2002                ; 常時ON
ANL
OUT M5960                ; Cover嵌合Z動作可能
LD CR2002                ; 常時ON
MPS
LD M5044                 ; Cover     NG処理確認ﾌﾗｸﾞｾｯﾄ1
OR M5047                 ; Cover     NG処理確認ﾌﾗｸﾞｾｯﾄ2
OR M5961                 ; Cover     NG処理確認
ANB M5962                ; NG解除確認
ANL
OUT M5961                ; Cover     NG処理確認
MPP
LD M5961                 ; Cover     NG処理確認
LD T1915                 ; 投入2 Cover-NGｽｲｯﾁ
OR M5950                 ; ﾗｲﾄｶｰﾃﾝ  遮光無し
ANL
ANB T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
ANL
OUT M5962                ; NG解除確認
LDF M5905                ; ﾕﾆｯﾄ5起動中
ZRES M5800 M5806         ; COVER嵌合Z 移動完了  COVER嵌合Z 移動完了待
;<h1/>■自動回路 
;
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M5000 M5399         ; ﾕﾆｯﾄ5ｽﾃｯﾌﾟ開始  ﾕﾆｯﾄ5動作完了
LDB M5905                ; ﾕﾆｯﾄ5起動中
MOV K0 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5905                 ; ﾕﾆｯﾄ5起動中
MOV D5000 Z1             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
OUT M5000:Z1             ; ﾕﾆｯﾄ5ｽﾃｯﾌﾟ開始
;自動運転ステップ開始
;運転開始時JIG番号ﾁｪｯｸ
;Jig No. check when AUTO start.
LD M5000                 ; ﾕﾆｯﾄ5ｽﾃｯﾌﾟ開始
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
ANB M39                  ; 日常点検ﾓｰﾄﾞ
MPS
AND= D94 E3424           ; COVER-Assy Jig ID  COVER-Assy Jig ID
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND<> D94 E3424          ; COVER-Assy Jig ID  COVER-Assy Jig ID
OUT M5963                ; Cover Jig No.異常
MPP
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3398                ; UT3ｻｲｸﾙ停止
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ吸着高さ移動
;Move to Cover set height
LD M5001                 ; 吸着高さ移動
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5960                ; Cover嵌合Z動作可能
ANB M5961                ; Cover     NG処理確認
AND M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
MPS
AND R3504                ; COVER吸着領域
DW K3 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MRD
ANB R3504                ; COVER吸着領域
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
@FMOV K0 D910 K8         ; BCR2読み取りﾃﾞｰﾀ
LD M5002                 ; Cover-Assy Z P31 move
MPS
DW K31 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M1                  ; COVER嵌合Z軸移動
MPP
AND M5800                ; COVER嵌合Z 移動完了
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;運転開始時ﾜｰｸ残ﾁｪｯｸ
;Remain work check when AUTO start.
LD M5003                 ; 運転開始時ﾜｰｸ残ﾁｪｯｸ
MPS
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
TMR K509 K5              ; ﾜｰｸ残検出ｳｪｲﾄ
CON
AND T509                 ; ﾜｰｸ残検出ｳｪｲﾄ
MPS
LDB T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
AND T503                 ; 嵌合ﾍｯﾄﾞ除材OK
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LD T1900                 ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
OR T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
OR T502                  ; 嵌合ﾍｯﾄﾞ吸着異常
ANL
DW K5 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5665                ; Cover嵌合ﾍｯﾄﾞ吸着ON
LD M5004
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANL
DW K10 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5678                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
;Err400:Cover remain  (when START)
LD M5005                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K400 D148             ; 警報No.
CON
SET M5920                ; Err400:ﾜｰｸ残
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5006                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5007                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
MPS
ANB M5850                ; 取り出し作業確認
DW K4 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND M5850                ; 取り出し作業確認
DW K3 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;ｶﾊﾞｰ吸着高さ移動
;Move to Cover set height
LD M5008                 ; ｶﾊﾞｰ吸着高さ移動
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
LDB M39                  ; 日常点検ﾓｰﾄﾞ
AND M5960                ; Cover嵌合Z動作可能
ANB M5961                ; Cover     NG処理確認
AND M5951                ; ASSY部   遮光無し
ANL
MPS
LD R3504                 ; COVER吸着領域
AND T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
ANL
DW K10 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MRD
LDB R3504                ; COVER吸着領域
ORB T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
@FMOV K0 D910 K8         ; BCR2読み取りﾃﾞｰﾀ
MPP
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3398                ; UT3ｻｲｸﾙ停止
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5009                 ; Cover-Assy Z P31 move
MPS
OUT M5653                ; Cover嵌合ﾍｯﾄﾞ 上昇
CON
DW K31 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M2                  ; COVER嵌合Z軸移動
MPP
LD M5800                 ; COVER嵌合Z 移動完了
AND T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
ANL
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;投入完了待ち
;waiting for Work set up end
LD M5010                 ; Cover投入依頼
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
LD M5950                 ; ﾗｲﾄｶｰﾃﾝ  遮光無し
OR T554                  ; 遮光無し （即決）
ANL
MPS
LDB M5961                ; Cover     NG処理確認
AND T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
ANL
MPS
LD T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
LD M5851                 ; 読取りPCBﾃﾞｰﾀ有
ANB B11F0                ; Case2 Change sw
LD M23                   ; ｺｰﾄﾞﾘｰﾀﾞ無視
AND> D3020 D5020         ; Y2ﾊﾞｯﾌｧ数  Coverﾊﾞｯﾌｧ数
ORL
ANL
ANL
DW K20 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
TMR K511 K10             ; ﾜｰｸ浮き検出ｳｪｲﾄ
CON
AND T511                 ; ﾜｰｸ浮き検出ｳｪｲﾄ
LD T502                  ; 嵌合ﾍｯﾄﾞ吸着異常
OR T503                  ; 嵌合ﾍｯﾄﾞ除材OK
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LDB T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
AND M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3398                ; UT3ｻｲｸﾙ停止
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;Err401:Cover Adsorption error
LD M5011                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K401 D148             ; 警報No.
CON
SET M5921                ; Err401:ｶﾊﾞｰ吸着異常
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5012                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5013                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K10 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;運転開始時JIG番号ﾁｪｯｸ
;Jig No. check when AUTO start.
LD M5020                 ; Jig番号確認
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
AND= D94 E3421           ; COVER-Assy Jig ID  PCB-Eject Jig ID
DW K30 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND<> D94 E3421          ; COVER-Assy Jig ID  PCB-Eject Jig ID
OUT M5964                ; 投入後　  Jig No.異常
;ｶﾊﾞｰﾗﾍﾞﾙ読み取り
;Cover label read
LD M5030                 ; ｶﾊﾞｰﾗﾍﾞﾙ読み取り
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
ANB M23                  ; ｺｰﾄﾞﾘｰﾀﾞ無視
MPS
AND M10323               ; BCR2     読取り完了
DW K40 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MRD
AND M10333               ; BCR2     読取りｴﾗｰ
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5980                ; BCR2　　　読み取り
MPP
AND M23                  ; ｺｰﾄﾞﾘｰﾀﾞ無視
DW K48 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;Err403:Code read error
LD M5031                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K403 D148             ; 警報No.
CON
SET M5923                ; Err403:読み取り異常
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5032                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5033                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
AND M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
MPS
AND T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
DW K30 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
ANB T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;ﾃﾞｰﾀ照合
;Data check
LD M5040                 ; ﾃﾞｰﾀ照合
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
SCMP D5210 D910          ; Case2 照合用ﾃﾞｰﾀ  BCR2読み取りﾃﾞｰﾀ
CON
MPS
LD CR2010                ; 演算結果がｾﾞﾛ
AND<>.D D5210 K0         ; Case2 照合用ﾃﾞｰﾀ
ANL
DW K48 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LD CR2009                ; 演算結果が負
OR CR2011                ; 演算結果が正
OR=.D D5210 K0           ; Case2 照合用ﾃﾞｰﾀ
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;Err404:Label verification error
LD M5041                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K404 D148             ; 警報No.
CON
SET M5924                ; Err404:ﾗﾍﾞﾙ照合異常
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5042                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
RES B1013                ; Remove_FLG
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5043                 ; 照合NGｴﾗｰ解除確認
ANB M71                  ; All Error PAUSE
MPS
LDB B11F0                ; Case2 Change sw
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
ANL
MPS
ANB B1013                ; Remove_FLG
DW K30 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND B1013                ; Remove_FLG
RES B1013                ; Remove_FLG
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND B11F0                ; Case2 Change sw
MPS
ANB B1013                ; Remove_FLG
DW K46 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND B1013                ; Remove_FLG
RES B1013                ; Remove_FLG
CON
DW K47 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5044                 ; Cover     NG処理確認ﾌﾗｸﾞｾｯﾄ1
AND M5961                ; Cover     NG処理確認
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5045                 ; ﾜｰｸ残ﾁｪｯｸ
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
ANB M5961                ; Cover     NG処理確認
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5046                 ; 照合ﾃﾞｰﾀ消去確認
ANB M5851                ; 読取りPCBﾃﾞｰﾀ有
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LDB M5851                ; 読取りPCBﾃﾞｰﾀ有
OR M5890                 ; 読取ﾃﾞｰﾀﾘｾｯﾄ確認
AND M5047                ; Cover     NG処理確認ﾌﾗｸﾞｾｯﾄ2
OUT M5890                ; 読取ﾃﾞｰﾀﾘｾｯﾄ確認
LD M5047                 ; Cover     NG処理確認ﾌﾗｸﾞｾｯﾄ2
AND M5961                ; Cover     NG処理確認
AND M5890                ; 読取ﾃﾞｰﾀﾘｾｯﾄ確認
DW K45 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;嵌合開始高さ移動
;Move to Assy-start height
LD M5048
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5951                ; ASSY部   遮光無し
AND M5960                ; Cover嵌合Z動作可能
TMR K524 K10             ; 移動開始ﾃﾞｨﾚｲ
CON
AND T524                 ; 移動開始ﾃﾞｨﾚｲ
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5049
MPS
DW K37 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M12                 ; COVER嵌合Z軸移動
MRD
LD M5800                 ; COVER嵌合Z 移動完了
AND T1908                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 下降限
ANL
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5642                ; Cover嵌合ﾍｯﾄﾞ 下降
;嵌合要求待ち
;Waiting for Assy-demand from Cover-Trasfer
LD M5050                 ; 嵌合要求待ち
MPS
LD T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
ANL
MPS
AND M3983                ; Y2　嵌合要求
DW K61 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LDB M3983                ; Y2　嵌合要求
AND M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3398                ; UT3ｻｲｸﾙ停止
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
ANB T501                 ; 嵌合ﾍｯﾄﾞ吸着OK
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;Err401:Cover Adsorption error
LD M5051                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K401 D148             ; 警報No.
CON
SET M5921                ; Err401:ｶﾊﾞｰ吸着異常
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5052                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5053                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;嵌合開始高さ移動
;Move to Assy-start height
LD M5061                 ; 嵌合開始高さ移動
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5951                ; ASSY部   遮光無し
AND M5960                ; Cover嵌合Z動作可能
AND=.L D31470 E2548      ; 軸4 現在座標  Case搬送2_TP_Pos35_Data
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5062                 ; Cover-Assy Z P35 move
MPS
DW K35 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M3                  ; COVER嵌合Z軸移動
MPP
AND M5800                ; COVER嵌合Z 移動完了
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;破壊ON
;Air release ON
LD M5063                 ; 吸着OFF
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB T1901                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
ANB T1902                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5676                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
CON
OUT M5682                ; Cover嵌合ﾍｯﾄﾞ破壊ON
;嵌合開始ｳｪｲﾄ
;Assy start delay
LD M5064                 ; 嵌合開始  Delay
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
TMR K512 E4404           ; 嵌合開始ﾃﾞｨﾚｲ  Y2嵌合開始ﾃﾞｨﾚｲ
CON
AND T512                 ; 嵌合開始ﾃﾞｨﾚｲ
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;嵌合終了位置移動
;Move to assy end height
LD M5065                 ; 嵌合押込み高さへ
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5951                ; ASSY部   遮光無し
AND M5960                ; Cover嵌合Z動作可能
AND=.L D31470 E2548      ; 軸4 現在座標  Case搬送2_TP_Pos35_Data
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5066                 ; Cover-Assy Z P36 move
MPS
DW K36 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M4                  ; COVER嵌合Z軸移動
MPP
AND M5800                ; COVER嵌合Z 移動完了
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;嵌合終了ｳｪｲﾄ
;Assy end delay
LD M5067                 ; 嵌合終了  Delay
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
TMR K513 E4404           ; 嵌合終了Delay  Y2嵌合開始ﾃﾞｨﾚｲ
CON
AND T513                 ; 嵌合終了Delay
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;退避高さ上昇
;Up to waiting position
LD M5068
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5951                ; ASSY部   遮光無し
AND M5960                ; Cover嵌合Z動作可能
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5069
MPS
DW K37 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M5                  ; COVER嵌合Z軸移動
MPP
AND M5800                ; COVER嵌合Z 移動完了
RES M5800                ; COVER嵌合Z 移動完了
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;ﾜｰｸ残ﾁｪｯｸ
;Work remain check
LD M5070                 ; ﾜｰｸ残確認
MPS
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
AND T503                 ; 嵌合ﾍｯﾄﾞ除材OK
DW K100 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LD T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
OR T502                  ; 嵌合ﾍｯﾄﾞ吸着異常
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5692                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
CON
OUT M5663                ; Cover嵌合ﾍｯﾄﾞ吸着ON
;*** Err407:Cover bring back
LD M5071                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K407 D148             ; 警報No.
CON
SET M5927                ; Err407:ｶﾊﾞｰお持ち帰り
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5072                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
RES B1013                ; Remove_FLG
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5073                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
MPS
ANB B1013                ; Remove_FLG
DW K70 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
AND B1013                ; Remove_FLG
RES B1013                ; Remove_FLG
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5074                 ; ｶﾊﾞｰ除材確認
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
ANB B1013                ; Remove_FLG
MPS
AND T503                 ; 嵌合ﾍｯﾄﾞ除材OK
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LD T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
OR T502                  ; 嵌合ﾍｯﾄﾞ吸着異常
ANL
DW K70 D5000             ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5075                 ; ﾍｯﾄﾞ吸着OFF
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5673                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
;*** 異常終了ﾊﾝﾄﾞｼｪｲｸ
;    Error end hand-shake
LD M5076                 ; 異常終了ﾊﾝﾄﾞｼｪｲｸ
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB M3983                ; Y2　嵌合要求
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5984                ; Y2へ嵌合異常
;***
;ﾍｯﾄﾞ吸着OFF
;Head adsorption OFF
LD M5100                 ; ﾍｯﾄﾞ吸着OFF
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MRD
OUT M5674                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
MPP
@FMOV K0 D910 K8         ; BCR2読み取りﾃﾞｰﾀ
;嵌合終了ﾊﾝﾄﾞｼｪｲｸ
;Assy end hand-shake
LD M5101                 ; 嵌合終了ﾊﾝﾄﾞｼｪｲｸ
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB M3983                ; Y2　嵌合要求
ANL
DW K300 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5983                ; Y2へ嵌合完了
;サイクル停止条件
;Cycle end check
LD M5300                 ; 1ｻｲｸﾙ終了
MPS
LDB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
ORB M3398                ; UT3ｻｲｸﾙ停止
ANL
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MRD
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M3398                ; UT3ｻｲｸﾙ停止
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
FMOV K0 D5210 K8         ; Case2 照合用ﾃﾞｰﾀ
;cycle終了時ｶﾊﾞｰ残ﾁｪｯｸ
;Cover remain check
LD M5301                 ; Chuck P1  move
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
LDB T1901                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
ANB T1902                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
DW K305 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
LD T1901                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT1
OR T1902                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着OUT2
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;Err400:Work remain
LD M5302                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K400 D148             ; 警報No.
CON
SET M5920                ; Err400:ﾜｰｸ残
CON
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5303                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5304                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K301 D5000            ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;念のため、吸着OFF
;Adsorption OFF
LD M5305
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANL
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
MPP
OUT M5677                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
LD M5306
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
AND M5960                ; Cover嵌合Z動作可能
@INC D5000               ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
LD M5307
MPS
OUT M5654                ; Cover嵌合ﾍｯﾄﾞ 上昇
CON
DW K31 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M13                 ; COVER嵌合Z軸移動
MPP
LD M5800                 ; COVER嵌合Z 移動完了
AND T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
ANL
RES M5800                ; COVER嵌合Z 移動完了
CON
@MOV K398 D5000          ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;終了
;Unit AUTO cycle end
LD M5398                 ; ﾕﾆｯﾄ5ｻｲｸﾙ停止
MPS
LD M70                   ; 自動起動停止指令(ｻｲｸﾙ停止)
AND M72                  ; 全ﾕﾆｯﾄ停止確認
ANL
@MOV K399 D5000          ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
FMOV K0 D910 K8          ; BCR2読み取りﾃﾞｰﾀ
MPP
ANB M70                  ; 自動起動停止指令(ｻｲｸﾙ停止)
DW K8 D5000              ; ﾕﾆｯﾄ5自動運転ｽﾃｯﾌﾟ
;<h1/>■原点復帰回路
;
;運転開始・ステッパ初期化・補助接点へ展開
LD CR2002                ; 常時ON
ZRES M5500 M5599         ; ﾕﾆｯﾄ5原点復帰開始
LDB M5904                ; ﾕﾆｯﾄ5原点復帰中
MOV K0 D5010             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5904                 ; ﾕﾆｯﾄ5原点復帰中
MOV D5010 Z1             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
OUT M5500:Z1             ; ﾕﾆｯﾄ5原点復帰開始
;原点復帰ステップ開始
;Origin start
LD M5500                 ; ﾕﾆｯﾄ5原点復帰開始
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;Cover嵌合Z　原点復帰
;Cover-Assy Z HOME start
LD M5501                 ; RCA4原点復帰
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5959                ; Cover嵌合Z原点復帰可能
AND M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5502                 ; RCA4 HOME
MPS
OUT @M31                 ; COVER嵌合Z軸原点復帰
MPP
AND M5810                ; COVER嵌合Z 原点復帰完了
RES M5810                ; COVER嵌合Z 原点復帰完了
CON
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;ﾜｰｸ有無Check
;Work check
LD M5503                 ; ﾜｰｸ残ﾁｪｯｸ
MPS
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
LDB T1900                ; ｶﾊﾞｰ投入_ｶﾊﾞｰ検出
AND T503                 ; 嵌合ﾍｯﾄﾞ除材OK
ANL
DW K7 D5010              ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
TMR K520 K10             ; ﾜｰｸ無し確認ﾃﾞｨﾚｲ
CON
AND T520                 ; ﾜｰｸ無し確認ﾃﾞｨﾚｲ
LD T501                  ; 嵌合ﾍｯﾄﾞ吸着OK
OR T502                  ; 嵌合ﾍｯﾄﾞ吸着異常
ANL
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
OUT M5664                ; Cover嵌合ﾍｯﾄﾞ吸着ON
;Err400:Cover remain
LD M5504                 ; ｴﾗｰ発生
ANB M71                  ; All Error PAUSE
DW K400 D148             ; 警報No.
CON
SET M5920                ; Err400:ﾜｰｸ残
CON
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5505                 ; ｴﾗｰ中
AND M71                  ; All Error PAUSE
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5506                 ; ｴﾗｰ解除確認
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K3 D5010              ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;吸着OFF
;Head adsorption OFF
LD M5507                 ; 吸着OFF
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
ANL
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
OUT M5675                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
;ｶﾊﾞｰ嵌合ﾍｯﾄﾞｼﾘﾝﾀﾞ　上昇
;Cover-Assy head cylinder Up
LD M5508                 ; 嵌合ﾍｯﾄﾞｼﾘﾝﾀﾞ上昇
MPS
LD M5913                 ; UNIT5     ｴﾗｰ・PAUSE無し
AND T1909                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇限
ANL
DW K10 D5010             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
OUT M5652                ; Cover嵌合ﾍｯﾄﾞ 上昇
;嵌合Z軸原点復帰完了ﾌﾗｸﾞ
;Assy-Z HOME OK flag set 
LD M5510                 ; Z軸　原点OK
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
AND M5990                ; 嵌合Z軸　原点復帰完了
DW K20 D5010             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;BCR check
LD M5520
MPS
AND M5913                ; UNIT5     ｴﾗｰ・PAUSE無し
MPS
LD M10305                ; ｺｰﾄﾞﾘｰﾀﾞﾄﾘｶﾞ有効ON
OR CR2002                ; 常時ON
ANL
DW K70 D5010             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
LD M10304                ; ｺｰﾄﾞﾘｰﾀﾞ通信NG PLS
AND CR2003               ; 常時OFF
ANL
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
MPP
OUT M10307               ; ｺｰﾄﾞﾘｰﾀﾞ通信確認入力
;Err402:BCR2 communication error
LD M5521
ANB M71                  ; All Error PAUSE
DW K402 D148             ; 警報No.
CON
SET M5922                ; Err402:BCR2通信異常
CON
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5522                 ; ｴﾗｰ発生
AND M71                  ; All Error PAUSE
@INC D5010               ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
LD M5523                 ; ｴﾗｰ中
AND T130                 ; ｴﾗｰﾘﾄﾗｲｳｪｲﾄ
DW K10 D5010             ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;原点復帰終了
LD M5570                 ; 原点復帰動作完了
AND M5900                ; ﾕﾆｯﾄ5原点
OUT M5578                ; ﾕﾆｯﾄ5原点復帰完了
LD M5578                 ; ﾕﾆｯﾄ5原点復帰完了
ANB M98                  ; 原点復帰開始
@MOV K79 D5010           ; ﾕﾆｯﾄ5原点復帰ｽﾃｯﾌﾟ
;<h1/>■手動回路 
;
LD M33                   ; 手動ﾓｰﾄﾞ
AND M34                  ; ﾒｲﾝ手動操作可能条件
ANB M5905                ; ﾕﾆｯﾄ5起動中
ANB M97                  ; 原点復帰動作中
MC
;Cover-Transfer Y
LD M5960                 ; Cover嵌合Z動作可能
AND M5950                ; ﾗｲﾄｶｰﾃﾝ  遮光無し
TMH K560 K20             ; 手動起動許可ｳｪｲﾄ
CON
AND T560                 ; 手動起動許可ｳｪｲﾄ
OUT M5610                ; Cover搬送Y動作可
LD CR2002                ; 常時ON
MPS
LD B1121                 ; Cover嵌合Z  吸着高さPB
AND M5610                ; Cover搬送Y動作可
OR M5611                 ; Cover_Y   P31 move
ANB M5800                ; COVER嵌合Z 移動完了
ANL
OUT M5611                ; Cover_Y   P31 move
DW K31 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M7                  ; COVER嵌合Z軸移動
MPP
ANF M5611                ; Cover_Y   P31 move
RES M5800                ; COVER嵌合Z 移動完了
LD CR2002                ; 常時ON
MPS
LD B1122                 ; Cover嵌合Z  嵌合開始高さPB
AND M5610                ; Cover搬送Y動作可
AND R3111                ; Case搬送Y2嵌合領域
OR M5612                 ; Cover_Y   P35 move
ANB M5800                ; COVER嵌合Z 移動完了
ANL
OUT M5612                ; Cover_Y   P35 move
DW K35 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M8                  ; COVER嵌合Z軸移動
MPP
ANF M5612                ; Cover_Y   P35 move
RES M5800                ; COVER嵌合Z 移動完了
LD CR2002                ; 常時ON
MPS
LD B1123                 ; Cover嵌合Z  押込高さPB
AND M5610                ; Cover搬送Y動作可
AND R3111                ; Case搬送Y2嵌合領域
OR M5613                 ; Cover_Y   P36 move
ANB M5800                ; COVER嵌合Z 移動完了
ANL
OUT M5613                ; Cover_Y   P36 move
DW K36 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M9                  ; COVER嵌合Z軸移動
MPP
ANF M5613                ; Cover_Y   P36 move
RES M5800                ; COVER嵌合Z 移動完了
LD CR2002                ; 常時ON
MPS
LD B1124                 ; Cover嵌合Z  待機高さPB
AND M5610                ; Cover搬送Y動作可
OR M5614                 ; Cover_Y   P37 move
ANB M5800                ; COVER嵌合Z 移動完了
ANL
OUT M5614                ; Cover_Y   P37 move
DW K37 D5050             ; Cover嵌合Z 移動No.
CON
OUT @M10                 ; COVER嵌合Z軸移動
MPP
ANF M5614                ; Cover_Y   P37 move
RES M5800                ; COVER嵌合Z 移動完了
;Cover Assy head
LD CR2002                ; 常時ON
MPS
ANP B112C                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着PB
MPS
ANB Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
OUT M5661                ; Cover嵌合ﾍｯﾄﾞ吸着ON
MPP
AND Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
OUT M5671                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
MPP
ANP B112D                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 破壊PB
MPS
ANB Y209F                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 破壊ON
OUT M5681                ; Cover嵌合ﾍｯﾄﾞ破壊ON
MPP
AND Y209F                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 破壊ON
OUT M5691                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
LD B112E                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 下降PB
OUT M5641                ; Cover嵌合ﾍｯﾄﾞ 下降
LD B112F                 ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇PB
OUT M5651                ; Cover嵌合ﾍｯﾄﾞ 上昇
;code-reader 2
LDP B10A8                ; BCR2 read PB
OR M5620                 ; BCR2 read
ANB M10323               ; BCR2     読取り完了
ANB M10333               ; BCR2     読取りｴﾗｰ
ANB T555                 ; ｺｰﾄﾞﾘｰﾄﾞﾀｲﾑｱｳﾄ
OUT M5620                ; BCR2 read
TMR K555 K60             ; ｺｰﾄﾞﾘｰﾄﾞﾀｲﾑｱｳﾄ
;Jog
LD R4604                 ; RCA4 SV
AND R4601                ; RCA4 HEND
ANB R4615                ; RCA4 EMGS
ANB R4603                ; RCA4 ALM
ANB M5800                ; COVER嵌合Z 移動完了
ANB M5802                ; COVER嵌合Z 指示移動中
ANB M5804                ; COVER嵌合Z 移動開始
MPS
AND B210E                ; Cover_Assy_Z軸+JOG PB
BOUT D32531 K8           ; RCA4 制御信号
MPP
AND B210F                ; Cover_Assy_Z軸-JOG PB
BOUT D32531 K7           ; RCA4 制御信号
MCR
;<h1/>Output
;COVER-Assy Z
LDB @M1                  ; COVER嵌合Z軸移動
ANB @M2                  ; COVER嵌合Z軸移動
ANB @M3                  ; COVER嵌合Z軸移動
ANB @M4                  ; COVER嵌合Z軸移動
ANB @M5                  ; COVER嵌合Z軸移動
ANB @M6                  ; COVER嵌合Z軸移動
ANB @M7                  ; COVER嵌合Z軸移動
ANB @M8                  ; COVER嵌合Z軸移動
ANB @M9                  ; COVER嵌合Z軸移動
ANB @M10                 ; COVER嵌合Z軸移動
ANB @M11                 ; COVER嵌合Z軸移動
ANB @M12                 ; COVER嵌合Z軸移動
ANB @M13                 ; COVER嵌合Z軸移動
INV
@SET M5804               ; COVER嵌合Z 移動開始
OUT M5802                ; COVER嵌合Z 指示移動中
;Z1 = (D5050-1) * 4        'ﾃﾞｰﾀ先頭ｲﾝﾃﾞｯｸｽ 1Point 4Data
;Z2 = Z1 + 2               '速度は2ﾜｰﾄﾞ後
;D5060.L = E34200.L:Z1     '目標位置ﾃﾞｰﾀｾｯﾄ
;D5062.D = #3              '位置決め完了幅0.03mm固定
;D5064.U = E34200.L:Z2     '速度ﾃﾞｰﾀｾｯﾄ
;RES(M5803)
;SET(M5805)                'ﾃﾞｰﾀｾｯﾄ完了ﾌﾗｸﾞ
LDP M5804                ; COVER嵌合Z 移動開始
NCJ K1000
;Z1 = (D5050-1) * 4        'ﾃﾞｰﾀ先頭ｲﾝﾃﾞｯｸｽ 1Point 4Data
;
LD CR2002                ; 常時ON
LDA D5050                ; Cover嵌合Z 移動No.
CON
EXT
CON
SUB.L +1
CON
MUL.L +4
CON
STA.L Z1
;Z2 = Z1 + 2               '速度は2ﾜｰﾄﾞ後
;
LD CR2002                ; 常時ON
LDA.L Z1
CON
ADD.L +2
CON
STA.L Z2
;D5060.L = E34200.L:Z1     '目標位置ﾃﾞｰﾀｾｯﾄ
;
LD CR2002                ; 常時ON
MOV.L E34200:Z1 D5060    ; RCA4 P1 Position data  RCA4 目標位置
;D5062.D = #3              '位置決め完了幅0.03mm固定
;
LD CR2002                ; 常時ON
MOV.D K3 D5062           ; RCA4 位置決め幅
;D5064.U = E34200.L:Z2     '速度ﾃﾞｰﾀｾｯﾄ
;
LD CR2002                ; 常時ON
MOV E34200:Z2 D5064      ; RCA4 P1 Position data  RCA4 位置決め速度
;RES(M5803)
;
LD CR2002                ; 常時ON
RES M5803
;SET(M5805)                'ﾃﾞｰﾀｾｯﾄ完了ﾌﾗｸﾞ
LD CR2002                ; 常時ON
SET M5805                ; COVER嵌合Z Data set 完了
LABEL K1000
;
LD M5805                 ; COVER嵌合Z Data set 完了
MSTRT IAI_CON_MOVE_4 D5060 D5062 D5064 D32324 D32524 @M0 @D1 M127 K50 M5803;RCA4 目標位置  RCA4 位置決め幅  RCA4 位置決め速度  RCA4 現在位置  RCA4 目標位置  COVER嵌合Z軸移動完了  RBC 非常停止
RES M5805                ; COVER嵌合Z Data set 完了
STG M5804                ; COVER嵌合Z 移動開始
AND @M0                  ; COVER嵌合Z軸移動完了
ANB _IAI_CON_MOVE_4
RES @M0                  ; COVER嵌合Z軸移動完了
CON
SET M5800                ; COVER嵌合Z 移動完了
CON
ENDS
;HOME
LDB @M31                 ; COVER嵌合Z軸原点復帰
ANB @M32                 ; COVER嵌合Z軸原点復帰
INV
@SET M5813               ; COVER嵌合Z 原点開始
OUT M5812                ; COVER嵌合Z 原点復帰中
LD M5813                 ; COVER嵌合Z 原点開始
MSTRT IAI_CON_HOME_4 D32324 D32524 @M30 @D1 M127 K50;RCA4 現在位置  RCA4 目標位置  COVER嵌合Z軸原点復帰完了  RBC 非常停止
STG M5813                ; COVER嵌合Z 原点開始
AND _IAI_CON_HOME_4
JMP M5814
STG M5814
AND @M30                 ; COVER嵌合Z軸原点復帰完了
JMP M5815                ; COVER嵌合Z 原点復帰完了待ち
STG M5815                ; COVER嵌合Z 原点復帰完了待ち
ANB _IAI_CON_HOME_4
RES @M30                 ; COVER嵌合Z軸原点復帰完了
CON
SET M5810                ; COVER嵌合Z 原点復帰完了
CON
ENDS
;Cover嵌合ﾍｯﾄﾞ
;Cover-Assy head
;Up/Down
LDP M5641                ; Cover嵌合ﾍｯﾄﾞ 下降
ORP M5642                ; Cover嵌合ﾍｯﾄﾞ 下降
ORP M5643                ; Cover嵌合ﾍｯﾄﾞ 下降
ORP M5644                ; Cover嵌合ﾍｯﾄﾞ 下降
OR M5640                 ; Cover嵌合ﾍｯﾄﾞ 下降補助
ANPB M5650               ; Cover嵌合ﾍｯﾄﾞ 上昇補助
OUT M5640                ; Cover嵌合ﾍｯﾄﾞ 下降補助
LDP M5651                ; Cover嵌合ﾍｯﾄﾞ 上昇
ORP M5652                ; Cover嵌合ﾍｯﾄﾞ 上昇
ORP M5653                ; Cover嵌合ﾍｯﾄﾞ 上昇
ORP M5654                ; Cover嵌合ﾍｯﾄﾞ 上昇
OR M5650                 ; Cover嵌合ﾍｯﾄﾞ 上昇補助
ANPB M5640               ; Cover嵌合ﾍｯﾄﾞ 下降補助
OUT M5650                ; Cover嵌合ﾍｯﾄﾞ 上昇補助
LD M5640                 ; Cover嵌合ﾍｯﾄﾞ 下降補助
OUT Y218A                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 下降
LD M5650                 ; Cover嵌合ﾍｯﾄﾞ 上昇補助
OUT Y218B                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 上昇
;Adsorption ON/OFF
LDP M5661                ; Cover嵌合ﾍｯﾄﾞ吸着ON
ORP M5662                ; Cover嵌合ﾍｯﾄﾞ吸着ON
ORP M5663                ; Cover嵌合ﾍｯﾄﾞ吸着ON
ORP M5664                ; Cover嵌合ﾍｯﾄﾞ吸着ON
ORP M5665                ; Cover嵌合ﾍｯﾄﾞ吸着ON
OR M5660                 ; Cover嵌合ﾍｯﾄﾞ吸着ON補助
ANB M5670                ; Cover嵌合ﾍｯﾄﾞ吸着OFF補助
ANPB M5680               ; Cover嵌合ﾍｯﾄﾞ破壊ON補助
OUT M5660                ; Cover嵌合ﾍｯﾄﾞ吸着ON補助
LDP M5671                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5672                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5673                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5674                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5675                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5676                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5677                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
ORP M5678                ; Cover嵌合ﾍｯﾄﾞ吸着OFF
OUT M5670                ; Cover嵌合ﾍｯﾄﾞ吸着OFF補助
LD M5660                 ; Cover嵌合ﾍｯﾄﾞ吸着ON補助
OUT Y209E                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 吸着ON
;Air release ON/OFF
LDP M5681                ; Cover嵌合ﾍｯﾄﾞ破壊ON
ORP M5682                ; Cover嵌合ﾍｯﾄﾞ破壊ON
ORP M5683                ; Cover嵌合ﾍｯﾄﾞ破壊ON
ORP M5684                ; Cover嵌合ﾍｯﾄﾞ破壊ON
OR M5680                 ; Cover嵌合ﾍｯﾄﾞ破壊ON補助
ANB M5690                ; Cover嵌合ﾍｯﾄﾞ破壊OFF補助
ANPB M5660               ; Cover嵌合ﾍｯﾄﾞ吸着ON補助
OUT M5680                ; Cover嵌合ﾍｯﾄﾞ破壊ON補助
LDP M5691                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
ORP M5692                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
ORP M5693                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
ORP M5694                ; Cover嵌合ﾍｯﾄﾞ破壊OFF
OUT M5690                ; Cover嵌合ﾍｯﾄﾞ破壊OFF補助
LD M5680                 ; Cover嵌合ﾍｯﾄﾞ破壊ON補助
OUT Y209F                ; ｶﾊﾞｰ嵌合ﾍｯﾄﾞ 破壊ON
;<h1/>異常検出
;
LD CR2002                ; 常時ON
MPS
LD M5963                 ; Cover Jig No.異常
OR M5964                 ; 投入後　  Jig No.異常
LDB B05A0                ; ﾕﾆｯﾄ5_異常1BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A0                 ; ﾕﾆｯﾄ5_Jig番号異常
ORL
ANL
OUT B0A0                 ; ﾕﾆｯﾄ5_Jig番号異常
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A0                 ; ﾕﾆｯﾄ5_異常1BZ停止
AND B0A0                 ; ﾕﾆｯﾄ5_Jig番号異常
ANL
OUT B05A0                ; ﾕﾆｯﾄ5_異常1BZ停止
LD M513                  ; RCA4　起動
AND R4603                ; RCA4 ALM
TMR K500 K100            ; RCA4 Error
LD CR2002                ; 常時ON
MPS
LD M64                   ; 運転準備
AND R4603                ; RCA4 ALM
OR T500                  ; RCA4 Error
LDB B05A1                ; ﾕﾆｯﾄ5_異常2BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A1                 ; RAC4ｱﾗｰﾑ_異常2
ORL
ANL
OUT B0A1                 ; RAC4ｱﾗｰﾑ_異常2
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A1                 ; ﾕﾆｯﾄ5_異常2BZ停止
AND B0A1                 ; RAC4ｱﾗｰﾑ_異常2
ANL
OUT B05A1                ; ﾕﾆｯﾄ5_異常2BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05A2                ; ﾕﾆｯﾄ5_異常3BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A2                 ; ﾕﾆｯﾄ5_異常3
ORL
ANL
OUT B0A2                 ; ﾕﾆｯﾄ5_異常3
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A2                 ; ﾕﾆｯﾄ5_異常3BZ停止
AND B0A2                 ; ﾕﾆｯﾄ5_異常3
ANL
OUT B05A2                ; ﾕﾆｯﾄ5_異常3BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05A3                ; ﾕﾆｯﾄ5_異常4BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A3                 ; ﾕﾆｯﾄ5_異常4
ORL
ANL
OUT B0A3                 ; ﾕﾆｯﾄ5_異常4
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A3                 ; ﾕﾆｯﾄ5_異常4BZ停止
AND B0A3                 ; ﾕﾆｯﾄ5_異常4
ANL
OUT B05A3                ; ﾕﾆｯﾄ5_異常4BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05A4                ; ﾕﾆｯﾄ5_異常5BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A4                 ; ﾕﾆｯﾄ5_異常5
ORL
ANL
OUT B0A4                 ; ﾕﾆｯﾄ5_異常5
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A4                 ; ﾕﾆｯﾄ5_異常5BZ停止
AND B0A4                 ; ﾕﾆｯﾄ5_異常5
ANL
OUT B05A4                ; ﾕﾆｯﾄ5_異常5BZ停止
LD CR2002                ; 常時ON
MPS
LD CR2003                ; 常時OFF
LDB B05A5                ; ﾕﾆｯﾄ5_異常6BZ停止
ORB M123                 ; 異常ﾘｾｯﾄﾊﾟﾙｽ
AND B0A5                 ; ﾕﾆｯﾄ5_異常6
ORL
ANL
OUT B0A5                 ; ﾕﾆｯﾄ5_異常6
MPP
LD M122                  ; ﾌﾞｻﾞｰ停止ﾊﾟﾙｽ
OR B05A5                 ; ﾕﾆｯﾄ5_異常6BZ停止
AND B0A5                 ; ﾕﾆｯﾄ5_異常6
ANL
OUT B05A5                ; ﾕﾆｯﾄ5_異常6BZ停止
;異常ﾌﾞｻﾞｰまとめ
LD B0A0                  ; ﾕﾆｯﾄ5_Jig番号異常
ANB B05A0                ; ﾕﾆｯﾄ5_異常1BZ停止
LD B0A1                  ; RAC4ｱﾗｰﾑ_異常2
ANB B05A1                ; ﾕﾆｯﾄ5_異常2BZ停止
ORL
LD B0A2                  ; ﾕﾆｯﾄ5_異常3
ANB B05A2                ; ﾕﾆｯﾄ5_異常3BZ停止
ORL
LD B0A3                  ; ﾕﾆｯﾄ5_異常4
ANB B05A3                ; ﾕﾆｯﾄ5_異常4BZ停止
ORL
LD B0A4                  ; ﾕﾆｯﾄ5_異常5
ANB B05A4                ; ﾕﾆｯﾄ5_異常5BZ停止
ORL
LD B0A5                  ; ﾕﾆｯﾄ5_異常6
ANB B05A5                ; ﾕﾆｯﾄ5_異常6BZ停止
ORL
OUT M5909                ; ﾕﾆｯﾄ5異常BZ補助
END
ENDH
