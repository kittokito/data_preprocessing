DEVICE:51
;MODULE:J_自動運転
;MODULE_TYPE:0
;<h1/>自動運転条件
LD T100                  ; 給水ﾀﾝｸ 液面 ﾘﾐｯﾄ ｵﾝ安定(未達)
OUT MR10000              ; 自動運転条件 給水ﾀﾝｸ液面 ﾘﾐｯﾄ
LD T103                  ; 給水ﾀﾝｸ 液面 上限 ｵﾌ安定(未達)
OUT MR10001              ; 自動運転条件 給水ﾀﾝｸ液面 上限
LD T106                  ; 給水ﾀﾝｸ 液面 下限 ｵﾝ安定(到達)
OUT MR10002              ; 自動運転条件 給水ﾀﾝｸ液面 下限
LD T110                  ; 貯水ﾀﾝｸ 液面 ﾘﾐｯﾄ ｵﾝ安定(未達)
OUT MR10003              ; 自動運転条件 貯水ﾀﾝｸ液面 ﾘﾐｯﾄ
LD T113                  ; 貯水ﾀﾝｸ 液面 上限1 ｵﾌ安定(未達)
OUT MR10004              ; 自動運転条件 貯水ﾀﾝｸ液面 上限1
LD T115                  ; 貯水ﾀﾝｸ 液面 上限2 ｵﾌ安定(未達)
OUT MR10005              ; 自動運転条件 貯水ﾀﾝｸ液面 上限2
LD T116                  ; 貯水ﾀﾝｸ 液面 下限 ｵﾝ安定(到達)
OUT MR10006              ; 自動運転条件 貯水ﾀﾝｸ液面 下限
LD T800                  ; a:給水ﾀﾝｸ圧力 大気圧 安定
OUT MR10007              ; 自動運転条件 給水ﾀﾝｸ 大気圧
LD T810                  ; c:貯水ﾀﾝｸ圧力 大気圧 安定
OUT MR10008              ; 自動運転条件 貯水ﾀﾝｸ 大気圧
LD T820                  ; d:排液圧力 加圧力 安定
OR MR5508                ; 選択中機種 排液ﾊﾟﾀｰﾝ 真空1 選択
OR MR5509                ; 選択中機種 排液ﾊﾟﾀｰﾝ 真空2 選択
OUT MR10009              ; 自動運転条件 排液 加圧力
LD T830                  ; e:流路ﾜｰｸ前圧力 大気圧 安定
OUT MR10010              ; 自動運転条件 流路ﾜｰｸ前 大気圧
LD T840                  ; f:流路ﾜｰｸ後圧力 大気圧 安定
OUT MR10011              ; 自動運転条件 流路ﾜｰｸ後 大気圧
LD T850                  ; g:ﾘｰｸﾃｽﾄ圧力 大気圧 安定
OUT MR10012              ; 自動運転条件 ﾘｰｸﾃｽﾄ 大気圧
LD T861                  ; ﾘｰｸﾃｽﾄ流量 ④ﾃｽﾄ流量 安定
OUT MR10013              ; 自動運転条件 ﾘｰｸﾃｽﾄ流量
LD T870                  ; 注入-排液流量 停止確認流量 安定
OUT MR10014              ; 自動運転条件 注入-排液流量
LD T121                  ; 真空ﾎﾟﾝﾌﾟ ｵﾌ安定
OUT MR10015              ; 自動運転条件 真空ﾎﾟﾝﾌﾟ停止中
LD T123                  ; 製品ﾎﾟﾝﾌﾟ ｵﾌ安定
OUT MR10100              ; 自動運転条件 製品ﾎﾟﾝﾌﾟ 停止中
LD T0                    ; PLC RUN 安定
OUT MR10101              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10102              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10103              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10104              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10105              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10106              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10107              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10108              ; 自動運転条件 予備
LD T0                    ; PLC RUN 安定
OUT MR10109              ; 自動運転条件 予備
LDB LR001                ; 自動運転ﾒﾓ
OUT MR10110              ; 自動運転条件 自動運転ﾒﾓ ｵﾌ
LD MR18915               ; ｺﾏﾝﾄﾞ操作 停止中
AND MR44915              ; 動作ﾙｰﾁﾝ停止中
OUT MR10111              ; 自動運転条件 ｺﾏﾝﾄﾞ操作停止中
LD MR23915               ; 単独操作 停止中
OUT MR10112              ; 自動運転条件 単独操作停止中
LD MR2015                ; 製品ﾎﾟﾝﾌﾟﾓｰﾀ ｲﾆｼｬﾙ 完了
AND MR2215               ; ﾛｰﾄﾞｾﾙ ｲﾆｼｬﾙ完了
AND MR3115               ; AD10 給水ﾀﾝｸ圧力 ｲﾆｼｬﾙ 完了
AND MR3215               ; AD11 貯水ﾀﾝｸ圧力 ｲﾆｼｬﾙ 完了
AND MR3315               ; AD12 排液圧力 ｲﾆｼｬﾙ 完了
AND MR3415               ; AD13 流路ﾜｰｸ前圧力 ｲﾆｼｬﾙ 完了
AND MR3615               ; AD20 流路ﾜｰｸ後圧力 ｲﾆｼｬﾙ 完了
AND MR3715               ; AD21 ﾘｰｸﾃｽﾄ圧力 ｲﾆｼｬﾙ 完了
AND MR3815               ; AD22 ﾘｰｸﾃｽﾄ流量 ｲﾆｼｬﾙ 完了
AND MR3915               ; AD23 注入-排液流量 ｲﾆｼｬﾙ 完了
AND MR4015               ; 電源投入時 大気開放 終了
OUT MR10113              ; 自動運転条件 機器ｲﾆｼｬﾙ完了
LD MR1615                ; 警報無し
OUT MR10114              ; 自動運転条件 警報無し
LD MR1115                ; 異常無し
OUT MR10115              ; 自動運転条件 異常無し
;<h1/>自動運転 ﾒｲﾝﾌﾗｸﾞ
LD T0                    ; PLC RUN 安定
AND MR1114               ; 即時停止 異常無し
MC
LD=.D MR10000 $FFFFFFFF  ; 自動運転条件 給水ﾀﾝｸ液面 ﾘﾐｯﾄ
ANB MR10204              ; 自動運転 ｵﾝ
OUT MR10200              ; 自動運転 ｵﾝIL
LD MR10204               ; 自動運転 ｵﾝ
OUT MR10201              ; 自動運転 ｵﾌIL
LD MR11015               ; 自動運転 終了
AND MR10201              ; 自動運転 ｵﾌIL
OUT MR10202              ; 自動運転 ｵﾌ
LDP B1100                ; 重量測定 TSW
ANB B1103                ; 停止 TSW
OR MR10204               ; 自動運転 ｵﾝ
ANB MR10202              ; 自動運転 ｵﾌ
MPS
ANB MR10204              ; 自動運転 ｵﾝ
OUT MR10203              ; ↑自動運転 ｵﾝ
MPP
OUT MR10204              ; 自動運転 ｵﾝ
LD MR10204               ; 自動運転 ｵﾝ
LD B1103                 ; 停止 TSW
ORB MR1115               ; 異常無し
ORB MR1615               ; 警報無し
OR MR10208               ; 自動運転 中止
ANL
OUT MR10208              ; 自動運転 中止
MCR
;<h1/>自動運転 動作
;<h1/>--- 1.⑩重量測定(注入前)
LD MR10204               ; 自動運転 ｵﾝ
OUT MR10300              ; ⑩重量測定(注入前) 移行
LD MR10300               ; ⑩重量測定(注入前) 移行
MPS
ANB MR10303              ; ⑩重量測定(注入前) ｾﾞﾛﾘｾｯﾄ 完了
OUT MR10302              ; ⑩重量測定(注入前) ｾﾞﾛﾘｾｯﾄ
MPP
LD MR42004               ; ﾛｰﾄﾞｾﾙ ｾﾞﾛ付近 確認 OK
AND MR42015              ; ﾛｰﾄﾞｾﾙ ｾﾞﾛﾘｾｯﾄ 終了
LD T130                  ; CC1 ﾛｰﾄﾞｾﾙ ｾﾞﾛ付近 安定
ANB MR42001              ; ﾛｰﾄﾞｾﾙ ｾﾞﾛﾘｾｯﾄ 起動中
ORL
OR MR10303               ; ⑩重量測定(注入前) ｾﾞﾛﾘｾｯﾄ 完了
ANL
OUT MR10303              ; ⑩重量測定(注入前) ｾﾞﾛﾘｾｯﾄ 完了
LD MR10303               ; ⑩重量測定(注入前) ｾﾞﾛﾘｾｯﾄ 完了
MPS
ANB MR10305              ; ⑩重量測定(注入前) 測定値ｸﾘｱ 完
OUT MR10304              ; ⑩重量測定(注入前) 測定値ｸﾘｱ
MOV.L +0 DM110           ; 製品重量(注入前)測定値 下位
MPP
LD MR10304               ; ⑩重量測定(注入前) 測定値ｸﾘｱ
OR MR10305               ; ⑩重量測定(注入前) 測定値ｸﾘｱ 完
ANL
OUT MR10305              ; ⑩重量測定(注入前) 測定値ｸﾘｱ 完
LD MR10305               ; ⑩重量測定(注入前) 測定値ｸﾘｱ 完
MPS
LDP B1100                ; 重量測定 TSW
ANB R23605               ; CC1 ﾛｰﾄﾞｾﾙ ｾﾞﾛ付近
ANB MR10208              ; 自動運転 中止
OR MR10306               ; ⑩重量測定(注入前) 開始
ANB MR10307              ; ⑩重量測定(注入前) 測定値取得可
ANL
OUT MR10306              ; ⑩重量測定(注入前) 開始
MPP
LD MR10306               ; ⑩重量測定(注入前) 開始
AND T131                 ; CC1 ﾛｰﾄﾞｾﾙ 安定(測定値) 安定
OR MR10307               ; ⑩重量測定(注入前) 測定値取得可
ANL
OUT MR10307              ; ⑩重量測定(注入前) 測定値取得可
LD MR10307               ; ⑩重量測定(注入前) 測定値取得可
MPS
ANB MR10309              ; ⑩重量測定(注入前) 測定値取得 完
OUT MR10308              ; ⑩重量測定(注入前) 測定値取得
MOV.L DM100 DM110        ; ﾛｰﾄﾞｾﾙ ﾘｱﾙ値 下位  製品重量(注入前)測定値 下位
MPP
LD MR10308               ; ⑩重量測定(注入前) 測定値取得
OR MR10309               ; ⑩重量測定(注入前) 測定値取得 完
ANL
OUT MR10309              ; ⑩重量測定(注入前) 測定値取得 完
LD MR10309               ; ⑩重量測定(注入前) 測定値取得 完
MPS
LD>=.L DM110 DM782       ; 製品重量(注入前)測定値 下位  ⑤注入 製品重量(注入前) 下限
AND<=.L DM110 DM780      ; 製品重量(注入前)測定値 下位  ⑤注入 製品重量(注入前) 上限
OR MR10310               ; ⑩重量測定(注入前) OK
ANB MR10311              ; ⑩重量測定(注入前) NG
ANL
OUT MR10310              ; ⑩重量測定(注入前) OK
MPP
ANB MR10310              ; ⑩重量測定(注入前) OK
OUT MR10311              ; ⑩重量測定(注入前) NG
LD MR10300               ; ⑩重量測定(注入前) 移行
MPS
LD MR10208               ; 自動運転 中止
OR MR10311               ; ⑩重量測定(注入前) NG
ANB MR10315              ; ⑩重量測定(注入前) 完了
ANL
OUT MR10314              ; ⑩重量測定(注入前) 中止
MPP
LD MR10310               ; ⑩重量測定(注入前) OK
ANB MR10314              ; ⑩重量測定(注入前) 中止
ANL
OUT MR10315              ; ⑩重量測定(注入前) 完了
;<h1/>--- 2.④ﾘｰｸﾃｽﾄ
LD MR10315               ; ⑩重量測定(注入前) 完了
OUT MR10400              ; ④ﾘｰｸﾃｽﾄ 移行
LD MR10400               ; ④ﾘｰｸﾃｽﾄ 移行
MPS
LDP B1101                ; ﾘｰｸﾃｽﾄ TSW
ANB MR10208              ; 自動運転 中止
OR MR10402               ; ④ﾘｰｸﾃｽﾄ 開始
ANB MR10403              ; ④ﾘｰｸﾃｽﾄ 終了
ANL
OUT MR10402              ; ④ﾘｰｸﾃｽﾄ 開始
MPP
LD MR28515               ; ④ﾘｰｸﾃｽﾄ 終了
OR MR10403               ; ④ﾘｰｸﾃｽﾄ 終了
ANL
MPS
OUT MR10403              ; ④ﾘｰｸﾃｽﾄ 終了
MPP
LDB MR28001              ; ④ﾘｰｸﾃｽﾄ 起動中
OR MR10404               ; ④ﾘｰｸﾃｽﾄ 終了確認
ANL
OUT MR10404              ; ④ﾘｰｸﾃｽﾄ 終了確認
LD MR10404               ; ④ﾘｰｸﾃｽﾄ 終了確認
MPS
LDB MR10208              ; 自動運転 中止
OR MR10406               ; ④ﾘｰｸﾃｽﾄ OK
ANB MR10407              ; ④ﾘｰｸﾃｽﾄ NG
ANL
OUT MR10406              ; ④ﾘｰｸﾃｽﾄ OK
MPP
ANB MR10406              ; ④ﾘｰｸﾃｽﾄ OK
OUT MR10407              ; ④ﾘｰｸﾃｽﾄ NG
LD MR10400               ; ④ﾘｰｸﾃｽﾄ 移行
MPS
LDB MR10402              ; ④ﾘｰｸﾃｽﾄ 開始
ANB MR10403              ; ④ﾘｰｸﾃｽﾄ 終了
AND MR10208              ; 自動運転 中止
OR MR10407               ; ④ﾘｰｸﾃｽﾄ NG
ANB MR10415              ; ④ﾘｰｸﾃｽﾄ 完了
ANL
OUT MR10414              ; ④ﾘｰｸﾃｽﾄ 中止
MPP
LD MR10406               ; ④ﾘｰｸﾃｽﾄ OK
ANB MR10414              ; ④ﾘｰｸﾃｽﾄ 中止
ANL
OUT MR10415              ; ④ﾘｰｸﾃｽﾄ 完了
;<h1/>--- 3.⑤注入(1回目)
LD MR10415               ; ④ﾘｰｸﾃｽﾄ 完了
OUT MR10500              ; ⑤注入(1回目) 移行
LD MR10500               ; ⑤注入(1回目) 移行
MPS
LDP B1102                ; 注入 TSW
ANB MR10208              ; 自動運転 中止
OR MR10502               ; ⑤注入(1回目) 開始
ANB MR10506              ; ⑤注入(1回目) 終了
ANL
OUT MR10502              ; ⑤注入(1回目) 開始
MRD
AND MR10502              ; ⑤注入(1回目) 開始
MPS
LD MR5500                ; 選択中機種 注入ﾊﾟﾀｰﾝ 真空 選択
OR MR10503               ; ⑤注入(1回目) ⑤-1(真空)
ANB MR10504              ; ⑤注入(1回目) ⑤-2(加圧)
ANB MR10505              ; ⑤注入(1回目) ⑤-3(真空+加圧)
ANL
OUT MR10503              ; ⑤注入(1回目) ⑤-1(真空)
MRD
LD MR5501                ; 選択中機種 注入ﾊﾟﾀｰﾝ 加圧 選択
OR MR10504               ; ⑤注入(1回目) ⑤-2(加圧)
ANB MR10503              ; ⑤注入(1回目) ⑤-1(真空)
ANB MR10505              ; ⑤注入(1回目) ⑤-3(真空+加圧)
ANL
OUT MR10504              ; ⑤注入(1回目) ⑤-2(加圧)
MPP
LD MR5502                ; 選択中機種 注入ﾊﾟﾀｰﾝ 真加 選択
OR MR10505               ; ⑤注入(1回目) ⑤-3(真空+加圧)
ANB MR10503              ; ⑤注入(1回目) ⑤-1(真空)
ANB MR10504              ; ⑤注入(1回目) ⑤-2(加圧)
ANL
OUT MR10505              ; ⑤注入(1回目) ⑤-3(真空+加圧)
MPP
LD MR10503               ; ⑤注入(1回目) ⑤-1(真空)
AND MR29515              ; ⑤-1 注入(真空) 終了
LD MR10504               ; ⑤注入(1回目) ⑤-2(加圧)
AND MR30515              ; ⑤-2 注入(加圧) 終了
ORL
LD MR10505               ; ⑤注入(1回目) ⑤-3(真空+加圧)
AND MR31515              ; ⑤-3 注入(真空+加圧) 終了
ORL
OR MR10506               ; ⑤注入(1回目) 終了
ANL
MPS
OUT MR10506              ; ⑤注入(1回目) 終了
MPP
LDB MR29001              ; ⑤-1 注入(真空) 起動中
ANB MR30001              ; ⑤-2 注入(加圧) 起動中
ANB MR31001              ; ⑤-3 注入(真空+加圧) 起動中
OR MR10507               ; ⑤注入(1回目) 終了確認
ANL
OUT MR10507              ; ⑤注入(1回目) 終了確認
LD MR10507               ; ⑤注入(1回目) 終了確認
MPS
LDB MR10208              ; 自動運転 中止
OR MR10508               ; ⑤注入(1回目) OK
ANB MR10509              ; ⑤注入(1回目) NG
ANL
OUT MR10508              ; ⑤注入(1回目) OK
MPP
ANB MR10508              ; ⑤注入(1回目) OK
OUT MR10509              ; ⑤注入(1回目) NG
LD MR10500               ; ⑤注入(1回目) 移行
MPS
LDB MR10502              ; ⑤注入(1回目) 開始
ANB MR10506              ; ⑤注入(1回目) 終了
AND MR10208              ; 自動運転 中止
OR MR10509               ; ⑤注入(1回目) NG
ANB MR10515              ; ⑤注入(1回目) 完了
ANL
OUT MR10514              ; ⑤注入(1回目) 中止
MPP
LD MR10508               ; ⑤注入(1回目) OK
ANB MR10514              ; ⑤注入(1回目) 中止
ANL
OUT MR10515              ; ⑤注入(1回目) 完了
;<h1/>--- 4.⑥循環洗浄
LD MR10515               ; ⑤注入(1回目) 完了
OUT MR10600              ; ⑥循環洗浄 移行
LD MR10600               ; ⑥循環洗浄 移行
MPS
LDB MR10208              ; 自動運転 中止
OR MR10602               ; ⑥循環洗浄 開始
ANB MR10606              ; ⑥循環洗浄 終了
ANL
OUT MR10602              ; ⑥循環洗浄 開始
MRD
AND MR10602              ; ⑥循環洗浄 開始
MPS
LD MR5504                ; 選択中機種 循環ﾊﾟﾀｰﾝ 製品 選択
OR MR10603               ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
ANB MR10604              ; ⑥循環洗浄 ⑥-2(真空)
ANB MR10605              ; ⑥循環洗浄 ⑥-3(加圧)
ANL
OUT MR10603              ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
MRD
LD MR5505                ; 選択中機種 循環ﾊﾟﾀｰﾝ 真空 選択
OR MR10604               ; ⑥循環洗浄 ⑥-2(真空)
ANB MR10603              ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
ANB MR10605              ; ⑥循環洗浄 ⑥-3(加圧)
ANL
OUT MR10604              ; ⑥循環洗浄 ⑥-2(真空)
MPP
LD MR5506                ; 選択中機種 循環ﾊﾟﾀｰﾝ 加圧 選択
OR MR10605               ; ⑥循環洗浄 ⑥-3(加圧)
ANB MR10603              ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
ANB MR10604              ; ⑥循環洗浄 ⑥-2(真空)
ANL
OUT MR10605              ; ⑥循環洗浄 ⑥-3(加圧)
MPP
LD MR10603               ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
AND MR32515              ; ⑥-1 循環洗浄(製品) 終了
LD MR10604               ; ⑥循環洗浄 ⑥-2(真空)
AND MR33515              ; ⑥-2 循環洗浄(真空) 終了
ORL
LD MR10605               ; ⑥循環洗浄 ⑥-3(加圧)
AND MR34515              ; ⑥-3 循環(加圧) 終了
ORL
OR MR10606               ; ⑥循環洗浄 終了
ANL
MPS
OUT MR10606              ; ⑥循環洗浄 終了
MPP
LDB MR32001              ; ⑥-1 循環洗浄(製品) 起動中
ANB MR33001              ; ⑥-2 循環洗浄(真空) 起動中
ANB MR34001              ; ⑥-3 循環洗浄(加圧) 起動中
OR MR10607               ; ⑥循環洗浄 終了確認
ANL
OUT MR10607              ; ⑥循環洗浄 終了確認
LD MR10606               ; ⑥循環洗浄 終了
MPS
LD MR10603               ; ⑥循環洗浄 ⑥-1(製品ﾎﾟﾝﾌﾟ)
AND MR32015              ; ⑥-1 循環流量 OK
LD MR10604               ; ⑥循環洗浄 ⑥-2(真空)
AND MR33113              ; ⑥-2 循環流量 OK
ORL
LD MR10605               ; ⑥循環洗浄 ⑥-3(加圧)
AND MR34109              ; ⑥-3 循環流量 OK
ORL
OR MR10608               ; ⑥循環洗浄 OK
ANB MR10609              ; ⑥循環洗浄 NG
ANL
OUT MR10608              ; ⑥循環洗浄 OK
MPP
ANB MR10608              ; ⑥循環洗浄 OK
OUT MR10609              ; ⑥循環洗浄 NG
LD MR10600               ; ⑥循環洗浄 移行
MPS
LDB MR10602              ; ⑥循環洗浄 開始
ANB MR10606              ; ⑥循環洗浄 終了
AND MR10208              ; 自動運転 中止
LD MR10607               ; ⑥循環洗浄 終了確認
AND MR10609              ; ⑥循環洗浄 NG
ORL
ANB MR10615              ; ⑥循環洗浄 完了
ANL
OUT MR10614              ; ⑥循環洗浄 中止
MPP
LD MR10607               ; ⑥循環洗浄 終了確認
AND MR10608              ; ⑥循環洗浄 OK
ANB MR10614              ; ⑥循環洗浄 中止
ANL
OUT MR10615              ; ⑥循環洗浄 完了
;<h1/>--- 5.⑦排液
LD MR10615               ; ⑥循環洗浄 完了
OUT MR10700              ; ⑦排液 移行
LD MR10700               ; ⑦排液 移行
MPS
LDB MR10208              ; 自動運転 中止
OR MR10702               ; ⑦排液 開始
ANB MR10706              ; ⑦排液 終了
ANL
OUT MR10702              ; ⑦排液 開始
MRD
AND MR10702              ; ⑦排液 開始
MPS
LD MR5508                ; 選択中機種 排液ﾊﾟﾀｰﾝ 真空1 選択
OR MR10703               ; ⑦排液 ⑦-1(真空1)
ANB MR10704              ; ⑦排液 ⑦-2(真空2)
ANB MR10705              ; ⑦排液 ⑦-3(加圧)
ANL
OUT MR10703              ; ⑦排液 ⑦-1(真空1)
MRD
LD MR5509                ; 選択中機種 排液ﾊﾟﾀｰﾝ 真空2 選択
OR MR10704               ; ⑦排液 ⑦-2(真空2)
ANB MR10703              ; ⑦排液 ⑦-1(真空1)
ANB MR10705              ; ⑦排液 ⑦-3(加圧)
ANL
OUT MR10704              ; ⑦排液 ⑦-2(真空2)
MPP
LD MR5510                ; 選択中機種 排液ﾊﾟﾀｰﾝ 加圧 選択
OR MR10705               ; ⑦排液 ⑦-3(加圧)
ANB MR10703              ; ⑦排液 ⑦-1(真空1)
ANB MR10704              ; ⑦排液 ⑦-2(真空2)
ANL
OUT MR10705              ; ⑦排液 ⑦-3(加圧)
MPP
LD MR10703               ; ⑦排液 ⑦-1(真空1)
AND MR36515              ; ⑦-1 排液(真空1) 終了
LD MR10704               ; ⑦排液 ⑦-2(真空2)
AND MR37515              ; ⑦-2 排液(真空2) 終了
ORL
LD MR10705               ; ⑦排液 ⑦-3(加圧)
AND MR38515              ; ⑦-3 排液(加圧) 終了
ORL
OR MR10706               ; ⑦排液 終了
ANL
MPS
OUT MR10706              ; ⑦排液 終了
MPP
LDB MR36001              ; ⑦-1 排液(真空1) 起動中
ANB MR37001              ; ⑦-2 排液(真空2) 起動中
ANB MR38001              ; ⑦-3 排液(加圧) 起動中
OR MR10707               ; ⑦排液 終了確認
ANL
OUT MR10707              ; ⑦排液 終了確認
LD MR10707               ; ⑦排液 終了確認
MPS
LDB MR10208              ; 自動運転 中止
OR MR10708               ; ⑦排液 OK
ANB MR10709              ; ⑦排液 NG
ANL
OUT MR10708              ; ⑦排液 OK
MPP
ANB MR10708              ; ⑦排液 OK
OUT MR10709              ; ⑦排液 NG
LD MR10700               ; ⑦排液 移行
MPS
LDB MR10702              ; ⑦排液 開始
ANB MR10706              ; ⑦排液 終了
AND MR10208              ; 自動運転 中止
OR MR10709               ; ⑦排液 NG
ANB MR10715              ; ⑦排液 完了
ANL
OUT MR10714              ; ⑦排液 中止
MPP
LD MR10708               ; ⑦排液 OK
ANB MR10714              ; ⑦排液 中止
ANL
OUT MR10715              ; ⑦排液 完了
;<h1/>--- 6.⑤注入(2回目)
LD MR10715               ; ⑦排液 完了
OUT MR10800              ; ⑤注入(2回目) 移行
LD MR10800               ; ⑤注入(2回目) 移行
MPS
LDB MR10208              ; 自動運転 中止
OR MR10802               ; ⑤注入(2回目) 開始
ANB MR10806              ; ⑤注入(2回目) 終了
ANL
OUT MR10802              ; ⑤注入(2回目) 開始
MRD
AND MR10802              ; ⑤注入(2回目) 開始
MPS
LD MR5500                ; 選択中機種 注入ﾊﾟﾀｰﾝ 真空 選択
OR MR10803               ; ⑤注入(2回目) ⑤-1(真空)
ANB MR10804              ; ⑤注入(2回目) ⑤-2(加圧)
ANB MR10805              ; ⑤注入(2回目) ⑤-3(真空+加圧)
ANL
OUT MR10803              ; ⑤注入(2回目) ⑤-1(真空)
MRD
LD MR5501                ; 選択中機種 注入ﾊﾟﾀｰﾝ 加圧 選択
OR MR10804               ; ⑤注入(2回目) ⑤-2(加圧)
ANB MR10803              ; ⑤注入(2回目) ⑤-1(真空)
ANB MR10805              ; ⑤注入(2回目) ⑤-3(真空+加圧)
ANL
OUT MR10804              ; ⑤注入(2回目) ⑤-2(加圧)
MPP
LD MR5502                ; 選択中機種 注入ﾊﾟﾀｰﾝ 真加 選択
OR MR10805               ; ⑤注入(2回目) ⑤-3(真空+加圧)
ANB MR10803              ; ⑤注入(2回目) ⑤-1(真空)
ANB MR10804              ; ⑤注入(2回目) ⑤-2(加圧)
ANL
OUT MR10805              ; ⑤注入(2回目) ⑤-3(真空+加圧)
MPP
LD MR10803               ; ⑤注入(2回目) ⑤-1(真空)
AND MR29515              ; ⑤-1 注入(真空) 終了
LD MR10804               ; ⑤注入(2回目) ⑤-2(加圧)
AND MR30515              ; ⑤-2 注入(加圧) 終了
ORL
LD MR10805               ; ⑤注入(2回目) ⑤-3(真空+加圧)
AND MR31515              ; ⑤-3 注入(真空+加圧) 終了
ORL
OR MR10806               ; ⑤注入(2回目) 終了
ANL
MPS
OUT MR10806              ; ⑤注入(2回目) 終了
MPP
LDB MR29001              ; ⑤-1 注入(真空) 起動中
ANB MR30001              ; ⑤-2 注入(加圧) 起動中
ANB MR31001              ; ⑤-3 注入(真空+加圧) 起動中
OR MR10807               ; ⑤注入(2回目) 終了確認
ANL
OUT MR10807              ; ⑤注入(2回目) 終了確認
LD MR10807               ; ⑤注入(2回目) 終了確認
MPS
LDB MR10208              ; 自動運転 中止
OR MR10808               ; ⑤注入(2回目) OK
ANB MR10809              ; ⑤注入(2回目) NG
ANL
OUT MR10808              ; ⑤注入(2回目) OK
MPP
ANB MR10808              ; ⑤注入(2回目) OK
OUT MR10809              ; ⑤注入(2回目) NG
LD MR10800               ; ⑤注入(2回目) 移行
MPS
LDB MR10802              ; ⑤注入(2回目) 開始
ANB MR10806              ; ⑤注入(2回目) 終了
AND MR10208              ; 自動運転 中止
OR MR10809               ; ⑤注入(2回目) NG
ANB MR10815              ; ⑤注入(2回目) 完了
ANL
OUT MR10814              ; ⑤注入(2回目) 中止
MPP
LD MR10808               ; ⑤注入(2回目) OK
ANB MR10814              ; ⑤注入(2回目) 中止
ANL
OUT MR10815              ; ⑤注入(2回目) 完了
;<h1/>--- 7.⑩重量測定(注入後)
LD MR10815               ; ⑤注入(2回目) 完了
OUT MR10900              ; ⑩重量測定(注入後) 移行
LDB MR10208              ; 自動運転 中止
AND MR10900              ; ⑩重量測定(注入後) 移行
ANB MR10906              ; ⑩重量測定(注入後) 開始
ANB MR10907              ; ⑩重量測定(注入後) 測定値 取得可
MPS
ANB MR10903              ; ⑩重量測定要求 ﾌﾞｻﾞｰ ｵﾌ
OUT MR10902              ; ⑩重量測定要求 ﾌﾞｻﾞｰ
MPP
LDP B100E                ; ﾌﾞｻﾞｰ停止 TSW
OR MR10903               ; ⑩重量測定要求 ﾌﾞｻﾞｰ ｵﾌ
ANL
OUT MR10903              ; ⑩重量測定要求 ﾌﾞｻﾞｰ ｵﾌ
LD MR10900               ; ⑩重量測定(注入後) 移行
MPS
ANB MR10905              ; ⑩重量測定(注入後) 測定値 ｸﾘｱ 完
OUT MR10904              ; ⑩重量測定(注入後) 測定値 ｸﾘｱ
MOV.L +0 DM120           ; 製品重量(注入後)測定値 下位
MOV.L +0 DM122           ; 注入量演算値 下位
MPP
LD MR10904               ; ⑩重量測定(注入後) 測定値 ｸﾘｱ
OR MR10905               ; ⑩重量測定(注入後) 測定値 ｸﾘｱ 完
ANL
OUT MR10905              ; ⑩重量測定(注入後) 測定値 ｸﾘｱ 完
LD MR10905               ; ⑩重量測定(注入後) 測定値 ｸﾘｱ 完
MPS
LDP B1100                ; 重量測定 TSW
ANB R23605               ; CC1 ﾛｰﾄﾞｾﾙ ｾﾞﾛ付近
ANB MR10208              ; 自動運転 中止
OR MR10906               ; ⑩重量測定(注入後) 開始
ANB MR10907              ; ⑩重量測定(注入後) 測定値 取得可
ANL
OUT MR10906              ; ⑩重量測定(注入後) 開始
MPP
LD MR10906               ; ⑩重量測定(注入後) 開始
AND T131                 ; CC1 ﾛｰﾄﾞｾﾙ 安定(測定値) 安定
OR MR10907               ; ⑩重量測定(注入後) 測定値 取得可
ANL
OUT MR10907              ; ⑩重量測定(注入後) 測定値 取得可
LD MR10907               ; ⑩重量測定(注入後) 測定値 取得可
MPS
ANB MR10909              ; ⑩重量測定(注入後) 測定値取得 完
OUT MR10908              ; ⑩重量測定(注入後) 測定値取得
MOV.L DM100 DM120        ; ﾛｰﾄﾞｾﾙ ﾘｱﾙ値 下位  製品重量(注入後)測定値 下位
CAL-.L DM120 DM110 DM122 ; 製品重量(注入後)測定値 下位  製品重量(注入前)測定値 下位  注入量演算値 下位
MPP
LD MR10908               ; ⑩重量測定(注入後) 測定値取得
OR MR10909               ; ⑩重量測定(注入後) 測定値取得 完
ANL
OUT MR10909              ; ⑩重量測定(注入後) 測定値取得 完
LD MR10909               ; ⑩重量測定(注入後) 測定値取得 完
MPS
LD>=.L DM122 DM792       ; 注入量演算値 下位  ⑤注入 冷却液注入量 下限 設定
AND<=.L DM122 DM790      ; 注入量演算値 下位  ⑤注入 冷却液注入量 上限 設定
OR MR10910               ; ⑩重量測定(注入後) OK
ANB MR10911              ; ⑩重量測定(注入後) NG
ANL
OUT MR10910              ; ⑩重量測定(注入後) OK
MPP
ANB MR10910              ; ⑩重量測定(注入後) OK
OUT MR10911              ; ⑩重量測定(注入後) NG
LD MR10900               ; ⑩重量測定(注入後) 移行
MPS
LD MR10208               ; 自動運転 中止
OR MR10911               ; ⑩重量測定(注入後) NG
ANB MR10915              ; ⑩重量測定(注入後) 完了
ANL
OUT MR10914              ; ⑩重量測定(注入後) 中止
MPP
LD MR10910               ; ⑩重量測定(注入後) OK
ANB MR10914              ; ⑩重量測定(注入後) 中止
ANL
OUT MR10915              ; ⑩重量測定(注入後) 完了
;<h1/>--- 8.自動運転終了
LD MR10314               ; ⑩重量測定(注入前) 中止
OR MR10414               ; ④ﾘｰｸﾃｽﾄ 中止
OR MR10514               ; ⑤注入(1回目) 中止
OR MR10614               ; ⑥循環洗浄 中止
OR MR10714               ; ⑦排液 中止
OR MR10814               ; ⑤注入(2回目) 中止
OR MR10914               ; ⑩重量測定(注入後) 中止
OR MR10915               ; ⑩重量測定(注入後) 完了
OUT MR11015              ; 自動運転 終了
;<h1/>--- 9.自動運転終了通知
LD LR001                 ; 自動運転ﾒﾓ
ANB MR10204              ; 自動運転 ｵﾝ
ANP B1103                ; 停止 TSW
OUT LR000                ; 自動運転ﾒﾓ ｵﾌ
LD MR10203               ; ↑自動運転 ｵﾝ
OR LR001                 ; 自動運転ﾒﾓ
ANB LR000                ; 自動運転ﾒﾓ ｵﾌ
OUT LR001                ; 自動運転ﾒﾓ
END
ENDH
