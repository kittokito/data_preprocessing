DEVICE:51
;MODULE:BCR
;MODULE_TYPE:0
;<h1/>非常停止処理
LD R50010                ; Emargency　Stop
ZRES @MR000 @MR915       ; ﾃﾞﾊﾞｯｸﾞ用ﾊﾞｰｺｰﾄﾞ読取開始
;ﾊﾞｰｺｰﾄﾞ読取
STG MR21003              ; ｻﾃﾗｲﾄへ照合要求
ANB @MR701               ; ｻﾃﾗｲﾄ照合中
@DW #0 DM380
CON
SET @MR701               ; ｻﾃﾗｲﾄ照合中
CON
JMP @MR702
STG @MR702
SET R50512               ; ﾊﾞｰｺｰﾄﾞ照合
CON
JMP @MR703
STG @MR703
AND R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
JMP @MR704
STG @MR704
ANB R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
MPS
AND R50513               ; ﾊﾞｰｺｰﾄﾞ照合OK
RES MR21204              ; ｲﾝｸ照合NG
CON
JMP @MR705
MPP
AND R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
SET MR21204              ; ｲﾝｸ照合NG
CON
JMP @MR705
STG @MR705
RES @MR701               ; ｻﾃﾗｲﾄ照合中
CON
SET MR21203              ; ｻﾃﾗｲﾄよりｲﾝｸ照合完了
CON
ENDS
;ﾊﾞｰｺｰﾄﾞ読取選択
LD R4008                 ; 切替SW ｻﾃﾗｲﾄ
DIFU MR49000             ; BCRﾁｪｯｸ ｻﾃﾗｲﾄ選択PLS
LD R4009                 ; 切替SW KD-DRUM
DIFU MR49001             ; BCRﾁｪｯｸ KD-DRUM選択PLS
LDB R4008                ; 切替SW ｻﾃﾗｲﾄ
ANB R4009                ; 切替SW KD-DRUM
OUT MR49002              ; BCR読取選択SW 中間位置
TMR @0 #3                ; BCR読取選択SW 中間位置ﾃﾞｨﾚｲ
;ﾃﾞﾊﾞｯｸﾞ用ﾊﾞｰｺｰﾄﾞ読取
LD R50510                ; ﾊﾞｰｺｰﾄﾞ入力SW
RES R30101               ; Port1 送信要求
CON
SET @MR000               ; ﾃﾞﾊﾞｯｸﾞ用ﾊﾞｰｺｰﾄﾞ読取開始
STG @MR000               ; ﾃﾞﾊﾞｯｸﾞ用ﾊﾞｰｺｰﾄﾞ読取開始
AND R30101               ; Port1 送信要求
JMP @MR001
STG @MR001
MPS
LDB R30001               ; Port1 送信完了
ANB R30002               ; Port1 受信ﾃﾞｰﾀ読出要求
ANL
JMP @MR002
MPP
LD R30001                ; Port1 送信完了
OR R30002                ; Port1 受信ﾃﾞｰﾀ読出要求
ANL
SET @MR005               ; 受信ｴﾗｰ
CON
ENDS
STG @MR002
BMOV DM30002 DM1100 #5   ; Port1 送信ﾃﾞｰﾀ1  確定 使用ｲﾝｸ名称1
MOV DM1100 DM4204        ; 確定 使用ｲﾝｸ名称1
MOV DM1101 DM4203
MOV DM1102 DM4202        ; 確定 使用ｲﾝｸ名称2
MOV DM1103 DM4201
MOV DM1104 DM4200        ; 確定 使用ｲﾝｸ名称3  ｲﾝｸﾊﾞｰｺｰﾄ1
MOV #0 DM1105
SET R50511               ; 機種No.比較要求
CON
TMR @2 #2                ; ｳｪｲﾄ
CON
AND @T2                  ; ｳｪｲﾄ
JMP @MR003
STG @MR003
RES R50511               ; 機種No.比較要求
CON
RES R30101               ; Port1 送信要求
CON
ENDS
;ﾊﾞｰｺｰﾄﾞ照合
LD R50512                ; ﾊﾞｰｺｰﾄﾞ照合
RES R50513               ; ﾊﾞｰｺｰﾄﾞ照合OK
CON
RES R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
CON
ZRES @MR101 @MR515       ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
CON
RES R50512               ; ﾊﾞｰｺｰﾄﾞ照合
CON
SET @MR100               ; ﾊﾞｰｺｰﾄﾞ照合開始
STG @MR100               ; ﾊﾞｰｺｰﾄﾞ照合開始
MPS
SET R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
JMP @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
MRD
FMOV #0 DM4010 #5        ; ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
MPP
FMOV #0 DM4020 #5        ; ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
STG @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
MPS
AND MR49000              ; BCRﾁｪｯｸ ｻﾃﾗｲﾄ選択PLS
RES @MR110               ; ｻﾃﾗｲﾄ ﾊﾞｰｺｰﾄﾞ読取完了記憶
CON
SET @MR108               ; ｻﾃﾗｲﾄ選択中
CON
SET @MR300               ; ｻﾃﾗｲﾄﾊﾞｰｺｰﾄﾞ読取
CON
JMP @MR102
MPP
AND MR49001              ; BCRﾁｪｯｸ KD-DRUM選択PLS
RES @MR111               ; ﾀﾝｸ ﾊﾞｰｺｰﾄﾞ読取完了記憶
CON
SET @MR109               ; ﾀﾝｸ選択中
CON
SET @MR300               ; ｻﾃﾗｲﾄﾊﾞｰｺｰﾄﾞ読取
CON
JMP @MR202
STG @MR102
MPS
AND @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
JMP @MR103
MPP
LDB @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
AND MR49002              ; BCR読取選択SW 中間位置
ANL
RES @MR108               ; ｻﾃﾗｲﾄ選択中
CON
JMP @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
STG @MR103
RES @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
CON
RES @MR108               ; ｻﾃﾗｲﾄ選択中
CON
SET @MR110               ; ｻﾃﾗｲﾄ ﾊﾞｰｺｰﾄﾞ読取完了記憶
CON
JMP @MR112
STG @MR202
MPS
AND @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
JMP @MR203
MPP
LDB @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
AND MR49002              ; BCR読取選択SW 中間位置
ANL
RES @MR109               ; ﾀﾝｸ選択中
CON
JMP @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
STG @MR203
RES @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
CON
RES @MR109               ; ﾀﾝｸ選択中
CON
SET @MR111               ; ﾀﾝｸ ﾊﾞｰｺｰﾄﾞ読取完了記憶
CON
JMP @MR112
STG @MR112
MPS
LD @MR110                ; ｻﾃﾗｲﾄ ﾊﾞｰｺｰﾄﾞ読取完了記憶
AND @MR111               ; ﾀﾝｸ ﾊﾞｰｺｰﾄﾞ読取完了記憶
ANL
JMP @MR113
MRD
LD @MR110                ; ｻﾃﾗｲﾄ ﾊﾞｰｺｰﾄﾞ読取完了記憶
ANB @MR111               ; ﾀﾝｸ ﾊﾞｰｺｰﾄﾞ読取完了記憶
ANL
JMP @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
MPP
LDB @MR110               ; ｻﾃﾗｲﾄ ﾊﾞｰｺｰﾄﾞ読取完了記憶
AND @MR111               ; ﾀﾝｸ ﾊﾞｰｺｰﾄﾞ読取完了記憶
ANL
JMP @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
STG @MR113
MPS
LDB LR30013              ; ﾊﾞｰｺｰﾄﾞ照合TESTﾓｰﾄﾞ
AND @T0                  ; BCR読取選択SW 中間位置ﾃﾞｨﾚｲ
ANL
MPS
SCMP DM1100 DM4010       ; 確定 使用ｲﾝｸ名称1  ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
AND CR2010               ; 演算結果がｾﾞﾛ
SCMP DM1100 DM4020       ; 確定 使用ｲﾝｸ名称1  ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
AND CR2010               ; 演算結果がｾﾞﾛ
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50513               ; ﾊﾞｰｺｰﾄﾞ照合OK
CON
@DW #100 DM380
CON
ENDS
MRD
SCMP DM1100 DM4010       ; 確定 使用ｲﾝｸ名称1  ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
ANB CR2010               ; 演算結果がｾﾞﾛ
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
CON
ENDS
MPP
SCMP DM1100 DM4020       ; 確定 使用ｲﾝｸ名称1  ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
ANB CR2010               ; 演算結果がｾﾞﾛ
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
CON
ENDS
MPP
LD LR30013               ; ﾊﾞｰｺｰﾄﾞ照合TESTﾓｰﾄﾞ
AND @T0                  ; BCR読取選択SW 中間位置ﾃﾞｨﾚｲ
ANL
MPS
SCMP DM1130 DM4010       ; TEST 使用ｲﾝｸ名称1  ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
AND CR2010               ; 演算結果がｾﾞﾛ
SCMP DM1130 DM4020       ; TEST 使用ｲﾝｸ名称1  ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
AND CR2010               ; 演算結果がｾﾞﾛ
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50513               ; ﾊﾞｰｺｰﾄﾞ照合OK
CON
@DW #100 DM380
CON
ENDS
MRD
SCMP DM1130 DM4010       ; TEST 使用ｲﾝｸ名称1  ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
ANB CR2010               ; 演算結果がｾﾞﾛ
@DW #101 DM380
CON
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
CON
ENDS
MPP
SCMP DM1130 DM4020       ; TEST 使用ｲﾝｸ名称1  ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
CON
ANB CR2010               ; 演算結果がｾﾞﾛ
@DW #101 DM380
CON
RES R50509               ; ﾊﾞｰｺｰﾄﾞ照合中
CON
SET R50514               ; ﾊﾞｰｺｰﾄﾞ照合NG
CON
ENDS
STG @MR300               ; ｻﾃﾗｲﾄﾊﾞｰｺｰﾄﾞ読取
AND R30101               ; Port1 送信要求
JMP @MR301
STG @MR301
RES R30101               ; Port1 送信要求
CON
MPS
LDB R30001               ; Port1 送信完了
ANB R30002               ; Port1 受信ﾃﾞｰﾀ読出要求
ANL
JMP @MR302
MPP
LD R30001                ; Port1 送信完了
OR R30002                ; Port1 受信ﾃﾞｰﾀ読出要求
ANL
SET @MR005               ; 受信ｴﾗｰ
CON
ENDS
STG @MR302
MPS
AND @MR108               ; ｻﾃﾗｲﾄ選択中
LDA DM30001              ; Port1 送信ﾃﾞｰﾀ長
CON
MPS
CMP #10
CON
AND CR2010               ; 演算結果がｾﾞﾛ
JMP @MR303
MPP
CMP #10
CON
ANB CR2010               ; 演算結果がｾﾞﾛ
SET @MR505               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ
CON
ENDS
MPP
AND @MR109               ; ﾀﾝｸ選択中
LDA DM30001              ; Port1 送信ﾃﾞｰﾀ長
CON
MPS
CMP #10
CON
AND CR2011               ; 演算結果が正
JMP @MR308
MPP
CMP #10
CON
ANB CR2011               ; 演算結果が正
SET @MR507
CON
ENDS
STG @MR303
JMP @MR304
BMOV DM30002 DM4010 #5   ; Port1 送信ﾃﾞｰﾀ1  ﾀﾝｸ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
MOV #0 DM4015
STG @MR308
JMP @MR304
BMOV DM30002 DM4020 #5   ; Port1 送信ﾃﾞｰﾀ1  ｻﾃﾗｲﾄ_ﾊﾞｰｺｰﾄﾞ入力ﾃﾞｰﾀﾊﾞｯﾌｧ
MOV #0 DM4025
STG @MR304
SET @MR315               ; ﾊﾞｰｺｰﾄﾞ読み取り完了
CON
ENDS
STG @MR505               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ
SET @MR510               ; 入力ﾐｽ出力時間
CON
MPS
OUT @MR512               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ出力
MPP
AND @T5                  ; 入力ﾐｽ出力時間
SET @MR514               ; 入力ﾐｽ出力完了
CON
RES @MR510               ; 入力ﾐｽ出力時間
CON
JMP @MR506               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ
STG @MR506               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ
SET @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
CON
ENDS
STG @MR507
SET @MR511               ; 入力ﾐｽ出力時間
CON
MPS
OUT @MR513               ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ出力
MPP
AND @T5                  ; 入力ﾐｽ出力時間
SET @MR514               ; 入力ﾐｽ出力完了
CON
RES @MR511               ; 入力ﾐｽ出力時間
CON
JMP @MR508
STG @MR508
SET @MR101               ; ﾊﾞｰｺｰﾄﾞﾁｪｯｸ開始
CON
ENDS
LD @MR512                ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ出力
OR @MR513                ; ﾊﾞｰｺｰﾄﾞ入力ﾐｽ出力
OUT R50508               ; ｻﾃﾗｲﾄ入力ﾐｽ出力時間
LD @MR510                ; 入力ﾐｽ出力時間
OR @MR511                ; 入力ﾐｽ出力時間
TMR @5 #15               ; 入力ﾐｽ出力時間
LD @MR514                ; 入力ﾐｽ出力完了
RES R30101               ; Port1 送信要求
END
ENDH
