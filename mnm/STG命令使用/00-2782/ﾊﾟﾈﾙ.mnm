DEVICE:52
;MODULE:ﾊﾟﾈﾙ
;MODULE_TYPE:0
;*****　軸ﾊﾟﾗﾒｰﾀ変更許可　*****
LD R42400                ; ﾊﾟﾗﾒｰﾀ変更許可SW
TMR #90 #20              ; ﾊﾟﾗﾒｰﾀ変更許可
LD T90                   ; ﾊﾟﾗﾒｰﾀ変更許可
OR R42403                ; ﾊﾟﾗﾒｰﾀ変更許可
LD>= DM3000 #21          ; 現在表示中   画面
AND<= DM3000 #24         ; 現在表示中   画面
OR= DM3000 #14           ; 現在表示中   画面
ANL
OUT R42403               ; ﾊﾟﾗﾒｰﾀ変更許可
LD CR2002                ; 常時ON
AND= DM3000 #2           ; 現在表示中   画面
OUT MR010                ; ﾒｲﾝ画面
;***** 電源投入時 *****
LD CR2008                ; 運転開始時1ｽｷｬﾝON
ORP T100                 ; 動作遅延ﾀｲﾏ-
LDB MR3000               ; 原点復帰中
ANB R50003               ; 原点復帰中
ANB R50007               ; 手動制御中
ANF R50006               ; 異常発生中
ORL
MPS
AND LR800                ; 自動ﾓｰﾄﾞSW選択
MOV #2 DM3001            ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
MOV #3 DM3001            ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;***** 手動/自動選択画面 *****
LD LR800                 ; 自動ﾓｰﾄﾞSW選択
@MOV #2 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LDB LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #3 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R112                  ; 戻るSW
MPS
AND LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #2 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #3 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R42002                ; 戻るSW
MPS
AND LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #2 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #3 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;***** 手動/設定選択画面 *****
LD R110                  ; 手動/設定選択SW
ANB MR3000               ; 原点復帰中
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #10 DM3001          ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;***** 手動/設定選択画面 *****
LD MR9910                ; 異常発生
@MOV #110 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LDF MR9910               ; 異常発生
MPS
AND LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #2 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
@MOV #3 DM3001           ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;***** ﾊﾟﾈﾙ表示 *****
LD CR2003                ; 常時OFF
OUT R49904               ; 全ｶﾊﾞｰ動作中表示
LDB MR40005              ; 全起動中
ANB @MR001               ; 手動中
OUT R49909               ; 停止中表示
LD MR40005               ; 全起動中
OUT R49910               ; 自動動作中表示
LDB MR40005              ; 全起動中
AND @MR001               ; 手動中
OUT R49911               ; 手動動作中表示
LD MR40005               ; 全起動中
AND MR40204              ; 全扉解除ON
OUT R49912               ; 一時停止中表示
LD MR3000                ; 原点復帰中
OUT R49913               ; 原点復帰中表示
;***** ｼｸﾞﾅﾙﾀﾜｰ *****
LD CR2002                ; 常時ON
AND>= DM3000 #41         ; 現在表示中   画面
AND<= DM3000 #45         ; 現在表示中   画面
OUT @MR001               ; 手動中
LDB MR4800               ; 外観検査NGﾒｯｾｰｼﾞ
ANB MR4801               ; 接合装置停止表示
ANB CR2003               ; 常時OFF
INV
ANB MR814                ; BZ　OFF記憶
OUT MR813                ; ｳｨﾝﾄﾞｳﾒｯｾｰｼﾞON集合
LD MR807                 ; ｳｨﾝﾄﾞｳBZ停止SW
OR MR814                 ; BZ　OFF記憶
LD MR4800                ; 外観検査NGﾒｯｾｰｼﾞ
OR MR4801                ; 接合装置停止表示
ANL
OUT MR814                ; BZ　OFF記憶
LDP MR4806               ; WF-ID読込み未完了MS
OR MR815                 ; BZ1秒ON
ANB T60
OUT MR815                ; BZ1秒ON
TMR #60 #5
LDB MR40501              ; 異常発生確認
ANB MR813                ; ｳｨﾝﾄﾞｳﾒｯｾｰｼﾞON集合
ANB MR815                ; BZ1秒ON
INV
OUT MR503                ; ﾌﾞｻﾞｰ
LD MR3200                ; 原点復帰完了FLG
ANB MR40005              ; 全起動中
ANB MR40204              ; 全扉解除ON
LDB MR3200               ; 原点復帰完了FLG
ANB R50006               ; 異常発生中
ANB MR40005              ; 全起動中
OR MR40204               ; 全扉解除ON
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANB MR3000               ; 原点復帰中
ANB @MR001               ; 手動中
OUT MR504                ; ｼｸﾞﾅﾙﾀﾜｰ_緑
LDB MR40005              ; 全起動中
ANB MR3000               ; 原点復帰中
ANB @MR001               ; 手動中
INV
OUT MR505                ; ｼｸﾞﾅﾙﾀﾜｰ_黄
LD R50006                ; 異常発生中
LDB MR3200               ; 原点復帰完了FLG
ANB MR40005              ; 全起動中
ORL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANB MR3000               ; 原点復帰中
ANB @MR001               ; 手動中
OUT MR506                ; ｼｸﾞﾅﾙﾀﾜｰ_赤
;***** 動作表示 *****
LDB LR800                ; 自動ﾓｰﾄﾞSW選択
OUT R43002               ; 手動操作ﾓｰﾄﾞ動作表示
LD LR800                 ; 自動ﾓｰﾄﾞSW選択
MPS
ANB LR501                ; 搬送ﾓｰﾄﾞ
OUT R43000               ; 通常運転ﾓｰﾄﾞ動作表示
MPP
AND LR501                ; 搬送ﾓｰﾄﾞ
OUT R43001               ; 搬送ﾓｰﾄﾞ動作表示
;****  WF転写後経過時間　分に換算 ****
LD MR610                 ; 転写後経過時間ｶｳﾝﾄ開始
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
AND< DM6006 #3600        ; 接着剤解凍経過時間秒
@INC DM6000
@INC DM6006              ; 接着剤解凍経過時間秒
LD CR2002                ; 常時ON
AND>= DM6000 #60
@INC.D DM6002
@MOV #0 DM6000
LD R4005                 ; 次WF投入要求信号
DIFU MR3910              ; 次WF投入要求信号ﾊﾟﾙｽ
LD R4005                 ; 次WF投入要求信号
@FMOV #0 DM6000 #10
END
ENDH
