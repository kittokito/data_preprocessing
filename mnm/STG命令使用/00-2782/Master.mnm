DEVICE:52
;MODULE:Master
;MODULE_TYPE:0
LD CR2007                ; 運転開始時1ｽｷｬﾝOFF
MPS
TMR #100 #20             ; 動作遅延ﾀｲﾏ-
MPP
AND CR2003               ; 常時OFF
@FMOV #0 DM900 #30       ; LOT番号読出し値
LD CR2002                ; 常時ON
FLIK #30 #30 MR000       ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
FLIK #15 #15 MR001       ; 0.15Sﾌﾘｯｶﾊﾟﾙｽ
FLIK #100 #100 MR002     ; 2ｓﾌﾘｯｶﾊﾟﾙｽ
;<h1/>**** 主幹自動運転 / 主幹自動運転 ****
LD R100                  ; 手動/自動ｾﾚｸﾀSW
ANB MR40005              ; 全起動中
DIFU MR40100             ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
LD MR40100               ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
ANB LR800                ; 自動ﾓｰﾄﾞSW選択
LDB MR40100              ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
AND LR800                ; 自動ﾓｰﾄﾞSW選択
ORL
OUT LR800                ; 自動ﾓｰﾄﾞSW選択
;***** 原点復帰 ******
LD MR1102                ; RB動作可
AND MR2000               ; ｱﾗｲﾅ動作可可
AND T12                  ; 安全扉閉確認
ANB MR2005               ; ｱﾗｲﾅｰ吸着ON
OUT MR3001               ; 原点復帰可
LDP R101                 ; 原点復帰SW
AND MR3001               ; 原点復帰可
OR MR3000                ; 原点復帰中
ANB MR3015               ; 原点復帰完了
ANB MR4006               ; 原点復帰ﾀｲﾑｵｰﾊﾞｰ
ANB MR1001               ; ﾕﾆｯﾄPORT1異常
ANB R4108                ; 搬送ﾛﾎﾞｯﾄｱﾗｰﾑ
ANB MR2001               ; ﾕﾆｯﾄ1PORT2異常
OUT MR3000               ; 原点復帰中
TMR #310 #1000           ; 原点復帰監視時間
TMH #300 #5              ; 原点復帰開始
LD MR3000                ; 原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LDB MR3000               ; 原点復帰中
AND MR3200               ; 原点復帰完了FLG
ORL
ANB MR40005              ; 全起動中
OUT R301                 ; 原点復帰PL
LDP T300                 ; 原点復帰開始
SET MR3002               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
STG MR3002               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
RES MR3100               ; 原点復帰完了FLG
CON
ANB MR3100               ; 原点復帰完了FLG
MPS
OUT MR3201               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰
MPP
AND MR1602               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
JMP MR3003               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
STG MR3003               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
ANB MR1602               ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始
ANB R4109                ; 搬送ﾛﾎﾞｯﾄ動作中信号
AND R4105                ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点ｾﾝｻｰ
RES MR911                ; 転写装置へﾊﾝﾄﾞ進入中
CON
RES MR915                ; 接合装置へﾊﾝﾄﾞ進入中
CON
ANB MR911                ; 転写装置へﾊﾝﾄﾞ進入中
ANB MR915                ; 接合装置へﾊﾝﾄﾞ進入中
JMP MR3004               ; 搬送ﾛﾎﾞｯﾄ旋回原点復帰開始
STG MR3004               ; 搬送ﾛﾎﾞｯﾄ旋回原点復帰開始
AND MR1606               ; 搬送ﾛﾎﾞｯﾄ旋回原点復帰開始
AND R35308               ; Port1 受信読出要求9
AND R34708               ; Port1 受信照合9
JMP MR3005               ; 搬送ﾛﾎﾞｯﾄZ軸原点復帰開始
STG MR3005               ; 搬送ﾛﾎﾞｯﾄZ軸原点復帰開始
AND MR1610               ; 搬送ﾛﾎﾞｯﾄZ軸原点復帰開始
AND R35309               ; Port1 受信読出要求10
AND R34709               ; Port1 受信照合10
JMP MR3006
STG MR3006
ANB MR1606               ; 搬送ﾛﾎﾞｯﾄ旋回原点復帰開始
ANB MR1610               ; 搬送ﾛﾎﾞｯﾄZ軸原点復帰開始
ANB R4109                ; 搬送ﾛﾎﾞｯﾄ動作中信号
AND R4105                ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点ｾﾝｻｰ
JMP MR3007               ; ｱﾗｲﾅｰ原点復帰開始
STG MR3007               ; ｱﾗｲﾅｰ原点復帰開始
AND MR2200               ; ｱﾗｲﾅｰ原点復帰
JMP MR3008               ; 原点復帰完了待ち
STG MR3008               ; 原点復帰完了待ち
ANB MR2200               ; ｱﾗｲﾅｰ原点復帰
AND= DM13104 #0          ; X軸、Y軸、θ軸動作状態
JMP MR3009               ; ｱﾗｲﾅｰWFｻｲｽﾞ指定
STG MR3009               ; ｱﾗｲﾅｰWFｻｲｽﾞ指定
JMP MR3010               ; ｱﾗｲﾅｰWFｻｲｽﾞ指定完了待ち
STG MR3010               ; ｱﾗｲﾅｰWFｻｲｽﾞ指定完了待ち
JMP MR3011               ; ｱﾗｲﾅｰｱﾗｲﾒﾝﾄ位置移動
STG MR3011               ; ｱﾗｲﾅｰｱﾗｲﾒﾝﾄ位置移動
AND MR2304               ; ｱﾗｲﾅｰｱﾗｲﾒﾝﾄ位置移動
JMP MR3012
STG MR3012
ANB MR2304               ; ｱﾗｲﾅｰｱﾗｲﾒﾝﾄ位置移動
TMR #301 #3
CON
AND T301
JMP MR3013
STG MR3013
OUT MR3202               ; ｱﾗｲﾅｰ状態読出し
TMR #302 #3
CON
AND T302
AND R37411               ; Port2 受信読出要求12
AND R36811               ; Port2 受信照合12
MPS
AND= DM13104 #0          ; X軸、Y軸、θ軸動作状態
JMP MR3014
MRD
LD DM13104.0             ; Y軸、θ軸動作中
OR DM13104.4             ; X軸動作中
ANL
JMP MR3012
MPP
LD<> DM13104 #0          ; X軸、Y軸、θ軸動作状態
ANB DM13104.0            ; Y軸、θ軸動作中
ANB DM13104.4            ; X軸動作中
ANL
JMP MR3014
STG MR3014
MPS
AND= DM13104 #0          ; X軸、Y軸、θ軸動作状態
SET MR3200               ; 原点復帰完了FLG
CON
JMP MR3015               ; 原点復帰完了
MPP
AND<> DM13104 #0         ; X軸、Y軸、θ軸動作状態
JMP MR3015               ; 原点復帰完了
STG MR3015               ; 原点復帰完了
ANB MR3000               ; 原点復帰中
ENDS
LDB MR3000               ; 原点復帰中
ZRES MR3002 MR3015       ; 搬送ﾛﾎﾞｯﾄｱｰﾑ原点復帰開始  原点復帰完了
LD MR3000                ; 原点復帰中
OUT R50003               ; 原点復帰中
;***** 自動運転 *****
LD MR3200                ; 原点復帰完了FLG
OUT MR40000              ; 装置原点
LDB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT MR40001              ; 全異常
LD MR1103                ; ｷｬﾘｱ正常
AND R4100                ; 真空元圧確認
AND T12                  ; 安全扉閉確認
AND MR1102               ; RB動作可
AND MR2102               ; ｱﾗｲﾅｰ動作可
LD MR901                 ; ﾊﾞｰｺｰﾄﾞLot_No.読込み完了
AND MR902                ; ﾚｼﾋﾟ_No.読込み完了
AND MR903                ; 作業者_No.読込み完了
OR LR501                 ; 搬送ﾓｰﾄﾞ
ANL
ANB MR2005               ; ｱﾗｲﾅｰ吸着ON
ANB R4106                ; ﾛﾎﾞｯﾄﾊﾝﾄﾞ圧力ｾﾝｻｰ信号
AND MR4600               ; PC準備OK
ANB MR4601               ; PCウォッチドグ
ANB MR4602               ; PCウォッチドグ
ANB MR7000               ; ﾊﾞｰｺｰﾄﾞ読込み開始
LDB LR501                ; 搬送ﾓｰﾄﾞ
AND R4009                ; 接合装置ﾌﾙｵｰﾄ
AND MR5800               ; 転写装置連動ﾓｰﾄﾞ
LD LR501                 ; 搬送ﾓｰﾄﾞ
AND R4007                ; 接合装置搬送ﾓｰﾄﾞ
ORL
ANL
ANB MR5707               ; 接着剤ﾎﾟｯﾄﾗｲﾌｵｰﾊﾞｰ
OUT MR40002              ; 起動条件
LDB MR703                ; 全起動OFF
ANB MR4106               ; 搬送ﾛﾎﾞｯﾄﾊﾝﾄﾞWF有り
ANB MR801                ; 中断SW
INV
OUT MR40003              ; 起動OFF
LDB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT MR40004              ; 原点復帰
LD R103                  ; 停止SW
TMR #40 #30              ; 強制停止SWON時間
CON
AND T40                  ; 強制停止SWON時間
OUT MR40010              ; 強制停止
LDP R102                 ; 起動SW
AND LR800                ; 自動ﾓｰﾄﾞSW選択
AND MR40002              ; 起動条件
OR MR40005               ; 全起動中
ANB MR10014              ; 接着剤不足時自動OFF
ANB MR10213              ; ｷｬﾘｱWF0 or 25枚自動OFF
ANB MR40003              ; 起動OFF
ANB MR30011              ; ｻｲｸﾙ停止
OUT MR40005              ; 全起動中
OUT R302                 ; ﾀｯﾁﾊﾟﾈﾙ起動ﾗﾝﾌﾟ
TMH #600 #10             ; 自動運転開始ﾃﾞｨﾚｰﾀｲﾏ
LD T600                  ; 自動運転開始ﾃﾞｨﾚｰﾀｲﾏ
DIFU MR40006             ; 自動運転開始ﾊﾟﾙｽ
LDB R103                 ; 停止SW
ANB MR904                ; 接合機最終ACTF LG
ANB MR905                ; ｷｬﾘｱWF無しFLG
ANB MR906                ; 最終封止板FLG
ANB MR907                ; ﾎﾟｯﾄﾗｲﾌ時間ｵｰﾊﾞｰFLG
INV
LDB MR406                ; ｷｬﾘｱWF枚数異常
ANB MR407                ; 接着剤不足異常
INV
ORL
OR MR30010               ; ｻｲｸﾙ停止受付
AND MR40005              ; 全起動中
OUT MR30010              ; ｻｲｸﾙ停止受付
LD MR30010               ; ｻｲｸﾙ停止受付
AND MR11812              ; 全起動OFF
TMR #170 #3
CON
AND T170
OUT MR30011              ; ｻｲｸﾙ停止
LDB MR40005              ; 全起動中
LD MR30010               ; ｻｲｸﾙ停止受付
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT R303                 ; ﾀｯﾁﾊﾟﾈﾙ停止ﾗﾝﾌﾟ
LD LR501                 ; 搬送ﾓｰﾄﾞ
MEP
LDB LR501                ; 搬送ﾓｰﾄﾞ
MEP
ORL
AND T100                 ; 動作遅延ﾀｲﾏ-
RES MR900                ; ﾏｯﾋﾟﾝｸﾞ済FLG
END
ENDH
