DEVICE:51
;MODULE:Display
;MODULE_TYPE:0
;<h1/>フリッカー
;Flicker
LD CR2002                ; 常時ON
FLIK #30 #30 @MR000      ; ﾌﾘｯｶ
;<h1/>スタートランプ
;START Switch Lamp
LDB R50007               ; 原点復帰　完了
ANB R50000               ; 元圧ｴﾗｰ
ANB R55000               ; TRAP
ANB R50006               ; 異常発生中
ANB R50003               ; 原点復帰中
AND R4004                ; Area Sensor(B接遮断)
OR R50006                ; 異常発生中
LD R50009                ; 一時停止中(ｴﾘｱｾﾝｻｰ)
AND R4004                ; Area Sensor(B接遮断)
ORL
LD R50007                ; 原点復帰　完了
ANB R50004               ; 動作中
AND R4004                ; Area Sensor(B接遮断)
LD< DM20000 #200         ; 表示中    ﾍﾟｰｼﾞNo.
OR> DM20000 #299         ; 表示中    ﾍﾟｰｼﾞNo.
ANL
ORL
LD R50004                ; 動作中
AND R50002               ; 手動中
AND R50401               ; Post Busy
AND R4004                ; Area Sensor(B接遮断)
ORL
AND @MR000               ; ﾌﾘｯｶ
LD R50004                ; 動作中
ANB R50002               ; 手動中
ANB R50009               ; 一時停止中(ｴﾘｱｾﾝｻｰ)
ANB R50006               ; 異常発生中
ORL
LD R50004                ; 動作中
AND R50002               ; 手動中
ANB R50009               ; 一時停止中(ｴﾘｱｾﾝｻｰ)
ANB R50006               ; 異常発生中
ANB R50401               ; Post Busy
ORL
OUT MR6001               ; STARTﾗﾝﾌﾟ
;<h1/>タワーランプ　赤
;TowerLamp RED
LD R50000                ; 元圧ｴﾗｰ
OR R55000                ; TRAP
LD R50006                ; 異常発生中
LDB R50007               ; 原点復帰　完了
ANB R50004               ; 動作中
ORL
AND @MR000               ; ﾌﾘｯｶ
ORL
OUT MR6004               ; ﾀﾜｰﾗﾝﾌﾟ赤
;<h1/>タワーランプ　黄
;TowerLamp YELLOW
LD R50004                ; 動作中
ANB R50103               ; 除材要求  黄ﾗﾝﾌﾟ点滅
LD R50103                ; 除材要求  黄ﾗﾝﾌﾟ点滅
AND @MR000               ; ﾌﾘｯｶ
ORL
ANB R50000               ; 元圧ｴﾗｰ
ANB R55000               ; TRAP
OUT MR6005               ; ﾀﾜｰﾗﾝﾌﾟ黄
;<h1/>タワーランプ　緑
;TowerLamp GREEN
LDB R50004               ; 動作中
ANB R50009               ; 一時停止中(ｴﾘｱｾﾝｻｰ)
LD R50009                ; 一時停止中(ｴﾘｱｾﾝｻｰ)
AND @MR000               ; ﾌﾘｯｶ
ORL
ANB R50000               ; 元圧ｴﾗｰ
ANB R55000               ; TRAP
ANB R50006               ; 異常発生中
OUT MR6006               ; ﾀﾜｰﾗﾝﾌﾟ緑
;<h1/>ブザー
;BUZZER
LD R50000                ; 元圧ｴﾗｰ
OR R50010                ; ﾌﾞｻﾞｰON   (ｴﾘｱｾﾝｻｰ)
OR R50013                ; 異常発生  ﾌﾞｻﾞｰON
OUT MR6007               ; ﾌﾞｻﾞｰ
;<h1/>エリアセンサー表示灯
;Area Sensor Lamp
LD R50008                ; ｴﾘｱｾﾝｻｰ　有効中
OUT MR6003               ; ｴﾘｱｾﾝｻｰﾗﾝﾌﾟ
END
ENDH
