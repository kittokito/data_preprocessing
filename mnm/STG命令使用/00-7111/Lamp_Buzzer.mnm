DEVICE:53
;MODULE:Lamp_Buzzer
;MODULE_TYPE:0
;<<フリッカー>>
;<<Flicker>>
LD CR2002                ; always on
FLIK #50 #50 MR005       ; Blinking(0.5S)
FLIK #20 #20 MR006       ; Blinking(0.2S)
;ﾜｰｸﾌﾗｸﾞﾘｾｯﾄ
;Product flag reset 
LD MR003                 ; product ok
OR MR004                 ; product defective product
TMR @1 #30
CON
AND @T1
RES MR003                ; product ok
CON
RES MR004                ; product defective product
;表示　/　Indixation
;<h1/>ｽﾀｰﾄswﾗﾝﾌﾟ/Start sw lamp
;点灯：動作中、検査結果不良
;点滅：動作開始要求
;Lit: In operation, poor inspection result
;Flashing: Operation start request
LD R2000                 ; start SW
OR MR004                 ; product defective product
LD MR304                 ; origin return in progress
OR MR306                 ; auto-run during
OR MR307                 ; mainte in action
ANB @MR001
ANB @MR002               ; jig seal req
ANB MR002                ; start SW blink request
ORL
LD MR002                 ; start SW blink request
OR MR202                 ; Error cancellation request
OR @MR002                ; jig seal req
AND MR005                ; Blinking(0.5S)
ORL
OUT MR6000               ; start SW lamp
LD MR106                 ; mainte_equip. leak chk during
LD= EM30000 #828         ; (TP)crnt during display scrn No.
OR= EM30000 #829         ; (TP)crnt during display scrn No.
ANL
OUT @MR002               ; jig seal req
;<h1/>ﾌﾞｻﾞｰ/Buzzer
;異常中、検査結果不良
;
;3秒ON：検査結果良品
;During abnormality, inspection result defect
;
;3 seconds ON: inspection results good product
LDP MR902                ; error
OR MR501                 ; buzzer ON request
ORP @MR000
SET MR6001               ; buzzer
LDB MR902                ; error
ANB MR501                ; buzzer ON request
MEP
ORP MR114                ; NG reset process buzzer cancel
ORF @MR000
RES MR6001               ; buzzer
LDF MR306                ; auto-run during
AND MR003                ; product ok
SET @MR000
LD @MR000
TMR @0 #30
CON
AND @T0
RES @MR000
;<h1/>ﾀﾜｰﾗﾝﾌﾟ赤/Tower lamp red
;点灯：重度異常中
;点滅：原点復帰要求、軽度異常中、検査異常中、検査結果不良
;On: Severe error
;Blinking: Home return request, mild abnormality, inspection abnormality, inspection result defect
;EM30025=1:原点復帰要求/EM 30025 = 1: Home return request
LD MR900                 ; severe err：pwr rstr necessary
LD MR902                 ; error
LD MR304                 ; origin return in progress
AND= EM30000 #0          ; (TP)crnt during display scrn No.
AND= EM30025 #1          ; (TP)Coment display
ORL
OR MR004                 ; product defective product
AND MR005                ; Blinking(0.5S)
ORL
OUT MR6002               ; tower lamp red
;<h1/>ﾀﾜｰﾗﾝﾌﾟ黄/Tower lamp yellow
;点灯：動作中、軽度異常中、検査異常中、検査結果不良
;点滅：検査結果良品
;Lighting: Operating, Mildly Abnormal, Inspection Failure, Inspection Result Failure
;Blinking: Good results
LDB MR304                ; origin return in progress
ANB MR306                ; auto-run during
ANB MR307                ; mainte in action
ANB MR004                ; product defective product
INV
LD MR902                 ; error
ANB MR900                ; severe err：pwr rstr necessary
ORL
LD MR003                 ; product ok
AND MR005                ; Blinking(0.5S)
ORL
OUT MR6003               ; tower lamp yellow
;<h1/>ﾀﾜｰﾗﾝﾌﾟ緑/Tower lamp green
;点灯：原点復帰要求、動作開始要求、定停止中、待機中
;/Lit: Home return request, operation start request, constant stop, waiting
;EM30025=1:原点復帰要求/EM 30025 = 1: Home return request
LD MR304                 ; origin return in progress
AND= EM30000 #0          ; (TP)crnt during display scrn No.
AND= EM30025 #1          ; (TP)Coment display
LDB MR304                ; origin return in progress
ANB MR306                ; auto-run during
ANB MR307                ; mainte in action
ORL
OUT MR6004               ; tower lamp green
END
ENDH
