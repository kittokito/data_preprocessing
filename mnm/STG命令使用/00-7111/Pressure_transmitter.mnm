DEVICE:53
;MODULE:Pressure_transmitter
;MODULE_TYPE:0
;<h1/>圧力取得/Get air pressure
;<h1/>①圧力伝送器_ｲﾝｸ供給入口側_現在圧力取得/Pressure transmitter Ink feed inlet side Current pressure acquisition
LD MR300                 ; Lside pressure acquisition
ANB @MR811               ; VAL_UT32_L20_Busy
ANB @MR810               ; VAL_UT32_L20 macro startup
ANB @EM4                 ; VAL_UT32_L20 macro BUSY
MEP
DW #1 DM17               ; VAL_UT32_Slave station number
CON
DW #0 DM100              ; L side Pressure result
CON
SET @MR811               ; VAL_UT32_L20_Busy
CON
SET @MR100
;<h1/>②圧力伝送器_ｲﾝｸ供給出側_現在圧力取得/Pressure transmitter Ink feed out side Current pressure acquisition
LD MR301                 ; Rside pressure acquisition
ANB @MR811               ; VAL_UT32_L20_Busy
ANB @MR810               ; VAL_UT32_L20 macro startup
ANB @EM4                 ; VAL_UT32_L20 macro BUSY
MEP
DW #2 DM17               ; VAL_UT32_Slave station number
CON
DW #0 DM101              ; R side Pressure result
CON
SET @MR811               ; VAL_UT32_L20_Busy
CON
SET @MR100
;<h1/>現在値取得ﾌﾟﾛｸﾞﾗﾑ(①②共通)/Current value acquisition program (common to ①②)
LDB @MR811               ; VAL_UT32_L20_Busy
ANB @MR810               ; VAL_UT32_L20 macro startup
ANB @EM4                 ; VAL_UT32_L20 macro BUSY
TMH @0 #5
;START/VAL_UT32_L20
STG @MR100
JMP @MR101
STG @MR101
FMOV #0 @DM44 #2         ; VAL_UT32_L20_Acquired Value
DW #0 @DM110             ; error details
CON
SET @MR810               ; VAL_UT32_L20 macro startup
CON
JMP @MR102
;完了待ち/VAL_UT32_L20
;WAIT END
STG @MR102
AND @MR812               ; VAL_UT32_L20_END
ANB @EM4                 ; VAL_UT32_L20 macro BUSY
RES @MR810               ; VAL_UT32_L20 macro startup
CON
RES @MR812               ; VAL_UT32_L20_END
CON
JMP @MR103
STG @MR103
MPS
ANB @MR900               ; macro error
MPS
AND= DM17 #1             ; VAL_UT32_Slave station number
MOV.S @DM44 DM100        ; VAL_UT32_L20_Acquired Value  L side Pressure result
MOV.S @DM44 DM102        ; VAL_UT32_L20_Acquired Value  Trend graph 1
MRD
AND= DM17 #2             ; VAL_UT32_Slave station number
MOV.S @DM44 DM101        ; VAL_UT32_L20_Acquired Value  R side Pressure result
MOV.S @DM44 DM103        ; VAL_UT32_L20_Acquired Value  Trend graph 2
MPP
JMP @MR115
MPP
AND @MR900               ; macro error
JMP @MR104
;<h1/>通信異常/communication error
STG @MR104
ANB MR902                ; error
LDA @DM110               ; error details
CON
STA DM148                ; error details
CON
SET MR901                ; comm error
CON
JMP @MR104
STG @MR105
ANB MR901                ; comm error
RES @MR900               ; macro error
CON
JMP @MR115
;End
STG @MR115
MPS
AND= DM17 #1             ; VAL_UT32_Slave station number
RES MR300                ; Lside pressure acquisition
MRD
AND= DM17 #2             ; VAL_UT32_Slave station number
RES MR301                ; Rside pressure acquisition
MPP
RES @MR811               ; VAL_UT32_L20_Busy
CON
ENDS
;<h1/>ﾏｸﾛ/Macro
;VAL_UT32_L20  KV-L20V/R 通信ﾏｸﾛ/Communication macro
;ﾏﾉﾒｰﾀの現在値取得/Acquire current value of manometer
;■ 引数説明
;  UR0/UM0 	: ユニット番号		            	[#1 - #48]
;  P0 		: パラメータ格納先					[DMxxx,EMxxx等]
;			:  Adrs+0 : ｽﾚｰﾌﾞ局番(1～31)
;			:  Adrs+1 : 通信ﾀｲﾑｱｳﾄ(#1 - #999 ×100ms)
;  P1		: 受信データを格納するメモリー		[DMxxx,EMxxx等]
;  P2		: エラーフラグ   0:OK 1:Error		[MRxxx等]
;  P3		: エラー情報						[DMxxx,EMxxx等]
;
;■ 機能
;  ・P0の(adr+0)で指定したチャンネル番号(1～31)を使用する。
;  
;  ・ﾃﾞｨｼﾞﾀﾙ指示調節計(YOKOGAWA UT32A)から圧力データを読出し指定メモリーエリアに格納する
;    <P1> 圧力ﾃﾞｰﾀの格納ｴﾘｱ
;   
;■ エラーフラグ(P3)をセットする条件
;  ・P0(Adr+0～3)が範囲外で呼ばれたとき。
;  ・通信エラー（ﾚｽﾎﾟﾝｽ異常、受信タイムアウト）が発生した時。
;  ・通信マクロデータが異常な時。
;  ・P3にエラー情報を格納する。
;		1:ﾃﾞｰﾀ数範囲外(1-999)
;    	2:受信ﾃﾞｰﾀ異常(ﾚｰｽﾎﾟﾝｽ2)
;    	3:通信ｴﾗｰ
;    	4:通信ﾀｲﾑｱｳﾄ
;    	5:通信ﾏｸﾛｴﾗｰ
;注意：検査左右で共通のﾏｸﾛを使用する為、起動前に排他ﾌﾗｸﾞをﾁｪｯｸする事
;Note: In order to use common macros on the examination left and right, check the exclusion flag before starting
;共通設定/Common setting
;通信ﾀｲﾑｱｳﾄ時間/Communication timeout time
LD CR2002                ; always on
LDA DM17                 ; VAL_UT32_Slave station number
CON
STA @DM42                ; VAL_UT32_Slave station number
CON
DW #100 @DM43            ; VAL_UT32_Communication timeout
LD @MR810                ; VAL_UT32_L20 macro startup
MEP
MSTRT VAL_UT32_L20_1 #2 @DM42 @DM44 @MR900 @DM110 @MR812;VAL_UT32_Slave station number  VAL_UT32_L20_Acquired Value  macro error  error details  VAL_UT32_L20_END
END
ENDH
