DEVICE:53
;MODULE:UT1
;MODULE_TYPE:0
;非常停止
LD R50010                ; Emergency　Stop
OR @MR001
ZRES @MR100 @MR9015      ; Err07 ﾜｰｸ浮き表示  Err130 解除完了
CON
OUT @MR001
TMR @59 #5
LD @T59
MDSTOP UT1
LDB R50300               ; Pause
ANB R50111               ; Err ST1
ANB R50110               ; Err INDEX
OUT @MR002               ; PAUSE・ERROR無し
LD R4101                 ; 1ST ﾜｰｸ浮き検出1(内側)
AND R4102                ; 1ST ﾜｰｸ浮き検出2(外側)
OR R601                  ; 浮きｾﾝｻ無視
TMH @2 #0                ; 浮き検出安定時間
CON
AND @T2                  ; 浮き検出安定時間
OUT @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
;<h1/>■ HOME
LD R50003                ; HOME
ANB R50201               ; UT1 Do
SET R50201               ; UT1 Do
CON
SET @MR1113              ; HOMEｽﾀｰﾄ
LDB R50003               ; HOME
ANB R50001               ; AUTO
ANB R50002               ; STEP
AND @MR1015              ; UT1 HOME完了
RES R50201               ; UT1 Do
CON
RES @MR1015              ; UT1 HOME完了
LD @MR1113               ; HOMEｽﾀｰﾄ
AND= DM4000 #3           ; TP 現在表示中画面
TMH @31 #10              ; ﾀｲﾐﾝｸﾞ
CON
AND @T31                 ; ﾀｲﾐﾝｸﾞ
DIFU @MR1114             ; HOMEｽﾀｰﾄPLS
LD @MR1114               ; HOMEｽﾀｰﾄPLS
SET @MR1100              ; 次ｽﾃｯﾌﾟへ
STG @MR1100              ; 次ｽﾃｯﾌﾟへ
AND @MR002               ; PAUSE・ERROR無し
RES @MR1113              ; HOMEｽﾀｰﾄ
CON
SET R50901               ; IND1 ST1  ｻｲｸﾙ動作中
CON
JMP @MR1101
STG @MR1101
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
JMP @MR1102
STG @MR1102
MPS
LD R1200                 ; Area sensor
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
ANL
JMP @MR1103
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR1101
STG @MR1103
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
RES MR6106               ; ｹｰｽ浮き押さえ　前進
CON
SET MR6107               ; ｹｰｽ浮き押さえ　後退
CON
JMP @MR1104
STG @MR1104
MPS
LD R1200                 ; Area sensor
ANB R4006                ; ｹｰｽ浮き押さえ 前進限
AND R4007                ; ｹｰｽ浮き押さえ 後退限
ANL
JMP @MR1915              ; HOME 終了へ
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR1103
;Home END
STG @MR1915              ; HOME 終了へ
AND @MR002               ; PAUSE・ERROR無し
AND MR600                ; IND1      ｻｲｸﾙ動作中
RES R50901               ; IND1 ST1  ｻｲｸﾙ動作中
CON
SET R50411               ; UT1        1ｻｲｸﾙ終了
CON
SET @MR1015              ; UT1 HOME完了
CON
ENDS
;<h1/>■ AUTO
LD R50001                ; AUTO
ANB R50201               ; UT1 Do
SET R50201               ; UT1 Do
CON
SET @MR4000              ; UT3 AUTOｽﾀｰﾄ
LDB R50003               ; HOME
ANB R50001               ; AUTO
ANB R50002               ; STEP
AND @MR4001              ; UT1 AUTO終了
RES R50201               ; UT1 Do
CON
RES @MR4001              ; UT1 AUTO終了
;AUTO Operation Start
STG @MR4000              ; UT3 AUTOｽﾀｰﾄ
SET R50503               ; ST1 動作中
CON
JMP @MR4003
STG @MR4003
MPS
ANB LR41107              ; AUTO-7 背面sw有効
SET @MR7900              ; CSICﾃﾞｰﾀｾｯﾄ
CON
JMP @MR4005
MPP
AND LR41107              ; AUTO-7 背面sw有効
JMP @MR4004              ; 背面吐出ﾓｰﾄﾞ終了待ち
STG @MR4004              ; 背面吐出ﾓｰﾄﾞ終了待ち
AND R50000               ; HOLD
RES R50503               ; ST1 動作中
CON
JMP @MR4908              ; 終了へ
STG @MR4005
MPS
AND R50209               ; IND1     動作開始
SET R50901               ; IND1 ST1  ｻｲｸﾙ動作中
CON
ANB R50000               ; HOLD
JMP @MR4006              ; 次ｽﾃｯﾌﾟへ
MPP
LDB R50209               ; IND1     動作開始
AND R50000               ; HOLD
AND R50213               ; IND1　　　払出し完了
ANL
JMP @MR4908              ; 終了へ
;ﾓﾃﾞﾙﾁｪｯｸ / Model check
STG @MR4006              ; 次ｽﾃｯﾌﾟへ
MPS
LD LR41100               ; AUTO-0    Normal
OR LR41101               ; AUTO-1 ｲﾝｸ選択1
OR LR41102               ; AUTO-2 ｲﾝｸ選択2
OR LR41103               ; AUTO-3 ｲﾝｸ選択3
OR LR41104               ; AUTO-4 ｲﾝｸ選択4
OR LR41105               ; AUTO-4 ｲﾝｸ選択1-3
ANB MR19008              ; 注入圧力異常記憶
ANL
SET R50210               ; Model OK
CON
RES R50406               ; ｽﾀｰﾄSWﾗﾝﾌﾟ点滅
CON
SET R50408               ; ｽﾀｰﾄSWﾗﾝﾌﾟ 点灯
CON
JMP @MR4400              ; 冶具解放へ
MRD
LDB LR41100              ; AUTO-0    Normal
ANB LR41101              ; AUTO-1 ｲﾝｸ選択1
ANB LR41102              ; AUTO-2 ｲﾝｸ選択2
ANB LR41103              ; AUTO-3 ｲﾝｸ選択3
ANB LR41104              ; AUTO-4 ｲﾝｸ選択4
ANB LR41105              ; AUTO-4 ｲﾝｸ選択1-3
ANL
SET @MR508               ; ﾓﾃﾞﾙｴﾗｰ表示へ
CON
JMP @MR4007              ; ﾓﾃﾞﾙｴﾗｰの時へ
MPP
AND MR19008              ; 注入圧力異常記憶
SET R50009               ; HOLDRQ
CON
JMP @MR4908              ; 終了へ
;ﾓﾃﾞﾙｴﾗｰの時 / When model erorr
STG @MR4007              ; ﾓﾃﾞﾙｴﾗｰの時へ
AND @MR509               ; ﾓﾃﾞﾙｴﾗｰ表示中
JMP @MR4008              ; 次ｽﾃｯﾌﾟへ
STG @MR4008              ; 次ｽﾃｯﾌﾟへ
ANB @MR509               ; ﾓﾃﾞﾙｴﾗｰ表示中
AND @MR515               ; ﾓﾃﾞﾙｴﾗｰ表示終了
RES R50210               ; Model OK
CON
RES @MR515               ; ﾓﾃﾞﾙｴﾗｰ表示終了
CON
SET R50009               ; HOLDRQ
CON
JMP @MR4908              ; 終了へ
;AUTO動作開始
;AUTO Operation Start
STG @MR4200              ; AUTO Main
MPS
AND R50209               ; IND1     動作開始
MPS
SET R50503               ; ST1 動作中
CON
SET R50901               ; IND1 ST1  ｻｲｸﾙ動作中
CON
JMP @MR4201              ; CSIC 書込み
MPP
AND= DM1040 #1           ; ST1(回転後) ﾜｰｸ有無(0=無)
SET MR39805              ; ﾜｰｸ未除材
MPP
LDB R50209               ; IND1     動作開始
AND R50000               ; HOLD
AND R50213               ; IND1　　　払出し完了
ANL
JMP @MR4908              ; 終了へ
;<h1/>  CSIC書込み/CSIC write
STG @MR4201              ; CSIC 書込み
MPS
LD MR003                 ; AP03 CSIC無し
OR<> DM1040 #1           ; ST1(回転後) ﾜｰｸ有無(0=無)
OR<> DM1045 #0           ; ST1(回転後) NGﾜｰｸ 0=OK
ANL
JMP @MR4400              ; 冶具解放へ
MPP
LDB MR003                ; AP03 CSIC無し
AND= DM1040 #1           ; ST1(回転後) ﾜｰｸ有無(0=無)
AND= DM1045 #0           ; ST1(回転後) NGﾜｰｸ 0=OK
AND @MR002               ; PAUSE・ERROR無し
AND @MR7901              ; CSICﾃﾞｰﾀｾｯﾄ完了
ANL
JMP @MR4202              ; Next STEP
;ｹｰｽｸﾗﾝﾌﾟ / Case clamp
STG @MR4202              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
SET MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
AND T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
JMP @MR4203              ; Next STEP
;浮き押さえ前進 / Case floating holder FWD
STG @MR4203              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
RES MR6107               ; ｹｰｽ浮き押さえ　後退
CON
SET MR6106               ; ｹｰｽ浮き押さえ　前進
CON
JMP @MR4204              ; Next STEP
STG @MR4204              ; Next STEP
MPS
LD R1200                 ; Area sensor
AND R4006                ; ｹｰｽ浮き押さえ 前進限
ANL
JMP @MR4205              ; Next STEP
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR4203              ; Next STEP
;ｹｰｽ浮き確認  / Case floating check
STG @MR4205              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
JMP @MR4300              ; Next STEP
MPP
ANB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
TMR @10 #20              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T10                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
SET @MR100               ; Err07 ﾜｰｸ浮き表示
CON
JMP @MR4206              ; Next STEP
STG @MR4206              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR107               ; Err07 解除完了
RES @MR107               ; Err07 解除完了
CON
JMP @MR4205              ; Next STEP
;CSIC書込み/CSIC write
STG @MR4300              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB @MR7602              ; IC書き込み中
SET R50702               ; CT4 CSIC書込み時間
CON
SET @MR7600              ; IC書き込み
CON
JMP @MR4301              ; Next STEP
STG @MR4301              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB @MR7602              ; IC書き込み中
AND @MR7601              ; IC書き込み　完了
RES @MR7601              ; IC書き込み　完了
CON
MPS
AND @MR1800              ; CSIC書込みOK
JMP @MR4306              ; OK
MPP
ANB @MR1800              ; CSIC書込みOK
MPS
ANB @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
JMP @MR4302              ; Next STEP
MPP
AND @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
JMP @MR4304              ; NG
;IC書き込み  自動リトライ
STG @MR4302              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
TMR @17 #3               ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T17                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
SET @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
CON
JMP @MR4300              ; Next STEP
;*** Err121 CSIC書込みｴﾗｰ / CSIC writing error
STG @MR4304              ; NG
RES @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
CON
SET @MR804               ; Err121 CSIC Data write ERR
CON
JMP @MR4305              ; Next STEP
STG @MR4305              ; Next STEP
AND @MR807               ; Err121 Release
RES R50702               ; CT4 CSIC書込み時間
CON
RES @MR807               ; Err121 Release
CON
JMP @MR4400              ; 冶具解放へ
;***
STG @MR4306              ; OK
RES @MR1800              ; CSIC書込みOK
CON
RES @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
CON
RES R50702               ; CT4 CSIC書込み時間
CON
JMP @MR4400              ; 冶具解放へ
;<h1/>  ｹｰｽ開放 / Case Release
;冶具解放
STG @MR4400              ; 冶具解放へ
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
RES MR6106               ; ｹｰｽ浮き押さえ　前進
CON
SET MR6107               ; ｹｰｽ浮き押さえ　後退
CON
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
JMP @MR4401              ; Next STEP
STG @MR4401              ; Next STEP
MPS
LD R1200                 ; Area sensor
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
ANB R4006                ; ｹｰｽ浮き押さえ 前進限
AND R4007                ; ｹｰｽ浮き押さえ 後退限
ANB T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
ANL
JMP @MR4402
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR4400              ; 冶具解放へ
STG @MR4402
MPS
RES R50700               ; CT2 MC動作時間
CON
SET R50701               ; CT3 OP作業時間
CON
SET R50310               ; 出力ｶｳﾝﾄ
CON
RES MR2202               ; ｴﾘｱｾﾝｻｰ監視中
CON
JMP @MR4500              ; 作業者動作完了待ち
MPP
LD= DM1040 #1            ; ST1(回転後) ﾜｰｸ有無(0=無)
AND= DM1045 #0           ; ST1(回転後) NGﾜｰｸ 0=OK
ANL
SET MR111                ; ｻｲｸﾙ完了ﾌﾞｻﾞｰ
;<h1/>  作業者動作完了待ち / Waiting for Pack remove & set up
STG @MR4500              ; 作業者動作完了待ち
AND @MR002               ; PAUSE・ERROR無し
MPS
RES R50408               ; ｽﾀｰﾄSWﾗﾝﾌﾟ 点灯
CON
AND R1200                ; Area sensor
AND R50105               ; ｽﾀｰﾄSW ON
JMP @MR4501              ; Next STEP
MPP
ANB MR39805              ; ﾜｰｸ未除材
SET R50406               ; ｽﾀｰﾄSWﾗﾝﾌﾟ点滅
LD MR39805               ; ﾜｰｸ未除材
ANB R1200                ; Area sensor
ANB R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
DW #0 DM1040             ; ST1(回転後) ﾜｰｸ有無(0=無)
CON
DW #0 DM1045             ; ST1(回転後) NGﾜｰｸ 0=OK
CON
RES MR39805              ; ﾜｰｸ未除材
;ﾜｰｸ除材確認 / Work non remove check
STG @MR4501              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES R50406               ; ｽﾀｰﾄSWﾗﾝﾌﾟ点滅
CON
SET R50408               ; ｽﾀｰﾄSWﾗﾝﾌﾟ 点灯
CON
MPS
ANB MR39805              ; ﾜｰｸ未除材
JMP @MR4600              ; Next STEP
MPP
AND MR39805              ; ﾜｰｸ未除材
SET @MR400               ; ﾜｰｸ未除材ｴﾗｰ表示へ
CON
JMP @MR4502              ; Next STEP
;ﾜｰｸ未除材の時 / When work non remove detection
STG @MR4502              ; Next STEP
AND @MR401               ; ﾜｰｸ未除材ｴﾗｰ表示中
JMP @MR4503              ; Next STEP
STG @MR4503              ; Next STEP
ANB @MR401               ; ﾜｰｸ未除材ｴﾗｰ表示中
AND @MR407               ; ﾜｰｸ未除材ｴﾗｰ表示終了
RES @MR407               ; ﾜｰｸ未除材ｴﾗｰ表示終了
CON
JMP @MR4504
STG @MR4504
MPS
AND R50000               ; HOLD
RES R50406               ; ｽﾀｰﾄSWﾗﾝﾌﾟ点滅
CON
JMP @MR4908              ; 終了へ
MPP
ANB R50000               ; HOLD
JMP @MR4500              ; 作業者動作完了待ち
;<h1/>  ｹｰｽ給材 / Case set
;ﾜｰｸ未給材ﾁｪｯｸ / Work non set check
STG @MR4600              ; Next STEP
JMP @MR4700              ; ﾜｰｸ浮きﾁｪｯｸへ
;ﾜｰｸ浮きﾁｪｯｸ / Work flotage check 
STG @MR4700              ; ﾜｰｸ浮きﾁｪｯｸへ
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
RES R50701               ; CT3 OP作業時間
CON
SET R50700               ; CT2 MC動作時間
CON
JMP @MR4708              ; ﾜｰｸｸﾗﾝﾌﾟ
MPP
ANB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
TMR @11 #20              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T11                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
SET @MR100               ; Err07 ﾜｰｸ浮き表示
CON
JMP @MR4701              ; ﾜｰｸ浮きの時へ
;ﾜｰｸ浮きの時 / When Work flotage check
STG @MR4701              ; ﾜｰｸ浮きの時へ
AND @MR107               ; Err07 解除完了
RES @MR107               ; Err07 解除完了
CON
JMP @MR4702              ; Next STEP
STG @MR4702              ; Next STEP
MPS
AND R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
JMP @MR4700              ; ﾜｰｸ浮きﾁｪｯｸへ
MPP
ANB R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
TMR @9 #20
CON
AND @T9
JMP @MR4400              ; 冶具解放へ
;ｶｰﾄﾘｯｼﾞｸﾗﾝﾌﾟ
;Cartridge clamping
STG @MR4708              ; ﾜｰｸｸﾗﾝﾌﾟ
AND @MR002               ; PAUSE・ERROR無し
ANB @MR7702              ; ﾜｰｸｸﾗﾝﾌﾟ中
SET @MR7700              ; ﾜｰｸｸﾗﾝﾌﾟ
CON
JMP @MR4709              ; Next STEP
STG @MR4709              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
RES @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
CON
JMP @MR4710              ; Next STEP
;ｸﾗﾝﾌﾟ後浮き上がり確認
;Floating check after clamp
STG @MR4710              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
JMP @MR4800              ; ｶﾗｰﾌﾞﾛｯｸﾁｪｯｸ
MPP
ANB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
TMR @12 #20              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T12                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
JMP @MR4711              ; ﾜｰｸ浮きの時へ
;ﾜｰｸ浮きの時 / When Work flotage check
STG @MR4711              ; ﾜｰｸ浮きの時へ
AND @MR002               ; PAUSE・ERROR無し
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
ANB T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
JMP @MR4712              ; Next STEP
STG @MR4712              ; Next STEP
RES MR2202               ; ｴﾘｱｾﾝｻｰ監視中
CON
SET @MR100               ; Err07 ﾜｰｸ浮き表示
CON
JMP @MR4713              ; Next STEP
STG @MR4713              ; Next STEP
AND @MR107               ; Err07 解除完了
RES @MR107               ; Err07 解除完了
CON
JMP @MR4400              ; 冶具解放へ
;海外向けｶｰﾄﾘｯｼﾞﾁｪｯｸ (ｶﾗｰﾌﾞﾛｯｸ)　　　　　　　　　　　　 DM216  1:Color block　有り　　　2:Color block　無し
;Cartridge for forigin check　(Color block　check)      DM216  1:Color block            2:No Color block
STG @MR4800              ; ｶﾗｰﾌﾞﾛｯｸﾁｪｯｸ
MPS
AND R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
MPS
AND MR012                ; AP12 ON=ﾘﾌﾞ検出無効
JMP @MR4807              ; Next STEP
MPP
LDB MR012                ; AP12 ON=ﾘﾌﾞ検出無効
AND= DM287 #3            ; ModelIdentficationNumber
ANL
MPS
AND= DM216 #0            ; CBCode
MPS
ANB R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
JMP @MR4807              ; Next STEP
MPP
AND R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
TMR @36 #20
CON
AND @T36
JMP @MR4801              ; NG　ｲﾎﾞ有り
MPP
AND= DM216 #1            ; CBCode
MPS
AND R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
JMP @MR4807              ; Next STEP
MPP
ANB R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
TMR @37 #20
CON
AND @T37
JMP @MR4803              ; NG ｲﾎﾞ無し
MPP
ANB R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
DW #0 DM1000             ; ST1 ﾜｰｸ有無(0=無)
CON
JMP @MR4900              ; Next STEP
;*** Err40 Color block ERR (ON)
STG @MR4801              ; NG　ｲﾎﾞ有り
SET MR39805              ; ﾜｰｸ未除材
CON
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
SET @MR704               ; Err40 Color block ERR (ON)
CON
JMP @MR4802              ; Next STEP
STG @MR4802              ; Next STEP
MPS
AND @MR707               ; Err40 Release
RES @MR707               ; Err40 Release
CON
JMP @MR4400              ; 冶具解放へ
MPP
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
RES MR2202               ; ｴﾘｱｾﾝｻｰ監視中
;*** Err41 Color block ERR (OFF)
STG @MR4803              ; NG ｲﾎﾞ無し
SET MR39805              ; ﾜｰｸ未除材
CON
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
SET @MR708               ; Err41 Color block ERR (OFF)
CON
JMP @MR4804              ; Next STEP
STG @MR4804              ; Next STEP
MPS
AND @MR711               ; Err41 Release
RES @MR711               ; Err41 Release
CON
JMP @MR4400              ; 冶具解放へ
MPP
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
RES MR2202               ; ｴﾘｱｾﾝｻｰ監視中
;***
STG @MR4807              ; Next STEP
DW #1 DM1000             ; ST1 ﾜｰｸ有無(0=無)
CON
JMP @MR4900              ; Next STEP
;AUTO 1ｻｲｸﾙ終了処理
;AUTO 1Cycle end Operation
STG @MR4900              ; Next STEP
AND MR600                ; IND1      ｻｲｸﾙ動作中
SET B025                 ; DC_Quality2
CON
RES R50503               ; ST1 動作中
CON
SET R50411               ; UT1        1ｻｲｸﾙ終了
CON
JMP @MR4908              ; 終了へ
STG @MR4908              ; 終了へ
RES R50901               ; IND1 ST1  ｻｲｸﾙ動作中
CON
ANB R50209               ; IND1     動作開始
AND R50409               ; IND1      ｻｲｸﾙ完了
MPS
LDB R50000               ; HOLD
ORB R50213               ; IND1　　　払出し完了
ANL
JMP @MR4200              ; AUTO Main
MPP
LD R50000                ; HOLD
AND R50213               ; IND1　　　払出し完了
ANL
JMP @MR4909              ; Next STEP
;AUTO 終了処理
;AUTO Finish Operation
STG @MR4909              ; Next STEP
RES R50701               ; CT3 OP作業時間
CON
RES R50700               ; CT2 MC動作時間
CON
RES @MR7901              ; CSICﾃﾞｰﾀｾｯﾄ完了
CON
RES R50408               ; ｽﾀｰﾄSWﾗﾝﾌﾟ 点灯
CON
RES R50406               ; ｽﾀｰﾄSWﾗﾝﾌﾟ点滅
CON
SET @MR4001              ; UT1 AUTO終了
CON
ENDS
;<h1/>■ STEP
LD R50002                ; STEP
ANB R50201               ; UT1 Do
SET R50201               ; UT1 Do
CON
SET @MR3000              ; UT1 STEP ｽﾀｰﾄ
LDB R50003               ; HOME
ANB R50001               ; AUTO
ANB R50002               ; STEP
AND @MR3915              ; UT1 STEP終了
RES R50201               ; UT1 Do
CON
RES @MR3915              ; UT1 STEP終了
LDB LR40408              ; UT3 STEP-8 CSIC機種切替
ANB LR40409              ; UT3 STEP-9 CSIC書込み
ANB LR40410              ; UT3 STEP-10 ﾘﾌﾞ検査
OUB @MR014               ; ST1 STEP Parameter OK
STG @MR3000              ; UT1 STEP ｽﾀｰﾄ
AND R50201               ; UT1 Do
JMP @MR3001              ; 次ｽﾃｯﾌﾟへ
;選択STEP確認 / Select step mode check
STG @MR3001              ; 次ｽﾃｯﾌﾟへ
MPS
AND LR40408              ; UT3 STEP-8 CSIC機種切替
JMP @MR3200              ; STEP08 CSIC機種切替
MRD
AND LR40409              ; UT3 STEP-9 CSIC書込み
JMP @MR3300              ; STEP-9 CSIC書込み
MRD
AND LR40410              ; UT3 STEP-10 ﾘﾌﾞ検査
JMP @MR3500              ; STEP-10 ﾘﾌﾞ検査
MPP
ANB @MR014               ; ST1 STEP Parameter OK
JMP @MR3015              ; STEP終了処理
;STEP終了処理
STG @MR3015              ; STEP終了処理
RES R50701               ; CT3 OP作業時間
CON
RES R50503               ; ST1 動作中
CON
RES R50700               ; CT2 MC動作時間
CON
SET R50411               ; UT1        1ｻｲｸﾙ終了
CON
SET @MR3915              ; UT1 STEP終了
CON
ENDS
;<h1/>  STEP08 CSIC機種切替
STG @MR3200              ; STEP08 CSIC機種切替
AND @MR002               ; PAUSE・ERROR無し
JMP @MR3201              ; Next STEP
STG @MR3201              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
SET @MR7500              ; CSIC機種切替
CON
JMP @MR3202              ; Next STEP
STG @MR3202              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR7501              ; CSIC PC機種切替Sub完了
MPS
AND @MR1806              ; CSIC機種切替OK
RES @MR7501              ; CSIC PC機種切替Sub完了
CON
JMP @MR3204              ; Next STEP
MPP
ANB @MR1806              ; CSIC機種切替OK
SET @MR800               ; Err120 CSIC Data set ERR
CON
RES @MR7501              ; CSIC PC機種切替Sub完了
CON
JMP @MR3203              ; Next STEP
STG @MR3203              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR803               ; Err120 Release
RES @MR803               ; Err120 Release
CON
JMP @MR3204              ; Next STEP
STG @MR3204              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB @MR7501              ; CSIC PC機種切替Sub完了
ANB @MR7502              ; CSIC PC機種切替Sub中
ANB @MR7500              ; CSIC機種切替
JMP @MR3015              ; STEP終了処理
;<h1/>  STEP09 CSIC書込
;ｹｰｽﾁｪｯｸ
STG @MR3300              ; STEP-9 CSIC書込み
AND @MR002               ; PAUSE・ERROR無し
MPS
LD R4100                 ; 1ST ﾜﾜｰｸ検出　  ON=有
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
ANL
JMP @MR3301              ; Next STEP
MPP
LDB R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
ORB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
ANL
TMR @33 #20
CON
AND @T33
JMP @MR3015              ; STEP終了処理
;ﾜｰｸｸﾗﾝﾌﾟ
STG @MR3301              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
SET @MR7700              ; ﾜｰｸｸﾗﾝﾌﾟ
CON
JMP @MR3302              ; Next STEP
STG @MR3302              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
RES @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
CON
JMP @MR3303              ; Next STEP
;ｸﾗﾝﾌﾟ後浮き上がり確認
;Floating check after clamp
STG @MR3303              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
JMP @MR3308              ; Next STEP
MPP
ANB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
TMR @13 #20              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T13                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
SET @MR100               ; Err07 ﾜｰｸ浮き表示
CON
JMP @MR3304              ; Next STEP
;ﾜｰｸ浮きの時 / When Work flotage check
STG @MR3304              ; Next STEP
AND @MR101               ; ﾜｰｸ浮き表示中
JMP @MR3305              ; Next STEP
STG @MR3305              ; Next STEP
ANB @MR101               ; ﾜｰｸ浮き表示中
AND @MR107               ; Err07 解除完了
RES @MR107               ; Err07 解除完了
CON
JMP @MR3306              ; Next STEP
STG @MR3306              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
JMP @MR3400              ; ｸﾗﾝﾌﾟ解除へ
;浮き押さえ前進
STG @MR3308              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
AND R1200                ; Area sensor
RES MR6107               ; ｹｰｽ浮き押さえ　後退
CON
SET MR6106               ; ｹｰｽ浮き押さえ　前進
CON
JMP @MR3309              ; Next STEP
STG @MR3309              ; Next STEP
MPS
LD R1200                 ; Area sensor
AND R4006                ; ｹｰｽ浮き押さえ 前進限
ANL
JMP @MR3310              ; Next STEP
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR3308              ; Next STEP
;CSIC 書込み
STG @MR3310              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
SET R50702               ; CT4 CSIC書込み時間
CON
SET @MR7600              ; IC書き込み
CON
JMP @MR3311              ; Next STEP
STG @MR3311              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB @MR7602              ; IC書き込み中
AND @MR7601              ; IC書き込み　完了
RES R50702               ; CT4 CSIC書込み時間
CON
RES @MR7601              ; IC書き込み　完了
CON
MPS
AND @MR1800              ; CSIC書込みOK
JMP @MR3314              ; Next STEP
MPP
ANB @MR1800              ; CSIC書込みOK
JMP @MR3312              ; Next STEP
;*** Err121 CSIC Data write ERR
STG @MR3312              ; Next STEP
SET @MR804               ; Err121 CSIC Data write ERR
CON
JMP @MR3313              ; Next STEP
STG @MR3313              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR807               ; Err121 Release
RES @MR807               ; Err121 Release
CON
JMP @MR3400              ; ｸﾗﾝﾌﾟ解除へ
;***
STG @MR3314              ; Next STEP
RES @MR1800              ; CSIC書込みOK
CON
JMP @MR3400              ; ｸﾗﾝﾌﾟ解除へ
;ｹｰｽ開放
STG @MR3400              ; ｸﾗﾝﾌﾟ解除へ
AND @MR002               ; PAUSE・ERROR無し
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3401              ; Next STEP
STG @MR3401              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES MR6106               ; ｹｰｽ浮き押さえ　前進
CON
SET MR6107               ; ｹｰｽ浮き押さえ　後退
CON
ANB R4006                ; ｹｰｽ浮き押さえ 前進限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3402              ; Next STEP
STG @MR3402              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
ANB T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3015              ; STEP終了処理
;<h1/>  STEP10 ﾘﾌﾞ検査
STG @MR3500              ; STEP-10 ﾘﾌﾞ検査
AND @MR002               ; PAUSE・ERROR無し
MPS
LD R4100                 ; 1ST ﾜﾜｰｸ検出　  ON=有
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
ANL
JMP @MR3501              ; Next STEP
MPP
LDB R4100                ; 1ST ﾜﾜｰｸ検出　  ON=有
ORB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
ANL
TMR @34 #20
CON
AND @T34
JMP @MR3015              ; STEP終了処理
STG @MR3501              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
SET @MR7700              ; ﾜｰｸｸﾗﾝﾌﾟ
CON
JMP @MR3502              ; Next STEP
STG @MR3502              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
RES @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
CON
JMP @MR3503              ; Next STEP
;ｸﾗﾝﾌﾟ後浮き上がり確認
;Floating check after clamp
STG @MR3503              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
JMP @MR3508              ; Next STEP
MPP
ANB @MR004               ; ｶｰﾄﾘｯｼﾞ浮きOK
TMR @14 #20              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T14                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
SET @MR100               ; Err07 ﾜｰｸ浮き表示
CON
JMP @MR3504              ; Next STEP
;ﾜｰｸ浮きの時 / When Work flotage check
STG @MR3504              ; Next STEP
AND @MR101               ; ﾜｰｸ浮き表示中
JMP @MR3505              ; Next STEP
STG @MR3505              ; Next STEP
ANB @MR101               ; ﾜｰｸ浮き表示中
AND @MR107               ; Err07 解除完了
RES @MR107               ; Err07 解除完了
CON
JMP @MR3506              ; Next STEP
STG @MR3506              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
JMP @MR3600              ; ｸﾗﾝﾌﾟ解除へ
;***
STG @MR3508              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND= DM287 #3            ; ModelIdentficationNumber
MPS
AND= DM216 #0            ; CBCode
MPS
ANB R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
JMP @MR3600              ; ｸﾗﾝﾌﾟ解除へ
MPP
AND R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
TMR @38 #20
CON
AND @T38
JMP @MR3509              ; ﾘﾌﾞ検査NG（ON）へ
MPP
AND= DM216 #1            ; CBCode
MPS
AND R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
JMP @MR3600              ; ｸﾗﾝﾌﾟ解除へ
MPP
ANB R4104                ; ｹｰｽ照合ｾﾝｻ1(4C) OUT1
TMR @39 #20
CON
AND @T39
JMP @MR3511              ; ﾘﾌﾞ検査NG（OFF）へ
;*** Err40 Color block ERR (ON)
STG @MR3509              ; ﾘﾌﾞ検査NG（ON）へ
AND @MR002               ; PAUSE・ERROR無し
SET @MR704               ; Err40 Color block ERR (ON)
CON
JMP @MR3510              ; Next STEP
STG @MR3510              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR707               ; Err40 Release
RES @MR707               ; Err40 Release
CON
JMP @MR3600              ; ｸﾗﾝﾌﾟ解除へ
;*** Err41 Color block ERR (OFF)
STG @MR3511              ; ﾘﾌﾞ検査NG（OFF）へ
AND @MR002               ; PAUSE・ERROR無し
SET @MR708               ; Err41 Color block ERR (OFF)
CON
JMP @MR3512              ; Next STEP
STG @MR3512              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
AND @MR711               ; Err41 Release
RES @MR711               ; Err41 Release
CON
JMP @MR3600              ; ｸﾗﾝﾌﾟ解除へ
;ｹｰｽ開放
STG @MR3600              ; ｸﾗﾝﾌﾟ解除へ
AND @MR002               ; PAUSE・ERROR無し
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3601              ; Next STEP
STG @MR3601              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES MR6106               ; ｹｰｽ浮き押さえ　前進
CON
SET MR6107               ; ｹｰｽ浮き押さえ　後退
CON
ANB R4006                ; ｹｰｽ浮き押さえ 前進限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3602              ; Next STEP
STG @MR3602              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
RES MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
SET MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
ANB T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
AND T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3015              ; STEP終了処理
;<h1/>■ Subroutine
;<h1/>  CSIC機種切替
;  CSIC Model Change
STG @MR7500              ; CSIC機種切替
MPS
ANB R611                 ; 出荷前CSIC無視
SET @MR7502              ; CSIC PC機種切替Sub中
CON
JMP @MR7503              ; Next STEP
MPP
AND R611                 ; 出荷前CSIC無視
SET @MR1806              ; CSIC機種切替OK
CON
JMP @MR7506              ; Next STEP
STG @MR7503              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
SET @MR1910              ; CSIC機種切替Pack
CON
RES @MR1806              ; CSIC機種切替OK
CON
JMP @MR7504              ; Next STEP
STG @MR7504              ; Next STEP
MPS
LD= DM287 #1             ; ModelIdentficationNumber
OR CR2002                ; 常時ON
AND MR13615              ; コマンド受付許可（非通信中)
ANL
ZRES MR13600 MR13611     ; CH1 CSIC書込み実行完了  CH2 CSIC機種切替実行失敗
CON
SET MR13508              ; Ch1選択
CON
RES MR13509              ; Ch2選択
CON
RES MR13510              ; ﾘﾄﾗｲ
CON
SET MR13502              ; CSIC機種切替実行開始
CON
JMP @MR7505              ; Next STEP
MPP
LD= DM287 #2             ; ModelIdentficationNumber
OR= DM287 #3             ; ModelIdentficationNumber
ANB CR2002               ; 常時ON
AND MR13815              ; コマンド受付許可（非通信中)
ANL
ZRES MR13800 MR13811     ; CH1 CSIC書込み実行完了  CH2 CSIC機種切替実行失敗
CON
SET MR13708              ; Ch1選択
CON
RES MR13709              ; Ch2選択
CON
RES MR13710              ; ﾘﾄﾗｲ
CON
SET MR13702              ; CSIC機種切替実行開始
CON
JMP @MR7505              ; Next STEP
STG @MR7505              ; Next STEP
MPS
LD= DM287 #1             ; ModelIdentficationNumber
OR CR2002                ; 常時ON
ANL
MPS
AND MR13604              ; CH1 CSIC機種切替実行完了
SET @MR1806              ; CSIC機種切替OK
CON
JMP @MR7506              ; Next STEP
MPP
AND MR13605              ; CH1 CSIC機種切替実行失敗
RES @MR1806              ; CSIC機種切替OK
CON
JMP @MR7506              ; Next STEP
MPP
LD= DM287 #2             ; ModelIdentficationNumber
OR= DM287 #3             ; ModelIdentficationNumber
ANB CR2002               ; 常時ON
ANL
MPS
AND MR13804              ; CH1 CSIC機種切替実行完了
SET @MR1806              ; CSIC機種切替OK
CON
JMP @MR7506              ; Next STEP
MPP
AND MR13805              ; CH1 CSIC機種切替実行失敗
RES @MR1806              ; CSIC機種切替OK
CON
JMP @MR7506              ; Next STEP
STG @MR7506              ; Next STEP
RES MR13804              ; CH1 CSIC機種切替実行完了
CON
RES MR13805              ; CH1 CSIC機種切替実行失敗
CON
RES MR13605              ; CH1 CSIC機種切替実行失敗
CON
RES MR13604              ; CH1 CSIC機種切替実行完了
CON
RES MR2501               ; CSIC model switch Driving
CON
RES @MR1910              ; CSIC機種切替Pack
CON
RES @MR7502              ; CSIC PC機種切替Sub中
CON
SET @MR7501              ; CSIC PC機種切替Sub完了
CON
ENDS
;<h1/>　IC書き込み
;　IC write
STG @MR7600              ; IC書き込み
SET @MR7602              ; IC書き込み中
CON
JMP @MR7603              ; Next STEP
STG @MR7603              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
SET MR2202               ; ｴﾘｱｾﾝｻｰ監視中
CON
RES MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
SET MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
JMP @MR7604              ; Next STEP
STG @MR7604              ; Next STEP
MPS
LD R1200                 ; Area sensor
ANB R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
AND R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
ANL
TMH @16 #15              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T16                 ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
MPS
ANB R611                 ; 出荷前CSIC無視
SET @MR1911              ; CSIC書込みPack
CON
JMP @MR7605              ; Next STEP
MPP
AND R611                 ; 出荷前CSIC無視
JMP @MR7609
MPP
ANB R1200                ; Area sensor
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR7603              ; Next STEP
;IC書き込み  自動リトライ
STG @MR7605              ; Next STEP
MPS
LD= DM287 #1             ; ModelIdentficationNumber
OR CR2002                ; 常時ON
AND MR13615              ; コマンド受付許可（非通信中)
ANL
MPS
ANB @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
RES MR13510              ; ﾘﾄﾗｲ
MRD
AND @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
SET MR13510              ; ﾘﾄﾗｲ
MPP
SET MR13508              ; Ch1選択
CON
RES MR13509              ; Ch2選択
CON
SET MR13500              ; CSIC書込み実行開始
CON
JMP @MR7606              ; Next STEP
MPP
LD= DM287 #2             ; ModelIdentficationNumber
OR= DM287 #3             ; ModelIdentficationNumber
ANB CR2002               ; 常時ON
AND MR13815              ; コマンド受付許可（非通信中)
ANL
MPS
ANB @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
RES MR13710              ; ﾘﾄﾗｲ
MRD
AND @MR7615              ; IC書き込み ﾘﾄﾗｲ指定
SET MR13710              ; ﾘﾄﾗｲ
MPP
SET MR13708              ; Ch1選択
CON
RES MR13709              ; Ch2選択
CON
SET MR13700              ; CSIC書込み実行開始
CON
JMP @MR7606              ; Next STEP
STG @MR7606              ; Next STEP
MPS
LD= DM287 #1             ; ModelIdentficationNumber
OR CR2002                ; 常時ON
ANL
MPS
AND MR13600              ; CH1 CSIC書込み実行完了
SET @MR1800              ; CSIC書込みOK
CON
JMP @MR7607              ; Next STEP
MPP
AND MR13601              ; CH1 CSIC書込み実行失敗
RES @MR1800              ; CSIC書込みOK
CON
JMP @MR7607              ; Next STEP
MPP
LD= DM287 #2             ; ModelIdentficationNumber
OR= DM287 #3             ; ModelIdentficationNumber
ANB CR2002               ; 常時ON
ANL
MPS
AND MR13800              ; CH1 CSIC書込み実行完了
SET @MR1800              ; CSIC書込みOK
CON
JMP @MR7607              ; Next STEP
MPP
AND MR13801              ; CH1 CSIC書込み実行失敗
RES @MR1800              ; CSIC書込みOK
CON
JMP @MR7607              ; Next STEP
STG @MR7607              ; Next STEP
RES @MR1911              ; CSIC書込みPack
CON
AND @MR002               ; PAUSE・ERROR無し
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
JMP @MR7608              ; Next STEP
STG @MR7608              ; Next STEP
RES MR13510              ; ﾘﾄﾗｲ
CON
RES MR13710              ; ﾘﾄﾗｲ
CON
RES MR13600              ; CH1 CSIC書込み実行完了
CON
RES MR13601              ; CH1 CSIC書込み実行失敗
CON
RES MR13800              ; CH1 CSIC書込み実行完了
CON
RES MR13801              ; CH1 CSIC書込み実行失敗
CON
RES @MR7602              ; IC書き込み中
CON
SET @MR7601              ; IC書き込み　完了
CON
ENDS
;仮
STG @MR7609
TMR @32 #10              ; 暫定ﾃﾞｨﾚｲ
CON
AND @T32                 ; 暫定ﾃﾞｨﾚｲ
SET @MR1911              ; CSIC書込みPack
CON
RES MR13510              ; ﾘﾄﾗｲ
CON
SET @MR1800              ; CSIC書込みOK
CON
JMP @MR7607              ; Next STEP
;<h1/>  ﾜｰｸｸﾗﾝﾌﾟ
;  Cartridge clamp
STG @MR7700              ; ﾜｰｸｸﾗﾝﾌﾟ
AND @MR002               ; PAUSE・ERROR無し
SET @MR7702              ; ﾜｰｸｸﾗﾝﾌﾟ中
CON
JMP @MR7703              ; Next STEP
STG @MR7703              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
ANB R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
AND R1200                ; Area sensor
SET MR2202               ; ｴﾘｱｾﾝｻｰ監視中
CON
RES MR6104               ; ICﾌﾟﾛｰﾌﾞ　前進
CON
SET MR6105               ; ICﾌﾟﾛｰﾌﾞ　後退
CON
JMP @MR7704              ; Next STEP
STG @MR7704              ; Next STEP
MPS
LD R1200                 ; Area sensor
ANB R4004                ; ICﾌﾟﾛｰﾌﾞ　前進限
AND R4005                ; ICﾌﾟﾛｰﾌﾞ　後退限
ANL
JMP @MR7705              ; Next STEP
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR7703              ; Next STEP
STG @MR7705              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
RES MR6012               ; 1ST ｸﾗﾝﾌﾟ開き
CON
SET MR6010               ; 1ST ｸﾗﾝﾌﾟ閉じ
CON
JMP @MR7706              ; Next STEP
STG @MR7706              ; Next STEP
MPS
LD R1200                 ; Area sensor
ANB T401                 ; 1ST ｸﾗﾝﾌﾟ開き OK
AND T400                 ; 1ST ｸﾗﾝﾌﾟ閉じ OK
ANL
JMP @MR7707              ; Next STEP
MPP
ANB R1200                ; Area sensor
SET R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
JMP @MR7703              ; Next STEP
STG @MR7707              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
RES @MR7702              ; ﾜｰｸｸﾗﾝﾌﾟ中
CON
SET @MR7701              ; ﾜｰｸｸﾗﾝﾌﾟ完了
CON
ENDS
;<h1/>  CSICﾃﾞｰﾀｾｯﾄ
;  CSIC data setup
STG @MR7900              ; CSICﾃﾞｰﾀｾｯﾄ
SET @MR7902              ; CSICﾃﾞｰﾀｾｯﾄ中
CON
MPS
AND MR003                ; AP03 CSIC無し
JMP @MR7905              ; ﾃﾞｰﾀｾｯﾄ完了
MPP
ANB MR003                ; AP03 CSIC無し
RES @MR7501              ; CSIC PC機種切替Sub完了
CON
ANB @MR7502              ; CSIC PC機種切替Sub中
ANB @MR7501              ; CSIC PC機種切替Sub完了
SET @MR7500              ; CSIC機種切替
CON
JMP @MR7903              ; Next STEP
STG @MR7903              ; Next STEP
AND @MR002               ; PAUSE・ERROR無し
MPS
AND @MR7501              ; CSIC PC機種切替Sub完了
MPS
AND @MR1806              ; CSIC機種切替OK
RES @MR7915              ; ﾘﾄﾗｲﾌﾗｸﾞ
CON
RES @MR7501              ; CSIC PC機種切替Sub完了
CON
JMP @MR7905              ; ﾃﾞｰﾀｾｯﾄ完了
MPP
ANB @MR1806              ; CSIC機種切替OK
SET @MR800               ; Err120 CSIC Data set ERR
CON
RES @MR7501              ; CSIC PC機種切替Sub完了
CON
JMP @MR7904              ; Next STEP
MPP
LDB @MR7501              ; CSIC PC機種切替Sub完了
ANB @MR7502              ; CSIC PC機種切替Sub中
ANB @MR7500              ; CSIC機種切替
ANL
TMR @5 #200              ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
CON
AND @T5                  ; ﾀｲﾐﾝｸﾞﾃﾞｨﾚｲ
MPS
ANB @MR7915              ; ﾘﾄﾗｲﾌﾗｸﾞ
JMP @MR7906              ; ﾘﾄﾗｲ
MPP
AND @MR7915              ; ﾘﾄﾗｲﾌﾗｸﾞ
JMP @MR7907              ; ﾀｲﾑｱｳﾄ
;Err120 CSIC Data set ERR
STG @MR7904              ; Next STEP
AND @MR803               ; Err120 Release
RES @MR803               ; Err120 Release
CON
JMP @MR7900              ; CSICﾃﾞｰﾀｾｯﾄ
STG @MR7905              ; ﾃﾞｰﾀｾｯﾄ完了
RES @MR1806              ; CSIC機種切替OK
CON
RES @MR7902              ; CSICﾃﾞｰﾀｾｯﾄ中
CON
SET @MR7901              ; CSICﾃﾞｰﾀｾｯﾄ完了
CON
ENDS
STG @MR7906              ; ﾘﾄﾗｲ
AND @MR002               ; PAUSE・ERROR無し
SET @MR7915              ; ﾘﾄﾗｲﾌﾗｸﾞ
CON
JMP @MR7900              ; CSICﾃﾞｰﾀｾｯﾄ
;Err122 CSIC Data set time out
STG @MR7907              ; ﾀｲﾑｱｳﾄ
AND @MR002               ; PAUSE・ERROR無し
SET @MR712               ; Err122 CSIC Data set time out
CON
JMP @MR7908              ; Next STEP
STG @MR7908              ; Next STEP
AND @MR715               ; Err122 Release
RES @MR7915              ; ﾘﾄﾗｲﾌﾗｸﾞ
CON
RES @MR715               ; Err122 Release
CON
JMP @MR7900              ; CSICﾃﾞｰﾀｾｯﾄ
;<h1/>■ Error
;***　ﾜｰｸ浮き表示 ERROR NO 07  /  Work flotage display 
STG @MR100               ; Err07 ﾜｰｸ浮き表示
ANB R50008               ; Error
SET @MR101               ; ﾜｰｸ浮き表示中
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR102               ; 次ｽﾃｯﾌﾟへ
MOV #7 DM400             ; ﾒｯｾｰｼﾞNo.
STG @MR102               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR103               ; 次ｽﾃｯﾌﾟへ
STG @MR103               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
RES @MR101               ; ﾜｰｸ浮き表示中
CON
SET @MR107               ; Err07 解除完了
CON
ENDS
;***　ﾜｰｸ未除材ｴﾗｰ表示 ERROR NO 05  /  work non remove erorr  display
STG @MR400               ; ﾜｰｸ未除材ｴﾗｰ表示へ
ANB R50008               ; Error
SET @MR401               ; ﾜｰｸ未除材ｴﾗｰ表示中
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR402               ; 次ｽﾃｯﾌﾟへ
MOV #5 DM400             ; ﾒｯｾｰｼﾞNo.
STG @MR402               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR403               ; 次ｽﾃｯﾌﾟへ
STG @MR403               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
RES @MR401               ; ﾜｰｸ未除材ｴﾗｰ表示中
CON
SET @MR407               ; ﾜｰｸ未除材ｴﾗｰ表示終了
CON
ENDS
;***　ﾜｰｸ未給材ｴﾗｰ表示 ERROR NO 06  /  work non set erorr  display
STG @MR408               ; ﾜｰｸ未給材ｴﾗｰ表示へ
ANB R50008               ; Error
SET @MR409               ; ﾜｰｸ未給材ｴﾗｰ表示中
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR410               ; 次ｽﾃｯﾌﾟへ
MOV #6 DM400             ; ﾒｯｾｰｼﾞNo.
STG @MR410               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR411               ; 次ｽﾃｯﾌﾟへ
STG @MR411               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
RES @MR409               ; ﾜｰｸ未給材ｴﾗｰ表示中
CON
SET @MR415               ; ﾜｰｸ未給材ｴﾗｰ表示終了
CON
ENDS
;***　ﾓﾃﾞﾙｴﾗｰ表示 ERROR NO 59  /  model erorr  display
STG @MR508               ; ﾓﾃﾞﾙｴﾗｰ表示へ
ANB R50008               ; Error
SET @MR509               ; ﾓﾃﾞﾙｴﾗｰ表示中
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR510               ; 次ｽﾃｯﾌﾟへ
MOV #59 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR510               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR511               ; 次ｽﾃｯﾌﾟへ
STG @MR511               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
RES @MR509               ; ﾓﾃﾞﾙｴﾗｰ表示中
CON
SET @MR515               ; ﾓﾃﾞﾙｴﾗｰ表示終了
CON
ENDS
;***** ｶﾗｰﾌﾞﾛｯｸ異常(無いのに検出) ERROR No.40  /  Color block error (Detected)  *****
;
STG @MR704               ; Err40 Color block ERR (ON)
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR705               ; 次ｽﾃｯﾌﾟへ
MOV #40 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR705               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR706               ; 次ｽﾃｯﾌﾟへ
STG @MR706               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR707               ; Err40 Release
CON
ENDS
;***** ｶﾗｰﾌﾞﾛｯｸ異常(有るのに検出できず) ERROR No.41  /  Color block error (No Detected)  *****
;
STG @MR708               ; Err41 Color block ERR (OFF)
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR709               ; 次ｽﾃｯﾌﾟへ
MOV #41 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR709               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR710               ; 次ｽﾃｯﾌﾟへ
STG @MR710               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR711               ; Err41 Release
CON
ENDS
;***** CSIC ﾃﾞｰﾀ書込みPC ﾃﾞｰﾀｾｯﾄ異常 ERROR No.120  /  CSIC PC Data-set error  *****
;
STG @MR800               ; Err120 CSIC Data set ERR
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR801               ; 次ｽﾃｯﾌﾟへ
MOV #120 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR801               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR802               ; 次ｽﾃｯﾌﾟへ
STG @MR802               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR803               ; Err120 Release
CON
ENDS
;***** CSIC ﾃﾞｰﾀ書込み異常 ERROR No.121  /  CSIC Data write error  *****
;
STG @MR804               ; Err121 CSIC Data write ERR
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR805               ; 次ｽﾃｯﾌﾟへ
MOV #121 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR805               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR806               ; 次ｽﾃｯﾌﾟへ
STG @MR806               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR807               ; Err121 Release
CON
ENDS
;***** CSIC ﾃﾞｰﾀﾃﾞｰﾀｾｯﾄﾀｲﾑｱｳﾄ ERROR No.122  /  CSIC Data set time out  *****
;
STG @MR712               ; Err122 CSIC Data set time out
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR713               ; 次ｽﾃｯﾌﾟへ
MOV #122 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR713               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR714               ; 次ｽﾃｯﾌﾟへ
STG @MR714               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR715               ; Err122 Release
CON
ENDS
END
ENDH
