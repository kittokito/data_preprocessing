DEVICE:53
;MODULE:Slave
;MODULE_TYPE:0
LD R50010                ; Emergency　Stop
ZRES @MR000 @MR915
;ミューティング処理
;<h1/>ミューティング処理
LD R1104                 ; INDEX位置決め完了
OR MR2201                ; ﾐｭｰﾃｨﾝｸﾞ中
ANB @MR200               ; Muting Can't
ANB R50106               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
OUT MR2201               ; ﾐｭｰﾃｨﾝｸﾞ中
TMH @10 #15
CON
AND @T10
OUT R50006               ; ﾐｭｰﾃｨﾝｸﾞ中
LDB MR2201               ; ﾐｭｰﾃｨﾝｸﾞ中
AND R50106               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
RES R50106               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
;Reset Light Curtain / ライトカーテンリセット
LD CR2002                ; 常時ON
TMR @7 #10               ; Light curtain reset delay
LDP R50001               ; AUTO
ORP R50002               ; STEP
ORP R50003               ; HOME
ORP MR2110               ; INDEX    非常停止
AND R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
ORP @T7                  ; Light curtain reset delay
RES R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
SHOT #10 MR2200          ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ
LD MR2200                ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ
RES MR1009               ; INDEX非常停止遮断
CON
RES MR1008               ; HOME完了前LC遮光
CON
RES MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
CON
SHOT #30 @MR008
LDB R50006               ; ﾐｭｰﾃｨﾝｸﾞ中
ANB R1201                ; ﾗｲﾄｶｰﾃﾝ
ANB MR2200               ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ
ANB @MR008
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
LD MR1011                ; INDEX監視
OR MR1012                ; INDEX監視
ANL
RES MR2105               ; INDEX電源ON
CON
RES MR2110               ; INDEX    非常停止
CON
SET MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
LD MR1009                ; INDEX非常停止遮断
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
ANB MR1008               ; HOME完了前LC遮光
RES MR2110               ; INDEX    非常停止
CON
RES MR2105               ; INDEX電源ON
CON
MPS
LD _Index
AND R50007               ; 原点復帰　完了
ANL
SET MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
MPP
LDB _Index
ORB R50007               ; 原点復帰　完了
ANL
SET MR1008               ; HOME完了前LC遮光
LDP MR1009               ; INDEX非常停止遮断
ANB MR1011               ; INDEX監視
ORP MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
LD CR2003                ; 常時OFF
ANB R50400               ; Call Post
ANB R1201                ; ﾗｲﾄｶｰﾃﾝ
ORL
ANB R50300               ; Pause
SET R50005               ; PAUSERQ
;Reset Light Curtain / ライトカーテンリセット
LD R50302                ; 下流ﾎﾟｰｽﾞ
OR R50300                ; Pause
OUT R50301               ; PAUSE2
;<h1/>PAUSE処理 ﾒｲﾝ
;PAUSE main routine
;一時停止条件
;１、Ｍａｉｎ　Ａｉｒ
;２、ＰＡＵＳＥ ＳＷ
;３、動作中のカバー開
LDB R50010               ; Emergency　Stop
MPS
TMH @0 #10               ; 起動遅延
CON
ANP @T0                  ; 起動遅延
SET @MR400               ; PAUSE Condition check           
MPP
TMR @40 #10
LD @MR400                ; PAUSE Condition check           
AND @T40
MPS
ANB R1100                ; ﾒｲﾝｴｱｰ 正常=ON
SET MR607                ; 動作中MainAirOff
MPP
LDB R5815                ; ｻﾃﾗｲﾄ ｴｱｰ元圧確認
ANB MR1212               ; (Y) ｻﾌﾞﾀﾝｸ攪拌中
ANB MR1412               ; (M) ｻﾌﾞﾀﾝｸ攪拌中
ANB MR1612               ; (C) ｻﾌﾞﾀﾝｸ攪拌中
ANB MR1812               ; (B) ｻﾌﾞﾀﾝｸ攪拌中
ANB MR1211               ; (Y) KDﾄﾞﾗﾑ攪拌中
ANB MR1411               ; (M) KDﾄﾞﾗﾑ攪拌中
ANB MR1611               ; (C) KDﾄﾞﾗﾑ攪拌中
ANB MR1811               ; (B) KDﾄﾞﾗﾑ攪拌中
ANB MR39911              ; ｻﾃﾗｲﾄ無し
ANL
SET MR608                ; 動作中ｻﾌﾞﾀﾝｸAirOff
STG @MR400               ; PAUSE Condition check           
LDB R1100                ; ﾒｲﾝｴｱｰ 正常=ON
OR R1002                 ; 操作BOX ﾎﾟｰｽﾞSW
OR R1006                 ; Rear PAUSE sw
ORB R1102                ; 安全ｶﾊﾞｰ 閉=ON
OR R50005                ; PAUSERQ
OR R50014                ; ﾗｲﾄｶｰﾃﾝ遮光記憶
ANL
LDB LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
LD LR49914               ; ｴﾗｰﾘｾｯﾄｷｰ要求
AND LR49915              ; ｴﾗｰﾘｾｯﾄｷｰ動作OK
ORL
ANL
RES R50005               ; PAUSERQ
CON
SET R50300               ; Pause
CON
JMP @MR401               ; Pause start                     
;一時停止確認
LDB R50100               ; PACK INDEX
ANB R50101               ; PACK UT1
ANB R50102               ; PACK UT2
ANB R50103               ; PACK UT3
ANB R50104               ; PACK UT4
OUT @MR001               ; PAUSE開始
STG @MR401               ; Pause start                     
MPS
AND @MR001               ; PAUSE開始
TMH @18 #5
CON
AND @T18
JMP @MR403               ; ST1-4 shutdown demand 
MPP
LDB @MR001               ; PAUSE開始
AND R1100                ; ﾒｲﾝｴｱｰ 正常=ON
ANB R1002                ; 操作BOX ﾎﾟｰｽﾞSW
ANB R1006                ; Rear PAUSE sw
AND R1102                ; 安全ｶﾊﾞｰ 閉=ON
ANL
OUT MR15100              ; SubInfo ﾎﾟｰｽﾞ処理中
CON
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
LD R1001                 ; 操作BOX ｽﾀｰﾄSW
OR R1005                 ; Rear Start sw
ANL
JMP @MR402               ; Next STEP
STG @MR402               ; Next STEP
RES R50300               ; Pause
CON
JMP @MR400               ; PAUSE Condition check           
LDP @MR403               ; ST1-4 shutdown demand 
MPS
AND _UT1
SET @MR101               ; ST1 Pause
CON
MDSTOP UT1
MPP
AND _UT3
SET @MR103               ; ST3 Pause
CON
MDSTOP UT3
STG @MR403               ; ST1-4 shutdown demand 
ANB _UT1
ANB _UT3
JMP @MR404               ; Next STEP
STG @MR404               ; Next STEP
MPS
LD R50007                ; 原点復帰　完了
OR MR1013                ; INDEX原点復帰完了
ANL
MPS
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
JMP @MR405               ; Next STEP
MPP
AND MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
JMP @MR500               ; RB status Memory
MPP
LDB R50007               ; 原点復帰　完了
ANB MR1013               ; INDEX原点復帰完了
ANL
JMP @MR500               ; RB status Memory
STG @MR405               ; Next STEP
MPS
AND MR1013               ; INDEX原点復帰完了
RES MR6805               ; INDEX位置決め 下降
CON
SET MR6804               ; INDEX位置決め 上昇
CON
AND R4008                ; INDEX位置決め 上昇限
JMP @MR500               ; RB status Memory
MPP
ANB MR1013               ; INDEX原点復帰完了
JMP @MR500               ; RB status Memory
;RBなどでﾎﾟｰｽﾞ前の状態を記憶したい場合はここで行う
STG @MR500               ; RB status Memory
SET MR2100               ; Cover Open
CON
SET R50415               ; PAUSE OK
CON
RES MR2105               ; INDEX電源ON
CON
JMP @MR600               ; Pause Return Start SW Wait
;カバー閉じ後、ＳＴＡＲＴ ｓｗ　ＯＮ
STG @MR600               ; Pause Return Start SW Wait
AND R1200                ; Area sensor
AND R1100                ; ﾒｲﾝｴｱｰ 正常=ON
ANB R1002                ; 操作BOX ﾎﾟｰｽﾞSW
ANB R1006                ; Rear PAUSE sw
LD R1001                 ; 操作BOX ｽﾀｰﾄSW
OR R1005                 ; Rear Start sw
ANL
RES R50415               ; PAUSE OK
CON
RES MR2100               ; Cover Open
CON
JMP @MR601               ; Next STEP
STG @MR601               ; Next STEP
TMH @1 #50               ; ｶﾊﾞｰ閉確認遅延
CON
AND @T1                  ; ｶﾊﾞｰ閉確認遅延
JMP @MR602               ; Next STEP
STG @MR602               ; Next STEP
MPS
LDB R1100                ; ﾒｲﾝｴｱｰ 正常=ON
OR R1002                 ; 操作BOX ﾎﾟｰｽﾞSW
OR R1006                 ; Rear PAUSE sw
ORB R1102                ; 安全ｶﾊﾞｰ 閉=ON
ANL
JMP @MR603               ; Next STEP
MPP
LD R1100                 ; ﾒｲﾝｴｱｰ 正常=ON
ANB R1002                ; 操作BOX ﾎﾟｰｽﾞSW
ANB R1006                ; Rear PAUSE sw
AND R1102                ; 安全ｶﾊﾞｰ 閉=ON
ANL
JMP @MR604               ; Next STEP
STG @MR603               ; Next STEP
SET MR2100               ; Cover Open
CON
JMP @MR600               ; Pause Return Start SW Wait
;ｶﾊﾞｰ閉確認
;Cover Close Detect
;RB復帰が居る場合はここでRB復帰のｻﾌﾞﾙｰﾁﾝを呼ぶ
STG @MR604               ; Next STEP
MPS
LD R50007                ; 原点復帰　完了
OR R50003                ; HOME
OR MR1013                ; INDEX原点復帰完了
ANL
JMP @MR605               ; Next STEP
MPP
LDB R50007               ; 原点復帰　完了
ANB R50003               ; HOME
ANB MR1013               ; INDEX原点復帰完了
ANL
JMP @MR901               ; Next STEP
STG @MR605               ; Next STEP
MPS
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
JMP @MR608               ; Next STEP
MPP
AND MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
JMP @MR700               ; Next STEP
;<h1/>①通常処理(ﾗｲﾄｶｰﾃﾝ遮光無し)
;　Normal routine (No shade of Light Curtain)
;最初に、カバーロックして、ＩＮＤＥＸの電源ＯＮ、
;サーボＯＮ（プログラムＮｏ４）
LDB MR2105               ; INDEX電源ON
TMH @4 #400
LD MR1012                ; INDEX監視
AND MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
RES MR2105               ; INDEX電源ON
CON
RES MR2106               ; INDEXﾌﾟﾛｸﾞﾗﾑ 1桁
CON
ZRES MR2108 MR2115       ; INDEXｽﾀｰﾄ  IDX ﾌﾟﾛｸﾞﾗﾑNo. 3bit
CON
SET @MR401               ; Pause start                     
CON
RES @MR200               ; Muting Can't
CON
ZRES @MR608 @MR915       ; Next STEP
CON
RES MR1012               ; INDEX監視
LD<> DM400 #179          ; ﾒｯｾｰｼﾞNo.
OUT @MR002               ; ST1-4停止要求
STG @MR608               ; Next STEP
MPS
AND @MR002               ; ST1-4停止要求
SET MR2110               ; INDEX    非常停止
CON
ANB MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
SET MR1012               ; INDEX監視
CON
MPS
LDB R2105                ; INDEX電源ON
AND @T4
ANL
JMP @MR609               ; Next STEP
MPP
AND R2105                ; INDEX電源ON
TMH @8 #30               ; Wait timer 08
CON
AND @T8                  ; Wait timer 08
JMP @MR610               ; Next STEP
MPP
ANB @MR002               ; ST1-4停止要求
SET R50011               ; TRAP
STG @MR609               ; Next STEP
TMH @6 #30
CON
AND @T6
AND T722                 ; Index電源再投入ﾃﾞｨﾚｲ
JMP @MR610               ; Next STEP
STG @MR610               ; Next STEP
SET MR2105               ; INDEX電源ON
CON
LD R1105                 ; INDEX起動待ち
ORB MR1013               ; INDEX原点復帰完了
ANL
JMP @MR611               ; Next STEP
;位置決めロケート下降 ブレーキ下降
;プログラムNo.　０から３　を選択　起動
STG @MR611               ; Next STEP
RES MR6804               ; INDEX位置決め 上昇
CON
SET MR6805               ; INDEX位置決め 下降
CON
AND R4009                ; INDEX位置決め 下降限
JMP @MR900               ; Pause processing Completion
;<h1/>②ﾗｲﾄｶｰﾃﾝ遮光時処理
;  Lighr Curtain shade routine
;回転中で遮光して止まった場合、次の位置ではなく、元の位置に戻す
;When it shades, it returns to a rotation starting position.
STG @MR700               ; Next STEP
MPS
AND<> DM400 #149         ; ﾒｯｾｰｼﾞNo.
RES MR1010               ; ﾗｲﾄｶｰﾃﾝ遮光
CON
RES MR1009               ; INDEX非常停止遮断
CON
SET MR2110               ; INDEX    非常停止
CON
SET MR1012               ; INDEX監視
CON
JMP @MR701
MPP
AND= DM400 #149          ; ﾒｯｾｰｼﾞNo.
SET R50011               ; TRAP
STG @MR701
MPS
LD<> DM400 #149          ; ﾒｯｾｰｼﾞNo.
AND @T4
ANL
TMH @11 #30
CON
AND @T11
AND T722                 ; Index電源再投入ﾃﾞｨﾚｲ
JMP @MR702               ; Next STEP
MPP
AND= DM400 #149          ; ﾒｯｾｰｼﾞNo.
SET R50011               ; TRAP
STG @MR702               ; Next STEP
SET MR2105               ; INDEX電源ON
CON
JMP @MR703               ; Next STEP
STG @MR703               ; Next STEP
AND R2105                ; INDEX電源ON
SET MR2107               ; INDEXﾘｾｯﾄ
CON
JMP @MR704               ; Next STEP
STG @MR704               ; Next STEP
AND R1106                ; INDEX Alarm1 正常=ON
AND R1107                ; INDEX Alarm2 正常=ON
RES MR2107               ; INDEXﾘｾｯﾄ
CON
JMP @MR705               ; Next STEP
;位置決めロケート下降 
;
STG @MR705               ; Next STEP
RES MR6804               ; INDEX位置決め 上昇
CON
SET MR6805               ; INDEX位置決め 下降
CON
AND R4009                ; INDEX位置決め 下降限
SET @MR200               ; Muting Can't
CON
JMP @MR706               ; Next STEP
STG @MR706               ; Next STEP
RES MR6102               ; 治具ｴｱｰﾉｽﾞﾙ上昇
CON
AND R4001                ; 1ST 治具ｴｱﾉｽﾞﾙ 下降限
SET R51405               ; IND2回転中(SET/RESET)
CON
RES @MR200               ; Muting Can't
CON
JMP @MR707               ; Next STEP
;INDEXを20度正転（ﾄﾞｸﾞ逃がし）
;INDEX 20°Rotation 
STG @MR707               ; Next STEP
AND R1105                ; INDEX起動待ち
SET MR2112               ; INDEXﾌﾟﾛｸﾞﾗﾑ 0bit
CON
SET MR2113               ; INDEXﾌﾟﾛｸﾞﾗﾑ 1bit
CON
SET MR2114               ; INDEXﾌﾟﾛｸﾞﾗﾑ 2bit
CON
JMP @MR708               ; Next STEP
STG @MR708               ; Next STEP
TMH @23 #20
CON
AND @T23
SET MR2106               ; INDEXﾌﾟﾛｸﾞﾗﾑ 1桁
CON
TMH @24 #20
CON
AND @T24
SET MR2108               ; INDEXｽﾀｰﾄ
CON
JMP @MR709               ; Next STEP
STG @MR709               ; Next STEP
ANB R1105                ; INDEX起動待ち
RES MR2108               ; INDEXｽﾀｰﾄ
CON
RES MR2106               ; INDEXﾌﾟﾛｸﾞﾗﾑ 1桁
CON
ZRES MR2112 MR2115       ; INDEXﾌﾟﾛｸﾞﾗﾑ 0bit  IDX ﾌﾟﾛｸﾞﾗﾑNo. 3bit
CON
JMP @MR710               ; Next STEP
STG @MR710               ; Next STEP
AND R1104                ; INDEX位置決め完了
SET MR2109               ; INDEXｱﾝｻ
CON
JMP @MR711               ; Next STEP
STG @MR711               ; Next STEP
ANB R1104                ; INDEX位置決め完了
SET MR1005               ; INDEX 近回り宣言
CON
RES R50205               ; INDEX回転中（SET/RST）
CON
RES MR2109               ; INDEXｱﾝｻ
CON
JMP @MR900               ; Pause processing Completion
;<h1/>PAUSE処理終了
;PAUSE routine END
LD @MR900                ; Pause processing Completion
MPS
AND @MR101               ; ST1 Pause
MDSTRT UT1
MPP
AND @MR103               ; ST3 Pause
MDSTRT UT3
STG @MR900               ; Pause processing Completion
RES R50014               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
LD _UT1
ORB @MR101               ; ST1 Pause
ANL
LD _UT3
ORB @MR103               ; ST3 Pause
ANL
JMP @MR901               ; Next STEP
STG @MR901               ; Next STEP
ZRES @MR101 @MR115       ; ST1 Pause  IND3_ST4 Pause
CON
RES MR1012               ; INDEX監視
CON
RES R50300               ; Pause
CON
JMP @MR400               ; PAUSE Condition check           
;
END
ENDH
