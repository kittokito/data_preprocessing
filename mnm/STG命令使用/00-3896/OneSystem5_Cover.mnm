DEVICE:53
;MODULE:OneSystem5_Cover
;MODULE_TYPE:0
;OneSystem Master-Module
;
;-----------------------------------------
;ModuleName	:	OneSystem5_Cover
;ModuleType	:	Master Module
;Function	:	Safety Cover/Pause
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;<h1/>Safety Cover1/安全ｶﾊﾞｰ1処理
;●安全ｶﾊﾞｰ有効sig出力
LD>= DM9200 #1
AND R59800
MPS
OUT R40200
MPP
AND CR2008
SET R46900
CON
SET @R009
;●電源投入時安全ｶﾊﾞｰﾛｯｸ&確認
;(ﾛｯｸしたのにﾛｯｸ入力が入らなければﾛｯｸ要求処理へ)
STG @R009
MPS
ANB R44303
SET R46700
CON
SET R46200
CON
JMP @R005
MPP
AND R44303
ENDS
;●ｶﾊﾞｰ開要求ｽｲｯﾁ(COVER_SW)待ち
;●安全ｶﾊﾞｰ不正開監視
;	安全ｶﾊﾞｰ開要求が出ていないのに安全ｶﾊﾞｰ入力がOFFした
LD R40200
ANB R46700
ANB R44400
ANB R44401
MPS
LD R44303
ANP R44402
ANL
SET R46700
CON
SET R46800
CON
SET @R000
MRD
LD @R010
ANB R44303
ANL
SET R46700
CON
SET R46200
CON
SET @R005
MPP
LDB @R010
ORB R46103
ANB R44303
ANL
MOV #65 DM2003
SET @R010
CON
SET R40009
LDF R46103
RES @R010
;●各Taskへﾎﾟｰｽﾞ要求通知(PAUSE)
STG @R000
MPS
LDB R46104
ANB R46103
ANB R50700
ANL
SET R44200
CON
JMP @R001
MPP
LD R46103
OR R46104
OR R50700
ANL
SET R40500
CON
JMP @R002
;●各TaskからのHALT出力待ち
;・起動中全TaskからのHALT待ち
STG @R001
AND=.D R42600 R41000
AND<> R41000 #0
SET R40500
CON
JMP @R002
;●SC_Before
;ｶﾊﾞｰﾛｯｸ解除可／不可確認(ﾕｰｻﾞｰ解放)
STG @R002
ANB R40500
MPS
AND R40600
RES R40600
CON
JMP @R003
MPP
AND R40601
RES R40601
CON
ZRES R42600:#0 R42600:#31
CON
RES R46800
CON
RES R44200
CON
RES R46700
CON
ENDS
;●安全ｶﾊﾞｰﾛｯｸ解除
STG @R003
RES R46900
CON
JMP @R004
STG @R004
RES R46800
CON
SET R46200
CON
JMP @R005
;●安全ｶﾊﾞｰﾛｯｸ要求(操作BOXｽﾀｰﾄSW or 3連BOXｽﾀｰﾄSW)待ち
STG @R005
LDP R44400
ORP R44406
ANL
SET R46900
CON
JMP @R006
;●安全ｶﾊﾞｰﾛｯｸ〜入力確認
;(ﾛｯｸ入力入らなければ再度ﾛｯｸ要求待ちへ)
STG @R006
MPS
AND R44303
SET R40501
CON
JMP @R007
MPP
ANB R44303
TMR @0 DM9352
CON
AND @T0
RES R46900
CON
JMP @R005
;●SC_After
;ｶﾊﾞｰﾛｯｸ可／不可確認(ﾕｰｻﾞｰ解放)
STG @R007
ANB R40501
MPS
AND R40700
RES R46200
CON
RES R40700
CON
JMP @R008
MPP
AND R40701
RES R40701
CON
JMP @R003
STG @R008
ANB R44400
ANB R50600
ZRES R42600:#0 R42600:#31
CON
RES R44200
CON
RES R46700
CON
ENDS
;<h1/>Safety Cover Open Sig Logic/安全ｶﾊﾞｰ出力論理設定
;LockRequest = 安全ｶﾊﾞｰ処理ｼｰｹﾝｽ内でｶﾊﾞｰ閉時にSET/ｶﾊﾞｰ開時にRESする中間sig
;
;ﾕｰｻﾞｰ設定(CoverOUT_Logic)の設定により、実ｺｲﾙ(COVER_LOCK*)出力の反転を行う
;OFF：A接点でｶﾊﾞｰ開出力
;ON：B接点でｶﾊﾞｰ開出力
;ｶﾊﾞｰ開出力をA接点に(ONでｶﾊﾞｰ開)
LDB R59408
MPS
AND R46900
SET R44310
MPP
ANB R46900
RES R44310
;ｶﾊﾞｰ開出力をB接点に(OFFでｶﾊﾞｰ開)
LD R59408
MPS
AND R46900
RES R44310
MPP
ANB R46900
SET R44310
END
ENDH
