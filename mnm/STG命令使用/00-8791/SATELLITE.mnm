DEVICE:53
;MODULE:SATELLITE
;MODULE_TYPE:0
LD R50010                ; Emergency　Stop
OR @MR001                ; 001
ZRES @MR002 @MR4915      ; ｻﾌﾞﾀﾝｸA大気開放
CON
OUT @MR001               ; 001
TMR @0 #5
LD @T0
MDSTOP SATELLITE
;***** ｻﾃﾗｲﾄ各種設定ﾒﾆｭｰ 選択  / Satellite various setting menu selection  ******
;
LD LR49800               ; ｻﾃﾗｲﾄ無しﾓｰﾄﾞ
OUT MR12211              ; ｻﾃﾗｲﾄ無し
LD= DM3040 $4400         ; ｲﾝｸ種1
OUT MR12214              ; 染料ｲﾝｸ
LD= DM3040 $5000         ; ｲﾝｸ種1
OUT MR12215              ; 顔料ｲﾝｸ
;<h1/>*****　ﾒｲﾝ ｱｷｭﾑﾚｰﾀｰ制御/Main acumlator control  @MR1000〜@MR1915　*****
;　
;　
LD R50004                ; TASKDO
LDB R50002               ; STEP
LD R50002                ; STEP
AND LR40102              ; STEP ST3 選択 flag
ORL
ANL
OR MR12014               ; 背面ｲﾝｸ吐出起動中
LDB R50004               ; TASKDO
AND R50007               ; 原点復帰　完了
LD MR12114               ; KDﾄﾞﾗﾑ交換検出FLG
OR LR39915               ; MODELNO 変更FLG
ANL
ORL
ANB MR12211              ; ｻﾃﾗｲﾄ無し
ANB @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
SET @MR1000              ; 次ｽﾃｯﾌﾟ
STG @MR1000              ; 次ｽﾃｯﾌﾟ
TMR @3 #1
CON
AND @T3
RES MR25915
CON
SET @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
CON
JMP @MR1001              ; 次ｽﾃｯﾌﾟ
STG @MR1001              ; 次ｽﾃｯﾌﾟ
AND @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
JMP @MR1002              ; 次ｽﾃｯﾌﾟ
STG @MR1002              ; 次ｽﾃｯﾌﾟ
MPS
LD LR39910               ; 電源投入FLG
OR LR39915               ; MODELNO 変更FLG
LD LR39911               ; ｲﾝｸｺｰﾄﾞ照合要求
AND MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
ORL
ANL
JMP @MR1100              ; 次ｽﾃｯﾌﾟ
MRD
LDB LR39910              ; 電源投入FLG
ANB LR39915              ; MODELNO 変更FLG
ANB LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
ANB MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
ANL
MPS
LD R50004                ; TASKDO
OR MR12014               ; 背面ｲﾝｸ吐出起動中
ANL
JMP @MR1400              ; 次ｽﾃｯﾌﾟ
MPP
LDB R50004               ; TASKDO
ANB MR12014              ; 背面ｲﾝｸ吐出起動中
ANL
JMP @MR1415              ; 終了処理
MPP
LDB MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
ANB @MR1100              ; 次ｽﾃｯﾌﾟ
ANL
MPS
AND R50004               ; TASKDO
JMP @MR1400              ; 次ｽﾃｯﾌﾟ
MPP
ANB R50004               ; TASKDO
JMP @MR1415              ; 終了処理
STG @MR1100              ; 次ｽﾃｯﾌﾟ
MPS
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
JMP @MR1101              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
MPS
AND R50004               ; TASKDO
MPS
ANB MR12103              ; ｲﾝｸ照合処理中
JMP @MR1101              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12103              ; ｲﾝｸ照合処理中
ENDS
MPP
ANB R50004               ; TASKDO
JMP @MR1101              ; 次ｽﾃｯﾌﾟ
STG @MR1101              ; 次ｽﾃｯﾌﾟ
MPS
AND CR2002               ; 常時ON
SET MR12103              ; ｲﾝｸ照合処理中
CON
SET @MR708               ; Err76 ｲﾝｸ照合表示へ
CON
JMP @MR1102              ; 次ｽﾃｯﾌﾟ
MPP
AND CR2003               ; 常時OFF
JMP @MR1200              ; 次ｽﾃｯﾌﾟ
STG @MR1102              ; 次ｽﾃｯﾌﾟ
AND @MR715               ; Err76 Release
RES @MR715               ; Err76 Release
CON
JMP @MR1103              ; 次ｽﾃｯﾌﾟ
STG @MR1103              ; 次ｽﾃｯﾌﾟ
JMP @MR1200              ; 次ｽﾃｯﾌﾟ
STG @MR1200              ; 次ｽﾃｯﾌﾟ
ANB @MR3702              ; ｲﾝｸ照合処理call 実行中
SET MR12112              ; 照合要求画面表示
CON
SET @MR3700              ; ｲﾝｸ照合処理call FLG
CON
JMP @MR1201              ; 次ｽﾃｯﾌﾟ
STG @MR1201              ; 次ｽﾃｯﾌﾟ
AND @MR3701              ; ｲﾝｸ照合処理call 完了
RES @MR3701              ; ｲﾝｸ照合処理call 完了
CON
JMP @MR1202              ; 次ｽﾃｯﾌﾟ
STG @MR1202              ; 次ｽﾃｯﾌﾟ
MPS
AND @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
RES MR12112              ; 照合要求画面表示
CON
MPS
AND MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
RES MR12103              ; ｲﾝｸ照合処理中
CON
JMP @MR1203              ; 次ｽﾃｯﾌﾟ
MPP
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
MPS
AND R59803               ; OfflineMode
RES LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
CON
RES MR12103              ; ｲﾝｸ照合処理中
CON
JMP @MR1203              ; 次ｽﾃｯﾌﾟ
MPP
ANB R59803               ; OfflineMode
JMP @MR1600
MPP
ANB @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
RES MR12112              ; 照合要求画面表示
CON
SET @MR700               ; Err77 ｲﾝｸ照合NG表示へ
CON
JMP @MR1300              ; ｲﾝｸ照合NGの時へ
STG @MR1600
SET MR20200              ; Sub:ｻﾃﾗｲﾄ情報切替開始
CON
JMP @MR1601
STG @MR1601
AND MR20201              ; Sub:ｻﾃﾗｲﾄ情報切替完了
RES MR20201              ; Sub:ｻﾃﾗｲﾄ情報切替完了
CON
JMP @MR1603
STG @MR1603
MPS
AND MR20204              ; ｻﾃﾗｲﾄ情報切替OK
RES LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
CON
RES MR12103              ; ｲﾝｸ照合処理中
CON
JMP @MR1203              ; 次ｽﾃｯﾌﾟ
MPP
ANB MR20204              ; ｻﾃﾗｲﾄ情報切替OK
SET @MR500               ; Err186:ｻﾃﾗｲﾄ切替NG
CON
JMP @MR1604
STG @MR1604
AND @MR503               ; Err186 Release
RES @MR503               ; Err186 Release
CON
JMP @MR1605
STG @MR1605
RES MR12103              ; ｲﾝｸ照合処理中
CON
JMP @MR1100              ; 次ｽﾃｯﾌﾟ
STG @MR1203              ; 次ｽﾃｯﾌﾟ
MPS
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
JMP @MR1400              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
MPS
ANB MR12100              ; 交換時照合2回目
JMP @MR1204              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12100              ; 交換時照合2回目
MPS
ANB LR115                ; PEB Only
SET @MR300               ; Err85
CON
JMP @MR1205              ; 次ｽﾃｯﾌﾟ
MPP
AND LR115                ; PEB Only
SET MR7313               ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ解除
CON
JMP @MR4900
STG @MR4900
SET MR12110
CON
OUT MR12111
CON
AND R1005                ; START(REAR)
TMR @14 #30
CON
AND @T14
RES MR12110
CON
SET @MR300               ; Err85
CON
JMP @MR1205              ; 次ｽﾃｯﾌﾟ
STG @MR1204              ; 次ｽﾃｯﾌﾟ
SET MR12100              ; 交換時照合2回目
CON
AND MR12101              ; TPより交換終了SW
RES LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
CON
JMP @MR1100              ; 次ｽﾃｯﾌﾟ
STG @MR1205              ; 次ｽﾃｯﾌﾟ
AND @MR303               ; Err85解除完了
RES @MR303               ; Err85解除完了
CON
JMP @MR1206              ; 次ｽﾃｯﾌﾟ
STG @MR1206              ; 次ｽﾃｯﾌﾟ
RES MR12100              ; 交換時照合2回目
CON
JMP @MR1207              ; 次ｽﾃｯﾌﾟ
STG @MR1207              ; 次ｽﾃｯﾌﾟ
RES MR7313               ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ解除
CON
MPS
AND R5006                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ確認
JMP @MR1208
MPP
ANB R5006                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ確認
TMR @5 #10
CON
AND @T5
SET @MR300               ; Err85
CON
JMP @MR1205              ; 次ｽﾃｯﾌﾟ
STG @MR1208
AND MR11711              ; KDﾄﾞﾗﾑ交換完了FLG
MPS
AND R50004               ; TASKDO
RES @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
CON
RES MR11711              ; KDﾄﾞﾗﾑ交換完了FLG
CON
ENDS
MPP
ANB R50004               ; TASKDO
RES @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
CON
RES MR11711              ; KDﾄﾞﾗﾑ交換完了FLG
CON
ENDS
;***** ｲﾝｸｺｰﾄﾞ照合NGの時 / When collation NG  *****
;
STG @MR1300              ; ｲﾝｸ照合NGの時へ
AND @MR707               ; Err77 Release
RES @MR707               ; Err77 Release
CON
JMP @MR1301              ; 次ｽﾃｯﾌﾟ
STG @MR1301              ; 次ｽﾃｯﾌﾟ
RES MR12103              ; ｲﾝｸ照合処理中
CON
JMP @MR1100              ; 次ｽﾃｯﾌﾟ
;<h1/>***** 染料/顔料判別　ﾊﾞﾌﾞﾘﾝｸﾞ処理 /    *****
;
STG @MR1400              ; 次ｽﾃｯﾌﾟ
MPS
LD R50003                ; HOME
LD @MR2013               ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給完了
OR LR39915               ; MODELNO 変更FLG
ANL
ANL
JMP @MR1401              ; 次ｽﾃｯﾌﾟ
MPP
LDB R50003               ; HOME
AND R5806                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5906                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
ANL
JMP @MR1401              ; 次ｽﾃｯﾌﾟ
STG @MR1401              ; 次ｽﾃｯﾌﾟ
MPS
LD LR39910               ; 電源投入FLG
OR LR39915               ; MODELNO 変更FLG
ANB R50004               ; TASKDO
AND R50007               ; 原点復帰　完了
ANL
JMP @MR1402              ; 次ｽﾃｯﾌﾟ
MPP
LDB LR39910              ; 電源投入FLG
ANB LR39915              ; MODELNO 変更FLG
ANL
JMP @MR1500
STG @MR1402              ; 次ｽﾃｯﾌﾟ
ANB @MR3502              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 実行中
SET @MR3800              ; ﾊﾞﾌﾞﾘﾝｸﾞ call
CON
JMP @MR1403              ; 次ｽﾃｯﾌﾟ
STG @MR1403              ; 次ｽﾃｯﾌﾟ
AND @MR3801              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 完了
RES @MR3801              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 完了
CON
JMP @MR1415              ; 終了処理
STG @MR1500
MPS
LD MR009                 ; Auto Interlock
ANB MR12014              ; 背面ｲﾝｸ吐出起動中
ANB LR39910              ; 電源投入FLG
ANL
SET MR25915
CON
JMP @MR1415              ; 終了処理
MRD
LDB MR009                ; Auto Interlock
ANB MR12014              ; 背面ｲﾝｸ吐出起動中
ANB LR39910              ; 電源投入FLG
ANL
JMP @MR1415              ; 終了処理
MPP
LD MR12014               ; 背面ｲﾝｸ吐出起動中
OR LR39910               ; 電源投入FLG
ANL
RES MR25915
CON
JMP @MR1415              ; 終了処理
STG @MR1415              ; 終了処理
TMR @12 #1
CON
AND @T12
RES @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
CON
ANB @MR1014              ; ﾒｲﾝｱｷｭﾑﾚｰﾀｰ制御実行中
RES MR2212               ; ST bubbling flag Rear Sw
CON
RES LR39910              ; 電源投入FLG
CON
RES LR39915              ; MODELNO 変更FLG
CON
ENDS
;<h1/>*****　ﾒｲﾝ ｻﾌﾞﾀﾝｸ供給制御 @MR2000〜@MR2915　*****
;　
;　
LD R50004                ; TASKDO
LDB R50002               ; STEP
LD R50002                ; STEP
AND LR40102              ; STEP ST3 選択 flag
ORL
ANL
OR MR12014               ; 背面ｲﾝｸ吐出起動中
LDB R50004               ; TASKDO
AND R50007               ; 原点復帰　完了
LD MR12114               ; KDﾄﾞﾗﾑ交換検出FLG
OR LR39915               ; MODELNO 変更FLG
ANL
ORL
ANB MR12211              ; ｻﾃﾗｲﾄ無し
ANB @MR3802              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 実行中
ANB LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
ANB @MR2014              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給実行中
SET @MR2000              ; 次ｽﾃｯﾌﾟ
STG @MR2000              ; 次ｽﾃｯﾌﾟ
MPS
ANB LR39915              ; MODELNO 変更FLG
SET @MR2014              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給実行中
CON
JMP @MR2001              ; 次ｽﾃｯﾌﾟ
MPP
AND LR39915              ; MODELNO 変更FLG
MPS
ANB @MR2013              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給完了
SET @MR2014              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給実行中
CON
JMP @MR2001              ; 次ｽﾃｯﾌﾟ
MPP
AND @MR2013              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給完了
ENDS
STG @MR2001              ; 次ｽﾃｯﾌﾟ
AND @MR2014              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給実行中
JMP @MR2100              ; 次ｽﾃｯﾌﾟ
STG @MR2100              ; 次ｽﾃｯﾌﾟ
MPS
LD LR39910               ; 電源投入FLG
OR LR39915               ; MODELNO 変更FLG
LDB LR39910              ; 電源投入FLG
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
ANB LR39915              ; MODELNO 変更FLG
ORL
ANL
JMP @MR2400              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給へ
MPP
LD MR12114               ; KDﾄﾞﾗﾑ交換検出FLG
ANB MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
ANL
JMP @MR2301              ; 次ｽﾃｯﾌﾟ
STG @MR2301              ; 次ｽﾃｯﾌﾟ
ANB @MR3502              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 実行中
SET @MR3800              ; ﾊﾞﾌﾞﾘﾝｸﾞ call
CON
JMP @MR2302              ; 次ｽﾃｯﾌﾟ
STG @MR2302              ; 次ｽﾃｯﾌﾟ
AND @MR3801              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 完了
RES @MR3801              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 完了
CON
JMP @MR2303              ; 次ｽﾃｯﾌﾟ
STG @MR2303              ; 次ｽﾃｯﾌﾟ
RES MR2212               ; ST bubbling flag Rear Sw
CON
RES MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
CON
JMP @MR2400              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給へ
STG @MR2400              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給へ
ANB @MR3502              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 実行中
ANB MR12310              ; KDﾄﾞﾗﾑ攪拌中
ANB MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
SET @MR3500              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call
CON
JMP @MR2401              ; 次ｽﾃｯﾌﾟへ
STG @MR2401              ; 次ｽﾃｯﾌﾟへ
AND @MR3501              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 完了
RES @MR3501              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 完了
CON
JMP @MR2402              ; 次ｽﾃｯﾌﾟへ
STG @MR2402              ; 次ｽﾃｯﾌﾟへ
SET @MR2013              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給完了
CON
JMP @MR2415              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給終了へ
STG @MR2415              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給終了へ
RES @MR2014              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給実行中
CON
ENDS
;ｻﾌﾞﾙｰﾁﾝ  /  Subroutine
;
;*****　KDﾄﾞﾗﾑ攪拌時間 / KD DRUM BUBBLING TIME　@MR3300〜MR3315　*****
;　
;　
STG MR12308              ; KDﾄﾞﾗﾑ攪拌call
ANB @MR3500              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call
MPS
SET MR12310              ; KDﾄﾞﾗﾑ攪拌中
CON
JMP @MR3002
MRD
ANB R41000               ; 背面SW有効表示
MPS
AND MR12215              ; 顔料ｲﾝｸ
MOV.D #9000 @DM0         ; KD撹拌時間
MPP
AND MR12214              ; 染料ｲﾝｸ
MOV.D #0 @DM0            ; KD撹拌時間
MPP
AND R41000               ; 背面SW有効表示
MOV.D #3600 @DM0         ; KD撹拌時間
SET @MR3000              ; 背面ﾓｰﾄﾞ指示
STG @MR3002
SET MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
SET MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
SET MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
SET MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
TMR @20 #10
CON
AND @T20
JMP @MR3003
STG @MR3003
SET MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
SET MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
TMR @13 #3
CON
AND @T13
JMP @MR3200
STG @MR3200
SET @MR312
CON
JMP @MR3201
STG @MR3201
AND @MR315
RES @MR315
CON
JMP @MR3004
STG @MR3004
TMR @1 @DM0              ; KD撹拌時間
MPS
LDB @MR3000              ; 背面ﾓｰﾄﾞ指示
OR R41000                ; 背面SW有効表示
AND @T1
OR MR12115               ; ﾊﾞﾌﾞﾘﾝｸﾞ回避
ANL
SET MR2901               ; KDｲﾝｸ循環完了FLG
CON
JMP @MR3010
MPP
LD @MR3000               ; 背面ﾓｰﾄﾞ指示
ANB R41000               ; 背面SW有効表示
ANL
RES MR2901               ; KDｲﾝｸ循環完了FLG
CON
JMP @MR3005
STG @MR3010
SET @MR1700
CON
JMP @MR3011
STG @MR3011
AND @MR1800
RES @MR1800
CON
JMP @MR3005
STG @MR3005
RES MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
RES MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
TMR @21 #5
CON
AND @T21
JMP @MR3006
STG @MR3006
RES MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
RES MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
RES MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
JMP @MR3007
STG @MR3007
RES @MR3000              ; 背面ﾓｰﾄﾞ指示
CON
SET MR12309              ; KDﾄﾞﾗﾑ攪拌時間call 完了
CON
RES MR12310              ; KDﾄﾞﾗﾑ攪拌中
CON
ENDS
;*****　ｻﾌﾞﾀﾝｸ攪拌時間 / SUBTABK BUBBLING TIME　@MR3400〜MR3415　*****
;　
;　
STG MR12300              ; ｻﾌﾞﾀﾝｸ攪拌時間call
SET MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
CON
JMP @MR3100
STG @MR3100
RES MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
RES MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
SET MR8701               ; ｻﾃﾗｲﾄA 捨て出し回収
CON
RES MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
RES MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
SET MR8901               ; ｻﾃﾗｲﾄB 捨て出し回収
CON
TMR @22 #10
CON
AND @T22
JMP @MR3101
STG @MR3101
SET MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
SET MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
SET MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
SET MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3102
STG @MR3102
MPS
AND MR12215              ; 顔料ｲﾝｸ
TMR @2 #9000
CON
AND @T2
JMP @MR3103
MRD
AND MR12214              ; 染料ｲﾝｸ
TMR @8 #0
CON
AND @T8
JMP @MR3103
MPP
AND MR12115              ; ﾊﾞﾌﾞﾘﾝｸﾞ回避
JMP @MR3103
STG @MR3103
RES MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
RES MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
TMR @23 #5
CON
AND @T23
JMP @MR3104
STG @MR3104
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES MR8701               ; ｻﾃﾗｲﾄA 捨て出し回収
CON
RES MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES MR8901               ; ｻﾃﾗｲﾄB 捨て出し回収
CON
JMP @MR3105
STG @MR3105
SET MR12301              ; ｻﾌﾞﾀﾝｸ攪拌時間call 完了
CON
RES MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
CON
ENDS
;display timing count bubbling
LD @MR200
OR @MR201
ANB @MR202
SET @MR202
LD @MR202
TMR #500 #18000
CON
AND T500
RES @MR200
CON
RES @MR201
CON
RES @MR202
;*****　ｻﾌﾞﾀﾝｸ ｲﾝｸ供給制御 / Subtank ink supply control　@MR4000〜@MR4015　*****
;｀modified for Pressurized subtank by Brian Stringer 6/26/2014
;' Change for the SP-5802,03,04,05 by Nat 8/18/2015　R135←R134
;　
STG @MR3500              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call
SET @MR3502              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 実行中
CON
JMP @MR3503
STG @MR3503
MPS
ANB MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
MPS
LDB R5806                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
ORB R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ORB R5906                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
ORB R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
JMP @MR3504
MPP
LD R5806                 ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
AND R5906                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
SET MR12209              ; ｱｷｭﾑﾚｰﾀｰ ｲﾝｸ供給OK
CON
JMP @MR3505
MPP
AND MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
JMP @MR3615
STG @MR3504
MPS
LDB R5806                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
ORB R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
SET @MR3508              ; ｻﾃﾗｲﾄA ｲﾝｸ供給
MRD
LD R5806                 ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
SET @MR011               ; ｻﾃﾗｲﾄA  ｲﾝｸ供給OK
MRD
LDB R5906                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
ORB R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
SET @MR3600              ; ｻﾃﾗｲﾄB ｲﾝｸ供給
MRD
LD R5906                 ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
SET @MR012               ; ｻﾃﾗｲﾄB  ｲﾝｸ供給OK
MPP
JMP @MR3614
STG @MR3505
RES MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
RES MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
RES MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
RES MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
JMP @MR3615
;Satellite A
STG @MR3508              ; ｻﾃﾗｲﾄA ｲﾝｸ供給
RES MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
SET MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
SET MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
MPS
ANB R9500                ; Pressurized Subtank Detection FL
SET @MR002               ; ｻﾌﾞﾀﾝｸA大気開放
CON
TMR @26 #1
CON
AND @T26
SET MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3509
MPP
AND R9500                ; Pressurized Subtank Detection FL
SET MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3509
STG @MR3509
LD R5806                 ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
OR @MR414                ; ｻﾌﾞﾀﾝｸHI検出表示中
OR MR12114               ; KDﾄﾞﾗﾑ交換検出FLG
ANL
TMR @27 #10
CON
AND @T27
RES MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3510
STG @MR3510
TMR @15 #1
CON
AND @T15
RES MR8613               ; ｻﾃﾗｲﾄA KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES @MR002               ; ｻﾌﾞﾀﾝｸA大気開放
CON
SET @MR011               ; ｻﾃﾗｲﾄA  ｲﾝｸ供給OK
CON
ENDS
;Satellite B
STG @MR3600              ; ｻﾃﾗｲﾄB ｲﾝｸ供給
RES MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
SET MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
SET MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
MPS
ANB R9500                ; Pressurized Subtank Detection FL
SET @MR003               ; ｻﾌﾞﾀﾝｸB大気開放
CON
TMR @28 #1
CON
AND @T28
SET MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3601
MPP
AND R9500                ; Pressurized Subtank Detection FL
SET MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3601
STG @MR3601
LD R5906                 ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
AND R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
OR @MR414                ; ｻﾌﾞﾀﾝｸHI検出表示中
OR MR12114               ; KDﾄﾞﾗﾑ交換検出FLG
ANL
TMR @29 #10
CON
AND @T29
RES MR8904               ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
JMP @MR3602
STG @MR3602
TMR @17 #1
CON
AND @T17
RES MR8813               ; ｻﾃﾗｲﾄB KDﾄﾞﾗﾑ撹拌 開
CON
RES MR8812               ; ｻﾃﾗｲﾄB ｲﾝｸ供給 開
CON
RES MR8815               ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
RES @MR003               ; ｻﾌﾞﾀﾝｸB大気開放
CON
SET @MR012               ; ｻﾃﾗｲﾄB  ｲﾝｸ供給OK
CON
ENDS
;
STG @MR3614
AND @MR011               ; ｻﾃﾗｲﾄA  ｲﾝｸ供給OK
AND @MR012               ; ｻﾃﾗｲﾄB  ｲﾝｸ供給OK
RES @MR011               ; ｻﾃﾗｲﾄA  ｲﾝｸ供給OK
CON
RES @MR012               ; ｻﾃﾗｲﾄB  ｲﾝｸ供給OK
CON
JMP @MR3615
STG @MR3615
SET @MR3501              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 完了
CON
RES @MR3502              ; ｻﾌﾞﾀﾝｸｲﾝｸ供給制御call 実行中
CON
ENDS
;*****　ｲﾝｸｺｰﾄﾞ照合処理 / INK CODE COLLATION　@MR4300〜@MR4315　*****
;　
;　
;<h1/>立上げ時対応の為、照合無効化
;Until setup, BC collect disable
STG @MR3700              ; ｲﾝｸ照合処理call FLG
RES @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
CON
SET @MR3702              ; ｲﾝｸ照合処理call 実行中
CON
JMP @MR3704              ; 次ｽﾃｯﾌﾟ
STG @MR3704              ; 次ｽﾃｯﾌﾟ
MPS
LDB LR000
ANB MR102                ; Sub：BCR照合実行中
ANL
@SET MR100               ; Sub:BCR照合
CON
JMP @MR3705              ; 次ｽﾃｯﾌﾟ
MPP
AND LR000
SET @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
CON
JMP @MR3708              ; 次ｽﾃｯﾌﾟ
STG @MR3705              ; 次ｽﾃｯﾌﾟ
AND MR101                ; Sub:BCR照合完了
RES MR101                ; Sub:BCR照合完了
CON
JMP @MR3706              ; 次ｽﾃｯﾌﾟ
STG @MR3706              ; 次ｽﾃｯﾌﾟ
JMP @MR3707              ; 次ｽﾃｯﾌﾟ
STG @MR3707              ; 次ｽﾃｯﾌﾟ
MPS
LDB MR105                ; 照合ｺｰﾄﾞﾃﾞｰﾀ異常
AND MR106                ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
ANL
SET @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
CON
JMP @MR3708              ; 次ｽﾃｯﾌﾟ
MPP
LD MR105                 ; 照合ｺｰﾄﾞﾃﾞｰﾀ異常
ANB MR106                ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
ANL
RES @MR3712              ; 照合ｺｰﾄﾞﾃﾞｰﾀOK
CON
JMP @MR3708              ; 次ｽﾃｯﾌﾟ
STG @MR3708              ; 次ｽﾃｯﾌﾟ
SET @MR3701              ; ｲﾝｸ照合処理call 完了
CON
RES @MR3702              ; ｲﾝｸ照合処理call 実行中
CON
ENDS
;*****　ﾊﾞﾌﾞﾘﾝｸﾞ / BUBBLING @MR4500〜@MR4515　*****
;　
;　
STG @MR3800              ; ﾊﾞﾌﾞﾘﾝｸﾞ call
SET @MR3802              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 実行中
CON
JMP @MR3804              ; 次ｽﾃｯﾌﾟ
STG @MR3804              ; 次ｽﾃｯﾌﾟ
SET MR2212               ; ST bubbling flag Rear Sw
CON
JMP @MR3805              ; 次ｽﾃｯﾌﾟ
STG @MR3805              ; 次ｽﾃｯﾌﾟ
RES MR12309              ; KDﾄﾞﾗﾑ攪拌時間call 完了
CON
RES MR12301              ; ｻﾌﾞﾀﾝｸ攪拌時間call 完了
CON
ANB MR12310              ; KDﾄﾞﾗﾑ攪拌中
ANB MR12302              ; ｻﾌﾞﾀﾝｸ攪拌中
JMP @MR3806              ; 次ｽﾃｯﾌﾟ
STG @MR3806              ; 次ｽﾃｯﾌﾟ
MPS
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
SET @MR200
CON
SET MR12300              ; ｻﾌﾞﾀﾝｸ攪拌時間call
CON
JMP @MR3807              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
JMP @MR3807              ; 次ｽﾃｯﾌﾟ
STG @MR3807              ; 次ｽﾃｯﾌﾟ
MPS
LDB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
AND MR12301              ; ｻﾌﾞﾀﾝｸ攪拌時間call 完了
ANL
RES @MR200
CON
RES MR12301              ; ｻﾌﾞﾀﾝｸ攪拌時間call 完了
CON
JMP @MR3808              ; 次ｽﾃｯﾌﾟ
MPP
AND MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
JMP @MR3808              ; 次ｽﾃｯﾌﾟ
STG @MR3808              ; 次ｽﾃｯﾌﾟ
SET @MR201
CON
SET MR12308              ; KDﾄﾞﾗﾑ攪拌call
CON
JMP @MR3809              ; 次ｽﾃｯﾌﾟ
STG @MR3809              ; 次ｽﾃｯﾌﾟ
AND MR12309              ; KDﾄﾞﾗﾑ攪拌時間call 完了
RES @MR201
CON
RES MR12309              ; KDﾄﾞﾗﾑ攪拌時間call 完了
CON
JMP @MR3810              ; 次ｽﾃｯﾌﾟ
STG @MR3810              ; 次ｽﾃｯﾌﾟ
SET @MR3812
CON
RES MR2212               ; ST bubbling flag Rear Sw
CON
SET MR805                ; ﾊﾞﾌﾞﾘﾝｸﾞ終了
CON
SET @MR3801              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 完了
CON
RES @MR3802              ; ﾊﾞﾌﾞﾘﾝｸﾞ call 実行中
CON
ENDS
;<h1/>Err
;
;***** ｲﾝｸ照合要求表示 ERROR NO 78  /  Ink CODE COLLATION display  *****
;
STG @MR708               ; Err76 ｲﾝｸ照合表示へ
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR709               ; 次ｽﾃｯﾌﾟへ
MOV #78 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR709               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR710               ; 次ｽﾃｯﾌﾟへ
STG @MR710               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
RES MR12115              ; ﾊﾞﾌﾞﾘﾝｸﾞ回避
CON
SET @MR715               ; Err76 Release
CON
ENDS
;***** ｲﾝｸ照合NG表示 ERROR NO 79  /  Ink CODE NG display  *****
;
STG @MR700               ; Err77 ｲﾝｸ照合NG表示へ
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR701               ; 次ｽﾃｯﾌﾟへ
MOV #79 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR701               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR702               ; 次ｽﾃｯﾌﾟへ
STG @MR702               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET @MR707               ; Err77 Release
CON
ENDS
;***** KDﾄﾞﾗﾑ交換表示 ERROR NO 86  /  KD DRUM CHANGE display  *****
;
LDB R50003               ; HOME
LDB R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ORB R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANL
ANB MR12211              ; ｻﾃﾗｲﾄ無し
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
ANB @MR614               ; KDﾄﾞﾗﾑ交換表示中
AND R50004               ; TASKDO
TMR @6 #300
LD R50003                ; HOME
AND MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
ANB LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
SET LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
;<h1/>Manual Barrel Change Request For External Drum qualification - Anton Swampillai (EPI)
LD @T6
LDB R50003               ; HOME
AND R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
AND R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANB MR12211              ; ｻﾃﾗｲﾄ無し
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
ANB @MR614               ; KDﾄﾞﾗﾑ交換表示中
AND R50004               ; TASKDO
AND MR24105              ; Manual Trigger BC
ORL
SET @MR608               ; KDﾄﾞﾗﾑ交換表示へ
LDB R50003               ; HOME
AND R5807                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
AND R5907                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出2B(中-上)
ANB MR12211              ; ｻﾃﾗｲﾄ無し
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
ANB @MR614               ; KDﾄﾞﾗﾑ交換表示中
AND R50004               ; TASKDO
ANB MR24105              ; Manual Trigger BC
ANB R42900               ; Setup SW 表示
OUT MR24106              ; Enable Manual
STG @MR608               ; KDﾄﾞﾗﾑ交換表示へ
ANB R50008               ; Error
AND R50007               ; 原点復帰　完了
MPS
SET @MR614               ; KDﾄﾞﾗﾑ交換表示中
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR609               ; 次ｽﾃｯﾌﾟへ
MPP
MOV #86 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR609               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR610               ; 次ｽﾃｯﾌﾟへ
STG @MR610               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
MPS
AND MR2905               ; ﾀﾝｸ交換時継続選択
RES MR24105              ; Manual Trigger BC
CON
RES MR2901               ; KDｲﾝｸ循環完了FLG
CON
RES MR2212               ; ST bubbling flag Rear Sw
CON
JMP @MR611               ; 次ｽﾃｯﾌﾟへ
MPP
ANB MR2905               ; ﾀﾝｸ交換時継続選択
RES @MR614               ; KDﾄﾞﾗﾑ交換表示中
CON
ENDS
STG @MR611               ; 次ｽﾃｯﾌﾟへ
SET LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
CON
SET MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
CON
SET MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
CON
JMP @MR612
STG @MR612
LDB LR39911              ; ｲﾝｸｺｰﾄﾞ照合要求
OR MR12100               ; 交換時照合2回目
ANL
SET MR7313               ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ解除
CON
ANB R5006                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ確認
JMP @MR613
STG @MR613
AND R5006                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ確認
SET MR11711              ; KDﾄﾞﾗﾑ交換完了FLG
CON
JMP @MR615
STG @MR615
RES MR12113              ; KDﾄﾞﾗﾑ交換未完了FLG
CON
RES @MR614               ; KDﾄﾞﾗﾑ交換表示中
CON
ENDS
;***** KDﾄﾞﾗﾑ交換完了確認 ERROR NO 87  /  KD Drum Change end　Check  *****
;
STG @MR300               ; Err85
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR301
MOV #87 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR301
AND R50008               ; Error
JMP @MR302
STG @MR302
ANB R50008               ; Error
SET @MR303               ; Err85解除完了
CON
ENDS
;***** KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ異常 ERROR NO 88  /  KD Drum Nozzle lock error  *****
;
LD R50004                ; TASKDO
ANB MR12114              ; KDﾄﾞﾗﾑ交換検出FLG
ANB @MR614               ; KDﾄﾞﾗﾑ交換表示中
ANB MR12211              ; ｻﾃﾗｲﾄ無し
AND R7313                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ解除
ANB R5006                ; KDﾄﾞﾗﾑﾉｽﾞﾙﾛｯｸ確認
ANB @MR309               ; KDﾉｽﾞﾙﾛｯｸ異常中
TMR @9 #10
LDP @T9
SET @MR308
STG @MR308
ANB R50008               ; Error
SET @MR309               ; KDﾉｽﾞﾙﾛｯｸ異常中
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR310
MOV #88 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR310
AND R50008               ; Error
JMP @MR311
STG @MR311
ANB R50008               ; Error
RES @MR309               ; KDﾉｽﾞﾙﾛｯｸ異常中
CON
ENDS
;***** ｻﾌﾞﾀﾝｸ LO ERROR NO 89  /  subtank LO error display  *****
;
LDB R50003               ; HOME
AND R50004               ; TASKDO
ANB MR12211              ; ｻﾃﾗｲﾄ無し
LDB R5806                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出1(下)
ORB R5906                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出1(下)
ANL
AND R50007               ; 原点復帰　完了
ANB R50008               ; Error
ANB @MR406               ; ｻﾌﾞﾀﾝｸLO検出表示中
TMR @18 #10
LD @T18
RES @MR2013              ; ﾒｲﾝｻﾌﾞﾀﾝｸ供給完了
CON
RES MR12209              ; ｱｷｭﾑﾚｰﾀｰ ｲﾝｸ供給OK
CON
SET @MR400               ; ｻﾌﾞﾀﾝｸLO検出表示へ
STG @MR400               ; ｻﾌﾞﾀﾝｸLO検出表示へ
ANB R50008               ; Error
SET @MR406               ; ｻﾌﾞﾀﾝｸLO検出表示中
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR401               ; 次ｽﾃｯﾌﾟへ
MOV #89 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR401               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR402               ; 次ｽﾃｯﾌﾟへ
STG @MR402               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
JMP @MR403               ; 次ｽﾃｯﾌﾟへ
STG @MR403               ; 次ｽﾃｯﾌﾟへ
OUT R59806               ; ｻﾌﾞﾀﾝｸLo検出中
CON
AND MR12209              ; ｱｷｭﾑﾚｰﾀｰ ｲﾝｸ供給OK
RES @MR406               ; ｻﾌﾞﾀﾝｸLO検出表示中
CON
ENDS
;***** ｻﾌﾞﾀﾝｸ HI ERROR NO 90  /  subtank HI error display  *****
;' Change for the SP-5802,03,04,05 by Nat 8/18/2015　R135←R134
;
LDB MR12211              ; ｻﾃﾗｲﾄ無し
AND R50004               ; TASKDO
ANB R50008               ; Error
LDB R5808                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出3(上)
ORB R5908                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出3(上)
ANL
ANB @MR414               ; ｻﾌﾞﾀﾝｸHI検出表示中
TMR @4 #10
LD @T4
RES MR8704               ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
CON
RES MR8612               ; ｻﾃﾗｲﾄA ｲﾝｸ供給 開
CON
RES MR8615               ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ ｲﾝｸ供給 開
CON
SET @MR408               ; ｻﾌﾞﾀﾝｸHI検出表示へ
STG @MR408               ; ｻﾌﾞﾀﾝｸHI検出表示へ
ANB R50008               ; Error
SET @MR414               ; ｻﾌﾞﾀﾝｸHI検出表示中
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR409               ; 次ｽﾃｯﾌﾟへ
MOV #90 DM400            ; ﾒｯｾｰｼﾞNo.
STG @MR409               ; 次ｽﾃｯﾌﾟへ
AND R50008               ; Error
JMP @MR410               ; 次ｽﾃｯﾌﾟへ
STG @MR410               ; 次ｽﾃｯﾌﾟへ
ANB R50008               ; Error
SET R50011               ; TRAP
CON
RES @MR414               ; ｻﾌﾞﾀﾝｸHI検出表示中
CON
ENDS
;Err109
;Err109 Start Bubbling Information 
STG @MR312
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR313
MOV #109 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR313
AND R50008               ; Error
JMP @MR314
STG @MR314
ANB R50008               ; Error
SET @MR315
CON
ENDS
;Err110
;Err110 Finish  bubbling KD Drum
STG @MR1700
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR1701
MOV #110 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR1701
AND R50008               ; Error
JMP @MR1702
STG @MR1702
ANB R50008               ; Error
SET @MR1800
CON
ENDS
;***** ｻﾃﾗｲﾄ切替NG ERROR NO 186  /  Satelite Change NG  *****
;
STG @MR500               ; Err186:ｻﾃﾗｲﾄ切替NG
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR501
MOV #186 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR501
AND R50008               ; Error
JMP @MR502
STG @MR502
ANB R50008               ; Error
SET @MR503               ; Err186 Release
CON
ENDS
;Err206 SEPAREL A Vacuuming NG
;セパレルA  真空圧異常
LD R2104                 ; 脱気用ﾎﾟﾝﾌﾟ
TMR @7 #200
LD R50004                ; TASKDO
AND @T7
LDB R5804                ; ｻﾃﾗｲﾄA ｾﾊﾟﾚﾙ 真空検出1
ORB R5805                ; ｻﾃﾗｲﾄA ｾﾊﾟﾚﾙ 真空検出2
ANL
TMR @24 #50
CON
AND @T24
ANB @MR803               ; Subtank Pressure Error#204
SET @MR800               ; Err203:Subtank Pressure NG
STG @MR800               ; Err203:Subtank Pressure NG
ANB R50008               ; Error
SET @MR803               ; Subtank Pressure Error#204
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR801
MOV #206 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR801
AND R50008               ; Error
JMP @MR802
STG @MR802
ANB R50008               ; Error
RES @MR803               ; Subtank Pressure Error#204
CON
ENDS
;Err207 SEPAREL B Vacuuming NG
;セパレルB  真空圧異常
LD R50004                ; TASKDO
AND @T7
LDB R5904                ; ｻﾃﾗｲﾄB ｾﾊﾟﾚﾙ 真空検出1
ORB R5905                ; ｻﾃﾗｲﾄB ｾﾊﾟﾚﾙ 真空検出2
ANL
TMR @16 #50
CON
AND @T16
ANB @MR807               ; Error #204 is occurring
SET @MR804               ; connection Err #204
STG @MR804               ; connection Err #204
ANB R50008               ; Error
SET @MR807               ; Error #204 is occurring
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR805
MOV #207 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR805
AND R50008               ; Error
JMP @MR806
STG @MR806
ANB R50008               ; Error
RES @MR807               ; Error #204 is occurring
CON
ENDS
;Err208 Diaphragm pump A flow rate detection
LD R50004                ; TASKDO
AND R5808                ; ｻﾃﾗｲﾄA ｻﾌﾞﾀﾝｸ 液面検出3(上)
AND R8704                ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
LDB R5802                ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT1
ORB R5803                ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT2
ANL
ANB @MR811               ; Error #205 is occurring
AND<> W1BC6 #5           ; ｻﾃﾗｲﾄNo_from Host
TMR @25 #100
CON
AND @T25
MPS
AND< DM1000 #2           ; Count err 208
SET @MR808               ; Pump flow rate Error #205
MRD
AND>= DM1000 #2          ; Count err 208
SET R2400                ; Disp  warning Err208
MPP
@INC DM1000              ; Count err 208
LD @MR3812
ANB T90
LDB @MR3812
AND T90
ORL
RES R2400                ; Disp  warning Err208
CON
RES @MR3812
CON
DW #0 DM1000             ; Count err 208
LD R8704                 ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
LD R5802                 ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT1
OR R5803                 ; ｻﾃﾗｲﾄA ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT2
ANL
TMR #90 #100
STG @MR808               ; Pump flow rate Error #205
ANB R50008               ; Error
SET @MR811               ; Error #205 is occurring
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR809               ; connection Err #205
MOV #208 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR809               ; connection Err #205
AND R50008               ; Error
JMP @MR810
STG @MR810
ANB R50008               ; Error
RES @MR811               ; Error #205 is occurring
CON
ENDS
;Err209 Diaphragm pump B flow rate detection
LD R50004                ; TASKDO
AND R5908                ; ｻﾃﾗｲﾄB ｻﾌﾞﾀﾝｸ 液面検出3(上)
AND R8904                ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
LDB R5902                ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT1
ORB R5903                ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT2
ANL
ANB @MR815
AND<> W1BC6 #5           ; ｻﾃﾗｲﾄNo_from Host
TMR @19 #100
CON
AND @T19
MPS
AND< DM1001 #2
SET @MR812
MRD
AND>= DM1001 #2
SET R2400                ; Disp  warning Err208
MPP
@INC DM1001
LD @MR3812
ANB T91
LDB @MR3812
AND T91
ORL
RES R2400                ; Disp  warning Err208
CON
RES @MR3812
CON
DW #0 DM1001
LD R8904                 ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ ON
LD R5902                 ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT1
OR R5903                 ; ｻﾃﾗｲﾄB ﾀﾞｲﾔﾌﾗﾑﾎﾟﾝﾌﾟ流量計OUT2
ANL
TMR #91 #100
STG @MR812
ANB R50008               ; Error
SET @MR815
CON
SET R50008               ; Error
CON
SET R50113               ; Err ST3
CON
JMP @MR813
MOV #209 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR813
AND R50008               ; Error
JMP @MR814
STG @MR814
ANB R50008               ; Error
RES @MR815
CON
ENDS
;
LD @MR002                ; ｻﾌﾞﾀﾝｸA大気開放
OR MR12302               ; ｻﾌﾞﾀﾝｸ攪拌中
OUT MR15001              ; ｻﾌﾞﾀﾝｸA大気開放
LD @MR003                ; ｻﾌﾞﾀﾝｸB大気開放
OR MR12302               ; ｻﾌﾞﾀﾝｸ攪拌中
OUT MR15501              ; ｻﾌﾞﾀﾝｸB大気開放
;Bypass heater cleaning request
;7 maret 2017
STG MR011                ; Bypass cleaning request
AND MR25915
JMP @MR4000
STG @MR4000
RES @MR4401
CON
SET @MR504
CON
JMP @MR4002
STG @MR4002
AND @MR507
MPS
AND @MR4401
SET R50915               ; Display cleaning request
CON
RES @MR507
CON
JMP @MR4003
MPP
ANB @MR4401
RES @MR507
CON
JMP @MR4000
LD R42906                ; Cleaning OK
SET @MR4401
LD @MR4401
OUT R42908
STG @MR4003
ANB R1100                ; ﾒｲﾝｴｱｰ 正常=ON
JMP @MR4004
STG @MR4004
AND R1100                ; ﾒｲﾝｴｱｰ 正常=ON
AND R1001                ; 操作BOX ｽﾀｰﾄSW
JMP @MR4005
STG @MR4005
TMR @30 #50
CON
AND @T30
RES MR009                ; Auto Interlock
CON
RES MR25915
CON
SET R50011               ; TRAP
CON
ENDS
;Error 210 Bypass cleaning request
STG @MR504
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51603               ; Err IND3ST3
CON
JMP @MR505
MOV #210 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR505
AND R50008               ; Error
JMP @MR506
STG @MR506
ANB R50008               ; Error
SET @MR507
CON
ENDS
END
ENDH
