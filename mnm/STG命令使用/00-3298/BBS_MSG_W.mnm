DEVICE:53
;MODULE:BBS_MSG_W
;MODULE_TYPE:1
;OneSystem SupportMacro
;
;-----------------------------------------
;Macro Name 	: 	BBS_MSG_W
;Macro Type	:	Self Holding
;Function	:	ｽﾃｰﾀｽ表示
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;引数：
;	V0	=	表示ｸﾞﾙｰﾌﾟ番号			#1〜8		
;	V1	=	表示行番号				#1〜3
;	P0	=	表示文字列格納ﾃﾞﾊﾞｲｽ
;	
;機能：
;	指定ｸﾞﾙｰﾌﾟの指定ｽﾃｰﾀｽ表示行に指定文字列を表示する
LD Run_Mode              ; _運転ﾓｰﾄﾞ指定
MEND
LD TRAPDO                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @R000 @R015         ; ﾏｸﾛ実行中  ﾏｸﾛ異常発生
CON
MEND
;<h1/>Argument Range Check/引数範囲ﾁｪｯｸ
LD< V0 #1                ; Group_No
OR> V0 #8                ; Group_No
OR< V1 #1                ; Line_No
OR> V1 #3                ; Line_No
SET MacroDown            ; _ｻﾎﾟｰﾄﾏｸﾛ引数異常発生
CON
SET @R015                ; ﾏｸﾛ異常発生
;<h1/>Init/初期化
LD @CR2008
SET @R000                ; ﾏｸﾛ実行中
;<h1/>Exclusive/排他占有
LDB Run_Mode             ; _運転ﾓｰﾄﾞ指定
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
CON
ANB SYSEXC_BBS1:Z10      ; _Group1　BBS排他
SET SYSEXC_BBS1:Z10      ; _Group1　BBS排他
CON
SET @R001                ; BBS表示
;<h1/>Char String Display Of BBS/BBS指定行文字列表示
STG @R001                ; BBS表示
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #63
CON
STA Z9
MRD
AND= V1 #1               ; Line_No
SMOV P0 BBS1_MsgBox1:Z9  ; Char_Str  BBS1_MsgBox1
MRD
AND= V1 #2               ; Line_No
SMOV P0 BBS1_MsgBox2:Z9  ; Char_Str  BBS1_MsgBox2
MRD
AND= V1 #3               ; Line_No
SMOV P0 BBS1_MsgBox3:Z9  ; Char_Str  BBS1_MsgBox3
MPP
JMP @R002                ; BBS排他開放
;<h1/>End Process/終了処理
;BBS排他開放
STG @R002                ; BBS排他開放
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
CON
RES SYSEXC_BBS1:Z10      ; _Group1　BBS排他
MRD
MEND
MPP
RES @R000                ; ﾏｸﾛ実行中
CON
ENDS
END
ENDH
