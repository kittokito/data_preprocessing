DEVICE:53
;MODULE:Display
;MODULE_TYPE:0
;フリッカー
;Flicker Timer
LD CR2002                ; Normal ON
FLIK #30 #30 @MR000
LD CR2002                ; Normal ON
FLIK #60 #60 @MR003
LD CR2002                ; Normal ON
FLIK #10 #10 @MR004
LD CR2002                ; Normal ON
FLIK #10 #120 @MR005
LD CR2002                ; Normal ON
FLIK #20 #20 R100        ; -0.2 Seconds flicker 0.2 seconds
LD CR2002                ; Normal ON
FLIK #50 #50 R101        ; -0.5 Seconds flicker 0.5 seconds
LD CR2002                ; Normal ON
FLIK #5 #50 @MR006
;タワーランプ (赤)
;Tower Lamp (Red)
LD R50010                ; Emaegency Stop
LDB R50007               ; HOME FINISHED
AND LR49914
ORL
LDB R50007               ; HOME FINISHED
ANB R50004               ; TASK DO
ANB LR49914
OR R50008                ; Whole ERROR
OR LR49914
OR MR511                 ; to error "1"
OR MR512                 ; to error"0"
AND @MR000
ORL
OUT MR7000               ; TOWER LAMP(R)
;タワーランプ (黄)
;Tower Lamp (Yellow)
LD MR2214
ANB R4000                ; START SW
AND CR2003               ; Normal OFF
OUT @MR001
LD R50004                ; TASK DO
ANB @MR001
ANB R51503
ANB MR15100
ANB MR15101              ; display label remove
LD @MR001
AND @MR000
ORL
LD R51503
OR MR15101               ; display label remove
LD CR2003                ; Normal OFF
ANB R51503
ANB R13300
AND R50007               ; HOME FINISHED
ORL
AND @MR004
ORL
ANB R50010               ; Emaegency Stop
OUT MR7001               ; TOWER LAMP(Y)
;タワーランプ (緑)
;Tower Lamp (Green)
LDB R50004               ; TASK DO
ANB R50300               ; Pause
ANB LR49914
LD R50300                ; Pause
AND @MR000
ORL
LD LR49914
ANB R50007               ; HOME FINISHED
ORL
ANB R50010               ; Emaegency Stop
OUT MR7002               ; TOWER LAMP(G)
;タワーランプ (緑)
;Tower Lamp (Green)
LD MR15100
AND @MR004
OUT MR7003               ; TOWER LAMP(B)
;ブザー
;Buzzer
LD R50010                ; Emaegency Stop
LD R50008                ; Whole ERROR
AND R50108               ; Error 
ANB R50015               ; Error SW-ON
ORL
OR LR49914
LD R51503
AND @MR003
ORL
LD CR2003                ; Normal OFF
ANB R51503
ANB R13300
AND R50007               ; HOME FINISHED
AND @MR005
ORL
LD MR15100
AND @MR004
ORL
LD MR100
AND @MR006
ORL
LD MR511                 ; to error "1"
AND @MR003
ORL
LD MR512                 ; to error"0"
AND @MR003
ORL
LD MR16500               ; Dummy check from cell 1
OR MR15101               ; display label remove
AND @MR006
ORL
OUT MR7004               ; TOWER LAMP BUZZER
;ポーズランプ
;Pause Switch Lamp
LD R50300                ; Pause
OUT MR2001               ; Paused lamp
OUT MR7006               ; Sub cover SW-BOX PAUSE LAMP
;スタートランプ
;Start Switch Lamp
LD R50004                ; TASK DO
ANB R50002               ; STEP mode
LD R50002                ; STEP mode
ANB R50401               ; Post Busy
ORL
ANB R50300               ; Pause
ANB R50008               ; Whole ERROR
OR R50408                ; Start PL lighting
LD R50300                ; Pause
AND R1102                ; Safty cover
LD R50008                ; Whole ERROR
ANB R51102
ORL
LD R50002                ; STEP mode
AND R50401               ; Post Busy
ORL
AND @MR000
ORL
OUT MR2000               ; Start lamp
OUT MR7005               ; Sub cover SW-BOX START LAMP
END
ENDH
