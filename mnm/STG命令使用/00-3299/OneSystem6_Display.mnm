DEVICE:53
;MODULE:OneSystem6_Display
;MODULE_TYPE:0
;OneSystem Master-Module
;
;-----------------------------------------
;ModuleName	:	OneSystem6_Display
;ModuleType	:	Master Module
;Function	:	Lamp/Buzzer
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;<h1/>*Lamp and Buzzer/ﾗﾝﾌﾟ|ﾌﾞｻﾞｰ制御
;<h1/>Group1/ｸﾞﾙｰﾌﾟ1
;StartSw_Lamp/ｽﾀｰﾄSWﾗﾝﾌﾟ
;・点灯：動作中
;・点滅：ｴﾗｰ発生中、安全ｶﾊﾞｰﾛｯｸ解除中、ｴｱｰ圧低下中、ﾒﾝﾃﾅﾝｽ動作Post中
LD ERROR1                ; _Group1　ｴﾗｰ発生中
OR OPEN1                 ; _安全ｶﾊﾞｰ1開中
OR AirState1             ; _Group1 ｴｱｰ元圧状態
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LDB STOPDO               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
LD AUTODO                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
OR HOMEDO                ; _原点復帰中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB ERROR1               ; _Group1　ｴﾗｰ発生中
ANB OPEN1                ; _安全ｶﾊﾞｰ1開中
ANB AirState1            ; _Group1 ｴｱｰ元圧状態
LD MAINTE_IN1            ; _Group1　ﾒﾝﾃ動作中
LD= HL_STAT1 #1          ; _Group1　ﾗﾝﾌﾟ状態
ANB ERROR1               ; _Group1　ｴﾗｰ発生中
ANB OPEN1                ; _安全ｶﾊﾞｰ1開中
ANB AirState1            ; _Group1 ｴｱｰ元圧状態
LD= HL_STAT1 #2          ; _Group1　ﾗﾝﾌﾟ状態
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
ORL
ANL
ORL
OUT START_HL1            ; *Group1　ｽﾀｰﾄSWﾗﾝﾌﾟ
;StopSw_Lamp/ｽﾄｯﾌﾟSWﾗﾝﾌﾟ
;・点灯：定停止移行中
LDB TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB STOPDO               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
LD HOMEDO                ; _原点復帰中　ﾓｰﾄﾞﾌﾗｸﾞ
OR AUTODO                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND HOLD                 ; _定停止移行中
LD MAINTEDO              ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND HOLDm1               ; _Group1　ﾒﾝﾃ動作定停止移行中
ORL
ANL
OUT STOP_HL1             ; *Group1　ｽﾄｯﾌﾟSWﾗﾝﾌﾟ
;CoverSw_Lamp/ｶﾊﾞｰSWﾗﾝﾌﾟ
;・点灯：安全ｶﾊﾞｰ開中
;・点滅：安全ｶﾊﾞｰ開処理中
LD PACKED1               ; _安全ｶﾊﾞｰ1開処理中
LDB OPEN1                ; _安全ｶﾊﾞｰ1開中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR OPEN1                 ; _安全ｶﾊﾞｰ1開中
ANL
OUT COVER_HL1            ; *Group1　ｶﾊﾞｰ開要求SWﾗﾝﾌﾟ
OUT MOPEN_HL1            ; *Group1　3連ｶﾊﾞｰ開要求SWﾗﾝﾌﾟ
;AreaSw_Lamp/ｴﾘｱｾﾝｻ復帰SWﾗﾝﾌﾟ
;・点滅：ｴﾘｱｾﾝｻ遮断処理中
LD AreaSenUse            ; _ｴﾘｱｾﾝｻ1使用時出力
ANB TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
AND AREADO1              ; _Group1　ｴﾘｱｾﾝｻ遮断中
MPS
LD<= AreaSen_Sw #2       ; _ｴﾘｱｾﾝｻ1復帰SW数
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
OUT ASTART1_HL1          ; *Group1　ｴﾘｱｾﾝｻ継続実行SW1ﾗﾝﾌﾟ
MPP
LD= AreaSen_Sw #2        ; _ｴﾘｱｾﾝｻ1復帰SW数
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ANL
OUT ASTART1_HL2          ; *Group1　ｴﾘｱｾﾝｻ継続実行SW2ﾗﾝﾌﾟ
;Continue StartSw_Lamp/3連ｽﾀｰﾄSWﾗﾝﾌﾟ
;・点滅：安全ｶﾊﾞｰ開中
LD OPEN1                 ; _安全ｶﾊﾞｰ1開中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT MSTART_HL1           ; *Group1　ｶﾊﾞｰ継続実行SWﾗﾝﾌﾟ
;Post/ﾒﾝﾃ動作区切り
;・ﾒﾝﾃ動作時のみ有効
;・Post SETでｽﾀｰﾄSW待ち状態
;・ｽﾀｰﾄSW押下でPost_OK成立
LDB MAINTEDO             ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
LDB ERROR1               ; _Group1　ｴﾗｰ発生中
ANB OPEN1                ; _安全ｶﾊﾞｰ1開中
ANB AREADO1              ; _Group1　ｴﾘｱｾﾝｻ遮断中
ANB AirState1            ; _Group1 ｴｱｰ元圧状態
AND Post1                ; _Group1　動作区切り開始
LD START_SW1             ; *Group1　ｽﾀｰﾄSW
OR Post1_Invalid         ; _Group1 Post無効宣言
ANL
ORL
OUT Post1_OK             ; _Group1　動作区切り完了
LD MAINTEDO              ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
MPS
LD Post1                 ; _Group1　動作区切り開始
ANB Post1_OK             ; _Group1　動作区切り完了
ANL
MOV #2 HL_STAT1          ; _Group1　ﾗﾝﾌﾟ状態
MPP
LDB Post1                ; _Group1　動作区切り開始
OR Post1_Invalid         ; _Group1 Post無効宣言
ANL
MOV #1 HL_STAT1          ; _Group1　ﾗﾝﾌﾟ状態
;Buzzer/ﾌﾞｻﾞｰ処理
;・非常停止、ｴｱｰ元圧低下などCPU全体に係わるﾌﾞｻﾞｰ出力はﾒｲﾝTP管轄のみ
;<ﾌﾞｻﾞｰOFF要求>
LDP BZOFF_SW1            ; *Group1　ﾌﾞｻﾞｰｵﾌSW
ORF TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ORF AirState1            ; _Group1 ｴｱｰ元圧状態
ORF ERROR1               ; _Group1　ｴﾗｰ発生中
ORF AREADO1              ; _Group1　ｴﾘｱｾﾝｻ遮断中
ORF Buzzer_RQ1           ; _Group1　ﾌﾞｻﾞｰ要求
LDF SYS_Bz1_RQ1          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求1
AND= Bz_STAT1 #0         ; _Group1　ﾌﾞｻﾞｰ状態
ORL
ORF SYS_Bz1_RQ2          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求2
ORF SYS_Bz1_RQ3          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求3
ORF SYS_Bz1_RQ4          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求4
RES @R100                ; Bz1_On_RQ
LDF SYS_Bz1_RQ1          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求1
AND @R200                ; Bz1_Flik_RQ
ORP BZOFF_SW1            ; *Group1　ﾌﾞｻﾞｰｵﾌSW
RES @R200                ; Bz1_Flik_RQ
;<ﾌﾞｻﾞｰON要求>
LDP TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ORP AirState1            ; _Group1 ｴｱｰ元圧状態
ORP ERROR1               ; _Group1　ｴﾗｰ発生中
LDP AREADO1              ; _Group1　ｴﾘｱｾﾝｻ遮断中
AND AreaSen_Bz           ; _ｴﾘｱｾﾝｻ1遮断時ﾌﾞｻﾞｰ出力有無
ORL
ORP Buzzer_RQ1           ; _Group1　ﾌﾞｻﾞｰ要求
LDP SYS_Bz1_RQ1          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求1
AND= Bz_STAT1 #0         ; _Group1　ﾌﾞｻﾞｰ状態
ORL
ORP SYS_Bz1_RQ2          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求2
ORP SYS_Bz1_RQ3          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求3
ORP SYS_Bz1_RQ4          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求4
ORP MR8013               ; 次接着剤解凍要求ﾒｯｾｰｼﾞ
SET @R100                ; Bz1_On_RQ
LDP SYS_Bz1_RQ1          ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求1
AND= Bz_STAT1 #1         ; _Group1　ﾌﾞｻﾞｰ状態
SET @R200                ; Bz1_Flik_RQ
;ﾌﾞｻﾞｰは出力ONが優先とする
;ﾌﾞｻﾞｰﾌﾘｯｶ要求(WARNING_DISP)中に別ﾌﾞｻﾞｰON要求が来たらそちらを優先
LD @R100                 ; Bz1_On_RQ
OR @R200                 ; Bz1_Flik_RQ
MPS
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OUT BZOFF_HL1            ; *Group1　ﾌﾞｻﾞｰｵﾌSWﾗﾝﾌﾟ
MPP
LDB @R100                ; Bz1_On_RQ
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
OR @R100                 ; Bz1_On_RQ
ANL
OUT BZ1                  ; *Group1　ﾌﾞｻﾞｰ
;<h1/>*Tower Lamp/ﾀﾜｰﾗﾝﾌﾟ制御
;指定範囲外ﾁｪｯｸ(異常値ならJIS準拠に)
LD< TWL_Select #0        ; _ﾀﾜｰﾗﾝﾌﾟ制御選択
OR> TWL_Select #10       ; _ﾀﾜｰﾗﾝﾌﾟ制御選択
MOV #0 TWL_Select        ; _ﾀﾜｰﾗﾝﾌﾟ制御選択
;●標準ﾀﾜｰﾗﾝﾌﾟ(JIS準拠)
;ﾕｰｻﾞｰｶｽﾀﾑﾀﾜｰﾗﾝﾌﾟ未使用時に使用
LD= TWL_Select #0        ; _ﾀﾜｰﾗﾝﾌﾟ制御選択
MC
;<h1/>Red/ﾀﾜｰﾗﾝﾌﾟ赤
;・点灯：非常停止中
;・点滅：ｴﾗｰ発生中、ｴｱｰ圧低下中
LD TRAPDO                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
OR ERROR1                ; _Group1　ｴﾗｰ発生中
OR SYS_RLMP1_RQ1         ; _Group1　ﾗﾝﾌﾟ赤点滅要求1
OR SYS_RLMP1_RQ2         ; _Group1　ﾗﾝﾌﾟ赤点滅要求2
OR SYS_RLMP1_RQ3         ; _Group1　ﾗﾝﾌﾟ赤点滅要求3
OR SYS_RLMP1_RQ4         ; _Group1　ﾗﾝﾌﾟ赤点滅要求4
OUT TOWER_R1             ; *Group1　ﾀﾜｰﾗﾝﾌﾟ赤
;<h1/>Green/ﾀﾜｰﾗﾝﾌﾟ緑
;・点灯：定停止中
;・点滅：安全ｶﾊﾞｰﾛｯｸ解除中、ｴﾘｱｾﾝｻ遮断QP、ｴｱｰ圧低下QP
LDB TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
AND STOPDO               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB OPEN1                ; _安全ｶﾊﾞｰ1開中
ANB AREADO1              ; _Group1　ｴﾘｱｾﾝｻ遮断中
ANB AirState1            ; _Group1 ｴｱｰ元圧状態
LD OPEN1                 ; _安全ｶﾊﾞｰ1開中
OR AREADO1               ; _Group1　ｴﾘｱｾﾝｻ遮断中
OR AirState1             ; _Group1 ｴｱｰ元圧状態
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
OUT TOWER_G1             ; *Group1　ﾀﾜｰﾗﾝﾌﾟ緑
;<h1/>Yellow/ﾀﾜｰﾗﾝﾌﾟ黄
;・点灯：動作中
;・点滅：ｵｰﾊﾞｰﾌﾛｰ状態（AUTOのみ）
LDB TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
LD AUTODO                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
LD OVERF1                ; _Group1　ｵｰﾊﾞｰﾌﾛｰ宣言
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORB OVERF1               ; _Group1　ｵｰﾊﾞｰﾌﾛｰ宣言
ANL
OR HOMEDO                ; _原点復帰中　ﾓｰﾄﾞﾌﾗｸﾞ
LD MAINTEDO              ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND MAINTE_IN1           ; _Group1　ﾒﾝﾃ動作中
ORL
ANL
OUT TOWER_Y1             ; *Group1　ﾀﾜｰﾗﾝﾌﾟ黄
MCR
;
END
ENDH
