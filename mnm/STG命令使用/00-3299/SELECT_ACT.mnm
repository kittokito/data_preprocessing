DEVICE:53
;MODULE:SELECT_ACT
;MODULE_TYPE:1
;OneSystem SupportMacro
;
;-----------------------------------------
;Macro Name 	: 	SELECT_ACT
;Macro Type	:	Self Holding
;Function	:	4択画面表示
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;引数：
;	V0	=	表示ｸﾞﾙｰﾌﾟ番号			#1〜8
;	V1	=	ﾀｽｸ番号					#0〜31
;	V2	=	画面番号				#0〜31
;	V3	=	選択SW1名称番号			#0〜100(#0は未表示指定)
;	V4	=	選択SW2名称番号			#0〜100(#0は未表示指定)
;	V5	=	選択SW3名称番号			#0〜100(#0は未表示指定)
;	V6	=	選択SW4名称番号			#0〜100(#0は未表示指定)
;	V7	=	ﾌﾞｻﾞｰ有無				#0=ﾌﾞｻﾞｰ無し、#1=ﾌﾞｻﾞｰ有り
;	P9	=	選択肢					#1〜#4						(戻り値)
;	
;機能：
;	指定TPに指定画面番号の選択画面を表示し、選択肢番号を戻り値として格納する
LD Run_Mode              ; _運転ﾓｰﾄﾞ指定
MOV #0 P9                ; Select_SW
MEND
LD TRAPDO                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @R000 @R015
CON
MEND
;<h1/>Argumant Range Check/引数範囲ﾁｪｯｸ
LD< V0 #1                ; Group_No
OR> V0 #8                ; Group_No
OR< V1 #0                ; Task_No
OR> V1 #100              ; Task_No
LD> V1 #31               ; Task_No
AND< V1 #100             ; Task_No
ORL
OR< V2 #0                ; Screen_No
OR> V2 #31               ; Screen_No
OR< V3 #0                ; SelectSw1No
OR> V3 #98               ; SelectSw1No
OR< V4 #0                ; SelectSw2No
OR> V4 #98               ; SelectSw2No
OR< V5 #0                ; SelectSw3No
OR> V5 #98               ; SelectSw3No
OR< V6 #0                ; SelectSw4No
OR> V6 #98               ; SelectSw4No
OR< V7 #0                ; Bz_Use
OR> V7 #1                ; Bz_Use
SET MacroDown            ; _ｻﾎﾟｰﾄﾏｸﾛ引数異常発生
CON
SET @R015
;<h1/>Init/初期化
LD @CR2008
ORB @R000
ANB Run_Mode             ; _運転ﾓｰﾄﾞ指定
MOV #0 P9                ; Select_SW
MOV #0 @EM0
;<h1/>Pause Request/ﾎﾟｰｽﾞ要求
;・ﾏｸﾛ実行中にPAUSE要求にてHALT実施
LDB Run_Mode             ; _運転ﾓｰﾄﾞ指定
AND<> V1 #100            ; Task_No
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #32
CON
ADD V1                   ; Task_No
CON
STA Z9
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
MPP
AND PAUSE1:Z10           ; _Grp1 ﾎﾟｰｽﾞ要求
SET HALT1:Z9             ; _Grp1_Task0_ﾎﾟｰｽﾞ許可
;<h1/>Exclusive/排他占有
;・優先ｴﾗｰ、通常ｴﾗｰ解除待ち
;・Input系とSelect系は同一排他(画面表示領域が同じなので)解除待ち
;・排他解除待ち中にPAUSE要求にてHALT実施
LDB Run_Mode             ; _運転ﾓｰﾄﾞ指定
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
CON
ANB TRAPDO               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB ERROR1:Z10           ; _Group1　ｴﾗｰ発生中
ANB SYSEXC_DSP1:Z10      ; _Group1　画面処理排他
SET SYSEXC_DSP1:Z10      ; _Group1　画面処理排他
SET @R000
CON
SET @R001
;<h1/>Select Action Display/選択画面表示
STG @R001
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #20
CON
STA Z7
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #6
CON
STA Z8
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #4
CON
STA Z9
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
MRD
MOV V2 KtoV_ACT1_MSG:Z8  ; Screen_No  _ACTﾒｯｾｰｼﾞ番号
MRD
AND<> V3 #0              ; SelectSw1No
SET KtoV_ACT1_DSP1:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
AND= V3 #0               ; SelectSw1No
RES KtoV_ACT1_DSP1:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
MOV V3 KtoV_ACT1_SW1:Z8  ; SelectSw1No  _ACT選択肢1名称番号
MRD
AND<> V4 #0              ; SelectSw2No
SET KtoV_ACT1_DSP2:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
AND= V4 #0               ; SelectSw2No
RES KtoV_ACT1_DSP2:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
MOV V4 KtoV_ACT1_SW2:Z8  ; SelectSw2No  _ACT選択肢2名称番号
MRD
AND<> V5 #0              ; SelectSw3No
SET KtoV_ACT1_DSP3:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
AND= V5 #0               ; SelectSw3No
RES KtoV_ACT1_DSP3:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
MOV V5 KtoV_ACT1_SW3:Z8  ; SelectSw3No  _ACT選択肢3名称番号
MRD
AND<> V6 #0              ; SelectSw4No
SET KtoV_ACT1_DSP4:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
AND= V6 #0               ; SelectSw4No
RES KtoV_ACT1_DSP4:Z9    ; _ACT選択ﾎﾞﾀﾝ表示／非表示
MRD
MOV V6 KtoV_ACT1_SW4:Z8  ; SelectSw4No  _ACT選択肢4名称番号
MRD
AND= V7 #1               ; Bz_Use
SET SYS_Bz1_RQ3:Z9       ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求3
MPP
TMH @0 #5
CON
AND @T0
AND= DM3000:Z7 #10       ; _MainTP　表示中ﾍﾟｰｼﾞNo
SET KtoV_ACT1_OPN:Z10    ; _ACT画面表示
CON
JMP @R002
;<h1/>Action Choise Waiting/選択待ち
LD @R002
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #6
CON
STA Z8
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
MPP
LDB PAUSE1:Z10           ; _Grp1 ﾎﾟｰｽﾞ要求
AND<> VtoK_ACT1_Ret:Z8 #0; _ACT復帰選択肢
ANL
MOV VtoK_ACT1_Ret:Z8 @EM0; _ACT復帰選択肢
;Error発生中はｴﾗｰ画面表示優先でY/N画面は消去
;Error解除待ち
STG @R002
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
MRD
AND ERROR1:Z10           ; _Group1　ｴﾗｰ発生中
RES KtoV_ACT1_OPN:Z10    ; _ACT画面表示
MRD
ANB ERROR1:Z10           ; _Group1　ｴﾗｰ発生中
SET KtoV_ACT1_OPN:Z10    ; _ACT画面表示
MPP
AND<> @EM0 #0
RES KtoV_ACT1_OPN:Z10    ; _ACT画面表示
CON
JMP @R003
;<h1/>End Process/終了処理
STG @R003
MPS
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #20
CON
STA Z10
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #6
CON
STA Z8
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #4
CON
STA Z9
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
MUL #32
CON
ADD V1                   ; Task_No
CON
STA Z7
MPP
AND<> DM3000:Z10 #1111   ; _MainTP　表示中ﾍﾟｰｼﾞNo
FMOV #0 KtoV_ACT1_MSG:Z8 #6;_ACTﾒｯｾｰｼﾞ番号
MPS
RES SYS_Bz1_RQ3:Z9       ; _Group1　ｼｽﾃﾑﾌﾞｻﾞｰ要求3
CON
RES HALT1:Z7             ; _Grp1_Task0_ﾎﾟｰｽﾞ許可
MRD
LDA V0                   ; Group_No
CON
SUB #1
CON
STA Z10
CON
RES SYSEXC_DSP1:Z10      ; _Group1　画面処理排他
MRD
MOV @EM0 P9              ; Select_SW
MRD
MEND
MPP
RES @R000
CON
ENDS
END
ENDH
