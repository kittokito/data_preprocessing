DEVICE:53
;MODULE:R搬送
;MODULE_TYPE:0
;非常停止
;Emergency Stop
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50010                ; 非常停止
ZRES @MR002 @MR3915
;ｾﾝｻ
LD R4703                 ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
OR R4704                 ; R_溶着 ﾊﾞｯﾌｧ位置ﾊﾟﾚｯﾄ有無
OUT @MR100               ; 搬送ﾜｰｸ有り
LD R4504                 ; R_搬送ﾁｬｯｸ1 開端
ANB R4505                ; R_搬送ﾁｬｯｸ1 閉端
AND R4506                ; R_搬送ﾁｬｯｸ2 開端
ANB R4507                ; R_搬送ﾁｬｯｸ2 閉端
AND R4508                ; R_搬送ﾁｬｯｸ3 開端
ANB R4509                ; R_搬送ﾁｬｯｸ3 閉端
OUT @MR101               ; 搬送ﾁｬｯｸ開端
LDB R4504                ; R_搬送ﾁｬｯｸ1 開端
AND R4505                ; R_搬送ﾁｬｯｸ1 閉端
ANB R4506                ; R_搬送ﾁｬｯｸ2 開端
AND R4507                ; R_搬送ﾁｬｯｸ2 閉端
ANB R4508                ; R_搬送ﾁｬｯｸ3 開端
AND R4509                ; R_搬送ﾁｬｯｸ3 閉端
OUT @MR102               ; 搬送ﾁｬｯｸ閉端
LDB R4500                ; R_搬送ﾄﾗﾝｽﾌｧ 送り端
AND R4501                ; R_搬送ﾄﾗﾝｽﾌｧ 戻り端
ANB R4502                ; R_搬送ﾁｬｯｸ前進端
AND R4503                ; R_搬送ﾁｬｯｸ後退端
AND @MR102               ; 搬送ﾁｬｯｸ閉端
ANB R4510                ; R_ﾋﾟｯﾁｼﾌﾄ戻り端
AND R4511                ; R_ﾋﾟｯﾁｼﾌﾄ出端
ANB R4512                ; R_搬送ｽﾄｯﾊﾟ前進端
AND R4513                ; R_搬送ｽﾄｯﾊﾟ 後退端
OUT @MR115               ; 搬送原点
LDB R50300               ; PAUSE中
ANB R50114               ; R側COF接合部ｴﾗｰ中
ANB R50108               ; ｴﾗｰ発生中
OUT @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
LD CR2002                ; 常時ON
OUT @MR111               ; 搬送動作可条件
LD CR2002                ; 常時ON
LDA DM22
CON
STA T11                  ; 給材PPからの給材待ち
LD R4703                 ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
OR R4704                 ; R_溶着 ﾊﾞｯﾌｧ位置ﾊﾟﾚｯﾄ有無
OR R4705                 ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
OR= DM512 #1
OR= DM513 #1
OR= DM514 #1
OUT @MR011               ; ﾄﾚｲ在席
;AUTO動作 / AUTO OPERATION
LD R50011                ; AUTO MODE
ANB MR11209              ; R側搬送ｻｲｸﾙ中
AND MR11009              ; R側搬送 start
MPS
AND LR41000              ; 自動ﾊﾟﾗﾒｰﾀ1　通常動作
RES @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
RES MR11409              ; R側搬送ｻｲｸﾙ終了
CON
SET MR11209              ; R側搬送ｻｲｸﾙ中
CON
RES MR11009              ; R側搬送 start
CON
SET @MR2000              ; AUTO START
MRD
AND LR41001              ; 自動ﾊﾟﾗﾒｰﾀ2　払い出し
RES @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
RES MR11409              ; R側搬送ｻｲｸﾙ終了
CON
SET MR11209              ; R側搬送ｻｲｸﾙ中
CON
RES MR11009              ; R側搬送 start
CON
SET @MR2000              ; AUTO START
MRD
AND LR41006              ; 自動ﾊﾟﾗﾒｰﾀ7
RES @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
RES MR11409              ; R側搬送ｻｲｸﾙ終了
CON
SET MR11209              ; R側搬送ｻｲｸﾙ中
CON
RES MR11009              ; R側搬送 start
CON
SET @MR2000              ; AUTO START
MPP
AND LR41003              ; 自動ﾊﾟﾗﾒｰﾀ4
RES @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
RES MR11409              ; R側搬送ｻｲｸﾙ終了
CON
RES MR11209              ; R側搬送ｻｲｸﾙ中
CON
RES MR11009              ; R側搬送 start
CON
SET @MR2502
STG @MR2000              ; AUTO START
MPS
ANB @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
JMP @MR2002              ; 初回1回のみ処理
MPP
AND @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
JMP @MR2007
STG @MR2002              ; 初回1回のみ処理
MPS
LD R4703                 ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
OR R4704                 ; R_溶着 ﾊﾞｯﾌｧ位置ﾊﾟﾚｯﾄ有無
OR R4705                 ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
SET @MR3000              ; 搬送ﾗｲﾝﾊﾟﾚｯﾄ有り
CON
ENDS
MPP
LDB R4703                ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
ANB R4704                ; R_溶着 ﾊﾞｯﾌｧ位置ﾊﾟﾚｯﾄ有無
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
LDA #0
CON
STA DM512
CON
STA DM513
CON
STA DM514
CON
STA DM515
CON
JMP @MR2003
STG @MR2003
MDSTRT R型
MDSTRT R溶着
RES MR6515               ; R_ｽﾄｯﾊﾟ前後 後退端
CON
SET MR6514               ; R_ｽﾄｯﾊﾟ前後 前進端
CON
AND R4512                ; R_搬送ｽﾄｯﾊﾟ前進端
JMP @MR2004
STG @MR2004
MPS
AND R4712                ; R_ﾃｰﾌﾟ出し部 ﾃｰﾌﾟ切れ
JMP @MR2005
MPP
ANB R4712                ; R_ﾃｰﾌﾟ出し部 ﾃｰﾌﾟ切れ
SET @MR3108              ; ﾌｨﾙﾑ切れ異常
CON
JMP @MR2010
STG @MR2010
AND @MR3115              ; ｴﾗｰ解除
RES @MR3115              ; ｴﾗｰ解除
CON
JMP @MR2004
STG @MR2005
SET R53310               ; R側ﾌｨﾙﾑ送り開始
CON
JMP @MR2006
STG @MR2006
AND R53311               ; R側ﾌｨﾙﾑ巻取り完了
SET R53101               ; Line-B（右） 給材可
CON
RES R53311               ; R側ﾌｨﾙﾑ巻取り完了
CON
JMP @MR2011
STG @MR2011
MPS
AND LR41000              ; 自動ﾊﾟﾗﾒｰﾀ1　通常動作
TMR #350 #5
CON
AND T350
MPS
AND>.D DM356 #100000     ; R側生産数
JMP @MR2013
MPP
AND<=.D DM356 #100000    ; R側生産数
JMP @MR2012
MPP
ANB LR41000              ; 自動ﾊﾟﾗﾒｰﾀ1　通常動作
MPS
AND>.D DM356 #100000     ; R側生産数
JMP @MR2013
MPP
AND<=.D DM356 #100000    ; R側生産数
JMP @MR2012
STG @MR2012
SET @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
JMP @MR2000              ; AUTO START
STG @MR2013
SET @MR3200              ; ﾋｰﾀｰﾍｯﾄﾞ交換要求
CON
JMP @MR2014
STG @MR2014
AND @MR3207              ; ｴﾗｰ解除
RES @MR3207              ; ｴﾗｰ解除
CON
JMP @MR2015
STG @MR2015
SET @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
JMP @MR2000              ; AUTO START
STG @MR2007
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB MR815                ; 右給材中
MPS
LD R53103                ; Line-B（右） 給材完了
AND R4703                ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
ANB R53501               ; 右側搬送中
ANL
DW #0 DM15006
CON
DW #1 DM512
CON
JMP @MR1000              ; 搬送開始
MPP
LDB R53103               ; Line-B（右） 給材完了
ANB R52002               ; 給材P&P　ﾊﾟﾚｯﾄ有り
ANB R52008               ; 給材中
ANL
MPS
LDB @MR011               ; ﾄﾚｲ在席
AND MR11404              ; 給材PPｻｲｸﾙ終了
ANL
JMP @MR2500              ; 終了動作
MPP
LDB MR11404              ; 給材PPｻｲｸﾙ終了
OR @MR011                ; ﾄﾚｲ在席
ANL
TMR #11 #9999            ; 給材PPからの給材待ち
CON
AND T11                  ; 給材PPからの給材待ち
JMP @MR2008
STG @MR2008
MPS
LD<> DM514 #0
OR<> DM513 #0
ANL
DW #0 DM15006
CON
JMP @MR1000              ; 搬送開始
MPP
LD= DM514 #0
AND= DM513 #0
ANL
JMP @MR2007
STG @MR1000              ; 搬送開始
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR111               ; 搬送動作可条件
RES R53103               ; Line-B（右） 給材完了
CON
SET MR12001              ; ﾄﾗﾝｽﾌｧ動作中
CON
RES R52002               ; 給材P&P　ﾊﾟﾚｯﾄ有り
CON
SET MR6602               ; R_搬送ﾁｬｯｸ1-2-3 開端
CON
RES MR6603               ; R_搬送ﾁｬｯｸ1-2-3 閉端
CON
AND @MR101               ; 搬送ﾁｬｯｸ開端
JMP @MR1001
STG @MR1001
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6604               ; R_搬送ﾁｬｯｸ前後 前進端
CON
RES MR6605               ; R_搬送ﾁｬｯｸ前後 後退端
CON
AND R4502                ; R_搬送ﾁｬｯｸ前進端
JMP @MR1002
STG @MR1002
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6602               ; R_搬送ﾁｬｯｸ1-2-3 開端
CON
SET MR6603               ; R_搬送ﾁｬｯｸ1-2-3 閉端
CON
AND @MR102               ; 搬送ﾁｬｯｸ閉端
JMP @MR1003
STG @MR1003
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6514               ; R_ｽﾄｯﾊﾟ前後 前進端
CON
SET MR6515               ; R_ｽﾄｯﾊﾟ前後 後退端
CON
AND R4513                ; R_搬送ｽﾄｯﾊﾟ 後退端
JMP @MR1004
STG @MR1004
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6600               ; R_ﾋﾟｯﾁｼﾌﾄｼﾘﾝﾀﾞ 上昇端
CON
RES MR6601               ; R_ﾋﾟｯﾁｼﾌﾄｼﾘﾝﾀﾞ 下降端
CON
SET MR6606               ; R_ﾁｬｯｸﾄﾗﾝｽﾌｧ 上昇端
CON
RES MR6607               ; R_ﾁｬｯｸﾄﾗﾝｽﾌｧ 下降端
CON
AND R4510                ; R_ﾋﾟｯﾁｼﾌﾄ戻り端
AND R4500                ; R_搬送ﾄﾗﾝｽﾌｧ 送り端
@LDA DM514
CON
@STA DM515
CON
@LDA DM513
CON
@STA DM514
CON
@LDA DM512
CON
@STA DM513
CON
DW #0 DM512
CON
@LDA DM476
CON
@STA DM478
CON
JMP @MR1005
STG @MR1005
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6515               ; R_ｽﾄｯﾊﾟ前後 後退端
CON
SET MR6514               ; R_ｽﾄｯﾊﾟ前後 前進端
CON
AND R4512                ; R_搬送ｽﾄｯﾊﾟ前進端
JMP @MR1006
STG @MR1006
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6603               ; R_搬送ﾁｬｯｸ1-2-3 閉端
CON
SET MR6602               ; R_搬送ﾁｬｯｸ1-2-3 開端
CON
AND @MR101               ; 搬送ﾁｬｯｸ開端
JMP @MR1007
STG @MR1007
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6605               ; R_搬送ﾁｬｯｸ前後 後退端
CON
RES MR6604               ; R_搬送ﾁｬｯｸ前後 前進端
CON
AND R4503                ; R_搬送ﾁｬｯｸ後退端
RES MR12001              ; ﾄﾗﾝｽﾌｧ動作中
CON
JMP @MR1008
STG @MR1008
MPS
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
MPS
LD<> #9 DM515
AND<> #8 DM515
ANL
JMP @MR1009
MRD
AND= #8 DM515
SET @MR3008              ; NGﾜｰｸ除材要求
CON
ENDS
MPP
AND= #9 DM515
SET @MR3100              ; NGﾜｰｸ除材要求
CON
ENDS
MPP
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
JMP @MR1009
STG @MR1009
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4712                ; R_ﾃｰﾌﾟ出し部 ﾃｰﾌﾟ切れ
JMP @MR1010
MPP
ANB R4712                ; R_ﾃｰﾌﾟ出し部 ﾃｰﾌﾟ切れ
SET @MR3108              ; ﾌｨﾙﾑ切れ異常
CON
JMP @MR1013
STG @MR1013
AND @MR3115              ; ｴﾗｰ解除
RES @MR3115              ; ｴﾗｰ解除
CON
JMP @MR1009
STG @MR1010
DIFU R53105              ; Line-B（右） 型動作要求
MPS
LD R4705                 ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
OR= #1 DM515
ANL
TMR @0 #5
CON
AND @T0
SET R54101               ; Line-B（右） 取出し要求
CON
JMP @MR1014
MPP
LDB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
AND= #0 DM515
ANL
RES R54101               ; Line-B（右） 取出し要求
CON
@SET R53114              ; R 移載完了
CON
JMP @MR1014
STG @MR1014
SET MR6601               ; R_ﾋﾟｯﾁｼﾌﾄｼﾘﾝﾀﾞ 下降端
CON
RES MR6600               ; R_ﾋﾟｯﾁｼﾌﾄｼﾘﾝﾀﾞ 上昇端
CON
SET MR6607               ; R_ﾁｬｯｸﾄﾗﾝｽﾌｧ 下降端
CON
RES MR6606               ; R_ﾁｬｯｸﾄﾗﾝｽﾌｧ 上昇端
CON
JMP @MR1011
STG @MR1011
ANB R53105               ; Line-B（右） 型動作要求
ANB R54101               ; Line-B（右） 取出し要求
AND R4511                ; R_ﾋﾟｯﾁｼﾌﾄ出端
MPS
ANB R4703                ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
SET R53101               ; Line-B（右） 給材可
CON
JMP @MR1012
MPP
AND R4703                ; R_溶着部 給材位置ﾊﾟﾚｯﾄ有無
JMP @MR1012
STG @MR1012
AND R53107               ; Line-B（右） 型動作完了
LD R53114                ; R 移載完了
ORB MR815                ; 右給材中
ANL
AND R4501                ; R_搬送ﾄﾗﾝｽﾌｧ 戻り端
RES MR12001              ; ﾄﾗﾝｽﾌｧ動作中
CON
RES R53107               ; Line-B（右） 型動作完了
CON
RES R53114               ; R 移載完了
CON
JMP @MR1100
STG @MR1100
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
JMP @MR2000              ; AUTO START
STG @MR2500              ; 終了動作
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MDSTOP R型
MDSTOP R溶着
TMR @4 #1
CON
AND @T4
JMP @MR2501
STG @MR2501
ANB _R型
ANB _R溶着
JMP @MR2502
STG @MR2502
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R53114               ; R 移載完了
CON
RES R53101               ; Line-B（右） 給材可
CON
RES @MR2001              ; 初回1回のみ動作ﾌﾗｸﾞ
CON
RES MR11209              ; R側搬送ｻｲｸﾙ中
CON
SET MR11409              ; R側搬送ｻｲｸﾙ終了
CON
ENDS
;ｱﾗｰﾑ処理
STG @MR3000              ; 搬送ﾗｲﾝﾊﾟﾚｯﾄ有り
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50114               ; R側COF接合部ｴﾗｰ中
CON
SET R51004               ; R側COF接合部表示
CON
SET R50609               ; ERR25
CON
JMP @MR3001
MOV #25 DM400            ; 表示画面No.
STG @MR3001
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3002
STG @MR3002
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR2002              ; 初回1回のみ処理
CON
ENDS
STG @MR3008              ; NGﾜｰｸ除材要求
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50114               ; R側COF接合部ｴﾗｰ中
CON
SET R51004               ; R側COF接合部表示
CON
SET R50610               ; ERR26
CON
JMP @MR3009
MOV #26 DM400            ; 表示画面No.
STG @MR3009
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3010
STG @MR3010
ANB R50008               ; 全体ｴﾗｰ発生
MPS
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
DW #0 DM515
CON
SET @MR1009
CON
ENDS
MPP
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
JMP @MR3008              ; NGﾜｰｸ除材要求
STG @MR3100              ; NGﾜｰｸ除材要求
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50114               ; R側COF接合部ｴﾗｰ中
CON
SET R51004               ; R側COF接合部表示
CON
SET R50612               ; ERR28
CON
JMP @MR3101
MOV #28 DM400            ; 表示画面No.
STG @MR3101
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3102
STG @MR3102
ANB R50008               ; 全体ｴﾗｰ発生
MPS
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
@INC DM360               ; R側溶着NG
CON
DW #0 DM515
CON
SET @MR1009
CON
ENDS
MPP
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
JMP @MR3008              ; NGﾜｰｸ除材要求
LD MR12001               ; ﾄﾗﾝｽﾌｧ動作中
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
AND CR2003               ; 常時OFF
@INC DM15008
STG @MR3108              ; ﾌｨﾙﾑ切れ異常
DW #1 DM550              ; R側　RB 指令ﾎﾟｼﾞｼｮﾝNo.
CON
SET MR1400               ; R溶着RBｽﾀｰﾄ
CON
JMP @MR3109
STG @MR3109
AND MR1401               ; R溶着RB移動完了
AND= DM551 #1            ; R側　RB 完了ﾎﾟｼﾞｼｮﾝNo.
RES MR1401               ; R溶着RB移動完了
CON
JMP @MR3110
STG @MR3110
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50114               ; R側COF接合部ｴﾗｰ中
CON
SET R51004               ; R側COF接合部表示
CON
SET R50711               ; ERR43 R_ﾌｨﾙﾑ切れ異常
CON
JMP @MR3111
MOV #43 DM400            ; 表示画面No.
STG @MR3111
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3112
STG @MR3112
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3115              ; ｴﾗｰ解除
CON
ENDS
STG @MR3200              ; ﾋｰﾀｰﾍｯﾄﾞ交換要求
ANB R50113               ; L側COF接合部ｴﾗｰ中
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50114               ; R側COF接合部ｴﾗｰ中
CON
SET R51004               ; R側COF接合部表示
CON
SET R50508               ; ERR08
CON
JMP @MR3201
MOV #8 DM400             ; 表示画面No.
STG @MR3201
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3202
STG @MR3202
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3207              ; ｴﾗｰ解除
CON
ENDS
END
ENDH
