DEVICE:53
;MODULE:Monitor
;MODULE_TYPE:0
LD R50010                ; Emergency　Stop
ZRES @MR000 @MR915       ; ﾋｰﾀｰ適温外  915
;原点復帰終了したので､ ヒーターON
;After the Home Position of All Unit, Heater1 Power ON
LDP R50007               ; 原点復帰　完了
SET MR2000               ; ﾋｰﾀｰ1 ON
;If Emergency Stop then Heater Power OFF
;Now no use this curcuit (Prevent for a fall of Temperture) 
;非常停止でヒーターOFF
;実際は使用しない。（ヒーターの温度低下防止策）
LD R50011                ; TRAP
AND CR2003               ; 常時OFF
RES MR2000               ; ﾋｰﾀｰ1 ON
;ヒーター1状態出力
;Heater1 Output of State
LD MR2000                ; ﾋｰﾀｰ1 ON
MPS
TMR @0 #10               ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
CON
AND @T0                  ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
ANB R1010                ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄ
TMR @1 #1                ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
CON
AND @T1                  ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
OUT @MR006               ; ﾋｰﾀｰ1過昇温
MPP
LD R1010                 ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄ
AND R1001                ; ﾋｰﾀｰ1 ALM2 適温
ANL
OUT @MR005               ; ﾋｰﾀｰ1適温
;ヒーター適温
;Heater Fit Temperature
LD @MR005                ; ﾋｰﾀｰ1適温
ANB @MR000               ; ﾋｰﾀｰ適温外
OUT R50212               ; ﾋｰﾀｰ適温
;Err189 ヒーター適温外エラー				 --> 継続処理
;Err189 Heater Error of Not Fit Temperture --> Deal with Continuity 
LD R50001                ; AUTO
LD R50002                ; STEP
LD LR40101               ; STEP ST2 選択 flag
OR LR40102               ; STEP ST3 選択 flag
OR LR40103               ; STEP ST4 選択 flag
ANL
ORL
ANB R50212               ; ﾋｰﾀｰ適温
ANB @MR000               ; ﾋｰﾀｰ適温外
TMR @15 #1               ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
LD @T15                  ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
ANB R50008               ; Error
SET @MR000               ; ﾋｰﾀｰ適温外
CON
SET R50110               ; Err INDEX
CON
SET R50008               ; Error
CON
SET @MR001               ; 001
MOV #189 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR001               ; 001
AND R50008               ; Error
JMP @MR002               ; 002
STG @MR002               ; 002
ANP R40006               ; ｴﾗｰﾘｾｯﾄSW
SET R42103               ; ﾌﾞｻﾞｰ停止ﾌﾗｸﾞ
CON
JMP @MR003               ; 003
STG @MR003               ; 003
ANB R50008               ; Error
RES R42103               ; ﾌﾞｻﾞｰ停止ﾌﾗｸﾞ
CON
RES @MR000               ; ﾋｰﾀｰ適温外
CON
ENDS
;Err191 ヒーター1 過昇温　  --> 非常停止処理
;Err191 Heater1 Over heat   --> Emergency STOP
LD @MR006                ; ﾋｰﾀｰ1過昇温
TMR @20 #1               ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
LD @T20                  ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2000               ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR101               ; 101
MOV #191 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR101               ; 101
AND R50008               ; Error
JMP @MR102               ; 102
STG @MR102               ; 102
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err196 ヒーター1 断線		   --> 非常停止処理
;Err196 Heater1 Snapping of Wire --> Emergency Stop
LD R50301                ; ﾋｰﾀｰ1断線
ANB @MR006               ; ﾋｰﾀｰ1過昇温
TMR @25 #1               ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
LD @T25                  ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2000               ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR109               ; 109
MOV #196 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR109               ; 109
AND R50008               ; Error
JMP @MR110               ; 110
STG @MR110               ; 110
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;<h1/>温調器ステータス読出し
;Temperature controller status reading
LD R50007                ; 原点復帰　完了
TMR @17 #20              ; 原点復帰ﾃﾞｨﾚｲ
LD @T17                  ; 原点復帰ﾃﾞｨﾚｲ
@SET @MR700              ; 温調器ST読み出し開始
STG @MR700               ; 温調器ST読み出し開始
ANB R50214               ; 通信中
JMP @MR701               ; 701
STG @MR701               ; 701
SET R50214               ; 通信中
CON
MPS
AND R75000               ; 通信実行中
JMP @MR702               ; 702
MPP
ANB R75000               ; 通信実行中
MSTRT TEMP_ST #21
STG @MR702               ; 702
RES R75000               ; 通信実行中
CON
RES R50214               ; 通信中
CON
MPS
ANB R75001               ; 通信異常
JMP @MR703               ; 703
MPP
AND R75001               ; 通信異常
SET @MR708               ; 温調器通信ｴﾗｰ
CON
RES R75001               ; 通信異常
CON
JMP @MR704               ; 704
STG @MR704               ; 704
AND @MR712               ; 温調器通信ｴﾗｰ解除
RES @MR712               ; 温調器通信ｴﾗｰ解除
CON
JMP @MR703               ; 703
STG @MR703               ; 703
TMR @18 #100             ; Port2 繰り返しﾃﾞｨﾚｲ
CON
AND @T18                 ; Port2 繰り返しﾃﾞｨﾚｲ
JMP @MR700               ; 温調器ST読み出し開始
;Err190 温調器通信エラー 
;Err190 Heater Controller communication error
STG @MR708               ; 温調器通信ｴﾗｰ
ANB R50008               ; Error
SET R50008               ; Error
CON
JMP @MR709               ; 709
MOV #190 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR709               ; 709
AND R50008               ; Error
JMP @MR710               ; 710
STG @MR710               ; 710
ANB R50008               ; Error
SET @MR712               ; 温調器通信ｴﾗｰ解除
CON
ENDS
;<h1/>温調器PV値読出し
;Temperature controller PV value reading
LD R50312                ; ﾋｰﾀｰ1溶着開始(PV取得)
AND= @DM0 #0
DW #21 @DM0
LD R50312                ; ﾋｰﾀｰ1溶着開始(PV取得)
ANB @MR515               ; ﾋｰﾀｰ溶着時間転送中
@SET @MR600              ; ﾋｰﾀｰ溶着時間転送開始
STG @MR600               ; ﾋｰﾀｰ溶着時間転送開始
SET @MR515               ; ﾋｰﾀｰ溶着時間転送中
CON
ANB R50214               ; 通信中
JMP @MR601               ; 601
STG @MR601               ; 601
SET R50214               ; 通信中
CON
MPS
AND R75000               ; 通信実行中
JMP @MR602               ; 602
MPP
ANB R75000               ; 通信実行中
MSTRT TEMP_PV @DM0
STG @MR602               ; 602
RES R75000               ; 通信実行中
CON
MPS
LDB R75001               ; 通信異常
AND= #21 @DM0
ANL
RES R50312               ; ﾋｰﾀｰ1溶着開始(PV取得)
CON
DW #0 @DM0
CON
JMP @MR603               ; 603
MPP
AND R75001               ; 通信異常
SET @MR708               ; 温調器通信ｴﾗｰ
CON
RES R75001               ; 通信異常
CON
MPS
ANB R50000               ; HOLD
SET R50009               ; HOLDRQ
CON
JMP @MR604               ; 通信異常
MPP
AND R50000               ; HOLD
JMP @MR604               ; 通信異常
STG @MR603               ; 603
RES R75000               ; 通信実行中
CON
MPS
LDB R50312               ; ﾋｰﾀｰ1溶着開始(PV取得)
AND @MR514               ; ﾋｰﾀｰ動作STｻｲｸﾙ停止中
ANL
MPS
RES R50214               ; 通信中
CON
RES @MR515               ; ﾋｰﾀｰ溶着時間転送中
CON
ENDS
MPP
SET @MR609               ; 温度ﾃﾞｰﾀ比較
CON
ENDS
MPP
AND R50312               ; ﾋｰﾀｰ1溶着開始(PV取得)
JMP @MR601               ; 601
STG @MR604               ; 通信異常
AND @MR712               ; 温調器通信ｴﾗｰ解除
RES @MR712               ; 温調器通信ｴﾗｰ解除
CON
JMP @MR603               ; 603
LD R50412                ; ST2 1ｻｲｸﾙ終了
AND R50413               ; ST3 1ｻｲｸﾙ終了
AND R50414               ; ST4 1ｻｲｸﾙ終了
OUT @MR514               ; ﾋｰﾀｰ動作STｻｲｸﾙ停止中
;温度データ比較・出力・格納
;Temperature Data Compare / Output / Store
STG @MR609               ; 温度ﾃﾞｰﾀ比較
MPS
AND<> DM290 DM280        ; 温度1  前回温度記憶1
SET R50305               ; 温度ﾃﾞｰﾀ出力
CON
JMP @MR610               ; 610
MPP
AND= DM290 DM280         ; 温度1  前回温度記憶1
JMP @MR610               ; 610
STG @MR610               ; 610
BMOV DM290 DM280 #8      ; 温度1  前回温度記憶1
ENDS
LDP R50001               ; AUTO
FMOV #0 DM280 #8         ; 前回温度記憶1
;Error162
;動作中MAIN AIR OFFｴﾗｰ
;Main AIR off during operation error
LD R50004                ; TASKDO
AND MR607                ; 動作中MainAirOff
ANB R50008               ; Error
@SET @MR800              ; 動作中Main Air OFF
STG @MR800               ; 動作中Main Air OFF
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50110               ; Err INDEX
CON
JMP @MR802               ; 802
MOV #162 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR802               ; 802
AND R50008               ; Error
RES MR607                ; 動作中MainAirOff
CON
JMP @MR803               ; 803
STG @MR803               ; 803
ANB R50008               ; Error
RES @MR801               ; 801
CON
ENDS
;Error163
;ﾌｨﾙﾑ送り出しﾓｰﾀｰｱﾗｰﾑ
;FILM FEED Motor Alarm
LDB R1100                ; ﾌｨﾙﾑ送りﾓｰﾀｰｱﾗｰﾑON=OK
ANB @MR804               ; ﾓｰﾀｰｱﾗｰﾑ記憶
TMR @31 #10              ; ﾓｰﾀｰｱﾗｰﾑﾃﾞｨﾚｲ
LD @T31                  ; ﾓｰﾀｰｱﾗｰﾑﾃﾞｨﾚｲ
ANB R50008               ; Error
SET @MR804               ; ﾓｰﾀｰｱﾗｰﾑ記憶
CON
SET R50111               ; Err ST1
CON
SET R50008               ; Error
CON
SET @MR805
MOV #163 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR805
AND R50008               ; Error
JMP @MR806
STG @MR806
ANB R50008               ; Error
@SET MR2010              ; ﾌｨﾙﾑ送りﾓｰﾀｰ電源ON
CON
TMR @14 #10
CON
AND @T14
@SET MR2102              ; ﾌｨﾙﾑ送りﾓｰﾀｰﾌﾞﾚｰｷﾘﾘｰｽ
CON
TMR @29 #10
CON
AND @T29
@RES MR2102              ; ﾌｨﾙﾑ送りﾓｰﾀｰﾌﾞﾚｰｷﾘﾘｰｽ
CON
RES @MR804               ; ﾓｰﾀｰｱﾗｰﾑ記憶
CON
ENDS
;Error179
;ｻｰﾓｽﾀｯﾄ異常検知
;Thermo Alarm
LDB R1101                ; ﾌｨﾙﾑ送りﾓｰﾀｰｻｰﾓｽﾀｯﾄ
TMR @32 #10              ; ｱﾗｰﾑ監視時間
CON
AND @T32                 ; ｱﾗｰﾑ監視時間
ANB @MR912               ; ﾌｨﾙﾑ送りｻｰﾓｽﾀｯﾄ異常検知
SET @MR909               ; 909
STG @MR909               ; 909
ANB R50008               ; Error
SET @MR912               ; ﾌｨﾙﾑ送りｻｰﾓｽﾀｯﾄ異常検知
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR910               ; 910
MOV #179 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR910               ; 910
AND R50008               ; Error
JMP @MR911               ; 911
STG @MR911               ; 911
ANB R50008               ; Error
RES @MR912               ; ﾌｨﾙﾑ送りｻｰﾓｽﾀｯﾄ異常検知
CON
SET R50011               ; TRAP
CON
ENDS
;Error161
;自重キャンセル圧異常
LD R1106                 ; 正圧監視 ONで正常
ANB R1112                ; 自重ｷｬﾝｾﾙ圧力検知OUT1
TMR @30 #10
CON
AND @T30
ANB @MR901               ; 自重キャンセル圧異常中
SET @MR900               ; 自重キャンセル圧異常
STG @MR900               ; 自重キャンセル圧異常
ANB R50008               ; Error
SET @MR901               ; 自重キャンセル圧異常中
CON
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
JMP @MR902               ; 902
MOV #161 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR902               ; 902
AND R50008               ; Error
JMP @MR903               ; 903
STG @MR903               ; 903
ANB R50008               ; Error
RES @MR901               ; 自重キャンセル圧異常中
CON
ENDS
;
;Sub Information Window
LD R50004                ; TASKDO
MC
LD MR15100               ; SubInfo ﾎﾟｰｽﾞ処理中
DW #0 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
LDB R50212               ; ﾋｰﾀｰ適温
ANB R50003               ; HOME
OUT MR15101              ; SubInfo ﾋｰﾀｰ適温待ち
CON
DW #1 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
;R15100〜16点をSub Information 表示用
LD<> $0000 MR15100       ; SubInfo ﾎﾟｰｽﾞ処理中
OUT R42100               ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示
MCR
;
LD MR2000                ; ﾋｰﾀｰ1 ON
OUT R42104               ; ﾋｰﾀｰ電源OK
;Index 電源再投入ﾃﾞｨﾚｲ
;Index Re-Power Delay 
LDB R2009                ; RB1 電源ON
TMR #200 #60             ; PCON電源再投入ﾃﾞｨﾚｲ
END
ENDH
