DEVICE:52
;MODULE:Origin
;MODULE_TYPE:0
LDB R51101               ; 原点復帰中
ZRES @MR000 @MR2215      ; 全原点位置  WFｽﾃｰｼﾞ原点復帰完了
;******* 原点位置確認 *******
LD @MR001                ; ｽｷｰｼﾞ部原点
AND @MR002               ; ﾌｨﾙﾑ転写部原点
AND @MR003               ; 巻取/巻出部原点
AND @MR004               ; WFｽﾃｰｼﾞ部原点
AND @MR005               ; 各軸原点
OUT @MR000               ; 全原点位置
LD MR20100               ; ｽｸﾚｰﾊﾟ_上昇確認
AND MR20105              ; ｽｷｰｼﾞ台_下降確認
AND MR20103              ; 保護ｶﾊﾞｰ閉確認
OUT @MR001               ; ｽｷｰｼﾞ部原点
LD MR20200               ; 主軸ﾛｰﾗ_ｸﾗﾝﾌﾟ開確認
OUT @MR002               ; ﾌｨﾙﾑ転写部原点
LD MR20300               ; 巻出ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
AND MR20400              ; 巻取ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
AND MR20402              ; 巻取ﾌｨﾙﾑ_ﾁｬｯｸ開確認
AND MR20500              ; ﾀﾞﾝｻｰﾛｰﾗ_ｸﾗﾝﾌﾟ開確認
AND MR20503              ; ｽﾌﾟﾘﾝｸﾞ_下降確認
AND MR20515              ; ﾀﾞﾝｻｰﾛｰﾗ後ｱﾝｸﾗﾝﾌﾟ
OUT @MR003               ; 巻取/巻出部原点
LD MR20000               ; WFｽﾃｰｼﾞ_前進確認
OUT @MR004               ; WFｽﾃｰｼﾞ部原点
LD MR12001               ; 転写ﾍｯﾄﾞ_待機位置
AND MR12101              ; ﾌｨﾙﾑ送り_待機位置
AND MR12301              ; ｽｸﾚｰﾊﾟ_待機位置
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
LD R507                  ; ﾌｨﾙﾑ巻取_ﾄﾙｸﾓｰﾀ駆動
OR CR2002                ; 常時ON
ANL
OUT @MR005               ; 各軸原点
;******* 原点復帰 *******
LD R51101                ; 原点復帰中
AND R005                 ; 安全ｶﾊﾞｰ入力
AND R4300                ; 元圧ｾﾝｻ
TMR @0 #5                ; 原点復帰中
CON
AND @T0                  ; 原点復帰中
DIFU @MR100              ; 原点復帰開始Pls
LD @MR100                ; 原点復帰開始Pls
ANB @MR101               ; 原点復帰実行中
ANB R51100               ; 原点復帰完了
SET @MR1000              ; MAIN原点復帰
SET @MR2000              ; ｽｸﾚｰﾊﾟ原点復帰
SET @MR2100              ; ﾌｨﾙﾑ送り原点復帰
;******* 原点復帰 *******
;***** MAIN原点復帰動作 *****
STG @MR1000              ; MAIN原点復帰
SET R51200               ; ｴﾘｱｾﾝｻ有効Flg
CON
SET @MR101               ; 原点復帰実行中
CON
AND R51200               ; ｴﾘｱｾﾝｻ有効Flg
AND @MR101               ; 原点復帰実行中
JMP @MR1001              ; ｽｷｰｼﾞ台真空破壊
;******* ｽｷｰｼﾞ部原点復帰動作 *******
;***** ｽｷｰｼﾞ台真空破壊 *****
STG @MR1001              ; ｽｷｰｼﾞ台真空破壊
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
SET MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
SET MR6202               ; ｽｷｰｼﾞ台_ﾊﾞｷｭｰﾑ破壊
CON
RES MR6203               ; ｽｷｰｼﾞ台_ﾊﾞｷｭｰﾑ吸着
CON
AND MR20301              ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
AND MR20108              ; ｽｷｰｼﾞ台_吸着OFF確認
JMP @MR1002              ; ｽｸﾚｰﾊﾟ/ｽｷｰｼﾞ台退避
;******* ｽｷｰｼﾞ部原点復帰動作 *******
;***** ｽｸﾚｰﾊﾟ/ｽｷｰｼﾞ台退避 *****
STG @MR1002              ; ｽｸﾚｰﾊﾟ/ｽｷｰｼﾞ台退避
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
RES MR6202               ; ｽｷｰｼﾞ台_ﾊﾞｷｭｰﾑ破壊
CON
RES MR6002               ; ｽｸﾚｰﾊﾟ_下降
CON
RES MR6106               ; ｽｷｰｼﾞ台_上昇
CON
ANB R6202                ; ｽｷｰｼﾞ台_ﾊﾞｷｭｰﾑ破壊SOL
AND MR20100              ; ｽｸﾚｰﾊﾟ_上昇確認
AND MR20105              ; ｽｷｰｼﾞ台_下降確認
JMP @MR1100              ; 下流たるみ取り
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** 下流たるみ取り *****
STG @MR1100              ; 下流たるみ取り
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1101              ; 1101
STG @MR1101              ; 1101
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR10000              ; 転写ﾍｯﾄﾞ_ｻｰﾎﾞON
CON
SET MR10400              ; 主軸ﾛｰﾗ_ｻｰﾎﾞON
CON
AND MR50107              ; 軸1ｻｰﾎﾞON
AND MR50507              ; 軸3ｻｰﾎﾞON
JMP @MR1102              ; 1102
STG @MR1102              ; 1102
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6000               ; 主軸ﾛｰﾗ_ｱﾝｸﾗﾝﾌﾟ
CON
SET MR6001               ; 主軸ﾛｰﾗ_ｸﾗﾝﾌﾟ
CON
SET MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20201              ; 主軸ﾛｰﾗ_ｸﾗﾝﾌﾟ閉確認
AND MR20301              ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
JMP @MR1103              ; 1103
STG @MR1103              ; 1103
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6112               ; 主軸ﾛｰﾗ_ﾊﾞｷｭｰﾑ破壊
CON
RES MR6110               ; 主軸ﾛｰﾗ_ﾊﾞｷｭｰﾑ吸着[弱]
CON
SET MR6111               ; 主軸ﾛｰﾗ_ﾊﾞｷｭｰﾑ吸着[強]
CON
AND MR20202              ; 主軸ﾛｰﾗ_圧力ON[強]確認
JMP @MR1104              ; 1104
STG @MR1104              ; 1104
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6100               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_閉
CON
SET MR6101               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_開
CON
RES MR6108               ; 段差ﾛｰﾗ後ｸﾗﾝﾌﾟ
CON
AND MR20402              ; 巻取ﾌｨﾙﾑ_ﾁｬｯｸ開確認
AND MR20515              ; ﾀﾞﾝｻｰﾛｰﾗ後ｱﾝｸﾗﾝﾌﾟ
JMP @MR1105              ; 1105
STG @MR1105              ; 1105
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR507                ; ﾌｨﾙﾑ巻取_ﾄﾙｸﾓｰﾀ駆動
CON
AND R507                 ; ﾌｨﾙﾑ巻取_ﾄﾙｸﾓｰﾀ駆動
JMP @MR1106              ; 1106
STG @MR1106              ; 1106
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6008               ; 巻取ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20401              ; 巻取ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
JMP @MR1107              ; 1107
STG @MR1107              ; 1107
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
TMR @1 #50               ; 下流たるみ取り時間
CON
AND @T1                  ; 下流たるみ取り時間
JMP @MR1200              ; 上流たるみ取り
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** 上流たるみ取り *****
STG @MR1200              ; 上流たるみ取り
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6004               ; 段差ﾛｰﾗ_ｸﾗﾝﾌﾟ
CON
SET MR6005               ; 段差ﾛｰﾗ_ｱﾝｸﾗﾝﾌﾟ
CON
RES MR6006               ; ｽﾌﾟﾘﾝｸﾞ_上昇
CON
AND MR20500              ; ﾀﾞﾝｻｰﾛｰﾗ_ｸﾗﾝﾌﾟ開確認
AND MR20503              ; ｽﾌﾟﾘﾝｸﾞ_下降確認
JMP @MR1201              ; 1201
STG @MR1201              ; 1201
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
LDB MR20506              ; ﾌｨﾙﾑ検知ｾﾝｻ1_上死点ON確認
ANB MR20508              ; ﾌｨﾙﾑ検知ｾﾝｻ2_上限ON確認
ANL
JMP @MR1202              ; 1202
MPP
LD MR20506               ; ﾌｨﾙﾑ検知ｾﾝｻ1_上死点ON確認
OR MR20508               ; ﾌｨﾙﾑ検知ｾﾝｻ2_上限ON確認
ANL
JMP @MR1205              ; 1205
STG @MR1202              ; 1202
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20300              ; 巻出ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
JMP @MR1203              ; 1203
STG @MR1203              ; 1203
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
SET MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
AND MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1204              ; 1204
STG @MR1204              ; 1204
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
LD MR20506               ; ﾌｨﾙﾑ検知ｾﾝｻ1_上死点ON確認
OR MR20508               ; ﾌｨﾙﾑ検知ｾﾝｻ2_上限ON確認
ANL
JMP @MR1205              ; 1205
STG @MR1205              ; 1205
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20301              ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
JMP @MR1206              ; 1206
STG @MR1206              ; 1206
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1207              ; 保護ｶﾊﾞｰ閉
;******* ｽｷｰｼﾞ部原点復帰動作 *******
;***** 保護ｶﾊﾞｰ閉 *****
STG @MR1207              ; 保護ｶﾊﾞｰ閉
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
RES MR6102               ; 保護ｶﾊﾞｰ_開
CON
SET MR6103               ; 保護ｶﾊﾞｰ_閉
CON
AND MR20103              ; 保護ｶﾊﾞｰ閉確認
JMP @MR1208              ; ｽｸﾚｰﾊﾟ原点復帰完了待ち
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** ｽｸﾚｰﾊﾟ原点復帰完了待ち *****
STG @MR1208              ; ｽｸﾚｰﾊﾟ原点復帰完了待ち
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
AND @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
JMP @MR1300              ; 転写ﾍｯﾄﾞ原点復帰
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** 転写ﾍｯﾄﾞ原点復帰 *****
STG @MR1300              ; 転写ﾍｯﾄﾞ原点復帰
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR10000              ; 転写ﾍｯﾄﾞ_ｻｰﾎﾞON
CON
AND MR50107              ; 軸1ｻｰﾎﾞON
MPS
MCALL GENTEN #2 #1 MR503 R000 MR12000;転写ﾍｯﾄﾞ_SCON1_HOME  転写ﾍｯﾄﾞ_SCON1_HEND  転写ﾍｯﾄﾞ_原点復帰完了
MPP
AND MR12000              ; 転写ﾍｯﾄﾞ_原点復帰完了
JMP @MR1301              ; 1301
STG @MR1301              ; 1301
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1300              ; 転写ﾍｯﾄﾞ原点復帰
RES MR503                ; 転写ﾍｯﾄﾞ_SCON1_HOME
CON
ANB R503                 ; 転写ﾍｯﾄﾞ_SCON1_HOME
AND MR51109              ; 軸1原点復帰完了記憶
JMP @MR1302              ; 1302
STG @MR1302              ; 1302
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR12000              ; 転写ﾍｯﾄﾞ_原点復帰完了
CON
ANB MR12000              ; 転写ﾍｯﾄﾞ_原点復帰完了
AND T2470                ; 軸1ｲﾝﾀｰﾊﾞﾙ
JMP @MR1400              ; ﾌｨﾙﾑ巻出
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** ﾌｨﾙﾑ巻出 *****
STG @MR1400              ; ﾌｨﾙﾑ巻出
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
LDB MR20510              ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
ANB MR20512              ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1401              ; 1401
MPP
LD MR20510               ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
OR MR20512               ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1404              ; 1404
STG @MR1401              ; 1401
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20300              ; 巻出ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
JMP @MR1402              ; 1402
STG @MR1402              ; 1402
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
AND MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1403              ; 1403
STG @MR1403              ; 1403
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
LD MR20510               ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
OR MR20512               ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1404              ; 1404
STG @MR1404              ; 1404
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1405              ; 1405
STG @MR1405              ; 1405
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20301              ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
JMP @MR1500              ; 転写ﾍｯﾄﾞ待機位置/WFｽﾃｰｼﾞ原点復帰
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** 転写ﾍｯﾄﾞ待機位置/WFｽﾃｰｼﾞ原点復帰 *****
STG @MR1500              ; 転写ﾍｯﾄﾞ待機位置/WFｽﾃｰｼﾞ原点復帰
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
RES @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
CON
RES @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
CON
ANB @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
ANB @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
@SET @MR2200             ; WFｽﾃｰｼﾞ原点復帰
MPP
LD @MR2200               ; WFｽﾃｰｼﾞ原点復帰
AND T2470                ; 軸1ｲﾝﾀｰﾊﾞﾙ
ANL
JMP @MR1501              ; 1501
STG @MR1501              ; 1501
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR11001              ; 転写ﾍｯﾄﾞ_待機位置移動
CON
AND MR11001              ; 転写ﾍｯﾄﾞ_待機位置移動
AND MR12001              ; 転写ﾍｯﾄﾞ_待機位置
JMP @MR1502              ; 1502
STG @MR1502              ; 1502
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1501              ; 1501
RES MR11001              ; 転写ﾍｯﾄﾞ_待機位置移動
CON
ANB MR11001              ; 転写ﾍｯﾄﾞ_待機位置移動
AND T2470                ; 軸1ｲﾝﾀｰﾊﾞﾙ
JMP @MR1600              ; ﾌｨﾙﾑ送り準備
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** ﾌｨﾙﾑ送り準備 *****
STG @MR1600              ; ﾌｨﾙﾑ送り準備
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR10400              ; 主軸ﾛｰﾗ_ｻｰﾎﾞON
CON
AND MR50507              ; 軸3ｻｰﾎﾞON
ANB R10104               ; 軸3 現在座標変更要求
U_WRCPOS.L #2 #3 +0
TMR @2 #2                ; 主軸ﾛｰﾗ現在座標変更要求待ち時間
CON
AND @T2                  ; 主軸ﾛｰﾗ現在座標変更要求待ち時間
JMP @MR1601              ; 1601
STG @MR1601              ; 1601
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET R10104               ; 軸3 現在座標変更要求
CON
AND R10104               ; 軸3 現在座標変更要求
AND R10904               ; 軸3 現在座標変更完了
AND= EM32620 #0          ; 軸3現在位置
JMP @MR1602              ; 1602
STG @MR1602              ; 1602
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR12200              ; 主軸ﾛｰﾗ_原点復帰完了
CON
AND MR12200              ; 主軸ﾛｰﾗ_原点復帰完了
AND MR51509              ; 軸3原点復帰完了記憶
JMP @MR1603              ; 1603
STG @MR1603              ; 1603
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES R10104               ; 軸3 現在座標変更要求
CON
RES MR12200              ; 主軸ﾛｰﾗ_原点復帰完了
CON
ANB MR12200              ; 主軸ﾛｰﾗ_原点復帰完了
ANB R10104               ; 軸3 現在座標変更要求
JMP @MR1604              ; 1604
STG @MR1604              ; 1604
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6100               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_閉
CON
SET MR6101               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_開
CON
AND MR20402              ; 巻取ﾌｨﾙﾑ_ﾁｬｯｸ開確認
JMP @MR1605              ; 1605
STG @MR1605              ; 1605
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR507                ; ﾌｨﾙﾑ巻取_ﾄﾙｸﾓｰﾀ駆動
CON
AND R507                 ; ﾌｨﾙﾑ巻取_ﾄﾙｸﾓｰﾀ駆動
JMP @MR1606              ; 1606
STG @MR1606              ; 1606
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6008               ; 巻取ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20401              ; 巻取ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
JMP @MR1607              ; 1607
STG @MR1607              ; 1607
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6000               ; 主軸ﾛｰﾗ_ｱﾝｸﾗﾝﾌﾟ
CON
RES MR6001               ; 主軸ﾛｰﾗ_ｸﾗﾝﾌﾟ
CON
RES MR6006               ; ｽﾌﾟﾘﾝｸﾞ_上昇
CON
AND MR20200              ; 主軸ﾛｰﾗ_ｸﾗﾝﾌﾟ開確認
AND MR20503              ; ｽﾌﾟﾘﾝｸﾞ_下降確認
JMP @MR1700              ; 原点復帰ﾌｨﾙﾑ送り
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;***** 原点復帰ﾌｨﾙﾑ送り動作 ******
STG @MR1700              ; 原点復帰ﾌｨﾙﾑ送り
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET @MR1714              ; 原点復帰ﾌｨﾙﾑ送り実行中
CON
RES @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
CON
AND @MR1714              ; 原点復帰ﾌｨﾙﾑ送り実行中
ANB @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
JMP @MR1701              ; 1701
STG @MR1701              ; 1701
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
@RES @MR1814             ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
CON
@RES @MR1815             ; 原点復帰ﾌｨﾙﾑ送り巻出完了
CON
ANB @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
ANB @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
@SET @MR1800             ; 原点復帰ﾌｨﾙﾑ送り巻出
MPP
AND @MR1800              ; 原点復帰ﾌｨﾙﾑ送り巻出
JMP @MR1702              ; 1702
STG @MR1702              ; 1702
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
AND T2472                ; 軸3ｲﾝﾀｰﾊﾞﾙ
JMP @MR1703              ; 1703
STG @MR1703              ; 1703
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR11203              ; 主軸ﾛｰﾗ_原点復帰ﾌｨﾙﾑ送り位置移動
CON
AND MR11203              ; 主軸ﾛｰﾗ_原点復帰ﾌｨﾙﾑ送り位置移動
AND MR12203              ; 主軸ﾛｰﾗ_原点復帰ﾌｨﾙﾑ送り位置
JMP @MR1704              ; 1704
STG @MR1704              ; 1704
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR11203              ; 主軸ﾛｰﾗ_原点復帰ﾌｨﾙﾑ送り位置移動
CON
ANB MR11203              ; 主軸ﾛｰﾗ_原点復帰ﾌｨﾙﾑ送り位置移動
AND T2472                ; 軸3ｲﾝﾀｰﾊﾞﾙ
JMP @MR1705              ; 1705
STG @MR1705              ; 1705
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6008               ; 巻取ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20400              ; 巻取ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
JMP @MR1706              ; 1706
STG @MR1706              ; 1706
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES @MR1714              ; 原点復帰ﾌｨﾙﾑ送り実行中
CON
SET @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
CON
ANB @MR1714              ; 原点復帰ﾌｨﾙﾑ送り実行中
AND @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
JMP @MR1707              ; 1707
STG @MR1707              ; 1707
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
AND @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
JMP @MR1708              ; 1708
STG @MR1708              ; 1708
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
CON
RES @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
CON
ANB @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
ANB @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
JMP @MR1900              ; 原点復帰完了確認
;******* ﾌｨﾙﾑ転写部原点復帰動作 *******
;******* 原点復帰ﾌｨﾙﾑ送り巻出動作 ********
STG @MR1800              ; 原点復帰ﾌｨﾙﾑ送り巻出
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1702              ; 1702
SET @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
CON
RES @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
CON
AND @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
ANB @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
JMP @MR1801              ; 1801
STG @MR1801              ; 1801
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1807              ; 1807
MPS
LDB @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
LD MR20506               ; ﾌｨﾙﾑ検知ｾﾝｻ1_上死点ON確認
OR MR20508               ; ﾌｨﾙﾑ検知ｾﾝｻ2_上限ON確認
ANL
ANB @MR1808              ; 1808
ANL
JMP @MR1802              ; 1802
MPP
LD @MR1715               ; 原点復帰ﾌｨﾙﾑ送り完了
ANB @MR1802              ; 1802
ANL
JMP @MR1808              ; 1808
STG @MR1802              ; 1802
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1808              ; 1808
RES MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20300              ; 巻出ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
JMP @MR1803              ; 1803
STG @MR1803              ; 1803
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
AND MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1804              ; 1804
STG @MR1804              ; 1804
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
LD MR20510               ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
OR MR20512               ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1805              ; 1805
STG @MR1805              ; 1805
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
CON
RES MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
CON
RES MR502                ; ﾌｨﾙﾑ巻出_DRV1_FREE
CON
ANB MR500                ; ﾌｨﾙﾑ巻出_DRV1_CW
ANB MR501                ; ﾌｨﾙﾑ巻出_DRV1_CCW
ANB R502                 ; ﾌｨﾙﾑ巻出_DRV1_FREE
JMP @MR1806              ; 1806
STG @MR1806              ; 1806
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20301              ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ確認
JMP @MR1807              ; 1807
STG @MR1807              ; 1807
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
LDB @MR1715              ; 原点復帰ﾌｨﾙﾑ送り完了
ANB @MR1808              ; 1808
ANL
JMP @MR1801              ; 1801
MPP
LD @MR1715               ; 原点復帰ﾌｨﾙﾑ送り完了
ANB @MR1801              ; 1801
ANL
JMP @MR1808              ; 1808
STG @MR1808              ; 1808
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
MPS
LDB MR20510              ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
ANB MR20512              ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1802              ; 1802
MPP
LD MR20510               ; ﾌｨﾙﾑ検知ｾﾝｻ3_基準ON確認
OR MR20512               ; ﾌｨﾙﾑ検知ｾﾝｻ4_下限ON確認
ANL
JMP @MR1809              ; 1809
STG @MR1809              ; 1809
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
CON
SET @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
CON
ANB @MR1814              ; 原点復帰ﾌｨﾙﾑ送り巻出実行中
AND @MR1815              ; 原点復帰ﾌｨﾙﾑ送り巻出完了
ENDS
;******* 原点復帰 *******
;***** 原点復帰完了確認 *****
STG @MR1900              ; 原点復帰完了確認
ANB R50300               ; Pause
ANB R50006               ; 異常発生中
ANB @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
AND @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
ANB @MR2114              ; ﾌｨﾙﾑ送り原点復帰実行中
AND @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
ANB @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
AND @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
JMP @MR1901              ; 1901
STG @MR1901              ; 1901
ANB R50300               ; Pause
ANB R50006               ; 異常発生中
RES @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
CON
RES @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
CON
RES @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
CON
ANB @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
ANB @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
ANB @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
JMP @MR1902              ; 1902
STG @MR1902              ; 1902
ANB R50300               ; Pause
ANB R50006               ; 異常発生中
RES MR6010               ; 巻出ﾌｨﾙﾑ押え_ｸﾗﾝﾌﾟ
CON
AND MR20300              ; 巻出ﾌｨﾙﾑ押え_ｱﾝｸﾗﾝﾌﾟ確認
AND @MR000               ; 全原点位置
JMP @MR1903              ; 1903
STG @MR1903              ; 1903
ANB R50300               ; Pause
ANB R50006               ; 異常発生中
RES @MR101               ; 原点復帰実行中
CON
SET R51100               ; 原点復帰完了
CON
RES R51200               ; ｴﾘｱｾﾝｻ有効Flg
CON
ANB @MR101               ; 原点復帰実行中
AND R51100               ; 原点復帰完了
ANB R51200               ; ｴﾘｱｾﾝｻ有効Flg
ENDS
;******* ｽｸﾚｰﾊﾟｰ原点復帰動作 ********
STG @MR2000              ; ｽｸﾚｰﾊﾟ原点復帰
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
SET @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
CON
RES @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
CON
AND @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
ANB @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
JMP @MR2001              ; 2001
STG @MR2001              ; 2001
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
RES MR12300              ; ｽｸﾚｰﾊﾟ_原点復帰完了
CON
ANB MR12300              ; ｽｸﾚｰﾊﾟ_原点復帰完了
JMP @MR2002              ; 2002
STG @MR2002              ; 2002
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
RES MR6002               ; ｽｸﾚｰﾊﾟ_下降
CON
AND MR20100              ; ｽｸﾚｰﾊﾟ_上昇確認
JMP @MR2003              ; 2003
STG @MR2003              ; 2003
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
SET MR10600              ; ｽｸﾚｰﾊﾟ_ｻｰﾎﾞON
CON
AND MR50707              ; 軸4ｻｰﾎﾞON
MPS
MCALL GENTEN #2 #4 MR2001 R004 MR12300;ｽｸﾚｰﾊﾟ_PCON_HOME  ｽｸﾚｰﾊﾟ_DRV3_HEND  ｽｸﾚｰﾊﾟ_原点復帰完了
MPP
AND MR12300              ; ｽｸﾚｰﾊﾟ_原点復帰完了
JMP @MR2004              ; 2004
STG @MR2004              ; 2004
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
ANB @MR2003              ; 2003
RES MR2001               ; ｽｸﾚｰﾊﾟ_PCON_HOME
CON
ANB R2001                ; ｽｸﾚｰﾊﾟ_PCON_HOME
AND MR51709              ; 軸4原点復帰完了記憶
JMP @MR2005              ; 2005
STG @MR2005              ; 2005
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50110               ; Err_転写UT
RES MR12300              ; ｽｸﾚｰﾊﾟ_原点復帰完了
CON
ANB MR12300              ; ｽｸﾚｰﾊﾟ_原点復帰完了
AND T2473                ; 軸4ｲﾝﾀｰﾊﾞﾙ
JMP @MR2006              ; 2006
STG @MR2006              ; 2006
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50110               ; Err_転写UT
SET MR11301              ; ｽｸﾚｰﾊﾟ_待機位置移動
CON
AND MR11301              ; ｽｸﾚｰﾊﾟ_待機位置移動
AND MR12301              ; ｽｸﾚｰﾊﾟ_待機位置
JMP @MR2007              ; 2007
STG @MR2007              ; 2007
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
ANB @MR2006              ; 2006
RES MR11301              ; ｽｸﾚｰﾊﾟ_待機位置移動
CON
ANB MR11301              ; ｽｸﾚｰﾊﾟ_待機位置移動
AND T2473                ; 軸4ｲﾝﾀｰﾊﾞﾙ
JMP @MR2008              ; 2008
STG @MR2008              ; 2008
ANB R50301               ; ｽｷｰｼﾞ部Pause
ANB R50109               ; Err_ｽｷｰｼﾞUT
RES @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
CON
SET @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
CON
ANB @MR2014              ; ｽｸﾚｰﾊﾟ原点復帰実行中
AND @MR2015              ; ｽｸﾚｰﾊﾟ原点復帰完了
ENDS
;******* ﾌｨﾙﾑ送り原点復帰動作 ********
STG @MR2100              ; ﾌｨﾙﾑ送り原点復帰
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET @MR2114              ; ﾌｨﾙﾑ送り原点復帰実行中
CON
RES @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
CON
AND @MR2114              ; ﾌｨﾙﾑ送り原点復帰実行中
ANB @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
JMP @MR2101              ; 2101
STG @MR2101              ; 2101
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR12100              ; ﾌｨﾙﾑ送り_原点復帰完了
CON
ANB MR12100              ; ﾌｨﾙﾑ送り_原点復帰完了
JMP @MR2102              ; 2102
STG @MR2102              ; 2102
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR6100               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_閉
CON
SET MR6101               ; 巻取ﾌｨﾙﾑﾁｬｯｸ_開
CON
AND MR20402              ; 巻取ﾌｨﾙﾑ_ﾁｬｯｸ開確認
JMP @MR2103              ; 2103
STG @MR2103              ; 2103
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR10200              ; ﾌｨﾙﾑ送り_ｻｰﾎﾞON
CON
AND MR50307              ; 軸2ｻｰﾎﾞON
MPS
MCALL GENTEN #2 #2 MR505 R002 MR12100;ﾌｨﾙﾑ送り_SCON2_HOME  ﾌｨﾙﾑ送り_SCON2_HEND  ﾌｨﾙﾑ送り_原点復帰完了
MPP
AND MR12100              ; ﾌｨﾙﾑ送り_原点復帰完了
JMP @MR2104              ; 2104
STG @MR2104              ; 2104
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR2103              ; 2103
RES MR505                ; ﾌｨﾙﾑ送り_SCON2_HOME
CON
ANB R505                 ; ﾌｨﾙﾑ送り_SCON2_HOME
AND MR51309              ; 軸2原点復帰完了記憶
JMP @MR2105              ; 2105
STG @MR2105              ; 2105
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES MR12100              ; ﾌｨﾙﾑ送り_原点復帰完了
CON
ANB MR12100              ; ﾌｨﾙﾑ送り_原点復帰完了
AND T2471                ; 軸2ｲﾝﾀｰﾊﾞﾙ
JMP @MR2106              ; 2106
STG @MR2106              ; 2106
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR11101              ; ﾌｨﾙﾑ送り_待機位置移動
CON
AND MR11101              ; ﾌｨﾙﾑ送り_待機位置移動
AND MR12101              ; ﾌｨﾙﾑ送り_待機位置
JMP @MR2107              ; 2107
STG @MR2107              ; 2107
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR2106              ; 2106
RES MR11101              ; ﾌｨﾙﾑ送り_待機位置移動
CON
ANB MR11101              ; ﾌｨﾙﾑ送り_待機位置移動
AND T2471                ; 軸2ｲﾝﾀｰﾊﾞﾙ
JMP @MR2108              ; 2108
STG @MR2108              ; 2108
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES @MR2114              ; ﾌｨﾙﾑ送り原点復帰実行中
CON
SET @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
CON
ANB @MR2114              ; ﾌｨﾙﾑ送り原点復帰実行中
AND @MR2115              ; ﾌｨﾙﾑ送り原点復帰完了
ENDS
;******* WFｽﾃｰｼﾞ原点復帰動作 ********
STG @MR2200              ; WFｽﾃｰｼﾞ原点復帰
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
ANB @MR1500              ; 転写ﾍｯﾄﾞ待機位置/WFｽﾃｰｼﾞ原点復帰
SET @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
CON
AND @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
JMP @MR2201              ; 2201
STG @MR2201              ; 2201
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
SET MR6104               ; WFｽﾃｰｼﾞ_前進
CON
RES MR6105               ; WFｽﾃｰｼﾞ_後退
CON
AND MR20000              ; WFｽﾃｰｼﾞ_前進確認
JMP @MR2202              ; 2202
STG @MR2202              ; 2202
ANB R50302               ; 転写部Pause
ANB R50110               ; Err_転写UT
RES @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
CON
SET @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
CON
ANB @MR2214              ; WFｽﾃｰｼﾞ原点復帰実行中
AND @MR2215              ; WFｽﾃｰｼﾞ原点復帰完了
ENDS
;*****　下流たるみ取り中　*****
LD R50003                ; 原点復帰中
ANB R50300               ; Pause
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
MPS
AND<> @MR1100 #0         ; 下流たるみ取り
OUT R52001               ; 下流たるみ取り中
MPP
AND<> @MR1200 #0         ; 上流たるみ取り
OUT R52002               ; 上流たるみ取り中
END
ENDH
