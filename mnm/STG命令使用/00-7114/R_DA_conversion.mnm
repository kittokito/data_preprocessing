DEVICE:53
;MODULE:R_DA_conversion
;MODULE_TYPE:0
;■電空ﾚｷﾞｭﾚｰﾀｰ/Electro-pneumatic regulator
LD R2010                 ; 元圧空検出_R
TMR @0 #5
CON
AND @T0
OUT R507                 ; 電空ﾚｷﾞｭﾚｰﾀｰR ON
;電空ﾚｷﾞｭﾚｰﾀｰ設定値
;Min：0000h = 0%
;Max：0FFFh = 100%
;Electro-regulator setting value
;Min: 0000h = 0%
;Max: 0FFFh = 100%
;例）ITV2030（0.5MPa仕様）で、設定圧力を0.3MPaにする場合：
;(0.3MPa/0.5MPa)×4095＝2457（DEC）＝999(HEX)
;データ"999"をマスタPLCより本電空レギュレータに送信することで、0.3MPa
;の圧力が設定されます。
;Example) To set the set pressure to 0.3 MPa with ITV 2030 (0.5 MPa specification):
;(0.3 MPa / 0.5 MPa) × 4095 = 2457 (DEC) = 999 (HEX)
;By transmitting the data "999" from the master PLC to this electro-pneumatic regulator, 0.3 MPa
;Pressure is set.
LD MR015                 ; R誤配管検査中
LDA ZF842                ; R現設定_誤配管検査_検査圧力
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
LDB LR1915               ; New MC leak
AND MR102                ; Rﾘｰｸ検査中
LDA EM560                ; R現在ﾘｰｸ検査部位数(1-6)
CON
SUB #1
CON
STA Z1
CON
MPS
LDB MR107                ; Rﾒﾝﾃ_装置ﾘｰｸ検査中
OR CR2002                ; 常時ON
ANL
LDA ZF874:Z1             ; R現設定_ﾘｰｸ検査1検査圧力設定値
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
MPP
LD MR107                 ; Rﾒﾝﾃ_装置ﾘｰｸ検査中
AND CR2003               ; 常時OFF
ANL
LDA EM1162:Z1            ; MCﾘｰｸ検査1 検査圧
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
LD LR1915                ; New MC leak
AND MR102                ; Rﾘｰｸ検査中
OR MR205                 ; Rﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
LDA ZF874                ; R現設定_ﾘｰｸ検査1検査圧力設定値
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
LD MR013                 ; R閉塞検査中
LDA EM563                ; R閉塞検査回数(1〜7)
CON
SUB #1
CON
STA Z1
CON
LDA ZF979:Z1             ; R現設定_閉塞検査1_検査圧力
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
LD MR105                 ; R逆止弁検査中
LDA ZF21                 ; R現在逆止弁検査部位(1-6)
CON
SUB #1
CON
STA Z1
CON
LDA ZF928:Z1             ; R現設定_逆止弁検査1_検査圧
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
LD= EM29996 #801         ; (R_TP)現在表示中画面No.
LDA DM7                  ; 手動時圧力R
CON
STA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
;ﾘｰｸﾃｽﾀ制限/使用圧10〜99.9kPa
;Leak tester restriction / working pressure 10 to 99.9 kPa
LD MR8706                ; R上流側流路切替ﾊﾞﾙﾌﾞ 25(ON=開)
AND> @DM0 #99            ; ｱﾅﾛｸﾞ出力値_仮格納
DW #99 @DM0              ; ｱﾅﾛｸﾞ出力値_仮格納
LD MR311                 ; R_原点復帰完了
ANB MR900                ; 重度異常：電源再投入必要
MPS
LDB MR403                ; R大気開放中
AND>= @DM0 #5            ; ｱﾅﾛｸﾞ出力値_仮格納
AND<= @DM0 #500          ; ｱﾅﾛｸﾞ出力値_仮格納
ANL
LDA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
CON
MUL #8
CON
ADD #10
CON
STA DM10802              ; R電空ﾚｷﾞｭﾚｰﾀｰ設定値
MPP
AND MR403                ; R大気開放中
DW #0 @DM0               ; ｱﾅﾛｸﾞ出力値_仮格納
CON
DW #0 DM10802            ; R電空ﾚｷﾞｭﾚｰﾀｰ設定値
;設定値ｸﾘｱ/Clear set value
LD CR2002                ; 常時ON
LD< @DM0 #5              ; ｱﾅﾛｸﾞ出力値_仮格納
OR> @DM0 #500            ; ｱﾅﾛｸﾞ出力値_仮格納
ORB MR311                ; R_原点復帰完了
ANL
DW #0 DM10802            ; R電空ﾚｷﾞｭﾚｰﾀｰ設定値
;電空ﾚｷﾞｭﾚｰﾀｰ現在値表示/Display of electropneumatic regulator current value
LD CR2002                ; 常時ON
LDA @DM0                 ; ｱﾅﾛｸﾞ出力値_仮格納
CON
STA DM47                 ; R電空ﾚｷﾞｭﾚｰﾀｰ現在設定値
END
ENDH
