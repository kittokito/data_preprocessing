DEVICE:53
;MODULE:Leaktester
;MODULE_TYPE:0
;ﾌｸﾀﾞ/FUKUDA FL-611
;<h1/>ｴﾗｰ監視/Error monitoring
LDB R2506                ; ﾘｰｸﾃｽﾀｰL RUN(測定中)
AND R2507                ; ﾘｰｸﾃｽﾀｰL RDY(測定用意完了)
ANB R2508                ; ﾘｰｸﾃｽﾀｰL END
ANB R2509                ; ﾘｰｸﾃｽﾀｰL ERR(異常ON)
AND R2511                ; ﾘｰｸﾃｽﾀｰL M/R(ON=ﾘﾓｰﾄ)
ANB R2514                ; ﾘｰｸﾃｽﾀｰL EXH-SV1
ANB R2515                ; ﾘｰｸﾃｽﾀｰL EXH-SV2
OUT @MR100               ; ﾘｰｸﾃｽﾀｰ実行条件OK
LDB R2509                ; ﾘｰｸﾃｽﾀｰL ERR(異常ON)
AND R2511                ; ﾘｰｸﾃｽﾀｰL M/R(ON=ﾘﾓｰﾄ)
INV
OUT @MR101               ; ﾘｰｸﾃｽﾀｰ実行中異常(=ON
;<h1/>Err17ﾘｰｸﾃｽﾀｴﾗｰ/Err 17 leak tester error
LD R2509                 ; ﾘｰｸﾃｽﾀｰL ERR(異常ON)
ANB MR902                ; ｴﾗｰ
MEP
SET @MR000
STG @MR000
DW #17 DM149             ; ｴﾗｰ画面詳細ﾒｯｾｰｼﾞ
CON
SET MR31100              ; Err17ﾘｰｸﾃｽﾀ-ｴﾗｰ
CON
SET MR902                ; ｴﾗｰ
CON
JMP @MR001
STG @MR001
ANB MR902                ; ｴﾗｰ
RES MR31100              ; Err17ﾘｰｸﾃｽﾀ-ｴﾗｰ
CON
ENDS
;<h1/>Err18ﾘｰｸﾃｽﾀﾏﾆｭｱﾙﾓｰﾄﾞ/Err 18 Leak Tester Manual Mode
LDB R2511                ; ﾘｰｸﾃｽﾀｰL M/R(ON=ﾘﾓｰﾄ)
LD MR306                 ; 自動運転中
OR MR307                 ; ﾒﾝﾃ動作中
ANL
ANB MR902                ; ｴﾗｰ
MEP
SET @MR002
STG @MR002
DW #18 DM149             ; ｴﾗｰ画面詳細ﾒｯｾｰｼﾞ
CON
SET MR31101              ; Err18ﾘｰｸﾃｽﾀ-ﾏﾆｭｱﾙﾓｰﾄﾞ
CON
SET MR902                ; ｴﾗｰ
CON
JMP @MR003
STG @MR003
ANB MR902                ; ｴﾗｰ
RES MR31101              ; Err18ﾘｰｸﾃｽﾀ-ﾏﾆｭｱﾙﾓｰﾄﾞ
CON
ENDS
;<h1/>Err21 通信異常/Err 21 Communication error
LDB R12003               ; Port1 処理ｴﾗｰ
ANB R12004               ; Port1 通信ｴﾗｰ
ANB R12006               ; Port1 設定ﾃﾞｰﾀｴﾗｰ
INV
ANB MR902                ; ｴﾗｰ
MEP
SET @MR004
STG @MR004
DW #21 DM149             ; ｴﾗｰ画面詳細ﾒｯｾｰｼﾞ
CON
SET MR31104              ; Err21ﾘｰｸﾃｽﾀｰ通信異常
CON
SET MR902                ; ｴﾗｰ
CON
JMP @MR005
STG @MR005
ANB MR902                ; ｴﾗｰ
RES MR31104              ; Err21ﾘｰｸﾃｽﾀｰ通信異常
CON
ENDS
;<h1/>■ﾘｰｸﾃｽﾀｰ動作/Leak Tester Operation
LDP MR30500              ; TP)検査強制終了
RES R12100               ; Port1 通信許可
CON
RES R12101               ; Port1 送信要求
CON
RES R12102               ; Port1 受信ﾃﾞｰﾀ読出完了
CON
RES MR20500              ; ﾘｰｸﾃｽﾀ-実行要求
CON
RES MR20501              ; ﾘｰｸﾃｽﾀ-実行完了
CON
RES MR20502              ; ﾘｰｸﾃｽﾀｰ処理異常有
CON
ZRES @MR000 @MR815
STG MR20500              ; ﾘｰｸﾃｽﾀ-実行要求
DW #0 @DM0               ; 測定結果書込み回数
CON
DW #0 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
CON
JMP @MR200
;用意状態ﾁｪｯｸ
;RDYまたはHOLDで測定開始可能
;Preparation status check
;Measurement can be started with RDY or HOLD
STG @MR200
ANB MR902                ; ｴﾗｰ
MPS
AND @MR100               ; ﾘｰｸﾃｽﾀｰ実行条件OK
JMP @MR201
MPP
ANB @MR100               ; ﾘｰｸﾃｽﾀｰ実行条件OK
TMR @3 #30
CON
AND @T3
JMP @MR214
;ｸﾞﾙｰﾌﾟ設定/Group setting
;※固定ｸﾞﾙｰﾌﾟ番号
;30：ﾘｰｸﾃｽﾀ公正
;31：ﾘｰｸNG調整
;
;* Fixed group number
;30: Leak tester fair
;31: Leak NG adjustment
;現設定_ﾘｰｸﾃｽﾀｰｸﾞﾙｰﾌﾟ番号
;Now setting Leak tester group nu
;
;ﾘｰｸ検査X_ﾘｰｸﾃｽﾀｸﾞﾙｰﾌﾟ番号2(=ON
;Leak inspectionX Group No2(=ON
LDB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
LDA EM510                ; 現在ﾘｰｸ検査部位数(1-6)
CON
SUB #1
CON
STA Z1
CON
BAND DM821 Z1            ; 現設定_検査ﾊﾟﾗﾒｰﾀｰ2
OUT @MR006               ; Group number 2 use
LD @MR201
MPS
ANB @MR006               ; Group number 2 use
MPS
AND DM826.0
SET MR6505               ; ﾘｰｸﾃｽﾀｰL GROUP1
MPP
ANB DM826.0
RES MR6505               ; ﾘｰｸﾃｽﾀｰL GROUP1
MPP
AND @MR006               ; Group number 2 use
MPS
AND DM836.0
SET MR6505               ; ﾘｰｸﾃｽﾀｰL GROUP1
MPP
ANB DM836.0
RES MR6505               ; ﾘｰｸﾃｽﾀｰL GROUP1
LD @MR201
MPS
ANB @MR006               ; Group number 2 use
MPS
AND DM826.1
SET MR6506               ; ﾘｰｸﾃｽﾀｰL GROUP2
MPP
ANB DM826.1
RES MR6506               ; ﾘｰｸﾃｽﾀｰL GROUP2
MPP
AND @MR006               ; Group number 2 use
MPS
AND DM836.1
SET MR6506               ; ﾘｰｸﾃｽﾀｰL GROUP2
MPP
ANB DM836.1
RES MR6506               ; ﾘｰｸﾃｽﾀｰL GROUP2
LD @MR201
MPS
ANB @MR006               ; Group number 2 use
MPS
AND DM826.2
SET MR6507               ; ﾘｰｸﾃｽﾀｰL GROUP4
MPP
ANB DM826.2
RES MR6507               ; ﾘｰｸﾃｽﾀｰL GROUP4
MPP
AND @MR006               ; Group number 2 use
MPS
AND DM836.2
SET MR6507               ; ﾘｰｸﾃｽﾀｰL GROUP4
MPP
ANB DM836.2
RES MR6507               ; ﾘｰｸﾃｽﾀｰL GROUP4
LD @MR201
MPS
ANB @MR006               ; Group number 2 use
MPS
AND DM826.3
SET MR6508               ; ﾘｰｸﾃｽﾀｰL GROUP8
MPP
ANB DM826.3
RES MR6508               ; ﾘｰｸﾃｽﾀｰL GROUP8
MPP
AND @MR006               ; Group number 2 use
MPS
AND DM836.3
SET MR6508               ; ﾘｰｸﾃｽﾀｰL GROUP8
MPP
ANB DM836.3
RES MR6508               ; ﾘｰｸﾃｽﾀｰL GROUP8
LD @MR201
MPS
ANB @MR006               ; Group number 2 use
MPS
AND DM826.4
SET MR6514               ; ﾘｰｸﾃｽﾀｰL GROUP16
MPP
ANB DM826.4
RES MR6514               ; ﾘｰｸﾃｽﾀｰL GROUP16
MPP
AND @MR006               ; Group number 2 use
MPS
AND DM836.4
SET MR6514               ; ﾘｰｸﾃｽﾀｰL GROUP16
MPP
ANB DM836.4
RES MR6514               ; ﾘｰｸﾃｽﾀｰL GROUP16
STG @MR201
ANB MR902                ; ｴﾗｰ
MPS
AND @MR100               ; ﾘｰｸﾃｽﾀｰ実行条件OK
TMH @0 #22               ; ｸﾞﾙｰﾌﾟ番号ｳｪｲﾄ
CON
AND @T0                  ; ｸﾞﾙｰﾌﾟ番号ｳｪｲﾄ
JMP @MR300
MPP
ANB @MR100               ; ﾘｰｸﾃｽﾀｰ実行条件OK
TMR @4 #300
CON
AND @T4
JMP @MR214
;<h1/> |_ﾘｰｸﾃｽﾀｰ実行条件NG/Leak tester execution condition NG
STG @MR214
DW #19 DM149             ; ｴﾗｰ画面詳細ﾒｯｾｰｼﾞ
CON
SET MR31102              ; Err19ﾘｰｸﾃｽﾀｰ実行条件NG
CON
SET MR902                ; ｴﾗｰ
CON
JMP @MR215
STG @MR215
ANB MR902                ; ｴﾗｰ
RES MR31102              ; Err19ﾘｰｸﾃｽﾀｰ実行条件NG
CON
JMP @MR200
;<h1/> 検査開始/Inspection starts
STG @MR300
DW #0 @DM57              ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ
CON
SET MR6501               ; ﾘｰｸﾃｽﾀｰL START
CON
JMP @MR301
STG @MR301
MPS
LDB @MR101               ; ﾘｰｸﾃｽﾀｰ実行中異常(=ON
AND R2506                ; ﾘｰｸﾃｽﾀｰL RUN(測定中)
ANL
RES MR6501               ; ﾘｰｸﾃｽﾀｰL START
CON
JMP @MR302
MPP
AND @MR101               ; ﾘｰｸﾃｽﾀｰ実行中異常(=ON
JMP @MR500               ; ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ
;END信号待ち/Waiting for END signal
STG @MR302
MPS
ANB @MR101               ; ﾘｰｸﾃｽﾀｰ実行中異常(=ON
MPS
AND @MR103               ; END検出
JMP @MR306
MPP
ANB @MR103               ; END検出
MPS
ANB DM504.1              ; ﾒﾓﾘｰｶｰﾄﾞ書込_測定ﾃﾞｰﾀ無効(=ON
SET @MR102               ; 測定中ﾃﾞｰﾀ読込
CON
JMP @MR303
MPP
AND DM504.1              ; ﾒﾓﾘｰｶｰﾄﾞ書込_測定ﾃﾞｰﾀ無効(=ON
JMP @MR303
MPP
AND @MR101               ; ﾘｰｸﾃｽﾀｰ実行中異常(=ON
JMP @MR500               ; ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ
;測定ﾃﾞｰﾀ読み込み/Read measurement data
;結果読み込み/Read results
STG @MR303
SET @MR700               ; Sub1ﾘｰｸ量読み込み
CON
JMP @MR304
STG @MR304
AND @MR701               ; Sub1_End
RES @MR701               ; Sub1_End
CON
RES @MR102               ; 測定中ﾃﾞｰﾀ読込
CON
MPS
AND @MR103               ; END検出
JMP @MR306
MPP
ANB @MR103               ; END検出
JMP @MR305
STG @MR305
MPS
AND< @DM57 DM15          ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ  測定ﾃﾞｰﾀ取込間隔
JMP @MR302
MPP
AND>= @DM57 DM15         ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ  測定ﾃﾞｰﾀ取込間隔
DW #0 @DM57              ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ
CON
INC @DM0                 ; 測定結果書込み回数
CON
JMP @MR302
;result
STG @MR306
RES @MR103               ; END検出
MPS
LDB MR106                ; ﾒﾝﾃ_装置ﾘｰｸ検査中
ORB LR1915               ; New MC leak
ANL
LDA EM510                ; 現在ﾘｰｸ検査部位数(1-6)
CON
SUB #1
CON
STA Z1
CON
LDA @DM0                 ; 測定結果書込み回数
CON
STA ZF19994:Z1           ; ﾘｰｸ検査1ﾃﾞｰﾀ取得数
CON
JMP @MR307
MPP
LD MR106                 ; ﾒﾝﾃ_装置ﾘｰｸ検査中
AND LR1915               ; New MC leak
ANL
LDA @DM0                 ; 測定結果書込み回数
CON
STA ZF19994              ; ﾘｰｸ検査1ﾃﾞｰﾀ取得数
CON
JMP @MR307
;測定NGﾁｪｯｸ/Measurement NG check
;装置ﾘｰｸ時補正量を含め判断するので、＋NG、-NG、2NGではNG判定しない
;→PLC側の閾値で判定する。
;Since the judgment is made including the device leak correction amount, NG judgment is not performed for + NG, -NG, 2 NG
;→ It is judged by the threshold on the PLC side.
STG @MR307
MPS
LDB R2504                ; ﾘｰｸﾃｽﾀｰL BNG
ANB R2505                ; ﾘｰｸﾃｽﾀｰL PNG
ANL
DW #1 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
CON
JMP @MR600
MPP
LD R2504                 ; ﾘｰｸﾃｽﾀｰL BNG
OR R2505                 ; ﾘｰｸﾃｽﾀｰL PNG
ANL
JMP @MR308
STG @MR308
DW #4 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
CON
MPS
AND R2501                ; ﾘｰｸﾃｽﾀｰL +NG
DW #5 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
AND R2502                ; ﾘｰｸﾃｽﾀｰL -NG
DW #6 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
AND R2503                ; ﾘｰｸﾃｽﾀｰL 2NG
DW #7 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
AND R2504                ; ﾘｰｸﾃｽﾀｰL BNG
DW #8 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
AND R2505                ; ﾘｰｸﾃｽﾀｰL PNG
DW #9 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MPP
JMP @MR600
;<h1/> |_ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ/Leak tester running error
;停止処理/Stop processing
STG @MR500               ; ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ
MPS
AND R2509                ; ﾘｰｸﾃｽﾀｰL ERR(異常ON)
DW #2 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
ANB R2511                ; ﾘｰｸﾃｽﾀｰL M/R(ON=ﾘﾓｰﾄ)
DW #3 DM20020            ; ﾘｰｸﾃｽﾀｰ結果ｺｰﾄﾞ(1=OK)
MRD
LDB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
ANB MR208                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀNG調整中
ANL
RES MR6509               ; ﾘｰｸﾃｽﾀｰL CAL
MPP
ZRES MR6500 MR6504       ; ﾘｰｸﾃｽﾀｰL STOP  ﾘｰｸﾃｽﾀｰL RETRY
CON
ZRES MR6510 MR6514       ; ﾘｰｸﾃｽﾀｰL PSW  ﾘｰｸﾃｽﾀｰL GROUP16
CON
SET MR6500               ; ﾘｰｸﾃｽﾀｰL STOP
CON
JMP @MR501
STG @MR501
TMH @2 #22               ; STOP信号ｳｪｲﾄ
CON
AND @T2                  ; STOP信号ｳｪｲﾄ
RES MR6500               ; ﾘｰｸﾃｽﾀｰL STOP
CON
JMP @MR502
;<h1/>Err20ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ/Err 20 leak tester running error
STG @MR502
ANB MR902                ; ｴﾗｰ
DW #20 DM149             ; ｴﾗｰ画面詳細ﾒｯｾｰｼﾞ
CON
SET MR31103              ; Err20ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ
CON
SET MR902                ; ｴﾗｰ
CON
JMP @MR503
STG @MR503
ANB MR902                ; ｴﾗｰ
SET MR20502              ; ﾘｰｸﾃｽﾀｰ処理異常有
CON
RES MR31103              ; Err20ﾘｰｸﾃｽﾀｰ実行中ｴﾗｰ
CON
JMP @MR600
;<h1/>■ﾘｰｸﾃｽﾀｰ終了処理/Leak tester termination processing
STG @MR600
MPS
LDB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
ANB MR208                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀNG調整中
ANL
RES MR6509               ; ﾘｰｸﾃｽﾀｰL CAL
MPP
ZRES MR6500 MR6504       ; ﾘｰｸﾃｽﾀｰL STOP  ﾘｰｸﾃｽﾀｰL RETRY
CON
ZRES MR6510 MR6514       ; ﾘｰｸﾃｽﾀｰL PSW  ﾘｰｸﾃｽﾀｰL GROUP16
CON
SET MR20501              ; ﾘｰｸﾃｽﾀ-実行完了
CON
ENDS
;<h1/>■ｻﾌﾞﾙｰﾁﾝ/Subroutine
;<h1/>Sub1ﾘｰｸ量読み込み/Sub1 Read leak amount
STG @MR700               ; Sub1ﾘｰｸ量読み込み
JMP @MR702
;設定
;ﾍｯﾀﾞ無し
;ﾃﾞﾐﾘﾀCR
;ﾃﾞｰﾀ長可変:0
;Configuration
;No header
;Demirita CR
;Variable data length: 0
STG @MR702
FMOV #0 DM11259 #256     ; Port1 受信ﾃﾞｰﾀ1
MOV #0 DM11520           ; Port1 受信ﾍｯﾀﾞ設定
MOV $0D00 DM11521        ; Port1 受信ﾃﾞﾘﾐﾀ設定
MOV #0 DM11522           ; Port1 受信ﾃﾞｰﾀ長設定
SET R12100               ; Port1 通信許可
CON
JMP @MR703
STG @MR703
SET R12101               ; Port1 送信要求
CON
MPS
LDB MR31104              ; Err21ﾘｰｸﾃｽﾀｰ通信異常
AND R12002               ; Port1 受信ﾃﾞｰﾀ読出要求
ANL
MPS
ANB R2508                ; ﾘｰｸﾃｽﾀｰL END
@INC @DM57               ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ
MRD
AND R2508                ; ﾘｰｸﾃｽﾀｰL END
RES @MR102               ; 測定中ﾃﾞｰﾀ読込
CON
SET @MR103               ; END検出
CON
AND R2505                ; ﾘｰｸﾃｽﾀｰL PNG
SET @MR108               ; PNG
MPP
LD< @DM0 #1000           ; 測定結果書込み回数
OR R2508                 ; ﾘｰｸﾃｽﾀｰL END
ANL
RES MR6504               ; ﾘｰｸﾃｽﾀｰL RETRY
CON
RES R12101               ; Port1 送信要求
CON
SET R12102               ; Port1 受信ﾃﾞｰﾀ読出完了
CON
JMP @MR704
MPP
AND MR31104              ; Err21ﾘｰｸﾃｽﾀｰ通信異常
RES MR6504               ; ﾘｰｸﾃｽﾀｰL RETRY
CON
JMP @MR705
STG @MR704
ANB R12002               ; Port1 受信ﾃﾞｰﾀ読出要求
JMP @MR713
;通信異常発生 解除後に再度読み込み/Read again after canceling communication error occurrence
STG @MR705
RES R12101               ; Port1 送信要求
CON
ANB MR31104              ; Err21ﾘｰｸﾃｽﾀｰ通信異常
AND R2507                ; ﾘｰｸﾃｽﾀｰL RDY(測定用意完了)
SET MR6504               ; ﾘｰｸﾃｽﾀｰL RETRY
CON
JMP @MR702
;測定中ﾃﾞｰﾀ読み込み/Reading data during measurement
;単位0.1Pa/Unit 0.1 Pa
LD @MR713
AND @MR102               ; 測定中ﾃﾞｰﾀ読込
ANB @MR103               ; END検出
BMOV DM11259 @DM36 #6    ; Port1 受信ﾃﾞｰﾀ1  測定中ﾘｰｸ量(ASC
;結果書込み
;受信ﾃﾞｰﾀ詳細
;2byte:測定ｸﾞﾙｰﾌﾟ
;4byte:判定結果
;5byte:Data Type /SData通常測定,MDataﾌｨｯﾃｨﾝｸﾞ
;13byte:result/数値6byte_単位6byte
;9byte:ﾜｰｸ圧/数値5byte_単位3byte
;10byte:補正値
;10byte:ｼｽﾃﾑ予約
;1byte:制御ｺｰﾄﾞEXT(03H)
;1byte:BCC
;1byte:改行ｺｰﾄﾞ/CR(0DH)
;Result writing
;Receive data details
;2 byte: Measurement group
;4 bytes: determination result
;5 byte: Data Type / SData normal measurement, MData fitting
;13 byte: result / number 6 byte_ unit 6 bytes
;9 byte: Work pressure / numerical value 5 byte_ unit 3 byte
;10 byte: Correction value
;10 bytes: System reservation
;1 byte: Control code EXT (03 H)
;1 byte: BCC
;1 byte: Line feed code / CR (0 DH)
LD @MR713
ANB @MR108               ; PNG
ANB @MR102               ; 測定中ﾃﾞｰﾀ読込
AND @MR103               ; END検出
BMOV DM11259 @DM1 #1     ; Port1 受信ﾃﾞｰﾀ1  結果読込_測定ｸﾞﾙｰﾌﾟ番号
BMOV DM11260 @DM3 #2     ; Port1 受信ﾃﾞｰﾀ2  結果読込_判定結果
SLEFT DM11262 @DM6 #5    ; Port1 受信ﾃﾞｰﾀ4  結果読込_ﾃﾞｰﾀﾀｲﾌﾟ
SMID DM11264 @DM10 #2 #13; Port1 受信ﾃﾞｰﾀ6  結果読込_ﾘｰｸ量結果
SLEFT DM11271 @DM18 #9   ; Port1 受信ﾃﾞｰﾀ13  結果読込_ﾜｰｸ圧
SMID DM11275 @DM24 #2 #10; Port1 受信ﾃﾞｰﾀ17  結果読込_補正値
;共通_ﾃﾞｰﾀ書き込みSTG/Common_data write STG
STG @MR713
JMP @MR714
;共通_読み出し完了/Common _ Read complete
STG @MR714
RES R12100               ; Port1 通信許可
CON
RES R12102               ; Port1 受信ﾃﾞｰﾀ読出完了
CON
MPS
AND @MR108               ; PNG
JMP @MR801
MPP
ANB @MR108               ; PNG
MPS
LD @MR102                ; 測定中ﾃﾞｰﾀ読込
ANB @MR103               ; END検出
ANL
JMP @MR715
MPP
LDB @MR102               ; 測定中ﾃﾞｰﾀ読込
AND @MR103               ; END検出
ANL
JMP @MR800
;測定中ﾃﾞｰﾀ読み込み/Reading data during measurement
;単位変更0.1Pa→1Pa
STG @MR715
MPS
AND>= @DM57 DM15         ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ  測定ﾃﾞｰﾀ取込間隔
RDASC @DM36 @DM43        ; 測定中ﾘｰｸ量(ASC  測定中ﾘｰｸ量(数値.S
MPS
LDB LR1915               ; New MC leak
ORB MR106                ; ﾒﾝﾃ_装置ﾘｰｸ検査中
OR MR208                 ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀNG調整中
ANB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
ANL
MPS
LDA EM510                ; 現在ﾘｰｸ検査部位数(1-6)
CON
SUB #1
CON
MUL #1000
CON
ADD @DM0                 ; 測定結果書込み回数
CON
STA Z1
MPP
LDA.S @DM43              ; 測定中ﾘｰｸ量(数値.S
CON
STA.S ZF20000:Z1         ; ﾘｰｸ検査1測定中差圧1
CON
JMP @MR815
MPP
LD MR106                 ; ﾒﾝﾃ_装置ﾘｰｸ検査中
AND LR1915               ; New MC leak
OR MR204                 ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
ANL
LDA @DM0                 ; 測定結果書込み回数
CON
STA Z1
CON
LDA.S @DM43              ; 測定中ﾘｰｸ量(数値.S
CON
STA.S ZF20000:Z1         ; ﾘｰｸ検査1測定中差圧1
CON
JMP @MR815
MPP
AND< @DM57 DM15          ; ﾃﾞｰﾀ取込間隔ｶｳﾝﾀ  測定ﾃﾞｰﾀ取込間隔
JMP @MR815
;結果の小数点位置対応
LD CR2003                ; 常時OFF
DW #1 @DM51              ; >.切り出し開始位置
CON
DW #1 @DM52              ; >.切り出し文字数
CON
DW #3 @DM53              ; <.切り出し開始位置
CON
DW #3 @DM54              ; <.切り出し文字数
CON
DW #1000 @DM55           ; MUL
CON
OUT @MR104               ; 0.000
LD CR2003                ; 常時OFF
DW #1 @DM51              ; >.切り出し開始位置
CON
DW #2 @DM52              ; >.切り出し文字数
CON
DW #4 @DM53              ; <.切り出し開始位置
CON
DW #2 @DM54              ; <.切り出し文字数
CON
DW #100 @DM55            ; MUL
CON
OUT @MR105               ; 00.00
LD CR2002                ; 常時ON
DW #1 @DM51              ; >.切り出し開始位置
CON
DW #3 @DM52              ; >.切り出し文字数
CON
DW #5 @DM53              ; <.切り出し開始位置
CON
DW #1 @DM54              ; <.切り出し文字数
CON
DW #10 @DM55             ; MUL
CON
OUT @MR106               ; 000.0
LD CR2003                ; 常時OFF
DW #0 @DM50              ; 結果読込_ﾘｰｸ量結果(数値 <.
CON
DW #1 @DM51              ; >.切り出し開始位置
CON
DW #4 @DM52              ; >.切り出し文字数
CON
DW #0 @DM53              ; <.切り出し開始位置
CON
DW #0 @DM54              ; <.切り出し文字数
CON
DW #1 @DM55              ; MUL
CON
OUT @MR107               ; 0000.
;結果書込み/Result writing
;単位の削除/Delete unit
;Result 数値(6ﾊﾞｲﾄ)+単位(6ﾊﾞｲﾄ)
;Result number (6 bytes) + unit (6 bytes)
LD @MR800
SMID @DM11 @DM56 #0 #1   ; 結果読込_ﾘｰｸ量結果  符号
SMID @DM11 @DM45 @DM51 @DM52;結果読込_ﾘｰｸ量結果  結果読込_ﾘｰｸ量結果(数値ASC >.  >.切り出し開始位置  >.切り出し文字数
MPS
ANB @MR107               ; 0000.
SMID @DM11 @DM47 @DM53 @DM54;結果読込_ﾘｰｸ量結果  結果読込_ﾘｰｸ量結果(数値ASC <.  <.切り出し開始位置  <.切り出し文字数
MPP
SCMP @DM56 "*"           ; 符号
CON
MPS
ANB CR2010               ; 演算結果がｾﾞﾛ
RDASC.S @DM45 @DM49      ; 結果読込_ﾘｰｸ量結果(数値ASC >.  結果読込_ﾘｰｸ量結果(数値 >.
MPS
ANB @MR107               ; 0000.
RDASC.S @DM47 @DM50      ; 結果読込_ﾘｰｸ量結果(数値ASC <.  結果読込_ﾘｰｸ量結果(数値 <.
MRD
SCMP @DM56 "+"           ; 符号
CON
AND CR2010               ; 演算結果がｾﾞﾛ
LDA.S @DM49              ; 結果読込_ﾘｰｸ量結果(数値 >.
CON
MUL.S @DM55              ; MUL
CON
ADD.S @DM50              ; 結果読込_ﾘｰｸ量結果(数値 <.
CON
STA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
MPP
SCMP @DM56 "-"           ; 符号
CON
AND CR2010               ; 演算結果がｾﾞﾛ
LDA.S @DM49              ; 結果読込_ﾘｰｸ量結果(数値 >.
CON
MUL.S @DM55              ; MUL
CON
ADD.S @DM50              ; 結果読込_ﾘｰｸ量結果(数値 <.
CON
MUL.S -1
CON
STA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
MPP
AND CR2010               ; 演算結果がｾﾞﾛ
DW.S +32767 @DM34        ; 結果読込_ﾘｰｸ量結果(数値.S
;STG
STG @MR800
JMP @MR801
LD @MR801
AND MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
ANB MR208                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀNG調整中
MPS
ANB @MR108               ; PNG
LDA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
CON
STA.S DM32               ; ﾘｰｸﾃｽﾀ公正結果
MPP
AND @MR108               ; PNG
DW.S +32767 DM32         ; ﾘｰｸﾃｽﾀ公正結果
LD @MR801
ANB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
AND MR208                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀNG調整中
MPS
ANB @MR108               ; PNG
LDA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
CON
STA.S DM40               ; ﾘｰｸﾃｽﾀNG調整結果
MPP
AND @MR108               ; PNG
DW.S +32767 DM40         ; ﾘｰｸﾃｽﾀNG調整結果
LD @MR801
ANB MR204                ; ﾒﾝﾃ_ﾘｰｸﾃｽﾀ公正中
MPS
LDB LR1915               ; New MC leak
ORB MR106                ; ﾒﾝﾃ_装置ﾘｰｸ検査中
ANL
LDA EM510                ; 現在ﾘｰｸ検査部位数(1-6)
CON
SUB #1
CON
STA Z1
CON
MPS
ANB @MR108               ; PNG
LDA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
CON
STA.S EM148:Z1           ; ﾘｰｸ検査1 ﾘｰｸ量(補正前)
MPP
AND @MR108               ; PNG
DW.S +32767 EM148:Z1     ; ﾘｰｸ検査1 ﾘｰｸ量(補正前)
MPP
LD LR1915                ; New MC leak
AND MR106                ; ﾒﾝﾃ_装置ﾘｰｸ検査中
ANL
MPS
ANB @MR108               ; PNG
LDA.S @DM34              ; 結果読込_ﾘｰｸ量結果(数値.S
CON
STA.S EM148              ; ﾘｰｸ検査1 ﾘｰｸ量(補正前)
MPP
AND @MR108               ; PNG
DW.S +32767 EM148        ; ﾘｰｸ検査1 ﾘｰｸ量(補正前)
STG @MR801
JMP @MR815
;終了/end
STG @MR815
RES @MR108               ; PNG
CON
SET @MR701               ; Sub1_End
CON
ENDS
END
ENDH
