DEVICE:53
;MODULE:Panel
;MODULE_TYPE:0
;ﾀｯﾁﾊﾟﾈﾙ接続確認
LD CR2002                ; 常時ON
MPS
@MOV #9999 DM3000        ; 表示中画面No.
MPP
AND= DM3000 #0           ; 表示中画面No.
SET MR109                ; TP接続確認
;言語選択
;Language choice
LD CR2002                ; 常時ON
MPS
LDB LR100                ; 言語切替SW0
ANB LR101                ; 言語切替SW1
ANB LR102                ; 言語切替SW2
ANP T2000                ; 起動遅延
ANL
SET LR100                ; 言語切替SW0
MRD
LD LR100                 ; 言語切替SW0
ANB LR101                ; 言語切替SW1
ANB LR102                ; 言語切替SW2
ANL
MOV #0 DM3025            ; 表示文字列ID
MRD
LDB LR100                ; 言語切替SW0
AND LR101                ; 言語切替SW1
ANB LR102                ; 言語切替SW2
ANL
MOV #1 DM3025            ; 表示文字列ID
MPP
LDB LR100                ; 言語切替SW0
ANB LR101                ; 言語切替SW1
AND LR102                ; 言語切替SW2
ANL
MOV #2 DM3025            ; 表示文字列ID
;表示画面 No. Set
;Screen No. Set
LDP R50010               ; Emargency Stop
OR CR2008                ; 運転開始時1ｽｷｬﾝON
DW #0 @DM0               ; 表示画面記憶
LDP R50008               ; Error
ANB R50300               ; Pause
LDP R50300               ; Pause
LDB R50008               ; Error
ORB R49900               ; Main_Error
ANL
ORL
LDP R50008               ; Error
ORP R49900               ; Main_Error
ANP R50300               ; Pause
ORL
AND<> #6 DM3000          ; 表示中画面No.
AND<> #30 DM3000         ; 表示中画面No.
AND<> DM3000 #3          ; 表示中画面No.
AND<> DM3000 #40         ; 表示中画面No.
AND<> DM3000 #41         ; 表示中画面No.
MOV DM3000 @DM0          ; 表示中画面No.  表示画面記憶
LDP R50001               ; AUTO
MOV #2 @DM0              ; 表示画面記憶
LD< #2 @DM0              ; 表示画面記憶
OUT @MR000               ; 調整画面表示
LD= DM3000 #0            ; 表示中画面No.
RES @MR001               ; 手動，ﾃｨｰﾁﾝｸﾞ画面表示記憶
LD= DM3000 #90           ; 表示中画面No.
OR= DM3000 #120          ; 表示中画面No.
SET @MR001               ; 手動，ﾃｨｰﾁﾝｸﾞ画面表示記憶
;画面切替
;Screen switch
LD CR2008                ; 運転開始時1ｽｷｬﾝON
LDP T2020                ; 動作中遅延
ANB R50003               ; HOME
ORL
MOV #0 DM3008            ; 切替ﾍﾟｰｼﾞ№
;STOP
LDB @MR000               ; 調整画面表示
ANB @MR001               ; 手動，ﾃｨｰﾁﾝｸﾞ画面表示記憶
ANB R50004               ; TASKDO
AND R50007               ; 原点復帰完了
ANB R50008               ; Error
ANB R50300               ; Pause
AND<> DM3000 #3          ; 表示中画面No.
AND<> DM3000 #15         ; 表示中画面No.
AND<> DM3000 #40         ; 表示中画面No.
AND<> DM3000 #41         ; 表示中画面No.
@MOV #2 DM3008           ; 切替ﾍﾟｰｼﾞ№
;HOME
LD R50003                ; HOME
ANB R50008               ; Error
ANB R50300               ; Pause
@MOV #1 DM3008           ; 切替ﾍﾟｰｼﾞ№
;AUTO
LD R50001                ; AUTO
ANB R50008               ; Error
ANB R50300               ; Pause
@MOV #2 DM3008           ; 切替ﾍﾟｰｼﾞ№
;STEP
LD R50002                ; STEP
ANB R50008               ; Error
ANB R50300               ; Pause
@MOV #15 DM3008          ; 切替ﾍﾟｰｼﾞ№
;Error
LD R50008                ; Error
OR R49900                ; Main_Error
ANB R55014               ; ﾌﾞｻﾞｰﾘｾｯﾄON
@MOV #30 DM3008          ; 切替ﾍﾟｰｼﾞ№
LDB R50008               ; Error
ANB R50003               ; HOME
MPS
AND R50007               ; 原点復帰完了
MPS
LDB @MR000               ; 調整画面表示
ANB MR005                ; TP[0015]: STEP画面
ANL
@MOV #2 DM3008           ; 切替ﾍﾟｰｼﾞ№
MPP
AND @MR000               ; 調整画面表示
@MOV @DM0 DM3008         ; 表示画面記憶  切替ﾍﾟｰｼﾞ№
MPP
LDB R50007               ; 原点復帰完了
ANB R50001               ; AUTO
ANB R50002               ; STEP
ANL
@MOV #0 DM3008           ; 切替ﾍﾟｰｼﾞ№
;動作ﾓｰﾄﾞ表示
;Operation mode display
LDB R50004               ; TASKDO
AND R50007               ; 原点復帰完了
ANB R50300               ; Pause
ANB R50008               ; Error
MPS
ANB MR46003              ; 自動ﾓｰﾄﾞLP(ﾊﾟﾈﾙ)
OUT R72000               ; TP "停止中"表示
MPP
AND MR46003              ; 自動ﾓｰﾄﾞLP(ﾊﾟﾈﾙ)
MPS
AND R50101               ; 内蓋閉め確認
MPS
AND R50102               ; ｴﾘｱｾﾝｻ
OUT R72004               ; TP "ｽﾀｰﾄ待ち"表示
MPP
ANB R50102               ; ｴﾘｱｾﾝｻ
OUT R72006               ; TP "ｴﾘｱｾﾝｻ遮光"表示
MPP
ANB R50101               ; 内蓋閉め確認
OUT R72007               ; TP "内蓋開"表示
LD R50001                ; AUTO
ANB R50300               ; Pause
ANB R50008               ; Error
MPS
LDB R50000               ; 即停止 HOLD
ANB R50103               ; 除材要求
ANB R50104               ; NG品ありｾｯﾄ
ANL
OUT R72001               ; TP "自動動作中"表示
MRD
AND R50000               ; 即停止 HOLD
OUT R72005               ; TP ""表示
MRD
AND R50103               ; 除材要求
OUT R72003               ; TP OK品取出""表示
MPP
AND R50104               ; NG品ありｾｯﾄ
OUT R72009               ; TP "NG品取出"表示
LD R50002                ; STEP
ANB R50300               ; Pause
ANB R50008               ; Error
OUT R72002               ; TP "ｽﾃｯﾌﾟ動作中"表示
LD R50300                ; Pause
OUT R72008               ; TP "一時停止"表示
;電源投入処理中表示
;Displays while processing the power supply turning on. 
LD T2000                 ; 起動遅延
OUT R73700               ; TP ｽﾀｰﾄ可能表示
;ｴﾗｰ画面 ｽｲｯﾁ/ﾗﾝﾌﾟ表示制御
;Switch/lamp display control of error screen
LDP R71700               ; ﾘﾄﾗｲ
SET LR000                ; ﾘﾄﾗｲSW選択PL(ﾊﾟﾈﾙ)
CON
RES LR001                ; 継続SW選択PL(ﾊﾟﾈﾙ)
CON
RES LR002                ; 終了
LDP R71701               ; 継続
RES LR000                ; ﾘﾄﾗｲSW選択PL(ﾊﾟﾈﾙ)
CON
SET LR001                ; 継続SW選択PL(ﾊﾟﾈﾙ)
CON
RES LR002                ; 終了
LDP R71702               ; 終了
RES LR000                ; ﾘﾄﾗｲSW選択PL(ﾊﾟﾈﾙ)
CON
RES LR001                ; 継続SW選択PL(ﾊﾟﾈﾙ)
CON
SET LR002                ; 終了
LD<> #30 DM3000          ; 表示中画面No.
@ZRES LR000 LR002        ; ﾘﾄﾗｲSW選択PL(ﾊﾟﾈﾙ)  終了
;軸状態表示
;Axis state display
;OFF:Normalcy
;ON :Abnormality
LD T2000                 ; 起動遅延
MPS
LD R34400                ; 軸1 軸ｴﾗｰ中
AND R32802               ; ｴﾗｰ中
ANL
OUT MR1102               ; 軸1 状態表示
MPP
LD R35200                ; 軸2 軸ｴﾗｰ中
AND R32802               ; ｴﾗｰ中
ANL
OUT MR1104               ; 軸2 状態表示
;画面表示
LD CR2002                ; 常時ON
MPS
AND= DM3000 #0           ; 表示中画面No.
OUT MR000                ; TP[0000]:初期画面
MRD
AND= DM3000 #2           ; 表示中画面No.
OUT MR002                ; TP[0000]: ﾒｲﾝ画面
MRD
AND= DM3000 #15          ; 表示中画面No.
OUT MR005                ; TP[0015]: STEP画面
MRD
AND= DM3000 #30          ; 表示中画面No.
OUT MR010                ; TP[0030]: ERROR画面
MRD
LD= DM3000 #100          ; 表示中画面No.
OR= DM3000 #101          ; 表示中画面No.
ANL
OUT MR100                ; TP[0015]: MANUAL画面
MPP
AND= DM3000 #120         ; 表示中画面No.
OUT MR101                ; TP[0015]: TEACH画面
LD CR2002                ; 常時ON
MPS
LD MR1207                ; 軸XY_待機位置P7
LD= DM1000 #7            ; 軸1 移動先ﾎﾟｲﾝﾄNo.
LD R34404                ; 軸1 軸制御中
OR R35204                ; 軸2 軸制御中
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT R80207               ; UT1-08 PL
MRD
LD MR1201                ; 軸XY_測定開始(左)P1
LD= DM1000 #1            ; 軸1 移動先ﾎﾟｲﾝﾄNo.
LD R34404                ; 軸1 軸制御中
OR R35204                ; 軸2 軸制御中
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT R80200               ; UT1-01 PL
MRD
LD MR1202                ; 軸XY_測定終了(左)P2
LD= DM1000 #2            ; 軸1 移動先ﾎﾟｲﾝﾄNo.
LD R34404                ; 軸1 軸制御中
OR R35204                ; 軸2 軸制御中
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT R80201               ; UT1-02 PL
MRD
LD MR1203                ; 軸XY_測定開始(左)P3
LD= DM1000 #3            ; 軸1 移動先ﾎﾟｲﾝﾄNo.
LD R34404                ; 軸1 軸制御中
OR R35204                ; 軸2 軸制御中
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT R80202               ; UT1-03 PL
MPP
LD MR1204                ; 軸XY_測定終了(左)P4
LD= DM1000 #4            ; 軸1 移動先ﾎﾟｲﾝﾄNo.
LD R34404                ; 軸1 軸制御中
OR R35204                ; 軸2 軸制御中
ANL
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
ORL
ANL
OUT R80203               ; UT1-04 PL
;日常点検判定結果
LD CR2002                ; 常時ON
MPS
AND LR110                ; 日常点検L OK記憶
OUT MR110                ; Stratos-L OK表示
MPP
AND LR111                ; 日常点検R OK記憶
OUT MR111                ; Stratos-R OK表示
;検査判定結果
LD MR31114               ; 検査結果取得完了
LD R50103                ; 除材要求
OR R50104                ; NG品ありｾｯﾄ
LDB R50001               ; AUTO
ANB R50002               ; STEP
ORL
ANL
OUT MR113                ; 判定結果表示
;L側結果表示
LD R50001                ; AUTO
ANB MR113                ; 判定結果表示
OUT R72501               ; L側 測定中
LD MR113                 ; 判定結果表示
MPS
LD MR610                 ; L側 OKまとめ
ANB MR609                ; L側 ﾜｰｸ無
ANL
OUT R72502               ; L側 OK表示
MRD
ANB MR610                ; L側 OKまとめ
OUT R72503               ; L側 NG表示
MPP
AND MR609                ; L側 ﾜｰｸ無
OUT R72504               ; L側 ﾜｰｸ無
LDB R72501               ; L側 測定中
ANB R72502               ; L側 OK表示
ANB R72503               ; L側 NG表示
ANB R72504               ; L側 ﾜｰｸ無
OUT R72500               ; L側 結果表示
;R側結果表示
LD R50001                ; AUTO
ANB MR113                ; 判定結果表示
OUT R72601               ; R側 測定中
LD MR113                 ; 判定結果表示
MPS
LD MR710                 ; R側 OKまとめ
ANB MR709                ; R側 ﾜｰｸ無
ANL
OUT R72602               ; R側 OK表示
MRD
ANB MR710                ; R側 OKまとめ
OUT R72603               ; R側 NG表示
MPP
AND MR709                ; R側 ﾜｰｸ無
OUT R72604               ; R側 ﾜｰｸ無
LDB R72601               ; R側 測定中
ANB R72602               ; R側 OK表示
ANB R72603               ; R側 NG表示
ANB R72604               ; R側 ﾜｰｸ無
OUT R72600               ; R側 結果表示
;検査ﾓｰﾄﾞ切替IL
LD R72000                ; TP "停止中"表示
AND LR110                ; 日常点検L OK記憶
AND LR111                ; 日常点検R OK記憶
AND T2125                ; 画像へ検査設定完了DLY
OUT MR410                ; 検査ﾓｰﾄﾞ切替OK
END
ENDH
