DEVICE:51
;MODULE:Display
;MODULE_TYPE:0
;<h1/>フリッカー
;Ｆｌｉｃｋｅｒ
LD CR2002                ; 常時ON
FLIK #30 #30 R50207      ; ﾌﾘｯｶ
;<h1/>スタートランプ
;Ｓｔａｒｔ  ｌａｍｐ
LDB R50007               ; 原点復帰完了
ANB R50000               ; E.STOP
ANB R55000               ; TRAP
ANB R50006               ; 異常発生中
ANB R50003               ; 原点復帰中
OR R50014                ; ｴﾗｰﾘｾｯﾄSW ON
LD R50009                ; 一時停止中
AND R1004                ; Area Sensor
ORL
LD R50007                ; 原点復帰完了
ANB R50004               ; 動作中
ORL
OR R55001                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OR R55002                ; ﾜｰｸｾｯﾄｴﾗｰ
OR R55003                ; ｴｱｰｸﾘｰﾝﾕﾆｯﾄALM
OR R55004                ; ｲｵﾅｲｻﾞﾕﾆｯﾄALM
OR R55005                ; ﾀﾝｸ 水無ｴﾗｰ
AND R50207               ; ﾌﾘｯｶ
LD R50004                ; 動作中
ANB R50009               ; 一時停止中
ANB R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ
ANB R55002               ; ﾜｰｸｾｯﾄｴﾗｰ
ANB R55003               ; ｴｱｰｸﾘｰﾝﾕﾆｯﾄALM
ANB R55004               ; ｲｵﾅｲｻﾞﾕﾆｯﾄALM
ANB R50014               ; ｴﾗｰﾘｾｯﾄSW ON
ORL
LD R50011                ; 自動選択
AND LR1000               ; 自動 ﾊﾟﾗﾒｰﾀ_0
LD R50012                ; 手動選択
LD LR000                 ; ﾀﾝｸ1 給水 LAMP
OR LR001                 ; ﾀﾝｸ1 排水 LAMP
OR LR002                 ; ﾀﾝｸ1 水戻 LAMP
OR LR003                 ; 手動動作_3 選択 LAMP
OR LR004                 ; ﾀﾝｸ2 給水 LAMP
OR LR005                 ; ﾀﾝｸ2 排水 LAMP
OR LR006                 ; ﾀﾝｸ2 水戻 LAMP
ANL
ORL
ORB R50007               ; 原点復帰完了
ANL
OUT R30000               ; Start1 sw Lamp
OUT R30001               ; Start2 sw  Lamp
;<h1/>タワーランプ　赤
;Ｔｏｗｅｒ  Ｌａｍｐ  Ｒｅｄ
LD R50000                ; E.STOP
OR R55000                ; TRAP
OR R50108                ; 停止表示
LD R50006                ; 異常発生中
LDB R50007               ; 原点復帰完了
ANB R50004               ; 動作中
ORL
OR R55001                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OR R55002                ; ﾜｰｸｾｯﾄｴﾗｰ
OR R55003                ; ｴｱｰｸﾘｰﾝﾕﾆｯﾄALM
OR R55004                ; ｲｵﾅｲｻﾞﾕﾆｯﾄALM
OR R55005                ; ﾀﾝｸ 水無ｴﾗｰ
AND R50207               ; ﾌﾘｯｶ
ORL
OUT R30002               ; ﾀﾜｰﾗﾝﾌﾟ赤
;<h1/>タワーランプ　黄
;Ｔｏｗｅｒ  Ｌａｍｐ  Ｙｅｌｌｏｗ
LD R50004                ; 動作中
OR R50206                ; 異常発生中表示
ANB R50000               ; E.STOP
ANB R55000               ; TRAP
ANB MR1010               ; ﾀﾝｸ1 水戻中
ANB MR1013               ; ﾀﾝｸ2 水戻中
LD MR1010                ; ﾀﾝｸ1 水戻中
OR MR1013                ; ﾀﾝｸ2 水戻中
AND R50207               ; ﾌﾘｯｶ
ORL
ANB R50108               ; 停止表示
OUT R30003               ; ﾀﾜｰﾗﾝﾌﾟ黄
;<h1/>タワーランプ　緑
;Ｔｏｗｅｒ  Ｌａｍｐ  Ｇｒｅｅｎ
LDB R50004               ; 動作中
ANB R50009               ; 一時停止中
LD R50009                ; 一時停止中
AND R50207               ; ﾌﾘｯｶ
ORL
ANB R50000               ; E.STOP
ANB R55000               ; TRAP
ANB R50006               ; 異常発生中
ANB R55005               ; ﾀﾝｸ 水無ｴﾗｰ
OUT R30004               ; ﾀﾜｰﾗﾝﾌﾟ緑
;<h1/>ブザー
;Ｂｕｚｚｅｒ
LD R50000                ; E.STOP
OR R55000                ; TRAP
OR R50010                ; ﾌﾞｻﾞｰON
OR R50013                ; 異常発生ﾌﾞｻﾞｰ ON
OR MR1000                ; ﾌﾞｻﾞｰON
OR MR2008                ; ﾌﾞｻﾞｰON
OR R50107                ; ﾌﾞｻﾞｰ要求
OUT R30005               ; ﾌﾞｻﾞｰ
;<h1/>エリアセンサー表示灯
;Ａｒｅａ  Ｓｅｎｓｏｒ  Ｄｉｓｐｌａｙ   Ｌａｍｐ
LD R50008                ; ｴﾘｱｾﾝｻｰ  有効中
OUT R34100               ; Area Sensor Lamp
END
ENDH
