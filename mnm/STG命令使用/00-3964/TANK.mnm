DEVICE:51
;MODULE:TANK
;MODULE_TYPE:0
;<h1/>非常停止　出力クリア
;Ｅｍｅｒｇｅｎｃｙ  Ｓｔｏｐ  Ｏｕｔｐｕｔ  Ｃｌｅａｒ
LDP MR1815               ; E.stop
OUT MR1814               ; E.stop
LD MR1814                ; E.stop
ZRES @MR000 @MR3915
;<h1/>原点復帰
;Ｒｅｔｕｒｎ  ｔｏ  ｈｏｍｅ  ｐｏｓｉｔｉｏｎ
LD R50003                ; 原点復帰中
DIFU @MR1000             ; 原点復帰 ｽﾀｰﾄ PLS
LD @MR1000               ; 原点復帰 ｽﾀｰﾄ PLS
SET @MR1001              ; 原点復帰 STG SET
STG @MR1001              ; 原点復帰 STG SET
AND R50003               ; 原点復帰中
SET R50200               ; ﾀﾝｸﾕﾆｯﾄ 動作中
CON
JMP @MR1002
STG @MR1002
AND R50003               ; 原点復帰中
TMR #410 #10             ; ﾘｾｯﾄ遅延
CON
AND T410                 ; ﾘｾｯﾄ遅延
RES R50200               ; ﾀﾝｸﾕﾆｯﾄ 動作中
CON
ENDS
;使用するタンクを選択する。
;基本的には、タンク１が優先される。
;Ｔｈｅ  ｔａｎｋ  ｔｏ  ｂｅ ｕｓｅｄ  ｉｓ  ｃｈｏｓｅｎ．
;Ｆｕｎｄａｍｅｎｔａｌｌｙ､  ｔａｎｋ１  ｉｓ  ｐｒｉｏｒｉｔｙ．
LD @MR1000               ; 原点復帰 ｽﾀｰﾄ PLS
MPS
LDB R1102                ; 新液ﾀﾝｸ1 下限
LD R1102                 ; 新液ﾀﾝｸ1 下限
AND R1105                ; 新液ﾀﾝｸ2 下限
ORL
ANL
SET MR1001               ; ﾀﾝｸ1
CON
RES MR1002               ; ﾀﾝｸ2
MPP
LD R1102                 ; 新液ﾀﾝｸ1 下限
ANB R1105                ; 新液ﾀﾝｸ2 下限
ANL
RES MR1001               ; ﾀﾝｸ1
CON
SET MR1002               ; ﾀﾝｸ2
;<h1/>手動運転
;Ｍａｎｕａｌ  Ｏｐｅｒａｔｉｏｎ
LD R50002                ; 手動中
RES R30106               ; 廃液ﾀﾝｸ1 給水
CON
RES R30107               ; 廃液ﾀﾝｸ2 給水
CON
DIFU @MR1300             ; 手動運転  ｽﾀｰﾄ PLS
LD @MR1300               ; 手動運転  ｽﾀｰﾄ PLS
SET @MR1301              ; 手動運転  STG SET
STG @MR1301              ; 手動運転  STG SET
AND R50002               ; 手動中
SET R50200               ; ﾀﾝｸﾕﾆｯﾄ 動作中
CON
MPS
AND LR000                ; ﾀﾝｸ1 給水 LAMP
MPS
LD R1100                 ; 新液ﾀﾝｸ1 ｵｰﾊﾞｰﾌﾛｰ
AND R1101                ; 新液ﾀﾝｸ1 上限
ANL
SET @MR1303              ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
CON
DIFU @MR1306             ; 手動 ﾀﾝｸ1 給水 PLS
CON
JMP @MR1400              ; 手動 ﾀﾝｸ1 給水状態確認
MPP
LDB R1100                ; 新液ﾀﾝｸ1 ｵｰﾊﾞｰﾌﾛｰ
ORB R1101                ; 新液ﾀﾝｸ1 上限
ANL
JMP @MR1405              ; ﾀﾝｸ1 給水不可
MRD
AND LR001                ; ﾀﾝｸ1 排水 LAMP
SET @MR1304              ; 手動 ﾀﾝｸ1 排水ﾓｰﾄﾞ
CON
DIFU @MR1307             ; 手動 ﾀﾝｸ1 排水 PLS
CON
JMP @MR1500              ; 手動 ﾀﾝｸ1 排水状態確認
MRD
AND LR002                ; ﾀﾝｸ1 水戻 LAMP
SET @MR1305              ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
CON
DIFU @MR1308             ; 手動 ﾀﾝｸ1 水戻 PLS
CON
JMP @MR1600              ; 手動 ﾀﾝｸ1 水戻状態確認
MRD
AND LR004                ; ﾀﾝｸ2 給水 LAMP
MPS
LD R1103                 ; 新液ﾀﾝｸ2 ｵｰﾊﾞｰﾌﾛｰ
AND R1104                ; 新液ﾀﾝｸ2 上限
ANL
SET @MR1309              ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
CON
DIFU @MR1312             ; 手動 ﾀﾝｸ2 給水 PLS
CON
JMP @MR1700              ; 手動 ﾀﾝｸ2 給水状態確認
MPP
LDB R1103                ; 新液ﾀﾝｸ2 ｵｰﾊﾞｰﾌﾛｰ
ORB R1104                ; 新液ﾀﾝｸ2 上限
ANL
JMP @MR1705              ; ﾀﾝｸ2 給水不可
MRD
AND LR005                ; ﾀﾝｸ2 排水 LAMP
SET @MR1310              ; 手動 ﾀﾝｸ2 排水ﾓｰﾄﾞ
CON
DIFU @MR1313             ; 手動 ﾀﾝｸ2 排水 PLS
CON
JMP @MR1800              ; 手動 ﾀﾝｸ2 排水状態確認
MRD
AND LR006                ; ﾀﾝｸ2 水戻 LAMP
SET @MR1311              ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
CON
DIFU @MR1314             ; 手動 ﾀﾝｸ2 水戻 PLS
CON
JMP @MR1900              ; 手動 ﾀﾝｸ2 水戻状態確認
MPP
LDB LR000                ; ﾀﾝｸ1 給水 LAMP
ANB LR001                ; ﾀﾝｸ1 排水 LAMP
ANB LR002                ; ﾀﾝｸ1 水戻 LAMP
ANB LR004                ; ﾀﾝｸ2 給水 LAMP
ANB LR005                ; ﾀﾝｸ2 排水 LAMP
ANB LR006                ; ﾀﾝｸ2 水戻 LAMP
ANL
JMP @MR1302              ; 手動運転 終了 STG
STG @MR1302              ; 手動運転 終了 STG
AND R50002               ; 手動中
RES R50200               ; ﾀﾝｸﾕﾆｯﾄ 動作中
CON
ENDS
;<h1/>手動　新液タンク１　給水
;ＭＡＮＵ  Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ１  Ｗａｔｅｒ  Ｓｕｐｐｌｙ
STG @MR1400              ; 手動 ﾀﾝｸ1 給水状態確認
AND R50002               ; 手動中
MPS
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
ANB R4004                ; 新液ﾀﾝｸ1 加圧検出
JMP @MR1401
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1404
STG @MR1401
MPS
AND R50002               ; 手動中
SET R30102               ; 新液ﾀﾝｸ1 新液給水
CON
LDB R1101                ; 新液ﾀﾝｸ1 上限
ORB R1100                ; 新液ﾀﾝｸ1 ｵｰﾊﾞｰﾌﾛｰ
ANL
OUT @MR1402              ; 手動 ﾀﾝｸ1 給水終了 ﾌﾞｻﾞｰ
CON
JMP @MR1404
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1404
STG @MR1404
AND R50002               ; 手動中
RES R30102               ; 新液ﾀﾝｸ1 新液給水
CON
RES @MR1303              ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>手動　タンク１　排水
;Ｍａｎｕａｌ  Ｔａｎｋ１  Ｄｒａｉｎ
;タンク１排水チャタリング防止タイマ
;
LD R1102                 ; 新液ﾀﾝｸ1 下限
ANB @MR1509              ; 廃水ﾀﾝｸ1 状態確認
ANB @MR1510              ; ﾀﾝｸ1 排水終了確認
TMR #420 DM340           ; 新液ﾀﾝｸ1 下限On
LDB R1102                ; 新液ﾀﾝｸ1 下限
ANB @MR1509              ; 廃水ﾀﾝｸ1 状態確認
ANB @MR1510              ; ﾀﾝｸ1 排水終了確認
TMR #421 DM340           ; 新液ﾀﾝｸ1 下限Off
LD R1108                 ; 廃液ﾀﾝｸ1 下限
ANB @MR1509              ; 廃水ﾀﾝｸ1 状態確認
ANB @MR1510              ; ﾀﾝｸ1 排水終了確認
TMR #422 DM340           ; 廃液ﾀﾝｸ1 下限On
LDB R1108                ; 廃液ﾀﾝｸ1 下限
ANB @MR1509              ; 廃水ﾀﾝｸ1 状態確認
ANB @MR1510              ; ﾀﾝｸ1 排水終了確認
TMR #423 DM340           ; 廃液ﾀﾝｸ1 下限Off
;タンク１排水開始
;
STG @MR1500              ; 手動 ﾀﾝｸ1 排水状態確認
AND R50002               ; 手動中
SET @MR1506              ; ﾀﾝｸ1 排水OK
CON
SET R30108               ; 廃液ﾀﾝｸ1 排水
CON
JMP @MR1501
LD @MR1506               ; ﾀﾝｸ1 排水OK
AND R50100               ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
SET @MR1507              ; ﾀﾝｸ1 排水 停止要求
STG @MR1501
AND R50002               ; 手動中
MPS
TMR #430 #2
CON
AND T430
MPS
LD T420                  ; 新液ﾀﾝｸ1 下限On
AND T423                 ; 廃液ﾀﾝｸ1 下限Off
ANL
SET @MR1508              ; ﾀﾝｸ1 排水終了
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
SET R30110               ; 廃液ﾀﾝｸ1 加圧
CON
JMP @MR1503
MRD
LD T421                  ; 新液ﾀﾝｸ1 下限Off
AND T422                 ; 廃液ﾀﾝｸ1 下限On
ANL
SET @MR1508              ; ﾀﾝｸ1 排水終了
CON
SET R30100               ; 新液ﾀﾝｸ1 加圧
CON
RES R30110               ; 廃液ﾀﾝｸ1 加圧
CON
JMP @MR1503
MRD
LD T421                  ; 新液ﾀﾝｸ1 下限Off
AND T423                 ; 廃液ﾀﾝｸ1 下限Off
ANL
SET R30100               ; 新液ﾀﾝｸ1 加圧
CON
SET R30110               ; 廃液ﾀﾝｸ1 加圧
CON
JMP @MR1503
MPP
LD T420                  ; 新液ﾀﾝｸ1 下限On
AND T422                 ; 廃液ﾀﾝｸ1 下限On
ANL
JMP @MR1503
MPP
DIFU @MR1509             ; 廃水ﾀﾝｸ1 状態確認
STG @MR1503
AND R50002               ; 手動中
MPS
ANB @MR1507              ; ﾀﾝｸ1 排水 停止要求
MPS
TMR #431 #2
CON
AND T431
MPS
LD T420                  ; 新液ﾀﾝｸ1 下限On
AND T423                 ; 廃液ﾀﾝｸ1 下限Off
LD T421                  ; 新液ﾀﾝｸ1 下限Off
AND T422                 ; 廃液ﾀﾝｸ1 下限On
ORL
ANB @MR1508              ; ﾀﾝｸ1 排水終了
ANL
JMP @MR1501
MPP
LD T420                  ; 新液ﾀﾝｸ1 下限On
AND T422                 ; 廃液ﾀﾝｸ1 下限On
ANL
OUT @MR1502              ; 手動 ﾀﾝｸ1 排水終了 ﾌﾞｻﾞｰ
CON
JMP @MR1505
MPP
DIFU @MR1510             ; ﾀﾝｸ1 排水終了確認
MPP
AND @MR1507              ; ﾀﾝｸ1 排水 停止要求
JMP @MR1505
STG @MR1505
ZRES @MR1506 @MR1508     ; ﾀﾝｸ1 排水OK  ﾀﾝｸ1 排水終了
CON
RES R30108               ; 廃液ﾀﾝｸ1 排水
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
RES R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES @MR1304              ; 手動 ﾀﾝｸ1 排水ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>手動　タンク１　水戻
;ＭＡＮＵ  Ｔａｎｋ２  Ｗａｔｅｒ  Ｒｅｔｕｒｎ
STG @MR1600              ; 手動 ﾀﾝｸ1 水戻状態確認
AND R50002               ; 手動中
MPS
LD R1101                 ; 新液ﾀﾝｸ1 上限
ANB R1108                ; 廃液ﾀﾝｸ1 下限
ANL
SET R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
TMR #400 #100
CON
AND T400
JMP @MR1601
MRD
LDB R1101                ; 新液ﾀﾝｸ1 上限
OR R1108                 ; 廃液ﾀﾝｸ1 下限
ANL
TMR #406 #10
CON
AND T406
OUT @MR1603              ; 手動 ﾀﾝｸ1 水戻終了 ﾌﾞｻﾞｰ
CON
JMP @MR1602
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1602
STG @MR1601
AND R50002               ; 手動中
MPS
SET R30104               ; 新液ﾀﾝｸ1 ろ過給水
CON
LDB R1101                ; 新液ﾀﾝｸ1 上限
OR R1108                 ; 廃液ﾀﾝｸ1 下限
ANL
OUT @MR1603              ; 手動 ﾀﾝｸ1 水戻終了 ﾌﾞｻﾞｰ
CON
JMP @MR1602
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1602
STG @MR1602
AND R50002               ; 手動中
RES R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES R30104               ; 新液ﾀﾝｸ1 ろ過給水
CON
RES @MR1305              ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>手動　新液タンク２　給水
;ＭＡＮＵ  Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ２  Ｗａｔｅｒ  Ｓｕｐｐｌｙ
STG @MR1700              ; 手動 ﾀﾝｸ2 給水状態確認
AND R50002               ; 手動中
MPS
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
ANB R4005                ; 新液ﾀﾝｸ2 加圧検出
JMP @MR1701
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1704
STG @MR1701
AND R50002               ; 手動中
MPS
SET R30103               ; 新液ﾀﾝｸ2 新液給水
CON
LDB R1104                ; 新液ﾀﾝｸ2 上限
ORB R1103                ; 新液ﾀﾝｸ2 ｵｰﾊﾞｰﾌﾛｰ
ANL
OUT @MR1702              ; 手動 ﾀﾝｸ2 給水終了 ﾌﾞｻﾞｰ
CON
JMP @MR1704
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1704
STG @MR1704
AND R50002               ; 手動中
RES R30103               ; 新液ﾀﾝｸ2 新液給水
CON
RES @MR1309              ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>手動　タンク２　排水
;ＭＡＮＵ  Ｔａｎｋ２  Ｄｒａｉｎ
;タンク２排水　ﾁｬﾀﾘﾝｸﾞ防止タイマ
;
LD R1105                 ; 新液ﾀﾝｸ2 下限
ANB @MR1809              ; 廃水ﾀﾝｸ2 状態確認
ANB @MR1810              ; ﾀﾝｸ2 排水終了確認
TMR #424 DM342           ; 新液ﾀﾝｸ2 下限On
LDB R1105                ; 新液ﾀﾝｸ2 下限
ANB @MR1809              ; 廃水ﾀﾝｸ2 状態確認
ANB @MR1810              ; ﾀﾝｸ2 排水終了確認
TMR #425 DM342           ; 新液ﾀﾝｸ2 下限Off
LD R1111                 ; 廃液ﾀﾝｸ2 下限
ANB @MR1809              ; 廃水ﾀﾝｸ2 状態確認
ANB @MR1810              ; ﾀﾝｸ2 排水終了確認
TMR #426 DM342           ; 廃液ﾀﾝｸ2 下限On
LDB R1111                ; 廃液ﾀﾝｸ2 下限
ANB @MR1809              ; 廃水ﾀﾝｸ2 状態確認
ANB @MR1810              ; ﾀﾝｸ2 排水終了確認
TMR #427 DM342           ; 廃液ﾀﾝｸ2 下限Off
;タンク２排水開始
;
STG @MR1800              ; 手動 ﾀﾝｸ2 排水状態確認
AND R50002               ; 手動中
SET @MR1806              ; ﾀﾝｸ2 排水OK
CON
SET R30109               ; 廃液ﾀﾝｸ2 排水
CON
JMP @MR1801
LD @MR1806               ; ﾀﾝｸ2 排水OK
AND R50100               ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
SET @MR1807              ; ﾀﾝｸ2 排水 停止要求
STG @MR1801
AND R50002               ; 手動中
MPS
TMR #432 #2
CON
AND T432
MPS
LD T424                  ; 新液ﾀﾝｸ2 下限On
AND T427                 ; 廃液ﾀﾝｸ2 下限Off
ANL
SET @MR1808              ; ﾀﾝｸ2 排水終了
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
SET R30111               ; 廃液ﾀﾝｸ2 加圧
CON
JMP @MR1803
MRD
LD T425                  ; 新液ﾀﾝｸ2 下限Off
AND T426                 ; 廃液ﾀﾝｸ2 下限On
ANL
SET @MR1808              ; ﾀﾝｸ2 排水終了
CON
SET R30101               ; 新液ﾀﾝｸ2 加圧
CON
RES R30111               ; 廃液ﾀﾝｸ2 加圧
CON
JMP @MR1803
MRD
LD T425                  ; 新液ﾀﾝｸ2 下限Off
AND T427                 ; 廃液ﾀﾝｸ2 下限Off
ANL
SET R30101               ; 新液ﾀﾝｸ2 加圧
CON
SET R30111               ; 廃液ﾀﾝｸ2 加圧
CON
JMP @MR1803
MPP
LD T424                  ; 新液ﾀﾝｸ2 下限On
AND T426                 ; 廃液ﾀﾝｸ2 下限On
ANL
JMP @MR1803
MPP
DIFU @MR1809             ; 廃水ﾀﾝｸ2 状態確認
STG @MR1803
AND R50002               ; 手動中
MPS
ANB @MR1807              ; ﾀﾝｸ2 排水 停止要求
MPS
TMR #433 #2
CON
AND T433
MPS
LD T424                  ; 新液ﾀﾝｸ2 下限On
AND T427                 ; 廃液ﾀﾝｸ2 下限Off
LD T425                  ; 新液ﾀﾝｸ2 下限Off
AND T426                 ; 廃液ﾀﾝｸ2 下限On
ORL
ANB @MR1808              ; ﾀﾝｸ2 排水終了
ANL
JMP @MR1801
MPP
LD T424                  ; 新液ﾀﾝｸ2 下限On
AND T426                 ; 廃液ﾀﾝｸ2 下限On
ANL
OUT @MR1802              ; 手動 ﾀﾝｸ2 排水終了 ﾌﾞｻﾞｰ
CON
JMP @MR1805
MPP
DIFU @MR1810             ; ﾀﾝｸ2 排水終了確認
MPP
AND @MR1807              ; ﾀﾝｸ2 排水 停止要求
JMP @MR1805
STG @MR1805
ZRES @MR1806 @MR1808     ; ﾀﾝｸ2 排水OK  ﾀﾝｸ2 排水終了
CON
RES R30109               ; 廃液ﾀﾝｸ2 排水
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
RES R30111               ; 廃液ﾀﾝｸ2 加圧
CON
RES @MR1310              ; 手動 ﾀﾝｸ2 排水ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>手動　タンク２　水戻
;ＭＡＮＵ  Ｔａｎｋ２  Ｗａｔｅｒ  Ｒｅｔｕｒｎ
STG @MR1900              ; 手動 ﾀﾝｸ2 水戻状態確認
AND R50002               ; 手動中
MPS
LD R1104                 ; 新液ﾀﾝｸ2 上限
ANB R1111                ; 廃液ﾀﾝｸ2 下限
ANL
SET R30111               ; 廃液ﾀﾝｸ2 加圧
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
TMR #401 #100
CON
AND T401
JMP @MR1901
MRD
LDB R1104                ; 新液ﾀﾝｸ2 上限
OR R1111                 ; 廃液ﾀﾝｸ2 下限
ANL
TMR #407 #10
CON
AND T407
OUT @MR1903              ; 手動 ﾀﾝｸ2 水戻終了 ﾌﾞｻﾞｰ
CON
JMP @MR1902
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1902
STG @MR1901
AND R50002               ; 手動中
MPS
SET R30105               ; 新液ﾀﾝｸ2 ろ過給水
CON
LDB R1104                ; 新液ﾀﾝｸ2 上限
OR R1111                 ; 廃液ﾀﾝｸ2 下限
ANL
OUT @MR1903              ; 手動 ﾀﾝｸ2 水戻終了 ﾌﾞｻﾞｰ
CON
JMP @MR1902
MPP
LD R50100                ; ｽﾀｰﾄSW ON_PLS
ANB @MR1300              ; 手動運転  ｽﾀｰﾄ PLS
ANL
JMP @MR1902
STG @MR1902
AND R50002               ; 手動中
RES R30111               ; 廃液ﾀﾝｸ2 加圧
CON
RES R30105               ; 新液ﾀﾝｸ2 ろ過給水
CON
RES @MR1311              ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
CON
JMP @MR1302              ; 手動運転 終了 STG
;<h1/>自動運転
;Ａｕｔｏ  Ｏｐｅｒａｔｉｏｎ
LD R50001                ; 自動中
MPS
TMR #408 #2
CON
AND T408
DIFU @MR2005             ; ﾀﾝｸ 確認PLS
MPP
TMR #405 #5
CON
AND T405
DIFU @MR2000             ; 自動ｽﾀｰﾄPLS
;自動運転が設定時間内に再動作されないと新液ﾀﾝｸの加圧をoffさせる
;
LDF R50001               ; 自動中
OR @MR2110               ; 自動運転 再起動待ち
ANB R50006               ; 異常発生中
ANB R55000               ; TRAP
ANB R50002               ; 手動中
ANB R50003               ; 原点復帰中
ANB @MR2111
LD R3100                 ; 新液ﾀﾝｸ1 加圧
OR R3101                 ; 新液ﾀﾝｸ2 加圧
ANL
OUT @MR2110              ; 自動運転 再起動待ち
MPS
AND LR1200               ; 機種1選択
TMR #3 DM6               ; 新液加圧 保持ﾀｲﾏｰ_1
MRD
AND LR1201               ; 機種2選択
TMR #33 DM66             ; 新液加圧 保持ﾀｲﾏｰ_2
MRD
AND LR1202               ; 機種3選択
TMR #63 DM126            ; 新液加圧 保持ﾀｲﾏｰ_3
MRD
AND LR1203               ; 機種4選択
TMR #93 DM186            ; 新液加圧 保持ﾀｲﾏｰ_4
MPP
AND LR1204               ; 機種5選択
TMR #123 DM246           ; 新液加圧 保持ﾀｲﾏｰ_5
LDP R50001               ; 自動中
AND @MR2110              ; 自動運転 再起動待ち
OUT @MR2111
LD LR1200                ; 機種1選択
AND T3                   ; 新液加圧 保持ﾀｲﾏｰ_1
LD LR1201                ; 機種2選択
AND T33                  ; 新液加圧 保持ﾀｲﾏｰ_2
ORL
LD LR1202                ; 機種3選択
AND T63                  ; 新液加圧 保持ﾀｲﾏｰ_3
ORL
LD LR1203                ; 機種4選択
AND T93                  ; 新液加圧 保持ﾀｲﾏｰ_4
ORL
LD LR1204                ; 機種5選択
AND T123                 ; 新液加圧 保持ﾀｲﾏｰ_5
ORL
RES R30112               ; 新液ﾀﾝｸ1 選択
CON
RES R30113               ; 新液ﾀﾝｸ2 選択
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
;使用するタンクを選択する。
;基本的には、タンク１が優先される。
;Ｔｈｅ  ｔａｎｋ  ｔｏ  ｂｅ  ｕｓｅｄ  ｉｓ  ｃｈｏｓｅｎ．
;Ｆｕｎｄａｍｅｎｔａｌｌｙ，  Ｔａｎｋ１  ｉｓ  ｐｒｉｏｒｉｔｙ．
LDP R50001               ; 自動中
LD MR1001                ; ﾀﾝｸ1
AND R1102                ; 新液ﾀﾝｸ1 下限
LD MR1002                ; ﾀﾝｸ2
AND R1105                ; 新液ﾀﾝｸ2 下限
ORL
ANL
RES R30112               ; 新液ﾀﾝｸ1 選択
CON
RES R30113               ; 新液ﾀﾝｸ2 選択
CON
RES MR1003
LD @MR2005               ; ﾀﾝｸ 確認PLS
ANB MR1003
MPS
LDB R1102                ; 新液ﾀﾝｸ1 下限
LD R1102                 ; 新液ﾀﾝｸ1 下限
AND R1105                ; 新液ﾀﾝｸ2 下限
ORL
ANL
SET MR1001               ; ﾀﾝｸ1
CON
RES MR1002               ; ﾀﾝｸ2
MPP
LD R1102                 ; 新液ﾀﾝｸ1 下限
ANB R1105                ; 新液ﾀﾝｸ2 下限
ANL
RES MR1001               ; ﾀﾝｸ1
CON
SET MR1002               ; ﾀﾝｸ2
;<h1/>タンク　水量確認
;Ｔａｎｋ  ｗａｔｅｒ  ｌｅｖｅｌ  ｃｈｅｃｋ
LD R1102                 ; 新液ﾀﾝｸ1 下限
AND R1108                ; 廃液ﾀﾝｸ1 下限
AND R1105                ; 新液ﾀﾝｸ2 下限
AND R1111                ; 廃液ﾀﾝｸ2 下限
OUT @MR2008              ; 水無し FLAG
LD @MR2000               ; 自動ｽﾀｰﾄPLS
MPS
AND @MR2008              ; 水無し FLAG
SET @MR2001              ; ﾀﾝｸ1-2 水無ｴﾗｰ
MPP
ANB @MR2008              ; 水無し FLAG
OUT @MR2009              ; 水量確認
LD @MR2009               ; 水量確認
MPS
AND MR1001               ; ﾀﾝｸ1
MPS
LD R1102                 ; 新液ﾀﾝｸ1 下限
ANB R1108                ; 廃液ﾀﾝｸ1 下限
ANL
MPS
ANB @MR2100              ; ﾀﾝｸ1 水戻中
SET @MR2004              ; ﾀﾝｸ1 選択
CON
SET @MR2101              ; ﾀﾝｸ1 水戻
MPP
AND @MR2100              ; ﾀﾝｸ1 水戻中
SET @MR2004              ; ﾀﾝｸ1 選択
MRD
LD R1105                 ; 新液ﾀﾝｸ2 下限
ANB R1111                ; 廃液ﾀﾝｸ2 下限
ANB @MR2200              ; ﾀﾝｸ2 水戻中
ANL
SET @MR2201              ; ﾀﾝｸ2 水戻
MPP
ANB R1102                ; 新液ﾀﾝｸ1 下限
SET @MR2003              ; 水量 OK
MRD
AND MR1002               ; ﾀﾝｸ2
MPS
LD R1102                 ; 新液ﾀﾝｸ1 下限
ANB R1108                ; 廃液ﾀﾝｸ1 下限
ANB @MR2100              ; ﾀﾝｸ1 水戻中
ANL
SET @MR2101              ; ﾀﾝｸ1 水戻
MPP
ANB R1105                ; 新液ﾀﾝｸ2 下限
SET @MR2003              ; 水量 OK
MPP
SET MR1003
;ＷＡＴＥＲ　ＵＴへスタート信号出力
;ＷＡＴＥＲ   Ｓｔａｒｔ  ｓｉｇｎａｌ  ｏｕｔｐｕｔ  ｔｏ  ＵＴ
LD @MR2003               ; 水量 OK
LD @MR2004               ; ﾀﾝｸ1 選択
AND @MR2105              ; ﾀﾝｸ1 水戻終了
ORL
OUT MR1100               ; Start OK Sig
;ＷＡＴＥＲ　ＵＴからの自動スタート確認の返答
;ＷＡＴＥＲ  Ａｎｓｗｅｒ  ｏｆ  ａｕｔｏ  ｓｔａｒｔ  ｃｈｅｃｋ  ｆｒｏｍ  ＵＴ．
LD R50201                ; WATER UT 自動中
RES @MR2004              ; ﾀﾝｸ1 選択
CON
RES @MR2105              ; ﾀﾝｸ1 水戻終了
CON
RES @MR2003              ; 水量 OK
;<h1/>自動水戻サイクル
;Ａｕｔｏ  Ｗａｔｅｒ  Ｒｅｔｕｒｎ  Ｃｙｃｌｅ
;<h1/>タンク１　水戻
;１．タンク１使用中で、新液タンク１が空になった場合
;２．最初から、新液タンク１に水が無かった場合
;ＴＡＮＫ１  Ｗａｔｅｒ  Ｒｅｔｕｒｎ
;１． Ｗｈｅｎ  ａ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ  ｉｓ  ｅｍｐｔｙ  ａｔ  ｔｈｅ  ｔｉｍｅ  ｏｆ  ｄｕｒｉｎｇ
;     ｔａｎｋ１  ｕｓｅ．
;２． Ｗｈｅｎ  ｔｈｅｒｅ  ｉｓ  ｎｏ  ｗａｔｅｒ  ｉｎ  ｔｈｅ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ  ｆｒｏｍ  ｔｈｅ
;     ｂｉｇｉｎｎｉｎｇ．
LDP MR2006               ; ﾀﾝｸ1 水戻
ORP MR2007               ; ﾀﾝｸ2 水戻
AND @MR2008              ; 水無し FLAG
SET @MR2001              ; ﾀﾝｸ1-2 水無ｴﾗｰ
LDP MR2006               ; ﾀﾝｸ1 水戻
SET @MR2101              ; ﾀﾝｸ1 水戻
STG @MR2101              ; ﾀﾝｸ1 水戻
ANB R50000               ; E.STOP
SET @MR2100              ; ﾀﾝｸ1 水戻中
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
RES R30106               ; 廃液ﾀﾝｸ1 給水
CON
SET R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES R30112               ; 新液ﾀﾝｸ1 選択
CON
SET @MR2300              ; ﾉｽﾞﾙ OFF
CON
TMR #402 #50
CON
AND T402
SET @MR2102              ; ﾀﾝｸ1 水戻 ｻｲｸﾙ確認
CON
JMP @MR2103
STG @MR2103
ANB R50000               ; E.STOP
SET R30104               ; 新液ﾀﾝｸ1 ろ過給水
CON
LDB R1101                ; 新液ﾀﾝｸ1 上限
OR R1108                 ; 廃液ﾀﾝｸ1 下限
ANL
JMP @MR2104
STG @MR2104
ANB R50000               ; E.STOP
RES R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES R30104               ; 新液ﾀﾝｸ1 ろ過給水
CON
TMR @1 #500
CON
AND @T1
MPS
AND @MR2004              ; ﾀﾝｸ1 選択
SET @MR2105              ; ﾀﾝｸ1 水戻終了
CON
RES @MR2102              ; ﾀﾝｸ1 水戻 ｻｲｸﾙ確認
CON
RES @MR2100              ; ﾀﾝｸ1 水戻中
CON
ENDS
MPP
ANB @MR2004              ; ﾀﾝｸ1 選択
RES @MR2102              ; ﾀﾝｸ1 水戻 ｻｲｸﾙ確認
CON
RES @MR2100              ; ﾀﾝｸ1 水戻中
CON
ENDS
;<h1/>タンク２　水戻
;１．タンク２使用中で、新液タンク２が空になった場合
;Ｔａｎｋ２  Ｗａｔｅｒ  Ｒｅｔｕｒｎ
;１． Ｗｈｅｎ  ｔｈｅ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ２  ｉｓ  ｅｍｐｔｙ  ａｔ ｔｈｅ  ｔｉｍｅ  ｏｆ  ｄｕｒｉｎｇ
;     ｔａｎｋ２  ｕｓｅ．
LDP MR2007               ; ﾀﾝｸ2 水戻
SET @MR2201              ; ﾀﾝｸ2 水戻
STG @MR2201              ; ﾀﾝｸ2 水戻
ANB R50000               ; E.STOP
SET @MR2200              ; ﾀﾝｸ2 水戻中
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
RES R30107               ; 廃液ﾀﾝｸ2 給水
CON
SET R30111               ; 廃液ﾀﾝｸ2 加圧
CON
RES R30113               ; 新液ﾀﾝｸ2 選択
CON
SET @MR2300              ; ﾉｽﾞﾙ OFF
CON
TMR #404 #50
CON
AND T404
SET @MR2202              ; ﾀﾝｸ2 水戻 ｻｲｸﾙ確認
CON
JMP @MR2203
STG @MR2203
ANB R50000               ; E.STOP
SET R30105               ; 新液ﾀﾝｸ2 ろ過給水
CON
LDB R1104                ; 新液ﾀﾝｸ2 上限
OR R1111                 ; 廃液ﾀﾝｸ2 下限
ANL
JMP @MR2204
STG @MR2204
ANB R50000               ; E.STOP
RES R30111               ; 廃液ﾀﾝｸ2 加圧
CON
RES R30105               ; 新液ﾀﾝｸ2 ろ過給水
CON
TMR @2 #500
CON
AND @T2
RES @MR2202              ; ﾀﾝｸ2 水戻 ｻｲｸﾙ確認
CON
RES @MR2200              ; ﾀﾝｸ2 水戻中
CON
ENDS
LD @MR2300               ; ﾉｽﾞﾙ OFF
LD R4004                 ; 新液ﾀﾝｸ1 加圧検出
OR R4005                 ; 新液ﾀﾝｸ2 加圧検出
ANL
TMR @0 #10
CON
AND @T0
RES @MR2300              ; ﾉｽﾞﾙ OFF
;<h1/>可変タイマー総括
;Ｍａｎａｇｅｍｅｎｔ  ｏｆ  Ｖａｒｉａｂｌｅ  Ｔｉｍｅｒ
LD @MR1305               ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
OR @MR1311               ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
OR @MR2102               ; ﾀﾝｸ1 水戻 ｻｲｸﾙ確認
OR @MR2202               ; ﾀﾝｸ2 水戻 ｻｲｸﾙ確認
MPS
AND LR1200               ; 機種1選択
TMR #0 DM0               ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_1
MRD
AND LR1201               ; 機種2選択
TMR #30 DM60             ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_2
MRD
AND LR1202               ; 機種3選択
TMR #60 DM120            ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_3
MRD
AND LR1203               ; 機種4選択
TMR #90 DM180            ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_4
MPP
AND LR1204               ; 機種5選択
TMR #120 DM240           ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_5
LD @MR1303               ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
OR @MR1309               ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
MPS
AND LR1200               ; 機種1選択
TMR #1 DM2               ; 給水ｻｲｸﾙ ﾀｲﾏｰ_1
MRD
AND LR1201               ; 機種2選択
TMR #31 DM62             ; 給水ｻｲｸﾙ ﾀｲﾏｰ_2
MRD
AND LR1202               ; 機種3選択
TMR #61 DM122            ; 給水ｻｲｸﾙ ﾀｲﾏｰ_3
MRD
AND LR1203               ; 機種4選択
TMR #91 DM182            ; 給水ｻｲｸﾙ ﾀｲﾏｰ_4
MPP
AND LR1204               ; 機種5選択
TMR #121 DM242           ; 給水ｻｲｸﾙ ﾀｲﾏｰ_5
LD @MR1304               ; 手動 ﾀﾝｸ1 排水ﾓｰﾄﾞ
OR @MR1310               ; 手動 ﾀﾝｸ2 排水ﾓｰﾄﾞ
MPS
AND LR1200               ; 機種1選択
TMR #2 DM4               ; 排水ｻｲｸﾙ ﾀｲﾏｰ_1
MRD
AND LR1201               ; 機種2選択
TMR #32 DM64             ; 排水ｻｲｸﾙ ﾀｲﾏｰ_2
MRD
AND LR1202               ; 機種3選択
TMR #62 DM124            ; 排水ｻｲｸﾙ ﾀｲﾏｰ_3
MRD
AND LR1203               ; 機種4選択
TMR #92 DM184            ; 排水ｻｲｸﾙ ﾀｲﾏｰ_4
MPP
AND LR1204               ; 機種5選択
TMR #122 DM244           ; 排水ｻｲｸﾙ ﾀｲﾏｰ_5
;<h1/>動作終了ブザー
;Ｂｕｚｚｅｒ  ｏｆ  Ｆｉｎｉｓｈ  Ｏｐｅｒａｔｉｏｎ
LD @MR1402               ; 手動 ﾀﾝｸ1 給水終了 ﾌﾞｻﾞｰ
OR @MR1502               ; 手動 ﾀﾝｸ1 排水終了 ﾌﾞｻﾞｰ
OR @MR1603               ; 手動 ﾀﾝｸ1 水戻終了 ﾌﾞｻﾞｰ
OR @MR1702               ; 手動 ﾀﾝｸ2 給水終了 ﾌﾞｻﾞｰ
OR @MR1802               ; 手動 ﾀﾝｸ2 排水終了 ﾌﾞｻﾞｰ
OR @MR1903               ; 手動 ﾀﾝｸ2 水戻終了 ﾌﾞｻﾞｰ
OR MR1000                ; ﾌﾞｻﾞｰON
ANB T201                 ; 動作終了ﾌﾞｻﾞｰONﾀｲﾏｰ
OUT MR1000               ; ﾌﾞｻﾞｰON
TMR #201 #30             ; 動作終了ﾌﾞｻﾞｰONﾀｲﾏｰ
LD @MR1303               ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
OUT MR1008               ; 手動 ﾀﾝｸ1 水戻ﾓｰﾄﾞ
LD @MR1304               ; 手動 ﾀﾝｸ1 排水ﾓｰﾄﾞ
OUT MR1009               ; 手動 ﾀﾝｸ1 排水ﾓｰﾄﾞ
LD @MR2100               ; ﾀﾝｸ1 水戻中
OUT MR1010               ; ﾀﾝｸ1 水戻中
LD @MR1309               ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
OUT MR1011               ; 手動 ﾀﾝｸ2 水戻ﾓｰﾄﾞ
LD @MR1310               ; 手動 ﾀﾝｸ2 排水ﾓｰﾄﾞ
OUT MR1012               ; 手動 ﾀﾝｸ2 排水ﾓｰﾄﾞ
LD @MR2200               ; ﾀﾝｸ2 水戻中
OUT MR1013               ; ﾀﾝｸ2 水戻中
LD @MR2300               ; ﾉｽﾞﾙ OFF
OUT MR1014               ; ﾉｽﾞﾙ OFF
;<h1/>エラー０
;元圧エラー
;Ｅｒｒｏｒ０
;Ｏｒｉｇｉｎａｌ  Ｐｒｅｓｓｕｒｅ  Ｆａｉｌｕｒｅ
LDB R1003                ; Air sw
ANB R50006               ; 異常発生中
SET @MR3000              ; 元圧ｴﾗｰ STG SET
STG @MR3000              ; 元圧ｴﾗｰ STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR100                ; 元圧ｴﾗｰ中
CON
JMP @MR3001
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #300 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #500 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #700 DM500
STG @MR3001
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3003
STG @MR3003
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR100                ; 元圧ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;エラー１
;真空ポンプ　トリップ
;Ｅｒｒｏｒ１
;Ｖａｃｕｕｍ  Ｐｕｍｐ  Ｔｈｅｒｍａｌ Ｔｒｉｐ
LD R000                  ; 真空ﾎﾟﾝﾌﾟ Trip
ANB R50006               ; 異常発生中
AND CR2003               ; 常時OFF
SET @MR3008              ; 真空ﾎﾟﾝﾌﾟ TRIP STG SET
STG @MR3008              ; 真空ﾎﾟﾝﾌﾟ TRIP STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR101                ; 真空ﾎﾟﾝﾌﾟ Tripｴﾗｰ中
CON
JMP @MR3009
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #301 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #501 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #701 DM500
STG @MR3009
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3010
STG @MR3010
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR101                ; 真空ﾎﾟﾝﾌﾟ Tripｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラー２
;新液タンク１　給水サイクルタイムエラー
;Ｅｒｒｏｒ２
;Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ１  Ｗａｔｅｒ  Ｓｕｐｐｌｙ  Ｃｙｃｌｅ  Ｔｉｍｅ  Ｅｒｒｏｒ
LD LR1200                ; 機種1選択
AND T1                   ; 給水ｻｲｸﾙ ﾀｲﾏｰ_1
LD LR1201                ; 機種2選択
AND T31                  ; 給水ｻｲｸﾙ ﾀｲﾏｰ_2
ORL
LD LR1202                ; 機種3選択
AND T61                  ; 給水ｻｲｸﾙ ﾀｲﾏｰ_3
ORL
LD LR1203                ; 機種4選択
AND T91                  ; 給水ｻｲｸﾙ ﾀｲﾏｰ_4
ORL
LD LR1204                ; 機種5選択
AND T121                 ; 給水ｻｲｸﾙ ﾀｲﾏｰ_5
ORL
ANB R50006               ; 異常発生中
SET @MR3100              ; ﾀﾝｸ1 給水ｻｲｸﾙｴﾗｰ STG SET
STG @MR3100              ; ﾀﾝｸ1 給水ｻｲｸﾙｴﾗｰ STG SET
RES R30102               ; 新液ﾀﾝｸ1 新液給水
CON
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR102                ; ﾀﾝｸ1 給水ｻｲｸﾙｴﾗｰ中
CON
JMP @MR3101
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #302 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #502 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #702 DM500
STG @MR3101
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3102
STG @MR3102
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR102                ; ﾀﾝｸ1 給水ｻｲｸﾙｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
LD MR102                 ; ﾀﾝｸ1 給水ｻｲｸﾙｴﾗｰ中
MPS
AND LR000                ; ﾀﾝｸ1 給水 LAMP
OUT R50300               ; ﾀﾝｸ1 給水ｴﾗｰ
MPP
AND LR004                ; ﾀﾝｸ2 給水 LAMP
OUT R50303               ; ﾀﾝｸ2 給水ｴﾗｰ
;<h1/>エラー３
;タンク１　排水サイクルタイムエラー
;Ｅｒｒｏｒ３
;Ｔａｎｋ１  Ｄｒａｉｎ  Ｃｙｃｌｅ  Ｔｉｍｅ  Ｅｒｒｏｒ
LD LR1200                ; 機種1選択
AND T2                   ; 排水ｻｲｸﾙ ﾀｲﾏｰ_1
LD LR1201                ; 機種2選択
AND T32                  ; 排水ｻｲｸﾙ ﾀｲﾏｰ_2
ORL
LD LR1202                ; 機種3選択
AND T62                  ; 排水ｻｲｸﾙ ﾀｲﾏｰ_3
ORL
LD LR1203                ; 機種4選択
AND T92                  ; 排水ｻｲｸﾙ ﾀｲﾏｰ_4
ORL
LD LR1204                ; 機種5選択
AND T122                 ; 排水ｻｲｸﾙ ﾀｲﾏｰ_5
ORL
ANB R50006               ; 異常発生中
SET @MR3108              ; ﾀﾝｸ1 排水ｻｲｸﾙｴﾗｰ STG SET
STG @MR3108              ; ﾀﾝｸ1 排水ｻｲｸﾙｴﾗｰ STG SET
RES R30108               ; 廃液ﾀﾝｸ1 排水
CON
RES R30100               ; 新液ﾀﾝｸ1 加圧
CON
RES R30101               ; 新液ﾀﾝｸ2 加圧
CON
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR103                ; ﾀﾝｸ1 排水ｻｲｸﾙｴﾗｰ中
CON
JMP @MR3109
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #303 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #503 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #703 DM500
STG @MR3109
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3110
STG @MR3110
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR103                ; ﾀﾝｸ1 排水ｻｲｸﾙｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
LD MR103                 ; ﾀﾝｸ1 排水ｻｲｸﾙｴﾗｰ中
MPS
AND LR001                ; ﾀﾝｸ1 排水 LAMP
OUT R50301               ; ﾀﾝｸ1 排水ｴﾗｰ
MPP
AND LR005                ; ﾀﾝｸ2 排水 LAMP
OUT R50304               ; ﾀﾝｸ2 排水ｴﾗｰ
;<h1/>エラー４
;タンク１　水戻サイクルタイムエラー
;Ｅｒｒｏｒ４
;Ｔａｎｋ１  Ｗａｔｅｒ  Ｒｅｔｕｒｎ  Ｃｙｃｌｅ  Ｔｉｍｅ  Ｅｒｒｏｒ
LD LR1200                ; 機種1選択
AND T0                   ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_1
LD LR1201                ; 機種2選択
AND T30                  ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_2
ORL
LD LR1202                ; 機種3選択
AND T60                  ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_3
ORL
LD LR1203                ; 機種4選択
AND T90                  ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_4
ORL
LD LR1204                ; 機種5選択
AND T120                 ; 水戻ｻｲｸﾙ ﾀｲﾏｰ_5
ORL
ANB R50006               ; 異常発生中
SET @MR3200              ; ﾀﾝｸ1 水戻ｻｲｸﾙｴﾗｰ STG SET
STG @MR3200              ; ﾀﾝｸ1 水戻ｻｲｸﾙｴﾗｰ STG SET
RES R30110               ; 廃液ﾀﾝｸ1 加圧
CON
RES R30104               ; 新液ﾀﾝｸ1 ろ過給水
CON
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR104                ; ﾀﾝｸ1 水戻ｻｲｸﾙｴﾗｰ中
CON
JMP @MR3201
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #304 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #504 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #704 DM500
STG @MR3201
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3202
STG @MR3202
AND R50015               ; ｴﾗｰﾘｾｯﾄON
RES R50109               ; Err TANK
CON
RES MR104                ; ﾀﾝｸ1 水戻ｻｲｸﾙｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
LD MR104                 ; ﾀﾝｸ1 水戻ｻｲｸﾙｴﾗｰ中
MPS
LD LR002                 ; ﾀﾝｸ1 水戻 LAMP
OR @MR2102               ; ﾀﾝｸ1 水戻 ｻｲｸﾙ確認
ANL
OUT R50302               ; ﾀﾝｸ1 水戻ｴﾗｰ
MPP
LD LR006                 ; ﾀﾝｸ2 水戻 LAMP
OR @MR2202               ; ﾀﾝｸ2 水戻 ｻｲｸﾙ確認
ANL
OUT R50305               ; ﾀﾝｸ2 水戻ｴﾗｰ
;<h1/>エラー５
;タンク１・２　水無エラー
;Ｅｒｒｏｒ５
;Ｔａｎｋ  １＆２　ｎｏ  Ｗａｔｅｒ  Ｅｒｒｏｒ
STG @MR2001              ; ﾀﾝｸ1-2 水無ｴﾗｰ
RES @MR2000              ; 自動ｽﾀｰﾄPLS
CON
ANB R50006               ; 異常発生中
MPS
LD R1102                 ; 新液ﾀﾝｸ1 下限
AND R1108                ; 廃液ﾀﾝｸ1 下限
LD R1105                 ; 新液ﾀﾝｸ2 下限
AND R1111                ; 廃液ﾀﾝｸ2 下限
ORL
ANL
SET @MR2010              ; ﾀﾝｸ1-2 空
CON
AND @MR2010              ; ﾀﾝｸ1-2 空
JMP @MR3208              ; ﾀﾝｸ1-2 水無ｴﾗｰ確認 STG SET
MRD
LDB R1105                ; 新液ﾀﾝｸ2 下限
ORB R1111                ; 廃液ﾀﾝｸ2 下限
AND R1102                ; 新液ﾀﾝｸ1 下限
AND R1108                ; 廃液ﾀﾝｸ1 下限
ANL
SET @MR2011              ; ﾀﾝｸ1 空
CON
AND @MR2011              ; ﾀﾝｸ1 空
JMP @MR3208              ; ﾀﾝｸ1-2 水無ｴﾗｰ確認 STG SET
MPP
LDB R1102                ; 新液ﾀﾝｸ1 下限
ORB R1108                ; 廃液ﾀﾝｸ1 下限
AND R1105                ; 新液ﾀﾝｸ2 下限
AND R1111                ; 廃液ﾀﾝｸ2 下限
ANL
SET @MR2012              ; ﾀﾝｸ2 空
CON
AND @MR2012              ; ﾀﾝｸ2 空
JMP @MR3208              ; ﾀﾝｸ1-2 水無ｴﾗｰ確認 STG SET
STG @MR3208              ; ﾀﾝｸ1-2 水無ｴﾗｰ確認 STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
SET R50109               ; Err TANK
CON
SET MR105                ; ﾀﾝｸ1-2 水無ｴﾗｰ中
CON
JMP @MR3209
MPS
AND @MR2010              ; ﾀﾝｸ1-2 空
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #305 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #505 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #705 DM500
MRD
AND @MR2011              ; ﾀﾝｸ1 空
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #311 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #511 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #711 DM500
MPP
AND @MR2012              ; ﾀﾝｸ2 空
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #312 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #512 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #712 DM500
STG @MR3209
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3210
STG @MR3210
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES @MR2010              ; ﾀﾝｸ1-2 空
CON
RES @MR2011              ; ﾀﾝｸ1 空
CON
RES @MR2012              ; ﾀﾝｸ2 空
CON
RES R50109               ; Err TANK
CON
RES MR105                ; ﾀﾝｸ1-2 水無ｴﾗｰ中
CON
RES R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラー６
;新液タンク１　満杯エラー
;新液タンク１の上限センサーがＯＮしなくて、オーバーフローが作動した
;Ｅｒｒｏｒ６
;Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ１  Ｆｕｌｌ
;Ｔｈｅ  ｕｐｐｅｒ  ｌｉｍｉｔ  ｓｅｎｓｏｒ  ｏｆ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ１  ｄｉｄ  ｎｏｔ  ｔｕｒｎ  ｏｎ  ａｎｄ
;ｏｖｅｒ  ｆｌｏｗ  ｓｅｎｓｏｒ  ｔｕｒｎｓ  ｏｎ．
LD R50001                ; 自動中
ANB R1100                ; 新液ﾀﾝｸ1 ｵｰﾊﾞｰﾌﾛｰ
AND T200                 ; 起動遅延
ANB R50006               ; 異常発生中
SET @MR3300              ; 新液ﾀﾝｸ1 満杯
STG @MR3300              ; 新液ﾀﾝｸ1 満杯
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR106                ; 新液ﾀﾝｸ1 満杯ｴﾗｰ中
CON
JMP @MR3301              ; 新液ﾀﾝｸ1 満杯 STG
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #306 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #506 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #706 DM500
STG @MR3301              ; 新液ﾀﾝｸ1 満杯 STG
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3302
STG @MR3302
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR106                ; 新液ﾀﾝｸ1 満杯ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラー７
;新液タンク２　満杯エラー
;新液タンク２の上限センサーがＯＮしなくて、オーバーフローが作動した
;Ｅｒｒｏｒ７
;Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ  Ｆｕｌｌ
;Ｔｈｅ  ｕｐｐｅｒ  ｌｉｍｉｔ  ｓｅｎｓｏｒ  ｏｆ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ２  ｄｉｄ  ｎｏｔ  ｔｕｒｎ   ｏｎ
;ａｎｄ  ｏｖｅｒ  ｆｌｏｗ  ｓｅｎｓｏｒ  ｔｕｒｎｓ  ｏｎ．
LD R50001                ; 自動中
ANB R1103                ; 新液ﾀﾝｸ2 ｵｰﾊﾞｰﾌﾛｰ
AND T200                 ; 起動遅延
ANB R50006               ; 異常発生中
SET @MR3308              ; 新液ﾀﾝｸ2 満杯
STG @MR3308              ; 新液ﾀﾝｸ2 満杯
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR107                ; 新液ﾀﾝｸ2 満杯ｴﾗｰ中
CON
JMP @MR3309              ; 新液ﾀﾝｸ2 満杯 STG
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #307 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #507 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #707 DM500
STG @MR3309              ; 新液ﾀﾝｸ2 満杯 STG
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3310
STG @MR3310
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR107                ; 新液ﾀﾝｸ2 満杯ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラー８
;廃液タンク１　満杯エラー
;廃液タンク１の上限センサーがＯＮしなくて、オーバーフローが作動した
;Ｅｒｒｏｒ８
;Ｗａｓｔｅ  Ｔａｎｋ１  Ｆｕｌｌ
;Ｔｈｅ  ｕｐｐｅｒ  ｌｉｍｉｔ  ｓｅｎｓｏｒ  ｏｆ  ｗａｓｔｅ  ｔａｎｋ１  ｄｉｄ  ｎｏｔ  ｔｕｒｎ  ｏｎ
;ａｎｄ  ｏｖｅｒ  ｆｌｏｗ  tｕｒｎｓ  ｏｎ．
LD R50001                ; 自動中
ANB R1106                ; 廃液ﾀﾝｸ1 ｵｰﾊﾞｰﾌﾛｰ
AND T200                 ; 起動遅延
ANB R50006               ; 異常発生中
SET @MR3400              ; 廃液ﾀﾝｸ1 満杯
STG @MR3400              ; 廃液ﾀﾝｸ1 満杯
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR108                ; 廃液ﾀﾝｸ1 満杯ｴﾗｰ中
CON
JMP @MR3401              ; 廃液ﾀﾝｸ1 満杯 STG
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #308 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #508 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #708 DM500
STG @MR3401              ; 廃液ﾀﾝｸ1 満杯 STG
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3402
STG @MR3402
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR108                ; 廃液ﾀﾝｸ1 満杯ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラー９
;新液タンク２　満杯エラー
;新液タンク２の上限センサーがＯＮしなくて、オーバーフローが作動した
;Ｅｒｒｏｒ９
;Ｎｅｗ  Ｌｉｑｕｉｄ  Ｔａｎｋ２  Ｆｕｌｌ
;Ｔｈｅ  ｕｐｐｅｒ  ｌｉｍｉｔ  ｓｅｎｓｏｒ  ｏｆ  ｎｅｗ  ｌｉｑｕｉｄ  ｔａｎｋ２  ｄｉｄ   ｎｏｔ  ｔｕｒｎ  ｏｎ
;ａｎｄ  ｏｖｅｒ  ｆｌｏｗ  ｔｕｒｎｓ  ｏｎ．
;
LD R50001                ; 自動中
ANB R1109                ; 廃液ﾀﾝｸ2 ｵｰﾊﾞｰﾌﾛｰ
AND T200                 ; 起動遅延
ANB R50006               ; 異常発生中
SET @MR3408              ; 廃液ﾀﾝｸ2 満杯
STG @MR3408              ; 廃液ﾀﾝｸ2 満杯
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR109                ; 廃液ﾀﾝｸ2 満杯ｴﾗｰ中
CON
JMP @MR3409              ; 廃液ﾀﾝｸ2 満杯 STG
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #309 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #509 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #709 DM500
STG @MR3409              ; 廃液ﾀﾝｸ2 満杯 STG
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3410
STG @MR3410
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR109                ; 廃液ﾀﾝｸ2 満杯ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラーＡ
;給水不可能
;タンク内に液が残っているのに、給水しようとしました。
;排水を行ってから給水して下さい。
;ＥｒｒｏｒＡ
;Ｗａｔｅｒ  Ｓｕｐｐｌｙ  ｉｓ  ｉｍｐｏｓｓｉｂｌｅ
;Ｉｔ  ｗａｓ  ｇｏｉｎｇ  ｔｏ  ｓｕｐｐｌｙ  ｗａｔｅｒ  ｔｈａｔ  ｗａｔｅｒ ｉｓ  ｒｅｍａｉｎ  ｉｎ  ｔｈｅ  ｔａｎｋ． 
;Ｐｌａｅｓｅ  ｗａｔｅｒ  ｓｕｐｐｌｙ  ａｆｔｅｒ  ｄｒａｉｎ．
;
;
STG @MR1405              ; ﾀﾝｸ1 給水不可
ANB R50006               ; 異常発生中
JMP @MR3500              ; ﾀﾝｸ 給水不可
STG @MR1705              ; ﾀﾝｸ2 給水不可
ANB R50006               ; 異常発生中
JMP @MR3500              ; ﾀﾝｸ 給水不可
STG @MR3500              ; ﾀﾝｸ 給水不可
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR110                ; ﾀﾝｸ 給水不可ｴﾗｰ中
CON
JMP @MR3501
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #310 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #510 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #710 DM500
STG @MR3501
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3502
STG @MR3502
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR110                ; ﾀﾝｸ 給水不可ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
;<h1/>エラーＢ
;タンク１　水無エラー
;Ｅｒｒｏｒ　Ｂ
;Ｔａｎｋ  １　ｎｏ  Ｗａｔｅｒ  Ｅｒｒｏｒ
;
STG @MR2006              ; ﾀﾝｸ1 水無
ANB R50006               ; 異常発生中
JMP @MR3508              ; ﾀﾝｸ1 水無STG SET
STG @MR3508              ; ﾀﾝｸ1 水無STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
SET R50109               ; Err TANK
CON
SET MR111                ; ﾀﾝｸ1 水無ｴﾗｰ中
CON
JMP @MR3509
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #311 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #511 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #711 DM500
STG @MR3509
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3510
STG @MR3510
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR111                ; ﾀﾝｸ1 水無ｴﾗｰ中
CON
RES R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
ENDS
;<h1/>エラーＣ
;タンク２　水無エラー
;Ｅｒｒｏｒ　Ｃ
;Ｔａｎｋ  ２　ｎｏ  Ｗａｔｅｒ  Ｅｒｒｏｒ
;
STG @MR2007              ; ﾀﾝｸ2 水無
ANB R50006               ; 異常発生中
JMP @MR3600              ; ﾀﾝｸ2 水無STG SET
STG @MR3600              ; ﾀﾝｸ2 水無STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
SET R50109               ; Err TANK
CON
SET MR112                ; ﾀﾝｸ2 水無ｴﾗｰ中
CON
JMP @MR3601
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #312 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #512 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #712 DM500
STG @MR3601
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR3602
STG @MR3602
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR112                ; ﾀﾝｸ2 水無ｴﾗｰ中
CON
RES R55005               ; ﾀﾝｸ 水無ｴﾗｰ
CON
ENDS
;<h1/>エラーＦ
;タンク　漏液発生
;ＥｒｒｏｒＦ
;Ｔａｎｋ  Ｌｅａｋ  Ｌｉｑｕｉｄ
LDB R1113                ; ﾀﾝｸ 漏液検出
ANB R50006               ; 異常発生中
SET @MR7800              ; ﾀﾝｸ 漏液 STG SET
STG @MR7800              ; ﾀﾝｸ 漏液 STG SET
RES MR2000               ; ｴﾘｱｾﾝｻ 　監視中
CON
SET R50108               ; 停止表示
CON
SET R50006               ; 異常発生中
CON
SET R50109               ; Err TANK
CON
SET MR115                ; ﾀﾝｸ 漏液ｴﾗｰ中
CON
JMP @MR7801
MPS
AND LR300                ; 英語表示 選択_ﾊﾟﾈﾙ
MOV #315 DM500
MRD
AND LR301                ; 日本語表示選択_ﾊﾟﾈﾙ
MOV #515 DM500
MPP
AND LR302                ; 中国語表示選択_ﾊﾟﾈﾙ
MOV #715 DM500
STG @MR7801
AND R50014               ; ｴﾗｰﾘｾｯﾄSW ON
JMP @MR7802
STG @MR7802
AND R50100               ; ｽﾀｰﾄSW ON_PLS
RES R50109               ; Err TANK
CON
RES MR115                ; ﾀﾝｸ 漏液ｴﾗｰ中
CON
SET R55000               ; TRAP
CON
ENDS
END
ENDH
