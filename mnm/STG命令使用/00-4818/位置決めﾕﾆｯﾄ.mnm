DEVICE:53
;MODULE:位置決めﾕﾆｯﾄ
;MODULE_TYPE:0
;**** 位置決めﾕﾆｯﾄ　入力処理　/ 位置決めﾕﾆｯﾄ　入力処理****
LD R4002                 ; ｱﾗｲﾒﾝﾄX軸前進端
TMR @1 #2                ; ｱﾗｲﾒﾝﾄX軸前進端
LD R4003                 ; ｱﾗｲﾒﾝﾄX軸後退端
TMR @2 #2                ; ｱﾗｲﾒﾝﾄX軸後退端
LD R4004                 ; ｱﾗｲﾒﾝﾄY軸前進端
TMR @3 #2                ; ｱﾗｲﾒﾝﾄY軸前進端
LD R4005                 ; ｱﾗｲﾒﾝﾄY軸後退端
TMR @4 #2                ; ｱﾗｲﾒﾝﾄY軸後退端
LD R4006                 ; ｼﾞｬﾏ板前進端
TMR @5 #2                ; ｼﾞｬﾏ板前進端
LD R4007                 ; ｼﾞｬﾏ板後退端
TMR @6 #2                ; ｼﾞｬﾏ板後退端
;**** 位置決めﾕﾆｯﾄ　ﾊﾟﾈﾙﾗﾝﾌﾟ　/ 位置決めﾕﾆｯﾄ　ﾊﾟﾈﾙ 　LAMP****
LD R4002                 ; ｱﾗｲﾒﾝﾄX軸前進端
OUT R81400               ; ｱﾗｲﾒﾝﾄX軸前進PL
LD R4003                 ; ｱﾗｲﾒﾝﾄX軸後退端
OUT R81401               ; ｱﾗｲﾒﾝﾄX軸後退端PL
LD R4004                 ; ｱﾗｲﾒﾝﾄY軸前進端
OUT R81402               ; ｱﾗｲﾒﾝﾄY軸前進端PL
LD R4005                 ; ｱﾗｲﾒﾝﾄY軸後退端
OUT R81403               ; ｱﾗｲﾒﾝﾄY軸後退端PL
LD R4006                 ; ｼﾞｬﾏ板前進端
OUT R81404               ; ｼﾞｬﾏ板前進端PL
LD R4007                 ; ｼﾞｬﾏ板後退端
OUT R81405               ; ｼﾞｬﾏ板後退端PL
LD MR6208                ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
OUT R81408               ; 位置決め部ｴｱｰﾌﾞﾛｰON　PL
LDB MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
OUT R81409               ; 位置決め部ｴｱｰﾌﾞﾛｰOFF　PL
;****位置決めﾕﾆｯﾄ停止条件 / 位置決めﾕﾆｯﾄ停止条件  ****
LD @MR304                ; 位置決め徐塵供給要求
ANB MR21001              ; 給材P&P位置決め位置供給中
LD @MR313
ANB MR23002              ; 転写SRB1徐塵部進入中
ORL
LDB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
ANB MR22015              ; 位置決め徐塵起動中
ORL
OUT MR41103              ; 位置決めﾕﾆｯﾄ停止可
;**** 位置決めﾕﾆｯﾄ　手動操作　/ 位置決めﾕﾆｯﾄ　MANUAL Operation ****
LD MR40300               ; 安全扉閉確認
MC
LD CR2002                ; 常時ON
OUT @MR100               ; ｱﾗｲﾒﾝﾄX軸前進可
LD R61400                ; ｱﾗｲﾒﾝﾄX軸前進SW
AND @MR100               ; ｱﾗｲﾒﾝﾄX軸前進可
SET MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
LD CR2002                ; 常時ON
OUT @MR101               ; ｱﾗｲﾒﾝﾄX軸後退可
LD R61401                ; ｱﾗｲﾒﾝﾄX軸後退SW
AND @MR101               ; ｱﾗｲﾒﾝﾄX軸後退可
RES MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
LD CR2002                ; 常時ON
ANB R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
OUT @MR102               ; ｱﾗｲﾒﾝﾄY軸前進可
LD R61402                ; ｱﾗｲﾒﾝﾄY軸前進SW
AND @MR102               ; ｱﾗｲﾒﾝﾄY軸前進可
SET MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
LD CR2002                ; 常時ON
OUT @MR103               ; ｱﾗｲﾒﾝﾄY軸後退可
LD R61403                ; ｱﾗｲﾒﾝﾄY軸後退SW
AND @MR103               ; ｱﾗｲﾒﾝﾄY軸後退可
RES MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
LD CR2002                ; 常時ON
OUT @MR104               ; ｼﾞｬﾏ板前進可
LD R61404                ; ｼﾞｬﾏ板前進SW
AND @MR104               ; ｼﾞｬﾏ板前進可
SET MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
LD CR2002                ; 常時ON
OUT @MR105               ; ｼﾞｬﾏ板後退可
LD R61405                ; ｼﾞｬﾏ板後退SW
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
LD CR2002                ; 常時ON
OUT @MR106               ; ｴｱｰﾌﾞﾛｰON可
LD R61408                ; ｴｱｰﾌﾞﾛｰON　SW
AND @MR106               ; ｴｱｰﾌﾞﾛｰON可
SET MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
LD MR6208                ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
MPS
TMR @10 DM0              ; ｴｱﾌﾞﾛｰ時間  ｴｱｰﾌﾞﾛｰ時間設定
MPP
AND @T10                 ; ｴｱﾌﾞﾛｰ時間
RES MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
MCR
;**** 位置決めﾕﾆｯﾄ　原点復帰　/ 位置決めﾕﾆｯﾄ　原点復帰 ****
LD CR2002                ; 常時ON
AND MR40300              ; 安全扉閉確認
OUT @MR200               ; 位置決めﾕﾆｯﾄ原点復帰可
OUT MR22009              ; 位置決めﾕﾆｯﾄ原点復帰可
LD MR40101               ; 各ﾕﾆｯﾄ原点復帰開始
ORP R61415               ; 手動位置決めﾕﾆｯﾄ原点復帰SW
AND @MR200               ; 位置決めﾕﾆｯﾄ原点復帰可
ANB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
OR MR22014               ; 位置決めﾕﾆｯﾄ原点復帰開始
ANB MR22011              ; 位置決め徐塵全異常
ANB @MR215               ; 位置決めﾕﾆｯﾄ原点復帰完了
OUT MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
OUT R81415               ; 位置決めﾕﾆｯﾄ原点復帰PL
TMH @20 #5               ; 位置決めﾕﾆｯﾄ原点復帰開始ﾃﾞｨﾚｰ
LDP @T20                 ; 位置決めﾕﾆｯﾄ原点復帰開始ﾃﾞｨﾚｰ
SET @MR201
STG @MR201
ANB MR40101              ; 各ﾕﾆｯﾄ原点復帰開始
JMP @MR202               ; ｴｱｰﾌﾞﾛOFF
STG @MR202               ; ｴｱｰﾌﾞﾛOFF
RES MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
CON
AND R4001                ; P&P給材ﾍｯﾄﾞ上昇端
JMP @MR203               ; ｼﾞｬﾏ板後退
STG @MR203               ; ｼﾞｬﾏ板後退
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T6                  ; ｼﾞｬﾏ板後退端
JMP @MR204               ; 位置決めｱﾗｲﾒﾝﾄX前進
STG @MR204               ; 位置決めｱﾗｲﾒﾝﾄX前進
SET MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
TMR @11 #8               ; 位置決めｱﾗｲﾒﾝﾄX前進待ち
CON
AND @T11                 ; 位置決めｱﾗｲﾒﾝﾄX前進待ち
MPS
AND @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
JMP @MR205               ; 位置決めｱﾗｲﾒﾝﾄY前進
MPP
ANB @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
JMP @MR207               ; 位置決めｱﾗｲﾒﾝﾄXY後退
STG @MR205               ; 位置決めｱﾗｲﾒﾝﾄY前進
SET MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
TMR @12 #7               ; 位置決めｱﾗｲﾒﾝﾄY前進待ち
CON
AND @T12                 ; 位置決めｱﾗｲﾒﾝﾄY前進待ち
JMP @MR206               ; 位置決めｱﾗｲﾒﾝﾄXY前進端確認
STG @MR206               ; 位置決めｱﾗｲﾒﾝﾄXY前進端確認
MPS
LD R4002                 ; ｱﾗｲﾒﾝﾄX軸前進端
AND R4004                ; ｱﾗｲﾒﾝﾄY軸前進端
ANL
JMP @MR215               ; 位置決めﾕﾆｯﾄ原点復帰完了
MPP
LDB R4002                ; ｱﾗｲﾒﾝﾄX軸前進端
ANB R4004                ; ｱﾗｲﾒﾝﾄY軸前進端
ANL
JMP @MR207               ; 位置決めｱﾗｲﾒﾝﾄXY後退
STG @MR207               ; 位置決めｱﾗｲﾒﾝﾄXY後退
RES MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
RES MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
AND R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
AND R4005                ; ｱﾗｲﾒﾝﾄY軸後退端
JMP @MR215               ; 位置決めﾕﾆｯﾄ原点復帰完了
STG @MR215               ; 位置決めﾕﾆｯﾄ原点復帰完了
LD MR22300               ; 位置決めﾕﾆｯﾄ原点復帰完了
ORB MR40014              ; 全原点復帰中
ANL
ANB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
ENDS
LD @MR215                ; 位置決めﾕﾆｯﾄ原点復帰完了
OR MR22300               ; 位置決めﾕﾆｯﾄ原点復帰完了
ANB MR40102              ; 各原点復帰完了FLGOFF
AND MR40014              ; 全原点復帰中
OUT MR22300              ; 位置決めﾕﾆｯﾄ原点復帰完了
LDB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
ZRES @MR201 @MR215       ; 位置決めﾕﾆｯﾄ原点復帰完了
;**** 位置決め徐塵　自動運転 / 位置決め徐塵　自動運転 ****
LD CR2002                ; 常時ON
LDP R4802                ; ｽｷｰｼﾞ確認SW
OR T95                   ; ｽｷｰｼﾞ確認SWﾃﾞｨﾚｰ
ANL
AND MR40610              ; STEP前半選択
ANB MR40112              ; 自動ﾓｰﾄﾞ
OR MR40112               ; 自動ﾓｰﾄﾞ
OUT MR22005              ; STEPﾊﾟﾙｽ
LD R4002                 ; ｱﾗｲﾒﾝﾄX軸前進端
AND R4004                ; ｱﾗｲﾒﾝﾄY軸前進端
LD R4003                 ; ｱﾗｲﾒﾝﾄX軸後退端
AND R4005                ; ｱﾗｲﾒﾝﾄY軸後退端
ORL
AND R4007                ; ｼﾞｬﾏ板後退端
OUT MR22010              ; 位置決め徐塵全原点
LDB MR22901              ; 位置決め除塵ｼﾘﾝﾀﾞｰ異常
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
ANB CR2003               ; 常時OFF
INV
OUT MR22011              ; 位置決め徐塵全異常
LD MR40015               ; 主幹自動起動中
OR MR40610               ; STEP前半選択
AND MR22010              ; 位置決め徐塵全原点
ANB MR22011              ; 位置決め徐塵全異常
ANB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
ANB MR22015              ; 位置決め徐塵起動中
OUT MR22012              ; 位置決め徐塵起動条件
LD MR22011               ; 位置決め徐塵全異常
OUT MR22013              ; 位置決め徐塵起動OFF
LD MR22005               ; STEPﾊﾟﾙｽ
AND MR22012              ; 位置決め徐塵起動条件
OR MR22015               ; 位置決め徐塵起動中
ANB MR22013              ; 位置決め徐塵起動OFF
LD MR40112               ; 自動ﾓｰﾄﾞ
OR MR40610               ; STEP前半選択
ANL
ANB @MR315               ; 位置決めPRG　END
OUT MR22015              ; 位置決め徐塵起動中
TMH @21 #10              ; 給材P&P自動起動ﾃﾞｨﾚｰ
LDP @T21                 ; 給材P&P自動起動ﾃﾞｨﾚｰ
SET @MR301
STG @MR301
MPS
AND MR22500              ; 位置決め1回目確認FLG
JMP @MR302               ; ｱﾗｲﾒﾝﾄ状態確認
MPP
ANB MR22500              ; 位置決め1回目確認FLG
JMP @MR400               ; 1回目確認動作開始
STG @MR302               ; ｱﾗｲﾒﾝﾄ状態確認
MPS
LD @T1                   ; ｱﾗｲﾒﾝﾄX軸前進端
AND @T3                  ; ｱﾗｲﾒﾝﾄY軸前進端
ANL
JMP @MR310               ; ｱﾗｲﾒﾝﾄXY前進
MPP
LD @T2                   ; ｱﾗｲﾒﾝﾄX軸後退端
AND @T4                  ; ｱﾗｲﾒﾝﾄY軸後退端
ANL
JMP @MR303               ; 位置決めｼﾞｬﾏ板後退
STG @MR303               ; 位置決めｼﾞｬﾏ板後退
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T6                  ; ｼﾞｬﾏ板後退端
JMP @MR304               ; 位置決め徐塵供給要求
STG @MR304               ; 位置決め徐塵供給要求
OUT MR22000              ; 位置決め徐塵供給要求
MPS
AND MR21000              ; 位置決めｼﾞｬﾏ板ON要求
SET MR22200              ; 位置決め動作中
CON
SET MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T5                  ; ｼﾞｬﾏ板前進端
JMP @MR305
MPP
LDB MR40015              ; 主幹自動起動中
ANB MR21015              ; 給材P&P起動中
ANB MR40610              ; STEP前半選択
ANL
TMR @25 #3               ; 主幹起動OFF確認
CON
AND @T25                 ; 主幹起動OFF確認
JMP @MR315               ; 位置決めPRG　END
STG @MR305
ANB MR21001              ; 給材P&P位置決め位置供給中
AND R4001                ; P&P給材ﾍｯﾄﾞ上昇端
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR306
STG @MR306
SET MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
TMR @22 #7               ; 位置決めｱﾗｲﾒﾝﾄX前進端確認ﾃﾞｨﾚｰ時
CON
AND @T22                 ; 位置決めｱﾗｲﾒﾝﾄX前進端確認ﾃﾞｨﾚｰ時
ANB R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
MPS
LD @T1                   ; ｱﾗｲﾒﾝﾄX軸前進端
AND MR22005              ; STEPﾊﾟﾙｽ
ANL
JMP @MR307
MPP
LDB @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
AND MR22005              ; STEPﾊﾟﾙｽ
ANL
JMP @MR309
STG @MR307
OUT MR40904              ; 払出しPZT再供給済
MPS
@INC DM5525              ; PZT　INPUT数
MPP
ANB MR40903              ; 払出しPZT再供給
SET MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
TMR @23 #5               ; 位置決めｱﾗｲﾒﾝﾄY前進端確認ﾃﾞｨﾚｰ時
CON
AND @T23                 ; 位置決めｱﾗｲﾒﾝﾄY前進端確認ﾃﾞｨﾚｰ時
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR308
STG @MR308
MPS
LD @T1                   ; ｱﾗｲﾒﾝﾄX軸前進端
AND @T3                  ; ｱﾗｲﾒﾝﾄY軸前進端
ANL
JMP @MR310               ; ｱﾗｲﾒﾝﾄXY前進
MPP
LDB @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
ANB @T3                  ; ｱﾗｲﾒﾝﾄY軸前進端
ANL
JMP @MR309
STG @MR309
MPS
ANB MR21405              ; P&P給材ﾍｯﾄﾞ動作1回記憶
@INC DM1500              ; PZT連続無しｶｳﾝﾄ
MRD
LD>= DM7002 #1           ; PZT投入数
AND MR21100              ; P&P給材ﾍｯﾄﾞ動作1回FLG
ANL
@DEC DM7002              ; PZT投入数
MPP
RES MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
RES MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T2                  ; ｱﾗｲﾒﾝﾄX軸後退端
AND @T4                  ; ｱﾗｲﾒﾝﾄY軸後退端
AND @T6                  ; ｼﾞｬﾏ板後退端
AND MR22005              ; STEPﾊﾟﾙｽ
MPS
OUT MR22100              ; PZT投入数一致受付ﾘｾｯﾄ
MPP
ANB MR40902              ; PZT投入数一致受付
JMP @MR315               ; 位置決めPRG　END
STG @MR310               ; ｱﾗｲﾒﾝﾄXY前進
@MOV #0 DM1500           ; PZT連続無しｶｳﾝﾄ
SET MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
SET MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
SET MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
AND @T3                  ; ｱﾗｲﾒﾝﾄY軸前進端
AND @T5                  ; ｼﾞｬﾏ板前進端
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR311               ; 位置決め部ｴｱｰﾌﾞﾛｰON
STG @MR311               ; 位置決め部ｴｱｰﾌﾞﾛｰON
SET MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
CON
TMR @24 DM0              ; 位置決め徐塵ｴｱｰﾌﾞﾛｰ時間  ｴｱｰﾌﾞﾛｰ時間設定
CON
AND @T24                 ; 位置決め徐塵ｴｱｰﾌﾞﾛｰ時間
RES MR6208               ; 位置決め部ｴｱｰﾌﾞﾛｰSOL補助
CON
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR312
STG @MR312
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T6                  ; ｼﾞｬﾏ板後退端
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR313
STG @MR313
MPS
RES MR22200              ; 位置決め動作中
CON
ANB MR22200              ; 位置決め動作中
OUT MR22001              ; 位置決めより転写SRB1へ取出し要求
MRD
AND MR23000              ; 位置決め徐塵へｱﾗｲﾒﾝﾄXY開要求
RES MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
RES MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
AND @T2                  ; ｱﾗｲﾒﾝﾄX軸後退端
AND @T4                  ; ｱﾗｲﾒﾝﾄY軸後退端
AND @T6                  ; ｼﾞｬﾏ板後退端
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR314
MPP
LDB MR40015              ; 主幹自動起動中
ANB MR21015              ; 給材P&P起動中
ANB MR23015              ; 転写SRB1自動起動中
ANB MR40610              ; STEP前半選択
ANL
TMR @26 #3               ; 主幹起動OFF確認
CON
AND @T26                 ; 主幹起動OFF確認
JMP @MR315               ; 位置決めPRG　END
STG @MR314
ANB MR23001              ; 転写SRB1取出し中
AND R8201                ; 転写SRB1位置決め上空位置
JMP @MR315               ; 位置決めPRG　END
STG @MR400               ; 1回目確認動作開始
JMP @MR401               ; ｱﾗｲﾒﾝﾄX前進
STG @MR401               ; ｱﾗｲﾒﾝﾄX前進
SET MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
TMR @15 #7               ; 位置決めｱﾗｲﾒﾝﾄX前進端確認ﾃﾞｨﾚｰ時
CON
AND @T15                 ; 位置決めｱﾗｲﾒﾝﾄX前進端確認ﾃﾞｨﾚｰ時
ANB R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
MPS
LD @T1                   ; ｱﾗｲﾒﾝﾄX軸前進端
AND MR22005              ; STEPﾊﾟﾙｽ
ANL
JMP @MR402               ; ｱﾗｲﾒﾝﾄY前進
MPP
LDB @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
AND MR22005              ; STEPﾊﾟﾙｽ
ANL
JMP @MR405               ; ｱﾗｲﾒﾝﾄXY戻り
STG @MR402               ; ｱﾗｲﾒﾝﾄY前進
SET MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
TMR @16 #5               ; 位置決めｱﾗｲﾒﾝﾄY前進端確認ﾃﾞｨﾚｰ時
CON
AND @T16                 ; 位置決めｱﾗｲﾒﾝﾄY前進端確認ﾃﾞｨﾚｰ時
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR403               ; ｱﾗｲﾒﾝﾄXY前進端確認
STG @MR403               ; ｱﾗｲﾒﾝﾄXY前進端確認
AND @T1                  ; ｱﾗｲﾒﾝﾄX軸前進端
AND @T3                  ; ｱﾗｲﾒﾝﾄY軸前進端
JMP @MR410               ; 起動時1回動作完了
STG @MR405               ; ｱﾗｲﾒﾝﾄXY戻り
RES MR6202               ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
CON
RES MR6204               ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
CON
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
CON
AND @T2                  ; ｱﾗｲﾒﾝﾄX軸後退端
AND @T4                  ; ｱﾗｲﾒﾝﾄY軸後退端
AND @T6                  ; ｼﾞｬﾏ板後退端
AND MR22005              ; STEPﾊﾟﾙｽ
JMP @MR410               ; 起動時1回動作完了
STG @MR410               ; 起動時1回動作完了
AND MR22500              ; 位置決め1回目確認FLG
JMP @MR315               ; 位置決めPRG　END
LD @MR410                ; 起動時1回動作完了
OR MR22500               ; 位置決め1回目確認FLG
LD MR40005               ; 全起動中
OR MR40610               ; STEP前半選択
ANL
OUT MR22500              ; 位置決め1回目確認FLG
STG @MR315               ; 位置決めPRG　END
RES MR22200              ; 位置決め動作中
CON
RES MR21405              ; P&P給材ﾍｯﾄﾞ動作1回記憶
CON
ANB MR22015              ; 位置決め徐塵起動中
ENDS
LDB MR22015              ; 位置決め徐塵起動中
ZRES @MR300 @MR415
;**** 位置決め除塵　異常処理/ 位置決め除塵　異常処理 ****
LDB MR4002               ; ｱﾗｲﾒﾝﾄX軸前進ｾﾝｻｰ異常
ANB MR4003               ; ｱﾗｲﾒﾝﾄX軸後退ｾﾝｻｰ異常
ANB MR4004               ; ｱﾗｲﾒﾝﾄY軸前進ｾﾝｻｰ異常
ANB MR4005               ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ異常
ANB MR4006               ; ｼﾞｬﾏ板前進ｾﾝｻｰ異常
ANB MR4007               ; ｼﾞｬﾏ板後退ｾﾝｻｰ異常
INV
OUT MR22901              ; 位置決め除塵ｼﾘﾝﾀﾞｰ異常
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR24014               ; 反転ﾕﾆｯﾄ原点復帰開始
OR= DM300 #38            ; 画面表示No.
AND T210                 ; 異常監視開始
OUT MR22915              ; 異常監視
LD R4002                 ; ｱﾗｲﾒﾝﾄX軸前進端
AND R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
LD R6202                 ; 位置決めｱﾗｲﾒﾝﾄX前後SOL
ANB R4002                ; ｱﾗｲﾒﾝﾄX軸前進端
ORL
AND MR22915              ; 異常監視
TMR @30 #50              ; ｱﾗｲﾒﾝﾄX軸前進ｾﾝｻｰ監視
CON
AND @T30                 ; ｱﾗｲﾒﾝﾄX軸前進ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4002               ; ｱﾗｲﾒﾝﾄX軸前進ｾﾝｻｰ異常
LD MR4002                ; ｱﾗｲﾒﾝﾄX軸前進ｾﾝｻｰ異常
@RES MR6202              ; 位置決めｱﾗｲﾒﾝﾄX前後SOL補助
LDB R6202                ; 位置決めｱﾗｲﾒﾝﾄX前後SOL
ANB R4003                ; ｱﾗｲﾒﾝﾄX軸後退端
AND MR22915              ; 異常監視
TMR @31 #50              ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ監視
CON
AND @T31                 ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4003               ; ｱﾗｲﾒﾝﾄX軸後退ｾﾝｻｰ異常
LD R4004                 ; ｱﾗｲﾒﾝﾄY軸前進端
AND R4005                ; ｱﾗｲﾒﾝﾄY軸後退端
LD R6204                 ; 位置決めｱﾗｲﾒﾝﾄY前後SOL
ANB R4004                ; ｱﾗｲﾒﾝﾄY軸前進端
ORL
AND MR22915              ; 異常監視
TMR @32 #50              ; ｱﾗｲﾒﾝﾄY軸前進ｾﾝｻｰ監視
CON
AND @T32                 ; ｱﾗｲﾒﾝﾄY軸前進ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4004               ; ｱﾗｲﾒﾝﾄY軸前進ｾﾝｻｰ異常
LD MR4004                ; ｱﾗｲﾒﾝﾄY軸前進ｾﾝｻｰ異常
@RES MR6204              ; 位置決めｱﾗｲﾒﾝﾄY前後SOL補助
LDB R6204                ; 位置決めｱﾗｲﾒﾝﾄY前後SOL
ANB R4005                ; ｱﾗｲﾒﾝﾄY軸後退端
AND MR22915              ; 異常監視
TMR @33 #50              ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ監視
CON
AND @T33                 ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4005               ; ｱﾗｲﾒﾝﾄY軸後退ｾﾝｻｰ異常
LD R4006                 ; ｼﾞｬﾏ板前進端
AND R4007                ; ｼﾞｬﾏ板後退端
LD R6206                 ; 位置決めｼﾞｬﾏ板前後SOL
ANB R4006                ; ｼﾞｬﾏ板前進端
ORL
AND MR22915              ; 異常監視
TMR @34 #50              ; ｼﾞｬﾏ板前進ｾﾝｻｰ監視
CON
AND @T34                 ; ｼﾞｬﾏ板前進ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4006               ; ｼﾞｬﾏ板前進ｾﾝｻｰ異常
LD MR4006                ; ｼﾞｬﾏ板前進ｾﾝｻｰ異常
RES MR6206               ; 位置決めｼﾞｬﾏ板前後SOL補助
LDB R6206                ; 位置決めｼﾞｬﾏ板前後SOL
ANB R4007                ; ｼﾞｬﾏ板後退端
AND MR22915              ; 異常監視
TMR @35 #50              ; ｼﾞｬﾏ板後退ｾﾝｻｰ監視
CON
AND @T35                 ; ｼﾞｬﾏ板後退ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4007               ; ｼﾞｬﾏ板後退ｾﾝｻｰ異常
;<h1/>ﾌﾟﾛｸﾞﾗﾑﾓﾆﾀｰ用　DM
LD @MR301
@MOV #301 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR302                ; ｱﾗｲﾒﾝﾄ状態確認
@MOV #302 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR303                ; 位置決めｼﾞｬﾏ板後退
@MOV #303 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR304                ; 位置決め徐塵供給要求
@MOV #304 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR305
@MOV #305 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR306
@MOV #306 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR307
@MOV #307 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR308
@MOV #308 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR309
@MOV #309 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR310                ; ｱﾗｲﾒﾝﾄXY前進
@MOV #310 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR311                ; 位置決め部ｴｱｰﾌﾞﾛｰON
@MOV #311 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR312
@MOV #312 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR313
@MOV #313 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR314
@MOV #314 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR315                ; 位置決めPRG　END
@MOV #315 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR400                ; 1回目確認動作開始
@MOV #400 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR401                ; ｱﾗｲﾒﾝﾄX前進
@MOV #401 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR402                ; ｱﾗｲﾒﾝﾄY前進
@MOV #402 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR403                ; ｱﾗｲﾒﾝﾄXY前進端確認
@MOV #403 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR404
@MOV #404 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR405                ; ｱﾗｲﾒﾝﾄXY戻り
@MOV #405 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR406
@MOV #406 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR407
@MOV #407 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR408
@MOV #408 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR409
@MOV #409 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR410                ; 起動時1回動作完了
@MOV #410 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR411
@MOV #411 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR412
@MOV #412 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR413
@MOV #413 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR414
@MOV #414 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LD @MR415
@MOV #415 DM502          ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
LDB MR22015              ; 位置決め徐塵起動中
MOV #0 DM502             ; 位置決めｽﾃｯﾌﾟﾓﾆﾀ
END
ENDH
