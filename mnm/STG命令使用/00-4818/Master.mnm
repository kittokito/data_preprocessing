DEVICE:53
;MODULE:Master
;MODULE_TYPE:0
LD CR2002                ; 常時ON
AND CR2007               ; 運転開始時1ｽｷｬﾝOFF
TMR #200 #10             ; CPU動作中遅延
MPS
AND T200                 ; CPU動作中遅延
TMR #202 #50             ; RB動作中遅延
MRD
LD T202                  ; RB動作中遅延
AND R8013                ; SRB1 AutoMode
AND R9613                ; SRB2 AutoMode
ANL
TMR #210 #30             ; 異常監視開始
MRD
@RES LR200               ; 自動ﾓｰﾄﾞ選択
CON
@RES LR201               ; 手動ﾓｰﾄﾞ選択
MPP
@RES LR500               ; 接着剤塗布途中軸停止FLG
LD CR2002                ; 常時ON
FLIK #30 #30 MR000       ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
FLIK #15 #15 MR001       ; 0.15Sﾌﾘｯｶﾊﾟﾙｽ
FLIK #150 #100 MR002     ; 1.5/1.0ｓﾌﾘｯｶﾊﾟﾙｽ
FLIK #100 #100 MR003     ; 1Sﾌﾘｯｶｰ
LD T200                  ; CPU動作中遅延
OUT MR2405               ; SRB1非常停止ﾘﾚｰON
MPS
OUT MR2407               ; SRB2非常停止ﾘﾚｰON
MPP
LDB MR28903              ; ｲﾝﾃﾞｯｸｽｴﾘｱｾﾝｻｰ感知ON自動続行
ANB MR28904              ; ｲﾝﾃﾞｯｸｽｴﾘｱｾﾝｻｰ感知ON原点復帰時
ANL
OUT MR2012               ; DDM1非常停止補助
LD T202                  ; RB動作中遅延
OUT R2400                ; SRB1電源ON　MC
OUT R2401                ; SRB2電源ON　MC
OUT R2402                ; DMM1電源ON　MC
LD CR2002                ; 常時ON
OUT R2500                ; RB-T3非常停止ﾘﾚｰ
OUT R2501                ; RB-T4非常停止ﾘﾚｰ
OUT R2502                ; RB-T1非常停止ﾘﾚｰ
OUT R2503                ; RB-T2非常停止ﾘﾚｰ
OUT R2504                ; RB-Y5非常停止ﾘﾚｰ
OUT R2505                ; RB-X6非常停止ﾘﾚｰ
OUT R2506                ; RB-I7非常停止ﾘﾚｰ
OUT R2507                ; RB-J8非常停止ﾘﾚｰ
OUT R2508                ; RB-F9非常停止ﾘﾚｰ
LD CR2002                ; 常時ON
OUT R2515                ; RB-JS10非常停止ﾘﾚｰ
OUT R2600                ; RB-AZ11非常停止ﾘﾚｰ
OUT R2411                ; RB-AD12非常停止ﾘﾚｰ
OUT R2602                ; RB-AX13非常停止ﾘﾚｰ
OUT R2603                ; RB-AY14非常停止ﾘﾚｰ
LD CR2002                ; 常時ON
OUT R2700                ; RB-TX3電源ON
OUT R2701                ; RB-TX4電源ON
OUT R2702                ; RB-F9電源ON
OUT R2410                ; RB-AD12電源ON
OUT R2704                ; RB-AX13電源ON
OUT R2705                ; RB-AY14電源ON
OUT R2706                ; RB-JS10電源ON
LD T202                  ; RB動作中遅延
OUT R46113               ; 給材P&PY5軸ｻｰﾎﾞON
OUT R46913               ; 給材P&PX6軸ｻｰﾎﾞON
MPS
OUT R47713               ; 外観I7軸ｻｰﾎﾞON
MPP
ANB R86305               ; 加圧治具　J8軸ｻｰﾎﾞOFF
OUT R48513               ; 治具供給＆排出J8軸ｻｰﾎﾞON
LD T202                  ; RB動作中遅延
OUT R38113               ; RB-T1X軸ｻｰﾎﾞON
OUT R38913               ; RB-T2Z軸ｻｰﾎﾞON
OUT R39713               ; RB-T3X軸ｻｰﾎﾞON
OUT R40513               ; RB-T4Z軸ｻｰﾎﾞON
MPS
OUT R54113               ; ｽｷｰｼﾞF9軸ｻｰﾎﾞON
MPP
ANB R86505               ; 治具ｾｯﾄJS10軸ｻｰﾎﾞOFF
OUT R54913               ; 加圧治具JS10軸ｻｰﾎﾞON
LD T202                  ; RB動作中遅延
MPS
LD R1615                 ; 元圧空確認
AND R5010                ; 塗布上下RBｱｼｽﾄｴｱｰ圧OUT1
ANL
OUT R24113               ; 接着塗布上下AZ11軸ｻｰﾎﾞON
MRD
OUT R24913               ; 接着吐出AD12軸ｻｰﾎﾞON
MRD
OUT R25713               ; 接着塗布AX13軸ｻｰﾎﾞON
MPP
OUT R26513               ; 接着塗布AY14軸ｻｰﾎﾞON
LD MR20010               ; PZTﾄﾚｲ全原点
AND MR21010              ; 給材P&P全原点
AND MR22010              ; 位置決め徐塵全原点
AND MR23010              ; 転写SRB1全原点
AND MR24010              ; 反転ﾕﾆｯﾄ全原点
AND MR25010              ; 外観検査全原点
AND MR26010              ; CAV搬送1　全原点
AND MR26410              ; CAV搬送2　全原点
AND MR27010              ; CAV位置決めSRB2全原点
AND MR28010              ; ｲﾝﾃﾞｯｸｽ全原点
AND MR29010              ; ﾙﾐﾗｰ供給全原点
AND MR29510              ; ﾙﾐﾗｰ組込み全原点
AND MR30010              ; 治具供給＆排出全原点
AND MR31010              ; ｽｷｰｼﾞ　全原点
AND MR32010              ; 接着塗布全原点
AND MR33010              ; 加圧治具ｾｯﾄ全原点
OUT MR40000              ; 全原点
OUT MR110
LDB MR20011              ; PZTﾄﾚｲ全異常
ANB MR21011              ; 給材P&P全異常
ANB MR22011              ; 位置決め徐塵全異常
ANB MR23011              ; 転写SRB1全異常
ANB MR24011              ; 反転ﾕﾆｯﾄ全異常
ANB MR25011              ; 外観検査全異常
ANB MR26011              ; CAV搬送1全異常
ANB MR27011              ; CAV位置決めSRB2全異常
ANB MR28011              ; ｲﾝﾃﾞｯｸｽ全異常
ANB MR28100              ; ｲﾝﾃﾞｯｸｽ上ｾﾝｻｰ異常
ANB MR29011              ; ﾙﾐﾗｰ供給全異常
ANB MR29511              ; ﾙﾐﾗｰ組込み全異常
ANB MR30011              ; 治具供給＆排出全異常
ANB MR31011              ; ｽｷｰｼﾞ　全異常
ANB MR32011              ; 接着剤塗布全異常
ANB MR33011              ; 加圧治具ｾｯﾄ全異常
ANB MR40910              ; 主幹異常一括
ANB MR40913              ; ｸﾘｰﾝﾕﾆｯﾄ  ALM
INV
OUT MR40001              ; 全異常
LD MR20009               ; PZTﾄﾚｲ全原点復帰可
AND MR21009              ; 給材P&P原点復帰可
AND MR22009              ; 位置決めﾕﾆｯﾄ原点復帰可
AND MR23009              ; 転写SRB1原点復帰可
AND MR24009              ; 反転ﾕﾆｯﾄ原点復帰可
AND MR25009              ; 外観検査原点復帰可
AND MR26009              ; CAV搬送1-2全原点復帰可
AND MR27009              ; CAV位置決めSRB2原点復帰可
AND MR28009              ; ｲﾝﾃﾞｯｸｽ全原点復帰可
AND MR29009              ; ﾙﾐﾗｰ供給組込み原点復帰可
AND MR30009              ; 治具供給＆排出全原点復帰可
AND MR31009              ; ｽｷｰｼﾞ原点復帰動作可
AND MR32009              ; 接着剤塗布部原点復帰可能
AND MR33009              ; 加圧治具ｾｯﾄ全原点復帰可
OUT MR40004              ; 全原点復帰可
LDB MR20015              ; PZTﾄﾚｲ自動起動中
ANB MR21015              ; 給材P&P起動中
ANB MR22015              ; 位置決め徐塵起動中
ANB MR23015              ; 転写SRB1自動起動中
ANB MR24015              ; 反転ﾕﾆｯﾄ自動起動中
ANB MR25015              ; 外観検査自動起動中
ANB MR26015              ; CAV搬送1自動起動中
ANB MR27015              ; CAV位置決めSRB2自動起動中
ANB MR28015              ; ｲﾝﾃﾞｯｸｽ全自動起動中
ANB MR29015              ; ﾙﾐﾗｰ供給自動運転開始
ANB MR29515              ; ﾙﾐﾗｰ組込み起動開始
ANB MR30015              ; 治具供給＆排出全自動起動中
ANB MR31015              ; ｽｷｰｼﾞ全自動起動中
ANB MR40015              ; 主幹自動起動中
ANB MR32015              ; 接着剤塗布自動起動中
ANB MR33015              ; 加圧治具ｾｯﾄ全自動起動中
INV
ANB MR40610              ; STEP前半選択
ANB MR40611              ; STEP後半選択
OUT MR40005              ; 全起動中
LD MR20014               ; PZTﾄﾚｲ全原点復帰開始
OR MR20010               ; PZTﾄﾚｲ全原点
LD MR21014               ; 給材P&P原点復帰開始
OR MR21010               ; 給材P&P全原点
ANL
LD MR22014               ; 位置決めﾕﾆｯﾄ原点復帰開始
OR MR22010               ; 位置決め徐塵全原点
ANL
LD MR23014               ; 転写SRB1原点復帰開始
OR MR23010               ; 転写SRB1全原点
ANL
LD MR24014               ; 反転ﾕﾆｯﾄ原点復帰開始
OR MR24010               ; 反転ﾕﾆｯﾄ全原点
ANL
LD MR25014               ; 外観検査原点復帰開始
OR MR25010               ; 外観検査全原点
ANL
LD MR27014               ; CAV位置決めSRB2原点復帰開始
OR MR27010               ; CAV位置決めSRB2全原点
ANL
LD MR29014               ; ﾙﾐﾗｰ供給組込み原点復帰開始
LD MR29010               ; ﾙﾐﾗｰ供給全原点
AND MR29510              ; ﾙﾐﾗｰ組込み全原点
ORL
ANL
LD MR28014               ; ｲﾝﾃﾞｯｸｽ全原点復帰開始
OR MR28010               ; ｲﾝﾃﾞｯｸｽ全原点
ANL
LD MR30014               ; 治具供給＆排出全原点復帰開始
OR MR30010               ; 治具供給＆排出全原点
ANL
LD MR31014               ; ｽｷｰｼﾞ全原点復帰開始
OR MR31010               ; ｽｷｰｼﾞ　全原点
ANL
LD MR32014               ; 接着塗布全原点復帰中
OR MR32010               ; 接着塗布全原点
ANL
LD MR26014               ; CAV搬送1原点復帰開始
OR MR26010               ; CAV搬送1　全原点
ANL
LD MR26414               ; CAV搬送2原点復帰開始
OR MR26410               ; CAV搬送2　全原点
ANL
LD MR33014               ; 加圧治具ｾｯﾄ全原点復帰開始
OR MR33010               ; 加圧治具ｾｯﾄ全原点
ANL
OUT MR40010              ; 全原点復帰起動確認
LD MR20300               ; PZTﾄﾚｲ全原点復帰完了
AND MR21300              ; 給材P&P原点復帰完了
AND MR22300              ; 位置決めﾕﾆｯﾄ原点復帰完了
AND MR23300              ; 転写SRB1原点復帰完了
AND MR24300              ; 反転ﾕﾆｯﾄ原点復帰完了
AND MR25300              ; 外観検査原点復帰完了
AND MR26300              ; CAV搬送1T3X軸原点復帰動完了FLG
AND MR27300              ; CAV位置決めSRB2原点復帰完了
AND MR28300              ; ｲﾝﾃﾞｯｸｽ全原点復帰完了
AND MR29300              ; ﾙﾐﾗｰ供給組込み原点復帰完了
AND MR30300              ; 治具供給＆排出全原点復帰完了
AND MR31300              ; ｽｷｰｼﾞ全原点復帰完了
AND MR32300              ; 接着塗布全原点復帰完了
AND MR32300              ; 接着塗布全原点復帰完了
AND MR33300              ; 治具給排ｾｯﾄ全原点復帰完了
OUT MR40011              ; 全原点復帰完了
LDB MR20014              ; PZTﾄﾚｲ全原点復帰開始
ANB MR21014              ; 給材P&P原点復帰開始
ANB MR22014              ; 位置決めﾕﾆｯﾄ原点復帰開始
ANB MR23014              ; 転写SRB1原点復帰開始
ANB MR24014              ; 反転ﾕﾆｯﾄ原点復帰開始
ANB MR25014              ; 外観検査原点復帰開始
ANB MR26014              ; CAV搬送1原点復帰開始
ANB MR27014              ; CAV位置決めSRB2原点復帰開始
ANB MR28014              ; ｲﾝﾃﾞｯｸｽ全原点復帰開始
ANB MR29014              ; ﾙﾐﾗｰ供給組込み原点復帰開始
ANB MR30014              ; 治具供給＆排出全原点復帰開始
ANB MR31014              ; ｽｷｰｼﾞ全原点復帰開始
ANB MR32014              ; 接着塗布全原点復帰中
ANB MR33014              ; 加圧治具ｾｯﾄ全原点復帰開始
OUT MR40012              ; 全原点復帰OFF
LDB MR20300              ; PZTﾄﾚｲ全原点復帰完了
ANB MR21300              ; 給材P&P原点復帰完了
ANB MR22300              ; 位置決めﾕﾆｯﾄ原点復帰完了
ANB MR23300              ; 転写SRB1原点復帰完了
ANB MR24300              ; 反転ﾕﾆｯﾄ原点復帰完了
ANB MR25300              ; 外観検査原点復帰完了
ANB MR26300              ; CAV搬送1T3X軸原点復帰動完了FLG
ANB MR27300              ; CAV位置決めSRB2原点復帰完了
ANB MR28300              ; ｲﾝﾃﾞｯｸｽ全原点復帰完了
ANB MR29300              ; ﾙﾐﾗｰ供給組込み原点復帰完了
ANB MR30300              ; 治具供給＆排出全原点復帰完了
ANB MR31300              ; ｽｷｰｼﾞ全原点復帰完了
ANB MR32300              ; 接着塗布全原点復帰完了
ANB MR33300              ; 治具給排ｾｯﾄ全原点復帰完了
OUT MR40013              ; 全原点復帰完了FLGOFF確認
LDP R210                 ; 原点復帰WSW
AND MR40004              ; 全原点復帰可
ANB MR40015              ; 主幹自動起動中
LDB MR40000              ; 全原点
OR CR2002                ; 常時ON
ANL
OR MR40014               ; 全原点復帰中
ANB R201                 ; STOP SW
ANB MR40001              ; 全異常
AND MR40415              ; 全扉閉確認
ANB @MR105               ; 全原点復帰完了
ANB R40910               ; 元圧空低下異常一括
OUT MR40014              ; 全原点復帰中
TMH @1 #5                ; 全原点復帰ﾃﾞｨﾚｰ
;<h1/>2015/10/31 原点復帰前の隣ｾﾙからの許可なし表示追加
;隣ｾﾙの電源が入っていない場合、原点復帰が出来ないので表示をする様に
LDB MR40110              ; 自動ﾓｰﾄﾞSW選択
AND MR20009              ; PZTﾄﾚｲ全原点復帰可
AND MR21009              ; 給材P&P原点復帰可
AND MR22009              ; 位置決めﾕﾆｯﾄ原点復帰可
AND MR23009              ; 転写SRB1原点復帰可
AND MR24009              ; 反転ﾕﾆｯﾄ原点復帰可
AND MR25009              ; 外観検査原点復帰可
AND MR27009              ; CAV位置決めSRB2原点復帰可
AND MR29009              ; ﾙﾐﾗｰ供給組込み原点復帰可
AND MR30009              ; 治具供給＆排出全原点復帰可
AND MR31009              ; ｽｷｰｼﾞ原点復帰動作可
AND MR32009              ; 接着剤塗布部原点復帰可能
LDB MR33009              ; 加圧治具ｾｯﾄ全原点復帰可
ORB MR26009              ; CAV搬送1-2全原点復帰可
ORB MR28009              ; ｲﾝﾃﾞｯｸｽ全原点復帰可
ANL
LDB R1509                ; CAV剥離ｾﾙよりｲﾝﾀｰﾛｯｸ2
ORB R1512                ; ﾄﾚﾁｪﾝよりｲﾝﾀｰﾛｯｸ1治具供給要求
ORB R1513                ; JIGﾄﾚﾁｪﾝよりｲﾝﾀｰﾛｯｸ2ｶｾｯﾄ供給
ANL
OUT MR40103              ; 隣ｾﾙ許可待ち
;↑ここまで
LD MR40014               ; 全原点復帰中
AND CR2006               ; 1sｸﾛｯｸﾊﾟﾙｽ
LD MR40000               ; 全原点
ANB MR40014              ; 全原点復帰中
ORL
ANB MR40005              ; 全起動中
OUT R310                 ; 全原点復帰中PL
LDP @T1                  ; 全原点復帰ﾃﾞｨﾚｰ
SET @MR101               ; 主幹原点復帰開始
STG @MR101               ; 主幹原点復帰開始
MPS
OUT MR40101              ; 各ﾕﾆｯﾄ原点復帰開始
MPP
AND MR40010              ; 全原点復帰起動確認
JMP @MR102               ; 全原点復帰完了待ち
STG @MR102               ; 全原点復帰完了待ち
AND MR40011              ; 全原点復帰完了
JMP @MR103               ; 各原点復帰OFF確認
STG @MR103               ; 各原点復帰OFF確認
AND MR40012              ; 全原点復帰OFF
JMP @MR104               ; 全原点復帰完了FLGOFF
STG @MR104               ; 全原点復帰完了FLGOFF
MPS
OUT MR40102              ; 各原点復帰完了FLGOFF
MPP
AND MR40013              ; 全原点復帰完了FLGOFF確認
JMP @MR105               ; 全原点復帰完了
STG @MR105               ; 全原点復帰完了
ANB MR40014              ; 全原点復帰中
ENDS
LDB MR40014              ; 全原点復帰中
ZRES @MR101 @MR105       ; 主幹原点復帰開始  全原点復帰完了
;<h1/>主幹自動運転 / 主幹自動運転 ****
LD R204                  ; 手動/自動ｾﾚｸﾀSW
DIFU MR40100             ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
LD MR40100               ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
ANB MR40110              ; 自動ﾓｰﾄﾞSW選択
LDB MR40100              ; 手動/自動ｾﾚｸﾀSWﾊﾟﾙｽ
AND MR40110              ; 自動ﾓｰﾄﾞSW選択
ORL
OUT MR40110              ; 自動ﾓｰﾄﾞSW選択
LD= W2001 #1             ; CAV剥離へ品種選択No.
AND= W2012 #1            ; CAV剥離品種選択No.
AND= W2031 #1            ; ﾄﾚﾁｪﾝ品種選択No.
LD= W2001 #2             ; CAV剥離へ品種選択No.
AND= W2012 #2            ; CAV剥離品種選択No.
AND= W2031 #2            ; ﾄﾚﾁｪﾝ品種選択No.
ORL
OUT MR40114              ; CAV品種選択OK
LD LR40003               ; 転写24点選択
OR LR40004               ; 転写32点選択
AND MR40000              ; 全原点
ANB MR40001              ; 全異常
ANB MR40014              ; 全原点復帰中
ANB MR40015              ; 主幹自動起動中
AND MR40300              ; 安全扉閉確認
AND MR29202              ; ﾙﾐﾗｰ供給ﾌｨﾙﾑ頭出し完了
AND R4011                ; ｽｷｰｼﾞ退避下降端
AND R4105                ; ﾌｨﾙﾑｸﾗﾝﾌﾟ閉(下降)端
AND R4401                ; ﾙﾐﾗｰｸﾗﾝﾌﾟ逃し下降端
AND MR40110              ; 自動ﾓｰﾄﾞSW選択
ANB MR31205              ; 転写ﾃｰﾌﾟｶｯﾄ
AND R4801                ; 転写ﾃｰﾌﾟ残量END
AND R4814                ; PZT NG排出ﾄﾚｲ検出
AND MR40114              ; CAV品種選択OK
LD>= DM7000 #1           ; CAV装置CAV残数
OR>= DM7001 #1           ; CAV搬送部CAV残数
OR>= DM7032 #1           ; ｲﾝﾃﾞｯｸｽ上治具数合計
ANL
OUT MR40002              ; 自動運転起動可
LD R200                  ; ｽﾀｰﾄSW
AND MR40002              ; 自動運転起動可
OR MR40015               ; 主幹自動起動中
ANB MR40105              ; ｻｲｸﾙ停止動作中
AND MR40112              ; 自動ﾓｰﾄﾞ
OUT MR40015              ; 主幹自動起動中
TMR #10 #5               ; 主幹自動起動開始ﾃﾞｨﾚｰ
LD R201                  ; STOP SW
AND MR40105              ; ｻｲｸﾙ停止動作中
TMR @10 #30              ; 強制ﾌﾟﾛｸﾞﾗﾑ停止
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
LD MR40005               ; 全起動中
AND MR40300              ; 安全扉閉確認
ORL
ANB @T10                 ; 強制ﾌﾟﾛｸﾞﾗﾑ停止
OUT MR40112              ; 自動ﾓｰﾄﾞ
LD MR40005               ; 全起動中
OUT R300                 ; 自動起動中PL
LD MR40105               ; ｻｲｸﾙ停止動作中
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LD MR40112               ; 自動ﾓｰﾄﾞ
ANB MR40005              ; 全起動中
ANB MR40902              ; PZT投入数一致受付
ANB MR40105              ; ｻｲｸﾙ停止動作中
ORL
OUT R301                 ; STOPﾗﾝﾌﾟ
LDB MR40110              ; 自動ﾓｰﾄﾞSW選択
OR R201                  ; STOP SW
LDB MR40001              ; 全異常
ANB MR30400              ; 払出し動作終了　自動終了
ANB MR3206               ; CAV画像ＮＧ異常1
ANB MR3207               ; PZT NG排出ﾜｰｸ満杯警報
ANB MR3408               ; CAV機種違い異常
INV
ORL
LDB R202                 ; ﾀｯﾁﾊﾟﾈﾙPAUSE SW
ANB R4809                ; 背面PAUSE SW
ANB R4909                ; ﾙﾐﾗｰ側扉開閉PAUSE-SW
INV
ORL
OR MR40105               ; ｻｲｸﾙ停止動作中
AND MR40005              ; 全起動中
OUT MR40105              ; ｻｲｸﾙ停止動作中
;<h1/>払出し / 払出し ****
LD MR21102               ; CAV残数小
AND MR40015              ; 主幹自動起動中
OR MR40902               ; PZT投入数一致受付
ANB MR30401              ; 払出しFLG　OFF
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR40005               ; 全起動中
ANL
ANB MR22100              ; PZT投入数一致受付ﾘｾｯﾄ
ANB MR27107              ; PZT投入数一致受付ﾘｾｯﾄ
OUT MR40902              ; PZT投入数一致受付
LDB MR28002              ; SRB2へｲﾝﾃﾞｯｸｽST2供給要求
ANB MR40901              ; 上流工程ﾜｰｸ有り
AND MR40005              ; 全起動中
OR MR40900               ; 払出し動作
AND MR40902              ; PZT投入数一致受付
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR40005               ; 全起動中
ANL
OUT MR40900              ; 払出し動作
LDB MR40900              ; 払出し動作
TMR #225 #5              ; 払出し動作修了
LD MR30310               ; 加圧ｶｾｯﾄ収納品排出要求
ANB MR40901              ; 上流工程ﾜｰｸ有り
AND MR40005              ; 全起動中
OR MR40905               ; 加圧ｶｾｯﾄ最終
AND MR40902              ; PZT投入数一致受付
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR40005               ; 全起動中
ANL
OUT MR40905              ; 加圧ｶｾｯﾄ最終
LDB MR2509               ; 電磁MG吸着ﾘﾚｰ
ANB MR22001              ; 位置決めより転写SRB1へ取出し要求
ANB MR2802               ; SRB1　MG吸着補助
ANB R4110                ; 反転ﾕﾆｯﾄﾁｬｯｸ閉端
ANB R4112                ; 外観検査ﾃｰﾌﾞﾙ吸着確認1
INV
OUT MR40907              ; 上流工程ﾜｰｸ有り
LDB MR2509               ; 電磁MG吸着ﾘﾚｰ
ANB MR22001              ; 位置決めより転写SRB1へ取出し要求
ANB MR2802               ; SRB1　MG吸着補助
ANB R4110                ; 反転ﾕﾆｯﾄﾁｬｯｸ閉端
ANB R4112                ; 外観検査ﾃｰﾌﾞﾙ吸着確認1
ANB R4915                ; CAV SRB2PZTﾁｬｯｸﾜｰｸ確認
INV
OR<> DM7003 #0           ; 接着治具投入数
OR<> DM7002 #0           ; PZT投入数
OUT MR40901              ; 上流工程ﾜｰｸ有り
;<h1/>扉制御 / 扉制御 ****
LDF R202                 ; ﾀｯﾁﾊﾟﾈﾙPAUSE SW
ORF R4809                ; 背面PAUSE SW
ORF R4909                ; ﾙﾐﾗｰ側扉開閉PAUSE-SW
LDF @T5                  ; 全安全扉閉確認時間
ANB R1612                ; 安全扉閉確認
ORL
OR MR40201               ; 全体PAUSE SW受付 ON
ANB MR40213              ; 全安全扉閉ﾊﾟﾙｽ
OUT MR40201              ; 全体PAUSE SW受付 ON
LDF R4805                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰPAUSE SW
ORF R202                 ; ﾀｯﾁﾊﾟﾈﾙPAUSE SW
ORF R4809                ; 背面PAUSE SW
ORF R4909                ; ﾙﾐﾗｰ側扉開閉PAUSE-SW
LDF @T6                  ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認時間
ANB R1614                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
ORL
OR MR40202               ; PZT PAUSE SW受付ON
ANB MR40214              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉ﾊﾟﾙｽ
OUT MR40202              ; PZT PAUSE SW受付ON
LDF R106                 ; 接着剤塗布扉PAUSE SW
ORF R202                 ; ﾀｯﾁﾊﾟﾈﾙPAUSE SW
ORF R4809                ; 背面PAUSE SW
ORF R4909                ; ﾙﾐﾗｰ側扉開閉PAUSE-SW
LDF @T7                  ; 接着剤塗布扉閉確認時間
ANB R1611                ; 接着剤塗布扉閉確認
ORL
ORP MR3412               ; 接着塗布ｼﾘﾝｼﾞ無し警報
ORP MR3413               ; 置台ｼﾘﾝｼﾞﾎﾟｯﾄﾗｲﾌｵｰﾊﾞｰ警報
OR MR40203               ; 接着剤PAUSE SW受付 ON
ANB MR40215              ; 接着剤塗布扉閉ﾊﾟﾙｽ
OUT MR40203              ; 接着剤PAUSE SW受付 ON
LDB MR40005              ; 全起動中
ANB MR40014              ; 全原点復帰中
ANB MR29200              ; ﾙﾐﾗｰ供給ﾌｨﾙﾑ頭出し中
ANB MR32015              ; 接着剤塗布自動起動中
LD B3656                 ; ﾄﾚﾁｪﾝからﾎﾟｰｽﾞ許可
ORB B3654                ; ﾄﾚﾁｪﾝからPLCRUN中
ANL
OR MR40204               ; 全扉解除ON
AND MR40201              ; 全体PAUSE SW受付 ON
OUT MR40204              ; 全扉解除ON
TMR @2 #15               ; 全扉解除ONﾃﾞｨﾚｰ
LDB MR20015              ; PZTﾄﾚｲ自動起動中
LDB MR20000              ; 供給P&Pへ取出し要求
ANB MR20100              ; PZT扉開可
ANB MR20101              ; PZT扉開可
INV
ORL
OR MR40205               ; PZT　扉解除ＯＮ
AND MR40202              ; PZT PAUSE SW受付ON
OUT MR40205              ; PZT　扉解除ＯＮ
TMR @3 #2                ; PZT　扉解除ＯＮﾃﾞｨﾚｰ
LDB MR31015              ; ｽｷｰｼﾞ全自動起動中
ANB MR32015              ; 接着剤塗布自動起動中
OR MR40206               ; 接着剤塗布扉解除ＯＮ
AND MR40203              ; 接着剤PAUSE SW受付 ON
OUT MR40206              ; 接着剤塗布扉解除ＯＮ
TMR @4 #2                ; 接着剤扉解除ＯＮﾃﾞｨﾚｰ
LDB R1612                ; 安全扉閉確認
OUT MR40207              ; 全安全扉開確認
LD R1612                 ; 安全扉閉確認
TMH @12 #15              ; 安全扉閉確認
TMR #15 #3               ; 安全扉閉確認
LDB R1614                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
OUT MR40208              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
LD R1614                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
TMR @13 #2               ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
LDB R1611                ; 接着剤塗布扉閉確認
OUT MR40209              ; 接着剤塗布扉開確認
LD R1611                 ; 接着剤塗布扉閉確認
TMR @14 #2               ; 接着剤塗布扉閉確認
LD @T12                  ; 安全扉閉確認
OUT MR40300              ; 安全扉閉確認
LD @T12                  ; 安全扉閉確認
AND @T13                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
OUT MR40413              ; 安全扉+PZT扉閉確認
LD @T14                  ; 接着剤塗布扉閉確認
OUT MR40414              ; 接着剤塗布扉閉確認
LD @T12                  ; 安全扉閉確認
AND @T13                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
AND @T14                 ; 接着剤塗布扉閉確認
OUT MR40415              ; 全扉閉確認
LDF R203                 ; ﾀｯﾁﾊﾟﾈﾙSTART SW
AND<> DM300 #110         ; 画面表示No.
ORF R4808                ; 背面START SW
ORF R4908                ; ﾙﾐﾗｰ側扉開閉START-SW
AND MR40207              ; 全安全扉開確認
AND @T2                  ; 全扉解除ONﾃﾞｨﾚｰ
OR MR40210               ; 全安全扉閉
ANB @T5                  ; 全安全扉閉確認時間
OUT MR40210              ; 全安全扉閉
DIFU MR40213             ; 全安全扉閉ﾊﾟﾙｽ
TMR @5 #5                ; 全安全扉閉確認時間
LDF @T5                  ; 全安全扉閉確認時間
AND R1612                ; 安全扉閉確認
OR MR40108               ; ﾄﾚﾁｪﾝへﾎﾟｰｽﾞ復帰要求受付
AND B3656                ; ﾄﾚﾁｪﾝからﾎﾟｰｽﾞ許可
MPS
OUT MR40108              ; ﾄﾚﾁｪﾝへﾎﾟｰｽﾞ復帰要求受付
MPP
AND T15                  ; 安全扉閉確認
OUT MR40109              ; ﾄﾚﾁｪﾝへﾎﾟｰｽﾞ復帰要求ON
LDF R4804                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰSTART SW
ORF R4808                ; 背面START SW
ORF R4908                ; ﾙﾐﾗｰ側扉開閉START-SW
ORF R203                 ; ﾀｯﾁﾊﾟﾈﾙSTART SW
AND MR40208              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
AND @T3                  ; PZT　扉解除ＯＮﾃﾞｨﾚｰ
OR MR40211               ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉
ANB @T6                  ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認時間
OUT MR40211              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉
DIFU MR40214             ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉ﾊﾟﾙｽ
TMR @6 #5                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認時間
LDF R105                 ; 接着剤塗布扉START SW
ORF R4808                ; 背面START SW
ORF R4908                ; ﾙﾐﾗｰ側扉開閉START-SW
ORF R203                 ; ﾀｯﾁﾊﾟﾈﾙSTART SW
AND MR40209              ; 接着剤塗布扉開確認
AND @T4                  ; 接着剤扉解除ＯＮﾃﾞｨﾚｰ
OR MR40212               ; 接着剤塗布扉閉
ANB @T7                  ; 接着剤塗布扉閉確認時間
OUT MR40212              ; 接着剤塗布扉閉
DIFU MR40215             ; 接着剤塗布扉閉ﾊﾟﾙｽ
TMR @7 #5                ; 接着剤塗布扉閉確認時間
LD MR40204               ; 全扉解除ON
OUT MR2413               ; 安全扉ﾛｯｸ解除
LDB MR40205              ; PZT　扉解除ＯＮ
INV
OUT MR2415               ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉解除
LDB MR40206              ; 接着剤塗布扉解除ＯＮ
INV
OUT MR2412               ; 接着剤塗布扉解除
LD R1612                 ; 安全扉閉確認
TMR @21 #15              ; 安全扉閉確認ﾃﾞｨﾚｰ
TMR #221 #5              ; 安全扉閉確認ﾃﾞｨﾚｰ
LDB R1612                ; 安全扉閉確認
TMR @31 #15              ; 安全扉開確認
LD R1611                 ; 接着剤塗布扉閉確認
TMR @22 #15              ; CAVﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
TMR #222 #5              ; CAVﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
LDB R1611                ; 接着剤塗布扉閉確認
TMR @32 #15              ; CAVﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
LD R1614                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
TMR @23 #15              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
TMR #223 #5              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
LDB R1614                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
TMR @33 #15              ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
LD MR40205               ; PZT　扉解除ＯＮ
AND @T3                  ; PZT　扉解除ＯＮﾃﾞｨﾚｰ
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LDB MR2415               ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉解除
ANB @T23                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
ORL
OUT MR6102               ; PZT扉START SWﾗﾝﾌﾟ補助
LD MR40202               ; PZT PAUSE SW受付ON
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LD MR40205               ; PZT　扉解除ＯＮ
AND @T33                 ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
ORL
OUT MR6103               ; PZT扉PAUSE　SWﾗﾝﾌﾟ補助
LD MR40206               ; 接着剤塗布扉解除ＯＮ
AND @T4                  ; 接着剤扉解除ＯＮﾃﾞｨﾚｰ
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LDB MR2412               ; 接着剤塗布扉解除
ANB @T22                 ; CAVﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認ﾃﾞｨﾚｰ
ORL
OUT R405                 ; 接着剤扉START SWﾗﾝﾌﾟ補助
LD MR40203               ; 接着剤PAUSE SW受付 ON
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LD MR40206               ; 接着剤塗布扉解除ＯＮ
AND @T32                 ; CAVﾄﾚｲﾁｪﾝｼﾞｬｰ扉開確認
ORL
OUT R406                 ; 接着剤扉PAUSE SWﾗﾝﾌﾟ補助
LD MR40204               ; 全扉解除ON
AND @T2                  ; 全扉解除ONﾃﾞｨﾚｰ
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LDB MR2413               ; 安全扉ﾛｯｸ解除
ANB @T21                 ; 安全扉閉確認ﾃﾞｨﾚｰ
ORL
OUT MR6106               ; 背面扉START SWﾗﾝﾌﾟ補助
OUT R6108                ; ﾙﾐﾗｰ側扉START SWﾗﾝﾌﾟ
LD MR6106                ; 背面扉START SWﾗﾝﾌﾟ補助
AND MR9915               ; 異常なし
LD MR40502               ; 異常確認ﾘｾｯﾄON(ﾌﾞｻﾞｰ停止）
AND MR001                ; 0.15Sﾌﾘｯｶﾊﾟﾙｽ
ORL
OUT R303                 ; ﾀｯﾁﾊﾟﾈﾙSTARTﾗﾝﾌﾟ
LD MR40201               ; 全体PAUSE SW受付 ON
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
LD MR40204               ; 全扉解除ON
AND @T31                 ; 安全扉開確認
ORL
OUT MR6107               ; 背面扉PAUSE SWﾗﾝﾌﾟ補助
OUT R302                 ; ﾀｯﾁﾊﾟﾈﾙPAUSEﾗﾝﾌﾟ
OUT R6109                ; ﾙﾐﾗｰ側PAUSE SWﾗﾝﾌﾟ
LD MR40902               ; PZT投入数一致受付
AND MR000                ; 0.3Sﾌﾘｯｶﾊﾟﾙｽ
OR LR40001               ; 救済動作選択
OUT R311                 ; 投入停止PL
LD MR40900               ; 払出し動作
ANB MR40901              ; 上流工程ﾜｰｸ有り
LD DM2602.0              ; ｲﾝﾃﾞｯｸｽST2接着治具有り
OR DM2603.0              ; ｲﾝﾃﾞｯｸｽST3接着治具有り
OR DM2604.0              ; ｲﾝﾃﾞｯｸｽST4接着治具有り
ANL
AND< DM7003 #1           ; 接着治具投入数
OUT R312                 ; ｲﾝﾃﾞｯｸｽ払出しPL
LD MR40005               ; 全起動中
AND MR40902              ; PZT投入数一致受付
AND<= DM7002 DM7003      ; PZT投入数  接着治具投入数
OR MR8000                ; 接着治具供給終了
ANB MR30401              ; 払出しFLG　OFF
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR40005               ; 全起動中
ANL
OUT MR8000               ; 接着治具供給終了
;<h1/>STEP選択     / STEP選択   ****
LDB MR49000              ; STEP 前半選択SW
ANB MR49001              ; STEP　後半選択SW
ANB MR49002              ; STEP 位置決め除塵選択SW
ANB MR49003              ; STEP 転写SRB1選択SW
ANB MR49004              ; STEP 反転ﾕﾆｯﾄ選択SW
ANB MR49005              ; STEP 外観検査選択SW
ANB MR49006              ; STEP ｽｷｰｼﾞ選択SW
ANB MR49007              ; STEP　SRB2以降選択SW
ANB MR49014              ; STEP 全体選択SW
INV
OUT MR40510              ; STEP選択SW ON
LD MR40510               ; STEP選択SW ON
DIFU MR40511             ; STEP選択SW ONﾊﾟﾙｽ
LD MR49000               ; STEP 前半選択SW
OR MR40600               ; STEP 前半選択
ANB MR40511              ; STEP選択SW ONﾊﾟﾙｽ
AND= DM300 #16           ; 画面表示No.
OUT MR40600              ; STEP 前半選択
OUT LR49000              ; STEP 前半選択PL
LD MR49001               ; STEP　後半選択SW
OR MR40601               ; STEP　後半選択
ANB MR40511              ; STEP選択SW ONﾊﾟﾙｽ
AND= DM300 #16           ; 画面表示No.
OUT MR40601              ; STEP　後半選択
OUT LR49001              ; STEP　後半選択
LD MR49014               ; STEP 全体選択SW
OR MR40614               ; STEP 全体選択
ANB MR40511              ; STEP選択SW ONﾊﾟﾙｽ
AND= DM300 #16           ; 画面表示No.
OUT MR40614              ; STEP 全体選択
OUT LR49014              ; STEP 全体選択PL
LDB MR40600              ; STEP 前半選択
ANB MR40614              ; STEP 全体選択
INV
OUT MR40610              ; STEP前半選択
LDB MR40601              ; STEP　後半選択
ANB MR40614              ; STEP 全体選択
INV
OUT MR40611              ; STEP後半選択
;<h1/>モード選択/ モード選択 ****
LD MR41000               ; 通常モード選択SW
ANB MR41001              ; 救済ﾓｰﾄﾞ選択SW
RES LR41001              ; 救済ﾓｰﾄﾞ
CON
SET LR41000              ; 通常ﾓｰﾄﾞ
LDB MR41000              ; 通常モード選択SW
AND MR41001              ; 救済ﾓｰﾄﾞ選択SW
RES LR41000              ; 通常ﾓｰﾄﾞ
CON
SET LR41001              ; 救済ﾓｰﾄﾞ
;<h1/>MAIN　異常処理
LDB MR1415               ; 元圧空低下異常
ANB MR1910               ; CAV品種選択異常
ANB MR1915               ; 元圧空低下異常補助
ANB MR1914               ; 真空圧異常補助
ANB MR1913               ; CAV剥離装置CAV残数ｾﾞﾛ異常
ANB MR4815               ; 真空圧異常
INV
OUT MR40910              ; 主幹異常一括
LDB MR1400               ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM
ANB MR1402               ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM
ANB MR1404               ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM
ANB MR1504               ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM
INV
OUT MR40913              ; ｸﾘｰﾝﾕﾆｯﾄ  ALM
LD T210                  ; 異常監視開始
ANB MR1915               ; 元圧空低下異常補助
ANB MR1415               ; 元圧空低下異常
ANB R1615                ; 元圧空確認
TMR @0 #5                ; 元圧空監視
CON
AND @T0                  ; 元圧空監視
SET MR1915               ; 元圧空低下異常補助
LD MR1915                ; 元圧空低下異常補助
RES MR2604               ; 引張りﾄﾙｸﾓｰﾀM1CW回転補助
CON
RES MR2608               ; 巻取ﾄﾙｸﾓｰﾀM2CW回転補助
CON
AND MR9915               ; 異常なし
SET MR1415               ; 元圧空低下異常
LDB R1400                ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM1
ORB R1401                ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM2
AND T210                 ; 異常監視開始
@SET MR1900              ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM補助
LD MR1900                ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM補助
AND MR9915               ; 異常なし
SET MR1400               ; ｸﾘｰﾝﾕﾆｯﾄ1 ALM
LDB R1402                ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM1
ORB R1403                ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM2
AND T210                 ; 異常監視開始
@SET MR1902              ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM補助
LD MR1902                ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM補助
AND MR9915               ; 異常なし
SET MR1402               ; ｸﾘｰﾝﾕﾆｯﾄ2 ALM
LDB R1404                ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM1
ORB R1405                ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM2
AND T210                 ; 異常監視開始
@SET MR1904              ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM補助
LD MR1904                ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM補助
AND MR9915               ; 異常なし
SET MR1404               ; ｸﾘｰﾝﾕﾆｯﾄ3 ALM
LDB R1503                ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM1
ORB R1504                ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM2
AND T210                 ; 異常監視開始
AND MR010                ; ｸﾘｰﾝﾕﾆｯﾄ4取り付け後削除
@SET MR1905              ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM補助
LD MR1905                ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM補助
AND MR9915               ; 異常なし
SET MR1504               ; ｸﾘｰﾝﾕﾆｯﾄ4 ALM
LDP R200                 ; ｽﾀｰﾄSW
AND MR40112              ; 自動ﾓｰﾄﾞ
ANB MR40005              ; 全起動中
ANB MR40114              ; CAV品種選択OK
SET MR1910               ; CAV品種選択異常
LD T210                  ; 異常監視開始
ANP R200                 ; ｽﾀｰﾄSW
AND MR40112              ; 自動ﾓｰﾄﾞ
ANB MR40005              ; 全起動中
AND= W2011 #0            ; CAV剥離よりCAV残数
AND= DM7001 #0           ; CAV搬送部CAV残数
AND= DM7032 #0           ; ｲﾝﾃﾞｯｸｽ上治具数合計
@SET MR1913              ; CAV剥離装置CAV残数ｾﾞﾛ異常
LD T210                  ; 異常監視開始
ANB MR1914               ; 真空圧異常補助
ANB MR4815               ; 真空圧異常
ANB R4815                ; 真空圧確認
TMR @8 #5                ; 真空圧確認
CON
AND @T8                  ; 真空圧確認
@SET MR1914              ; 真空圧異常補助
LD MR1914                ; 真空圧異常補助
AND MR9915               ; 異常なし
SET MR4815               ; 真空圧異常
LDB R1614                ; PZTﾄﾚｲﾁｪﾝｼﾞｬｰ扉閉確認
OUT MR41500              ; ﾄﾚｲﾁｪﾝｼﾞｬｰ扉開表示
LDB R1612                ; 安全扉閉確認
OUT MR41501              ; 安全扉開表示
LDB R1611                ; 接着剤塗布扉閉確認
OUT MR41502              ; 接着剤塗布扉開表示
LDB R79905               ; ID1510ｶﾗｰ選択SW
ANB R79906               ; ID9510ﾌﾞﾗｯｸ選択SW
INV
ANB MR41206              ; 装置内ﾜｰｸ無し
AND MR9915               ; 異常なし
SET MR4814               ; 装置内ﾜｰｸ有り
;<h1/>ﾜｰｸ投入数、残数管理
LD< W2011 #1             ; CAV剥離よりCAV残数
AND< DM7000 #1           ; CAV装置CAV残数
AND MR40015              ; 主幹自動起動中
OR MR40700               ; CAV剥離残数無し確認
ANB MR30401              ; 払出しFLG　OFF
AND MR40112              ; 自動ﾓｰﾄﾞ
OUT MR40700              ; CAV剥離残数無し確認
LD CR2002                ; 常時ON
ANB MR40700              ; CAV剥離残数無し確認
MOV W2011 DM7000         ; CAV剥離よりCAV残数  CAV装置CAV残数
LD CR2002                ; 常時ON
MPS
LDA DM2211               ; CAV搬送1　P1ﾜｰｸ有り
CON
ADD DM2212               ; CAV搬送1　P2ﾜｰｸ有り
CON
ADD DM2213               ; CAV搬送1　P3ﾜｰｸ有り
CON
ADD DM2214               ; CAV搬送1　P4ﾜｰｸ有り
CON
ADD DM2215               ; CAV搬送1　P5ﾜｰｸ有り
CON
ADD DM2216               ; CAV搬送1　P6ﾜｰｸ有り
CON
STA DM7010               ; CAV搬送1在荷数
MRD
LDA DM2221               ; CAV搬送2　P1ﾜｰｸ有り
CON
ADD DM2222               ; CAV搬送2　P2ﾜｰｸ有り
CON
ADD DM2223               ; CAV搬送2　P3ﾜｰｸ有り
CON
ADD DM2224               ; CAV搬送2　P4ﾜｰｸ有り
CON
ADD DM2225               ; CAV搬送2　P5ﾜｰｸ有り
CON
ADD DM2226               ; CAV搬送2　P6ﾜｰｸ有り
CON
STA DM7011               ; CAV搬送2在荷数
MPP
LDA DM7000               ; CAV装置CAV残数
CON
ADD DM7010               ; CAV搬送1在荷数
CON
ADD DM7011               ; CAV搬送2在荷数
CON
STA DM7001               ; CAV搬送部CAV残数
END
ENDH
