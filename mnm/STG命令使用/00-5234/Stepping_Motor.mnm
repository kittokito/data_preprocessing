DEVICE:53
;MODULE:Stepping_Motor
;MODULE_TYPE:0
LDP R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @MR008 @MR515       ; つなぎ目 検出記憶
LD= DM60000 #1           ; TaskNo /ERROR_DISP
AND R46208               ; _Group1　ｴﾗｰ発生中
OUT @MR001               ; TASK1 ERROR
;    ﾌｰﾌﾟ送りﾓｰﾀ 動作条件
LDB R4410                ; ﾌｨﾙﾀ切断 上昇端
AND R4411                ; ﾌｨﾙﾀ切断 下降端
OUT MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
;-- タイトル								: CPU高速カウンタ設定(CTH0)
;-- 動作モード							: アップダウンカウント
;-- 入力方式								: 外部入力(位相差(1逓倍))
;-- 入力時定数							: 20μs
;-- カウント範囲							: 32ビット符号あり
;-- カウント方式							: リニアカウンタ
;-- 現在値(CTH0)の初期化					: しない
;-- コンパレータ							: 使用しない
;-- プリセット							: 使用しない
;-- カウンタイネーブル					: 使用しない
;-- 入力方式								: 外部入力(位相差(4逓倍))
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES CR2113               ; CTH0ｶｳﾝﾄ入力方式選択
CON
RES CR2114               ; CTH0ｶｳﾝﾄ入力方式選択
CON
SET CR2115               ; CTH0ｶｳﾝﾄ入力方式選択
;-- 入力時定数							: 20μs
LD CR2008                ; 運転開始時1ｽｷｬﾝON
MOV #1 CM1620            ; 入力時定数設定
SET CR2305               ; CPUﾕﾆｯﾄ入力時定数設定
;-- カウント範囲							: 32ビット符号あり
LD CR2008                ; 運転開始時1ｽｷｬﾝON
BSET CM1608 #7           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
;-- カウント方式							: リニアカウンタ
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES CR2402               ; CTH0ｶｳﾝﾀ方式選択
;-- 現在値(CTH0)の初期化					: しない
;-- コンパレータ							: 使用しない
;-- コンパレータ設定を初期化
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES CR2103               ; CTC0ON時CTH0自動ﾘｾｯﾄ
CON
SET CR2104               ; CTC0ｺﾝﾊﾟﾚｰﾀ一致出力禁止
CON
RES CR2105               ; ｺﾝﾊﾟﾚｰﾀ一致出力OFF
CON
RES CR2106               ; ｺﾝﾊﾟﾚｰﾀ一致出力ON
CON
RES CR2107               ; ｺﾝﾊﾟﾚｰﾀ一致出力ON/OFF反転
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES CR2112               ; CTC1ON時CTH0自動ﾘｾｯﾄ
CON
SET CR2108               ; CTC1ｺﾝﾊﾟﾚｰﾀ一致出力禁止
CON
RES CR2109               ; ｺﾝﾊﾟﾚｰﾀ一致出力OFF
CON
RES CR2110               ; ｺﾝﾊﾟﾚｰﾀ一致出力ON
CON
RES CR2111               ; ｺﾝﾊﾟﾚｰﾀ一致出力ON/OFF反転
;-- プリセット							: 使用しない
;-- プリセット設定を初期化
LD CR2008                ; 運転開始時1ｽｷｬﾝON
RES CR2400               ; CTH0外部ﾌﾟﾘｾｯﾄ入力の設定
CON
RES CR2401               ; CTH0外部ﾌﾟﾘｾｯﾄ入力の設定
;-- カウンタイネーブル					: 使用しない
;-- カウンタイネーブル設定を初期化
LD CR2008                ; 運転開始時1ｽｷｬﾝON
BRES CM1608 #0           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
CON
BRES CM1608 #1           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
CON
BRES CM1608 #2           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
LD CR2008                ; 運転開始時1ｽｷｬﾝON
BRES CM1608 #4           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
CON
BRES CM1608 #5           ; CTH0ｲﾈｰﾌﾞﾙﾘｾｯﾄ設定
;-- CPU高速カウンタの動作
;-- 入力方式								: 外部入力(位相差(4逓倍))
LD CR2002                ; 常時ON
CTH.L #0 R010            ; ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示  ﾌｰﾌﾟ送りｴﾝｺｰﾀﾞ A相
;JOG速度設定
LD CR2002                ; 常時ON
MOV.D DM7014 CM2012      ; 基礎ﾃﾞｰﾀ8 ﾌｨﾙﾀ送り JOG速度  X軸JOG動作運転速度
MOV #100 CM2010          ; X軸JOG動作起動速度
;ﾌｨﾙﾀ送り量設定
LD CR2002                ; 常時ON
MOV.D DM7016 CM2060      ; 基礎ﾃﾞｰﾀ9 ﾌｨﾙﾀ送り量  ﾎﾟｲﾝﾄ0 目標値/移動量
LDA.D CM2060             ; ﾎﾟｲﾝﾄ0 目標値/移動量
CON
NEG.D
CON
STA.D CM2070             ; ﾎﾟｲﾝﾄ1 目標値/移動量
LD CR2002                ; 常時ON
MPS
LDA.D CM2060             ; ﾎﾟｲﾝﾄ0 目標値/移動量
CON
ADD.D DM7018             ; 基礎ﾃﾞｰﾀ10 ﾌｨﾙﾀ送り量 正常範囲
CON
MUL.D #80
CON
DIV.D #100
CON
STA DM31050              ; ﾌｨﾙﾀ送り量 正常範囲上限(+側)
MRD
LDA.D CM2060             ; ﾎﾟｲﾝﾄ0 目標値/移動量
CON
SUB.D DM7018             ; 基礎ﾃﾞｰﾀ10 ﾌｨﾙﾀ送り量 正常範囲
CON
MUL.D #80
CON
DIV.D #100
CON
STA DM31052              ; ﾌｨﾙﾀ送り量 正常範囲下限(+側)
MRD
LDA DM31050              ; ﾌｨﾙﾀ送り量 正常範囲上限(+側)
CON
NEG
CON
STA DM31054              ; ﾌｨﾙﾀ送り量 正常範囲上限(-側)
MPP
LDA DM31052              ; ﾌｨﾙﾀ送り量 正常範囲下限(+側)
CON
NEG
CON
STA DM31056              ; ﾌｨﾙﾀ送り量 正常範囲下限(-側)
;ﾄﾙｸﾓｰﾀｰ　ﾄﾙｸ制御を外部ﾎﾞﾘｭｰﾑに切替
LD CR2002                ; 常時ON
OUT MR2102               ; 巻出しﾄﾙｸﾓｰﾀ　INT/EXT
OUT MR2106               ; 巻取りﾄﾙｸﾓｰﾀ　INT/EXT
;<h1/>S/Mﾋﾟｯﾁ送り- 開始(ｴﾝｺｰﾀﾞ側へ回す)
STG MR20808              ; ﾌｰﾌﾟ -ﾋﾟｯﾁ送り
AND MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
SET MR2100               ; 巻出しﾄﾙｸﾓｰﾀ　CW
CON
RES MR2101               ; 巻出しﾄﾙｸﾓｰﾀ　CCW
CON
SET MR2104               ; 巻取りﾄﾙｸﾓｰﾀ　CW
CON
RES MR2105               ; 巻取りﾄﾙｸﾓｰﾀ　CCW
CON
SET MR20810              ; ﾌｰﾌﾟ -ﾋﾟｯﾁ送り中
CON
JMP @MR200
STG @MR200
DW.D #0 CM2020           ; X軸現在位置
CON
RES CTH0                 ; ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
CON
PLSX #0
CON
JMP @MR201
STG @MR201
ANB CR3006               ; X軸位置決め完了ﾘﾚｰ
JMP @MR202
STG @MR202
AND CR3006               ; X軸位置決め完了ﾘﾚｰ
MPS
LD<=.S DM31054 CTH0      ; ﾌｨﾙﾀ送り量 正常範囲上限(-側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
AND>=.S DM31056 CTH0     ; ﾌｨﾙﾀ送り量 正常範囲下限(-側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
ANL
JMP @MR205
MPP
LD>.S DM31054 CTH0       ; ﾌｨﾙﾀ送り量 正常範囲上限(-側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
OR<.S DM31056 CTH0       ; ﾌｨﾙﾀ送り量 正常範囲下限(-側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
ANL
JMP @MR203               ; ﾌｰﾌﾟ 送り量異常
;*** ﾌｰﾌﾟ送り量異常
STG @MR203               ; ﾌｰﾌﾟ 送り量異常
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET R39004               ; ｴﾗｰ中
CON
DW #84 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2000 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR204
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR204
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES @MR101               ; Task0 ｴﾗｰ完了
CON
RES R39004               ; ｴﾗｰ中
CON
JMP @MR200
;***
;CW / ｴﾝｺｰﾀﾞ側へ　CCW/ 巻取り側へ
STG @MR205
MPS
LD R4408                 ; ﾌｰﾌﾟ位置ズレ検出1
AND R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANL
JMP @MR210
MRD
LD R4408                 ; ﾌｰﾌﾟ位置ズレ検出1
ANB R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANL
OUT @MR012               ; ﾌｰﾌﾟﾋﾟｯﾁ送り-時 CW
MRD
LDB R4408                ; ﾌｰﾌﾟ位置ズレ検出1
AND R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANL
OUT @MR013               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
MRD
LDB R4408                ; ﾌｰﾌﾟ位置ズレ検出1
ANB R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANL
JMP @MR206               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ
MPP
TMR @0 #50
CON
AND @T0
JMP @MR208               ; ﾌｰﾌﾟ 横ｽﾞﾚ補正 ﾀｲﾑｱｯﾌﾟｴﾗｰ
;*** ﾌｰﾌﾟ横ｽﾞﾚｴﾗｰ
STG @MR206               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
CON
SET R39004               ; ｴﾗｰ中
CON
DW #83 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2000 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR207
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR207
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES @MR101               ; Task0 ｴﾗｰ完了
CON
RES R39004               ; ｴﾗｰ中
CON
JMP @MR205
;*** ﾌｰﾌﾟ横ｽﾞﾚ補正ﾀｲﾑｱｯﾌﾟｴﾗｰ
STG @MR208               ; ﾌｰﾌﾟ 横ｽﾞﾚ補正 ﾀｲﾑｱｯﾌﾟｴﾗｰ
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET R39004               ; ｴﾗｰ中
CON
DW #82 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2000 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR209
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR209
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES @MR101               ; Task0 ｴﾗｰ完了
CON
RES R39004               ; ｴﾗｰ中
CON
JMP @MR205
;***
STG @MR210
RES MR20810              ; ﾌｰﾌﾟ -ﾋﾟｯﾁ送り中
CON
SET MR20809              ; ﾌｰﾌﾟ -ﾋﾟｯﾁ送り完了
CON
ENDS
;<h1/>ﾌｰﾌﾟﾋﾟｯﾁ送り+ 開始(巻取り側へ送る)
STG MR20800              ; ﾌｰﾌﾟ +ﾋﾟｯﾁ送り
SET MR2100               ; 巻出しﾄﾙｸﾓｰﾀ　CW
CON
RES MR2101               ; 巻出しﾄﾙｸﾓｰﾀ　CCW
CON
SET MR2104               ; 巻取りﾄﾙｸﾓｰﾀ　CW
CON
RES MR2105               ; 巻取りﾄﾙｸﾓｰﾀ　CCW
CON
SET MR20802              ; ﾌｰﾌﾟ +ﾋﾟｯﾁ送り中
CON
JMP @MR300
;1ﾋﾟｯﾁ送り開始
STG @MR300
AND MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
TMR @3 #1
CON
AND @T3
RES @MR010               ; 再送り
CON
RES CTH0                 ; ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
CON
PLSX #1
CON
JMP @MR301
STG @MR301
ANB CR3006               ; X軸位置決め完了ﾘﾚｰ
JMP @MR302
;送り動作中つなぎ目確認
STG @MR302
MPS
LD R4402                 ; ﾌｰﾌﾟｴﾝﾄﾞ検出
ANB R4404                ; ﾌｰﾌﾟつなぎ目検出
ANB @MR008               ; つなぎ目 検出記憶
AND= DM31002 #0          ; つなぎ目検出後ﾌﾟﾚｽ回数
AND= DM31004 #0          ; つなぎ目正常後送り回数
ANL
SET @MR008               ; つなぎ目 検出記憶
CON
DW #5 DM31002            ; つなぎ目検出後ﾌﾟﾚｽ回数
CON
DW #11 DM31004           ; つなぎ目正常後送り回数
MRD
LD<> DM31002 #0          ; つなぎ目検出後ﾌﾟﾚｽ回数
OR<> DM31004 #0          ; つなぎ目正常後送り回数
ANB @MR008               ; つなぎ目 検出記憶
ANL
SET @MR008               ; つなぎ目 検出記憶
MPP
AND CR3006               ; X軸位置決め完了ﾘﾚｰ
MPS
LD<=.S DM31052 CTH0      ; ﾌｨﾙﾀ送り量 正常範囲下限(+側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
AND>=.S DM31050 CTH0     ; ﾌｨﾙﾀ送り量 正常範囲上限(+側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
ANL
JMP @MR305
MPP
LD<.S DM31050 CTH0       ; ﾌｨﾙﾀ送り量 正常範囲上限(+側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
OR>.S DM31052 CTH0       ; ﾌｨﾙﾀ送り量 正常範囲下限(+側)  ｴﾝｺｰﾀﾞﾊﾟﾙｽ表示
ANL
JMP @MR303               ; ﾌｰﾌﾟ 送り量異常
;*** ﾌｰﾌﾟ送り量異常
STG @MR303               ; ﾌｰﾌﾟ 送り量異常
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET R39004               ; ｴﾗｰ中
CON
DW #84 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2003 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR304
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR304
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES @MR101               ; Task0 ｴﾗｰ完了
CON
RES R39004               ; ｴﾗｰ中
CON
MPS
AND @MR105               ; SW1
JMP @MR300
MPP
AND @MR108               ; SW4
SET MR20803              ; ﾌｰﾌﾟ送り中止
CON
JMP @MR315
;***
;CW / ｴﾝｺｰﾀﾞ側へ  CCW/ 巻取り側へ  ﾋﾟｯﾁ補正動作
STG @MR305
MPS
LD R4408                 ; ﾌｰﾌﾟ位置ズレ検出1
AND R4409                ; ﾌｰﾌﾟ位置ズレ検出2
OR @MR009                ; 送り異常時ﾌｰﾌﾟ送り記憶
ANL
RES @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
CON
JMP @MR311
MPP
ANB @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
MPS
LD R4408                 ; ﾌｰﾌﾟ位置ズレ検出1
ANB R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANB @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
ANL
OUT @MR014               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CW
MRD
LDB R4408                ; ﾌｰﾌﾟ位置ズレ検出1
AND R4409                ; ﾌｰﾌﾟ位置ズレ検出2
OR @MR506                ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
ANL
OUT @MR015               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
MRD
LDB R4408                ; ﾌｰﾌﾟ位置ズレ検出1
ANB R4409                ; ﾌｰﾌﾟ位置ズレ検出2
ANB @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
ANL
JMP @MR306               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ
MPP
TMR @1 #50
CON
AND @T1
JMP @MR309               ; ﾌｰﾌﾟ 横ｽﾞﾚ補正 ﾀｲﾑｱｯﾌﾟｴﾗｰ
;*** ﾌｰﾌﾟ横ｽﾞﾚｴﾗｰ
STG @MR306               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET R39004               ; ｴﾗｰ中
CON
DW #83 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2800 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR307
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR307
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES @MR101               ; Task0 ｴﾗｰ完了
CON
RES R39004               ; ｴﾗｰ中
CON
MPS
AND @MR105               ; SW1
SET @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
CON
JMP @MR305
MPP
AND @MR106               ; SW2
SET @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
CON
DW #12 DM31006           ; 送り異常後送り回数
CON
RES @MR506               ; ﾌｰﾌﾟ 横ｽﾞﾚｴﾗｰ 発生記憶
CON
JMP @MR311
;*** ﾌｰﾌﾟ横ｽﾞﾚ補正ﾀｲﾑｱｯﾌﾟｴﾗｰ
STG @MR309               ; ﾌｰﾌﾟ 横ｽﾞﾚ補正 ﾀｲﾑｱｯﾌﾟｴﾗｰ
MPS
LDB R41200               ; TASK0 PAUSE 要求
ANB R39004               ; ｴﾗｰ中
ANL
SET R39004               ; ｴﾗｰ中
CON
DW #82 @DM1              ; ErrorNo /ERROR_DISP
CON
DW #4408 @DM2            ; SensorNo /ERROR_DISP
CON
DW $2800 @DM9            ; ReturnSW /ERROR_DISP
CON
SET @MR100               ; Task0 ERROR
CON
JMP @MR310
MPP
AND R41200               ; TASK0 PAUSE 要求
SET @MR600               ; Main1 HALT許可
STG @MR310
SET @MR600               ; Main1 HALT許可
CON
AND @MR101               ; Task0 ｴﾗｰ完了
RES R39004               ; ｴﾗｰ中
CON
RES @MR101               ; Task0 ｴﾗｰ完了
CON
MPS
AND @MR105               ; SW1
JMP @MR305
MPP
AND @MR106               ; SW2
SET @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
CON
DW #12 DM31006           ; 送り異常後送り回数
CON
JMP @MR311
;***
;終了 or 次送り実施  判断
STG @MR311
MPS
LDB @MR008               ; つなぎ目 検出記憶
LD @MR008                ; つなぎ目 検出記憶
AND<> DM31002 #0         ; つなぎ目検出後ﾌﾟﾚｽ回数
ORL
ANB @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
ANL
JMP @MR315
MPP
LD @MR008                ; つなぎ目 検出記憶
AND= DM31002 #0          ; つなぎ目検出後ﾌﾟﾚｽ回数
OR @MR009                ; 送り異常時ﾌｰﾌﾟ送り記憶
ANL
JMP @MR312
;繋ぎ目検出送り中判断
STG @MR312
MPS
ANB @MR008               ; つなぎ目 検出記憶
JMP @MR313
MPP
AND @MR008               ; つなぎ目 検出記憶
MPS
LD= DM31002 #0           ; つなぎ目検出後ﾌﾟﾚｽ回数
OR @MR009                ; 送り異常時ﾌｰﾌﾟ送り記憶
ANL
MPS
AND<> DM31004 #0         ; つなぎ目正常後送り回数
SET @MR010               ; 再送り
CON
JMP @MR313
MPP
AND= DM31004 #0          ; つなぎ目正常後送り回数
RES @MR008               ; つなぎ目 検出記憶
CON
JMP @MR313
MRD
LD R4404                 ; ﾌｰﾌﾟつなぎ目検出
AND<> DM31004 #0         ; つなぎ目正常後送り回数
ANL
@DEC DM31004             ; つなぎ目正常後送り回数
MPP
LD @MR009                ; 送り異常時ﾌｰﾌﾟ送り記憶
AND<> DM31002 #0         ; つなぎ目検出後ﾌﾟﾚｽ回数
ANL
@DEC DM31002             ; つなぎ目検出後ﾌﾟﾚｽ回数
;送りエラーに伴うﾌｰﾌﾟ送り中判断
STG @MR313
MPS
ANB @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
JMP @MR314
MPP
AND @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
MPS
AND<> DM31006 #0         ; 送り異常後送り回数
SET @MR010               ; 再送り
CON
JMP @MR314
MRD
AND= DM31006 #0          ; 送り異常後送り回数
RES @MR009               ; 送り異常時ﾌｰﾌﾟ送り記憶
CON
JMP @MR314
MPP
AND<> DM31006 #0         ; 送り異常後送り回数
@DEC DM31006             ; 送り異常後送り回数
;次送り必要可否判断
STG @MR314
MPS
AND @MR010               ; 再送り
JMP @MR300
MPP
ANB @MR010               ; 再送り
JMP @MR315
;ﾋﾟｯﾁ送り終了
STG @MR315
MPS
RES @MR010               ; 再送り
CON
RES MR20802              ; ﾌｰﾌﾟ +ﾋﾟｯﾁ送り中
CON
SET MR20801              ; ﾌｰﾌﾟ +ﾋﾟｯﾁ送り完了
CON
ENDS
MPP
LD @MR008                ; つなぎ目 検出記憶
AND<> DM31002 #0         ; つなぎ目検出後ﾌﾟﾚｽ回数
ANL
@DEC DM31002             ; つなぎ目検出後ﾌﾟﾚｽ回数
;<h1/>JOG OUTPUT
LD R46100                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
OR R46102                ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
LD MR20714               ; ﾌｰﾌﾟ送りﾓｰﾀ +JOG
OR @MR012                ; ﾌｰﾌﾟﾋﾟｯﾁ送り-時 CW
OR @MR014                ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CW
ANL
LD R46100                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
OR R46102                ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
LD MR20713               ; ﾌｰﾌﾟ送りﾓｰﾀ -JOG
OR @MR013                ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
OR @MR015                ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
ANL
LD R46100                ; _自動運転中　ﾓｰﾄﾞﾌﾗｸﾞ
OR R46102                ; _ﾒﾝﾃﾅﾝｽ運転中　ﾓｰﾄﾞﾌﾗｸﾞ
AND MR20715              ; ﾌｰﾌﾟ送りﾓｰﾀ I.L.
ANB @MR012               ; ﾌｰﾌﾟﾋﾟｯﾁ送り-時 CW
ANB @MR013               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
ANB @MR014               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CW
ANB @MR015               ; ﾌｰﾌﾟﾋﾟｯﾁ送り+時 CCW
ANB CR2003               ; 常時OFF
JOGX
;<h1/>*ERROR
;MyTask Error Process Substance/エラー処理実体
STG @MR100               ; Task0 ERROR
ANB R39002               ; ERROR_DISP Busy
MPS
ZRES @MR105 @MR108       ; SW1  SW4
CON
DISN @DM9 @DM3 #4        ; ReturnSW /ERROR_DISP
MRD
DW MyTask @DM0           ; 自ﾀｽｸ番号  TaskNo /ERROR_DISP
CON
BMOV @DM0 DM60000 #7     ; TaskNo /ERROR_DISP  TaskNo /ERROR_DISP
MPP
SET R39000               ; ERROR_DISP Start
CON
SET @MR102               ; Task0 ERROR Busy
CON
JMP @MR103
STG @MR103
AND R39002               ; ERROR_DISP Busy
AND= DM60008 @DM1        ; ReturnNo /ERROR_DISP  ErrorNo /ERROR_DISP
MPS
AND= DM60007 @DM6        ; ReturnSW /ERROR_DISP
SET @MR105               ; SW1
CON
JMP @MR104
MRD
AND= DM60007 @DM5        ; ReturnSW /ERROR_DISP
SET @MR106               ; SW2
CON
JMP @MR104
MRD
AND= DM60007 @DM4        ; ReturnSW /ERROR_DISP
SET @MR107               ; SW3
CON
JMP @MR104
MPP
AND= DM60007 @DM3        ; ReturnSW /ERROR_DISP
SET @MR108               ; SW4
CON
JMP @MR104
STG @MR104
ANB R39002               ; ERROR_DISP Busy
RES @MR102               ; Task0 ERROR Busy
CON
SET @MR101               ; Task0 ｴﾗｰ完了
CON
ENDS
;<h1/>HALT
LD R41201                ; TASK1 PAUSE 要求
AND @MR600               ; Main1 HALT許可
TMR @2 #2
CON
AND @T2
SET MR20815              ; Steping motor HALT許可
LD MR20815               ; Steping motor HALT許可
ORB R41201               ; TASK1 PAUSE 要求
ANB @MR001               ; TASK1 ERROR
RES @MR600               ; Main1 HALT許可
LDF R41201               ; TASK1 PAUSE 要求
RES MR20815              ; Steping motor HALT許可
END
ENDH
