DEVICE:53
;MODULE:移載PP
;MODULE_TYPE:0
;非常停止 / Emergency stop
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50010                ; 非常停止
ZRES @MR002 @MR2915
CON
ZRES R54000 R54315       ; 移載PPZ軸 上昇位置
CON
OUT @MR001
TMR @0 #5                ; Wait Timer-00
;条件ﾌﾗｸﾞ / Flug
LD R54000                ; 移載PPZ軸 上昇位置
OR R54004                ; 移載PPZ軸 待機位置
AND R54010               ; 移載PPX軸 待機位置
ANB R4114                ; 移載_ﾁｬｯｸ 開端
AND R4115                ; 移載_ﾁｬｯｸ 閉端
OUT @MR005               ; 移載PP部ｼﾘﾝﾀﾞ原位置
LDB R50300               ; PAUSE中
ANB R50113               ; L側COF接合部ｴﾗｰ中
ANB R50108               ; ｴﾗｰ発生中
OUT @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
LD>.D DM2452 #19000      ; 除材P&P Y軸_現在座標表示用
OUT R54301               ; 除材RB Y軸干渉外
;AUTO動作 / AUTO OPERATION
LD R50011                ; AUTO MODE
ANB MR11206              ; 移載PPｻｲｸﾙ中
AND MR11006              ; 移載PP start
LD LR41000               ; 自動ﾊﾟﾗﾒｰﾀ1　通常動作
OR LR41001               ; 自動ﾊﾟﾗﾒｰﾀ2　払い出し
OR LR41003               ; 自動ﾊﾟﾗﾒｰﾀ4
OR LR41006               ; 自動ﾊﾟﾗﾒｰﾀ7
ANL
SET MR11206              ; 移載PPｻｲｸﾙ中
CON
RES MR11006              ; 移載PP start
CON
SET @MR2000              ; AUTO START
STG @MR2000              ; AUTO START
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R38208               ; 移載P&P X軸 P1 位置　表示
DW #0 DM15016
CON
RES MR12004              ; 移載動作開始
CON
MPS
LDB R54101               ; Line-B（右） 取出し要求
AND R54100               ; Line-A（左） 取出し要求
AND R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
JMP @MR2001
MRD
LD R54100                ; Line-A（左） 取出し要求
AND R54101               ; Line-B（右） 取出し要求
AND R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
JMP @MR2001
MRD
LDB R54100               ; Line-A（左） 取出し要求
AND R54101               ; Line-B（右） 取出し要求
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
JMP @MR2008
MRD
LDB R54100               ; Line-A（左） 取出し要求
AND R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
TMR #730 #30
CON
AND T730
JMP @MR1900
MRD
LDB R54101               ; Line-B（右） 取出し要求
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
TMR #731 #30
CON
AND T731
JMP @MR1908
MPP
LDB R54100               ; Line-A（左） 取出し要求
ANB R54101               ; Line-B（右） 取出し要求
ANB R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
AND MR11408              ; L側搬送ｻｲｸﾙ終了
AND MR11409              ; R側搬送ｻｲｸﾙ終了
ANL
JMP @MR2300
;L側搬送台　取出し位置に不明な個体ﾊﾟﾚｯﾄを検出した
STG @MR1900
SET @MR3000              ; L側取出位置不明ﾊﾟﾚｯﾄ有り
CON
JMP @MR1901
STG @MR1901
AND @MR3007              ; ｴﾗｰ解除
RES @MR3007              ; ｴﾗｰ解除
CON
JMP @MR2000              ; AUTO START
;R側搬送台　取出し位置に不明な個体ﾊﾟﾚｯﾄを検出した
STG @MR1908
SET @MR3008              ; R側取出位置不明ﾊﾟﾚｯﾄ有り
CON
JMP @MR1909
STG @MR1909
AND @MR3015              ; ｴﾗｰ解除
RES @MR3015              ; ｴﾗｰ解除
CON
JMP @MR2000              ; AUTO START
;左側搬送台へ移載ﾛﾎﾞｯﾄを移動
STG @MR2001
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR12004              ; 移載動作開始
CON
SET MR9010               ; 左側動作中
CON
RES R54100               ; Line-A（左） 取出し要求
CON
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2002
STG @MR2002
SET @MR2600              ; RB X軸　Line_A 側移動動作
CON
JMP @MR2003
STG @MR2003
AND @MR2607              ; RB X軸　Line_A 側移動動作完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR2607              ; RB X軸　Line_A 側移動動作完了
CON
JMP @MR2004
STG @MR2004
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
SET MR6102               ; 移載ﾁｬｯｸ 開
CON
AND R4114                ; 移載_ﾁｬｯｸ 開端
JMP @MR2005
MPP
ANB R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
JMP @MR1800
;L側搬送台　取出し位置の個体ﾊﾟﾚｯﾄが無くなった？
STG @MR1800
SET @MR3100              ; L側取り出し位置ﾊﾟﾚｯﾄ紛失
CON
JMP @MR1801
STG @MR1801
AND @MR3107              ; ｴﾗｰ解除
RES R54100               ; Line-A（左） 取出し要求
CON
RES @MR3107              ; ｴﾗｰ解除
CON
JMP @MR2210
;ﾊﾟﾚｯﾄﾁｬｯｸ下降OK
STG @MR2005
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R4303                ; L_搬送ﾁｬｯｸ前後 後退端
AND R4301                ; L_搬送ﾄﾗﾝｽﾌｧ戻り端
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2006
STG @MR2006
SET @MR2700              ; RB Z軸　Line-A（左)側下降動作
CON
JMP @MR2007
STG @MR2007
AND @MR2707              ; RB Z軸　Line-A（左)側下降動作完
SET R53112               ; L 移載完了
CON
DW #0 DM505
CON
RES @MR2900              ; 一時停止禁止
CON
RES @MR2707              ; RB Z軸　Line-A（左)側下降動作完
CON
JMP @MR2100
;右側搬送台へ移載ﾛﾎﾞｯﾄを移動
STG @MR2008
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR12004              ; 移載動作開始
CON
SET MR9011               ; 右側動作中
CON
RES R54101               ; Line-B（右） 取出し要求
CON
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2009
STG @MR2009
SET @MR2608              ; RB X軸　Line_B 側移動動作
CON
JMP @MR2010
STG @MR2010
AND @MR2615              ; RB X軸　Line_B 側移動動作完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR2615              ; RB X軸　Line_B 側移動動作完了
CON
JMP @MR2011
STG @MR2011
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
SET MR6102               ; 移載ﾁｬｯｸ 開
CON
AND R4114                ; 移載_ﾁｬｯｸ 開端
JMP @MR2012
MPP
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
JMP @MR1808
;R側搬送台　取出し位置の個体ﾊﾟﾚｯﾄが無くなった？
STG @MR1808
SET @MR3108              ; R側取り出し位置ﾊﾟﾚｯﾄ紛失
CON
JMP @MR1809
STG @MR1809
AND @MR3115              ; ｴﾗｰ解除
RES R54101               ; Line-B（右） 取出し要求
CON
RES @MR3115              ; ｴﾗｰ解除
CON
JMP @MR2210
;ﾊﾟﾚｯﾄﾁｬｯｸ下降OK
STG @MR2012
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R4503                ; R_搬送ﾁｬｯｸ後退端
AND R4501                ; R_搬送ﾄﾗﾝｽﾌｧ 戻り端
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2013
STG @MR2013
SET @MR2708              ; RB Z軸　Line-B（右)側下降動作
CON
JMP @MR2014
STG @MR2014
AND @MR2715              ; RB Z軸　Line-A（左)側下降動作完
RES @MR2900              ; 一時停止禁止
CON
SET R53114               ; R 移載完了
CON
DW #0 DM515
CON
RES @MR2715              ; RB Z軸　Line-A（左)側下降動作完
CON
JMP @MR2100
;ﾊﾟﾚｯﾄをつかみ、上昇させる
STG @MR2100
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6102               ; 移載ﾁｬｯｸ 開
CON
AND R4115                ; 移載_ﾁｬｯｸ 閉端
TMR #410 #2
CON
AND T410
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2101
STG @MR2101
SET @MR2800              ; RB Z軸　上昇動作
CON
JMP @MR2102
STG @MR2102
AND @MR2807              ; RB Z軸　左側下降動作完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR2807              ; RB Z軸　左側下降動作完了
CON
JMP @MR2108
STG @MR2108
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2109
STG @MR2109
SET @MR1000              ; RB X軸　ﾊﾞｯﾌｧ台前待避位置移動
CON
JMP @MR2110
STG @MR2110
AND @MR1007              ; RB X軸　ﾊﾞｯﾌｧ台前待避位置移動完
RES @MR2900              ; 一時停止禁止
CON
RES @MR1007              ; RB X軸　ﾊﾞｯﾌｧ台前待避位置移動完
CON
JMP @MR2200
STG @MR2200
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R54301               ; 除材RB Y軸干渉外
ANB R54302               ; 除材RB Y軸　ﾊﾞｯﾌｧ部除材中
ANB R4208                ; ﾜｰｸ確認ｾﾝｻ
ANB R54305               ; ﾊﾞｯﾌｧ位置　ﾜｰｸ除材要求
SET R54303               ; 移載PP ﾊﾞｯﾌｧ部移載中
CON
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2201
STG @MR2201
SET @MR1008              ; RB X軸　ﾊﾞｯﾌｧ台位置移動
CON
JMP @MR2202
STG @MR2202
AND @MR1015              ; RB X軸　ﾊﾞｯﾌｧ台位置移動完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR1015              ; RB X軸　ﾊﾞｯﾌｧ台位置移動完了
CON
JMP @MR2203
STG @MR2203
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2204
STG @MR2204
SET @MR1100              ; RB Z軸　ﾊﾞｯﾌｧ部下降動作
CON
JMP @MR2205
STG @MR2205
AND @MR1107              ; RB Z軸　ﾊﾞｯﾌｧ部下降動作完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR1107              ; RB Z軸　ﾊﾞｯﾌｧ部下降動作完了
CON
JMP @MR2206
STG @MR2206
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6102               ; 移載ﾁｬｯｸ 開
CON
AND R4114                ; 移載_ﾁｬｯｸ 開端
TMR #411 #2
CON
AND T411
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2207
STG @MR2207
SET @MR2800              ; RB Z軸　上昇動作
CON
JMP @MR2208
STG @MR2208
AND @MR2807              ; RB Z軸　左側下降動作完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR2807              ; RB Z軸　左側下降動作完了
CON
RES MR12004              ; 移載動作開始
CON
JMP @MR2209
STG @MR2209
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R54303               ; 移載PP ﾊﾞｯﾌｧ部移載中
CON
MPS
LDB R54101               ; Line-B（右） 取出し要求
AND R54100               ; Line-A（左） 取出し要求
LD R54100                ; Line-A（左） 取出し要求
AND R54101               ; Line-B（右） 取出し要求
ORL
ANL
SET R54305               ; ﾊﾞｯﾌｧ位置　ﾜｰｸ除材要求
CON
JMP @MR2001
MRD
LDB R54100               ; Line-A（左） 取出し要求
AND R54101               ; Line-B（右） 取出し要求
ANL
SET R54305               ; ﾊﾞｯﾌｧ位置　ﾜｰｸ除材要求
CON
JMP @MR2008
MPP
LDB R54100               ; Line-A（左） 取出し要求
ANB R54101               ; Line-B（右） 取出し要求
ANB R4702                ; L_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANB R4705                ; R_溶着部 取出位置ﾊﾟﾚｯﾄ有無
ANL
JMP @MR2210
STG @MR2210
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET @MR2900              ; 一時停止禁止
CON
JMP @MR2211
STG @MR2211
SET @MR1200              ; RB X軸　待機位置移動
CON
JMP @MR2212
STG @MR2212
AND @MR1207              ; RB X軸　待機位置移動完了
RES @MR2900              ; 一時停止禁止
CON
RES @MR1207              ; RB X軸　待機位置移動完了
CON
SET R54305               ; ﾊﾞｯﾌｧ位置　ﾜｰｸ除材要求
CON
JMP @MR2000              ; AUTO START
STG @MR2300
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R53113               ; Line-B（右） 移載完了
CON
RES R53112               ; L 移載完了
CON
RES MR11206              ; 移載PPｻｲｸﾙ中
CON
SET MR11405              ; 移載PPｻｲｸﾙ終了
CON
ENDS
LD @MR2900               ; 一時停止禁止
OUT R51102               ; 移載PP PACK
STG @MR2600              ; RB X軸　Line_A 側移動動作
MPS
AND MR14502              ; 移載P&P X軸_移動完了
JMP @MR2601
MPP
MCALL POINT_MOVE1 #2 #3 #2 MR14511 MR14502;移載P&P X軸_位置決め異常  移載P&P X軸_移動完了
STG @MR2601
RES MR14502              ; 移載P&P X軸_移動完了
CON
ANB MR14502              ; 移載P&P X軸_移動完了
SET @MR2607              ; RB X軸　Line_A 側移動動作完了
CON
ENDS
STG @MR2608              ; RB X軸　Line_B 側移動動作
MPS
AND MR14502              ; 移載P&P X軸_移動完了
JMP @MR2609
MPP
MCALL POINT_MOVE1 #2 #3 #3 MR14511 MR14502;移載P&P X軸_位置決め異常  移載P&P X軸_移動完了
STG @MR2609
RES MR14502              ; 移載P&P X軸_移動完了
CON
ANB MR14502              ; 移載P&P X軸_移動完了
SET @MR2615              ; RB X軸　Line_B 側移動動作完了
CON
ENDS
STG @MR1000              ; RB X軸　ﾊﾞｯﾌｧ台前待避位置移動
MPS
AND MR14502              ; 移載P&P X軸_移動完了
JMP @MR1001
MPP
MCALL POINT_MOVE1 #2 #3 #4 MR14511 MR14502;移載P&P X軸_位置決め異常  移載P&P X軸_移動完了
STG @MR1001
RES MR14502              ; 移載P&P X軸_移動完了
CON
ANB MR14502              ; 移載P&P X軸_移動完了
SET @MR1007              ; RB X軸　ﾊﾞｯﾌｧ台前待避位置移動完
CON
ENDS
STG @MR1008              ; RB X軸　ﾊﾞｯﾌｧ台位置移動
MPS
AND MR14502              ; 移載P&P X軸_移動完了
JMP @MR1009
MPP
MCALL POINT_MOVE1 #2 #3 #5 MR14511 MR14502;移載P&P X軸_位置決め異常  移載P&P X軸_移動完了
STG @MR1009
RES MR14502              ; 移載P&P X軸_移動完了
CON
ANB MR14502              ; 移載P&P X軸_移動完了
SET @MR1015              ; RB X軸　ﾊﾞｯﾌｧ台位置移動完了
CON
ENDS
STG @MR1200              ; RB X軸　待機位置移動
MPS
AND MR14502              ; 移載P&P X軸_移動完了
JMP @MR1201
MPP
MCALL POINT_MOVE1 #2 #3 #1 MR14511 MR14502;移載P&P X軸_位置決め異常  移載P&P X軸_移動完了
STG @MR1201
RES MR14502              ; 移載P&P X軸_移動完了
CON
ANB MR14502              ; 移載P&P X軸_移動完了
SET @MR1207              ; RB X軸　待機位置移動完了
CON
ENDS
STG @MR2700              ; RB Z軸　Line-A（左)側下降動作
MPS
AND MR14702              ; 移載P&P Z軸_移動完了
JMP @MR2701
MPP
MCALL POINT_MOVE1 #2 #4 #2 MR14711 MR14702;移載P&P Z軸_位置決め異常  移載P&P Z軸_移動完了
STG @MR2701
RES MR14702              ; 移載P&P Z軸_移動完了
CON
ANB MR14702              ; 移載P&P Z軸_移動完了
SET @MR2707              ; RB Z軸　Line-A（左)側下降動作完
CON
ENDS
STG @MR2708              ; RB Z軸　Line-B（右)側下降動作
MPS
AND MR14702              ; 移載P&P Z軸_移動完了
JMP @MR2709
MPP
MCALL POINT_MOVE1 #2 #4 #3 MR14711 MR14702;移載P&P Z軸_位置決め異常  移載P&P Z軸_移動完了
STG @MR2709
RES MR14702              ; 移載P&P Z軸_移動完了
CON
ANB MR14702              ; 移載P&P Z軸_移動完了
SET @MR2715              ; RB Z軸　Line-A（左)側下降動作完
CON
ENDS
STG @MR2800              ; RB Z軸　上昇動作
MPS
AND MR14702              ; 移載P&P Z軸_移動完了
JMP @MR2801
MPP
MCALL POINT_MOVE1 #2 #4 #1 MR14711 MR14702;移載P&P Z軸_位置決め異常  移載P&P Z軸_移動完了
STG @MR2801
RES MR14702              ; 移載P&P Z軸_移動完了
CON
ANB MR14702              ; 移載P&P Z軸_移動完了
SET @MR2807              ; RB Z軸　左側下降動作完了
CON
ENDS
STG @MR1100              ; RB Z軸　ﾊﾞｯﾌｧ部下降動作
MPS
AND MR14702              ; 移載P&P Z軸_移動完了
JMP @MR1101
MPP
MCALL POINT_MOVE1 #2 #4 #4 MR14711 MR14702;移載P&P Z軸_位置決め異常  移載P&P Z軸_移動完了
STG @MR1101
RES MR14702              ; 移載P&P Z軸_移動完了
CON
ANB MR14702              ; 移載P&P Z軸_移動完了
SET @MR1107              ; RB Z軸　ﾊﾞｯﾌｧ部下降動作完了
CON
ENDS
LD MR12004               ; 移載動作開始
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
AND CR2003               ; 常時OFF
@INC DM15016
;ｱﾗｰﾑ処理
STG @MR3000              ; L側取出位置不明ﾊﾟﾚｯﾄ有り
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50115               ; 移載、除材P&P部ｴﾗｰ中
CON
SET R51005               ; 移載、除材P&P部表示
CON
SET R50513               ; ERR13
CON
JMP @MR3001
MOV #13 DM400            ; 表示画面No.
STG @MR3001
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3002
STG @MR3002
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3007              ; ｴﾗｰ解除
CON
ENDS
STG @MR3008              ; R側取出位置不明ﾊﾟﾚｯﾄ有り
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50115               ; 移載、除材P&P部ｴﾗｰ中
CON
SET R51005               ; 移載、除材P&P部表示
CON
SET R50514               ; ERR14
CON
JMP @MR3009
MOV #14 DM400            ; 表示画面No.
STG @MR3009
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3010
STG @MR3010
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3015              ; ｴﾗｰ解除
CON
ENDS
STG @MR3100              ; L側取り出し位置ﾊﾟﾚｯﾄ紛失
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50115               ; 移載、除材P&P部ｴﾗｰ中
CON
SET R51005               ; 移載、除材P&P部表示
CON
SET R50707               ; ERR39
CON
JMP @MR3101
MOV #39 DM400            ; 表示画面No.
STG @MR3101
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3102
STG @MR3102
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3107              ; ｴﾗｰ解除
CON
ENDS
STG @MR3108              ; R側取り出し位置ﾊﾟﾚｯﾄ紛失
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50115               ; 移載、除材P&P部ｴﾗｰ中
CON
SET R51005               ; 移載、除材P&P部表示
CON
SET R50710               ; ERR42 R_ﾌｨﾙﾑ下限異常
CON
JMP @MR3109
MOV #42 DM400            ; 表示画面No.
STG @MR3109
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3110
STG @MR3110
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR3115              ; ｴﾗｰ解除
CON
ENDS
END
ENDH
