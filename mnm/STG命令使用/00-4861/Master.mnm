DEVICE:53
;MODULE:Master
;MODULE_TYPE:0
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50914                ; HOMEsw ON
RES LR200                ; 自動ﾓｰﾄﾞ　   選択
CON
ZRES @MR000 @MR1915      ; ｽﾀｰﾄsw ON PLS
;各種裏設定
;Sercret settings
LD LR310                 ; 2号機以降
ANB CR2002               ; 常時ON
OUT R52900               ; 2号機以降
LD LR400                 ; ｼｭｰﾀｲｵﾅｲｻﾞ不使用
OR CR2002                ; 常時ON
OUT R52908               ; ｼｭｰﾀｲｵﾅｲｻﾞ不使用
LD LR401                 ; ｲｵﾅｲｻﾞ無視
OR CR2002                ; 常時ON
OUT R52909               ; ｲｵﾅｲｻﾞ無視
LD LR402                 ; 巻取りﾓｰﾀ不使用
OUT R52910               ; 巻取りﾓｰﾀ不使用
LD LR403                 ; 圧着時ACF吸着有り
OUT R52911               ; 圧着時ACF吸着有り
;ﾌﾗｸﾞ管理
;Flag management
LDB R50000               ; 即停止
ANB R50900               ; TRAP
OUT R55000               ; 即停止/TRAP 入力無し
LD R49802                ; MAIN     払出し完了
SET R49807               ; 全ﾕﾆｯﾄ払出し完了
LDB R49802               ; MAIN     払出し完了
RES R49807               ; 全ﾕﾆｯﾄ払出し完了
;ＣＰＵ起動時モジュール起動
;Ｍｏｄｕｌｅ  ｓｔａｒｔｓ  ａｔ ｔｈｅ  ｔｉｍｅ  ｏｆ  ＣＰＵ  ｓｔａｒｔｉｎｇ．
LD CR2002                ; 常時ON
AND CR2007               ; 運転開始時1ｽｷｬﾝOFF
AND R55000               ; 即停止/TRAP 入力無し
ANB R49915               ; 表示用　　 TRAP
TMR #200 #10             ; 動作中遅延
MPS
AND T200                 ; 動作中遅延
TMR #202 #100
MRD
MDSTRT MAIN_ST
MRD
@RES R205                ; 背面表示無効
CON
@RES LR200               ; 自動ﾓｰﾄﾞ　   選択
CON
@RES LR201               ; 手動ﾓｰﾄﾞ　   選択
MRD
DW $0000 DM31115         ; Port2 ﾃﾞｰﾀ格納単位
CON
DW $0000 DM31116         ; Port2 通信ﾓｰﾄﾞ
CON
DW $0000 DM31118         ; Port2 送信用ﾍｯﾀﾞ
CON
DW $0000 DM31119         ; Port2 送信用ﾃﾞﾘﾐﾀ
CON
DW $0200 DM31120         ; Port2 受信用ﾍｯﾀﾞ
CON
DW $0000 DM31121         ; Port2 受信用ﾃﾞﾘﾐﾀ
MPP
@DW #1000 DM3101
;スタートスイッチ管理
;Ｓｔａｒｔ  ＳＷ   ｍａｎａｇｅｍｅｎｔ
;
LD R1100                 ; BOX START_SW
OR R52004                ; ｽﾀｰﾄ_SW
AND _MAIN_ST
DIFU @MR000              ; ｽﾀｰﾄsw ON PLS
LD @MR000                ; ｽﾀｰﾄsw ON PLS
OR @MR001                ; ｽﾀｰﾄsw ON
ANB @MR002               ; ｽﾀｰﾄsw OFF
OUT @MR001               ; ｽﾀｰﾄsw ON
OUT R51001               ; ｽﾀｰﾄsw ON
LD @MR001                ; ｽﾀｰﾄsw ON
ANB R1100                ; BOX START_SW
ANB R52004               ; ｽﾀｰﾄ_SW
OUT @MR002               ; ｽﾀｰﾄsw OFF
LD @MR001                ; ｽﾀｰﾄsw ON
DIFU R51000              ; ｽﾀｰﾄsw ON PLS
;＠モード選択/Select Mode
;原点復帰
;ＨＯＭＥ
LD R51101                ; 原点復帰
OUT R50003               ; 原点復帰中
LDB R50004               ; 動作中
ANB R51100               ; 原点復帰完了
AND R29113               ; 軸1 ｻｰﾎﾞON
AND R29913               ; 軸2 ｻｰﾎﾞON
AND R30713               ; 軸3 ｻｰﾎﾞON
AND R31513               ; 軸4 ｻｰﾎﾞON
AND<> DM450 #3005        ; ﾒｯｾｰｼﾞNo.
OUT R50203               ; 原点復帰待ち
LDP R52004               ; ｽﾀｰﾄ_SW
ANB R50301               ; MEIN部    Pause
AND R50203               ; 原点復帰待ち
RES LR200                ; 自動ﾓｰﾄﾞ　   選択
CON
RES LR201                ; 手動ﾓｰﾄﾞ　   選択
CON
RES R51100               ; 原点復帰完了
CON
RES MR50007              ; 原点復帰　完了
LDP R52004               ; ｽﾀｰﾄ_SW
ANB R50301               ; MEIN部    Pause
ANB LR200                ; 自動ﾓｰﾄﾞ　   選択
ANB R50001               ; 自動中
ANB R50002               ; ステップ動作中
ANB R51102               ; 原点復帰中
ANB LR201                ; 手動ﾓｰﾄﾞ　   選択
AND R50203               ; 原点復帰待ち
DIFU R50914              ; HOMEsw ON
LDF R52004               ; ｽﾀｰﾄ_SW
ANB R50301               ; MEIN部    Pause
DIFU @MR208              ; HOME Start PLS
;MAIN-unit
LDB R51100               ; 原点復帰完了
ANB R50001               ; 自動中
ANB R50002               ; ステップ動作中
ANB MR1401               ; RB4移動完了
AND R29113               ; 軸1 ｻｰﾎﾞON
AND R29913               ; 軸2 ｻｰﾎﾞON
AND R30713               ; 軸3 ｻｰﾎﾞON
AND R31513               ; 軸4 ｻｰﾎﾞON
OUT @MR100               ; HOME ｽﾀｰﾄ条件
LD @MR100                ; HOME ｽﾀｰﾄ条件
ANB R50301               ; MEIN部    Pause
OUT R51103               ; HOMEsw    Interlock
LD @MR103                ; HOME 動作中
OR @MR104                ; HOME終了
AND R51100               ; 原点復帰完了
ANB @MR105               ; HOME     終了確認
SET R50206               ; 原点復帰完了確認
CON
OUT @MR104               ; HOME終了
LD @MR100                ; HOME ｽﾀｰﾄ条件
AND @MR208               ; HOME Start PLS
ANB R50006               ; 異常発生中
ANB R50109               ; Err MAIN
AND R1003                ; SAFETY COVER(ON=閉)
AND T200                 ; 動作中遅延
AND R55000               ; 即停止/TRAP 入力無し
ANB R51100               ; 原点復帰完了
RES R50206               ; 原点復帰完了確認
CON
SET R51101               ; 原点復帰
LD R51102                ; 原点復帰中
OR @MR103                ; HOME 動作中
AND R51101               ; 原点復帰
ANB @MR105               ; HOME     終了確認
OUT @MR103               ; HOME 動作中
;HOME終了操作  /  HOME End Process
LD @MR104                ; HOME終了
OUT @MR105               ; HOME     終了確認
LDP @MR105               ; HOME     終了確認
OR R50915                ; 原点復帰終了ﾌﾞｻﾞｰON
ANB T103                 ; HOME終了 ﾌﾞｻﾞｰON
OUT R50915               ; 原点復帰終了ﾌﾞｻﾞｰON
TMR #103 #4              ; HOME終了 ﾌﾞｻﾞｰON
;
LDB R50001               ; 自動中
ANB R50002               ; ステップ動作中
AND MR50007              ; 原点復帰　完了
AND= DM400 #2            ; 現在表示中   画面
ANB MR100                ; ST原点FLG
MPS
ANP R52010               ; ﾓｰﾄﾞSW(TP)
RES R51100               ; 原点復帰完了
CON
SET MR50010              ; 原点復帰要求解除
CON
RES MR50007              ; 原点復帰　完了
MPP
LDP R52004               ; ｽﾀｰﾄ_SW
AND CR2003               ; 常時OFF
ANL
RES R51100               ; 原点復帰完了
CON
SET MR50010              ; 原点復帰要求解除
CON
RES MR50007              ; 原点復帰　完了
LD MR50010               ; 原点復帰要求解除
ANP R52005               ; 自動ﾓｰﾄﾞ停止PB(TP)
SET R51100               ; 原点復帰完了
CON
RES MR50010              ; 原点復帰要求解除
CON
SET MR50007              ; 原点復帰　完了
;
LDB R50001               ; 自動中
ANB R50002               ; ステップ動作中
AND R50206               ; 原点復帰完了確認
AND= DM400 #2            ; 現在表示中   画面
AND R52005               ; 自動ﾓｰﾄﾞ停止PB(TP)
SET MR50007              ; 原点復帰　完了
;モード選択
;Ｍｏｄｅ  Ｓｅｌｅｃｔｉｏｎ
LDB R50001               ; 自動中
ANB R50004               ; 動作中
AND R50005               ; 停止中
MPS
LD R43000                ; AUTO PARA  -0確認
OR R43001                ; AUTO PARA  -1確認
OR R43002                ; AUTO PARA  -2確認
OR R43003                ; AUTO PARA  -3確認
OR R43004                ; AUTO PARA  -4確認
OR R43005                ; AUTO PARA  -5確認
OR R43006                ; AUTO PARA  -6確認
OR R43007                ; AUTO PARA  -7確認
OR R43008                ; AUTO PARA  -8確認
ANL
MPS
LDP R52010               ; ﾓｰﾄﾞSW(TP)
ANB LR200                ; 自動ﾓｰﾄﾞ　   選択
AND MR100                ; ST原点FLG
ANL
DIFU @MR200              ; 自動ﾓｰﾄﾞ   SW ON
MPP
LDP R52010               ; ﾓｰﾄﾞSW(TP)
AND LR200                ; 自動ﾓｰﾄﾞ　   選択
ANL
DIFU @MR201              ; 手動ﾓｰﾄﾞ   SW ON
MPP
LDB R43000               ; AUTO PARA  -0確認
ANB R43001               ; AUTO PARA  -1確認
ANB R43002               ; AUTO PARA  -2確認
ANB R43003               ; AUTO PARA  -3確認
ANB R43004               ; AUTO PARA  -4確認
ANB R43005               ; AUTO PARA  -5確認
ANB R43006               ; AUTO PARA  -6確認
ANB R43007               ; AUTO PARA  -7確認
ANB R43008               ; AUTO PARA  -8確認
ANL
MPS
ANP R52010               ; ﾓｰﾄﾞSW(TP)
MOV DM450 DM990          ; ﾒｯｾｰｼﾞNo.
MOV #3006 DM450          ; ﾒｯｾｰｼﾞNo.
MPP
ANF R52010               ; ﾓｰﾄﾞSW(TP)
MOV DM990 DM450          ; ﾒｯｾｰｼﾞNo.
LD @MR200                ; 自動ﾓｰﾄﾞ   SW ON
SET LR200                ; 自動ﾓｰﾄﾞ　   選択
CON
RES LR201                ; 手動ﾓｰﾄﾞ　   選択
LD @MR201                ; 手動ﾓｰﾄﾞ   SW ON
SET LR201                ; 手動ﾓｰﾄﾞ　   選択
CON
RES LR200                ; 自動ﾓｰﾄﾞ　   選択
LD LR200                 ; 自動ﾓｰﾄﾞ　   選択
ANB LR201                ; 手動ﾓｰﾄﾞ　   選択
OUT R62010
LDP R51100               ; 原点復帰完了
ORP R49808
SET LR201                ; 手動ﾓｰﾄﾞ　   選択
CON
RES LR200                ; 自動ﾓｰﾄﾞ　   選択
;
;ＡＵＴＯ
;
;ﾌﾗｸﾞ制御
;FLG Control
LD CR2002                ; 常時ON
LD= DM400 #2             ; 現在表示中   画面
OR= DM400 #3             ; 現在表示中   画面
OR= DM400 #4             ; 現在表示中   画面
OR= DM400 #5             ; 現在表示中   画面
OR= DM3100 #1051
ANL
OUT R49201               ; MAIN 画面表示中
LD R52005                ; 自動ﾓｰﾄﾞ停止PB(TP)
OR MR401                 ; ACF残量検出
AND R50001               ; 自動中
SET R50100               ; 自動ﾓｰﾄﾞ停止FLG
CON
SET R49800               ; ｻｲｸﾙ停止・払出し開始
;AUTO処理開始
;AUTO START
LD R49201                ; MAIN 画面表示中
ANP R52004               ; ｽﾀｰﾄ_SW
AND LR200                ; 自動ﾓｰﾄﾞ　   選択
ANB R50001               ; 自動中
AND R1003                ; SAFETY COVER(ON=閉)
DIFU @MR301              ; 自動ｽﾀｰﾄ　  SW ON
;起動条件OKならSTART
;START when States OK
LD @MR301                ; 自動ｽﾀｰﾄ　  SW ON
AND LR200                ; 自動ﾓｰﾄﾞ　   選択
AND MR100                ; ST原点FLG
AND R4105                ; ﾃｰﾌﾟ残少
ANB R53306               ; ACF残量警告
OR R50001                ; 自動中
AND R55000               ; 即停止/TRAP 入力無し
AND R51100               ; 原点復帰完了
ANB @MR305               ; 自動終了
RES R50008               ; ｻｲｸﾙ停止FLG
CON
OUT R50001               ; 自動中
;AUTO 停止監視　開始
;AUTO STOP process Start
LD R50001                ; 自動中
@RES MR1500              ; 断続ﾓｰﾄﾞ
CON
DIFU @MR302              ; 自動ｽﾀｰﾄPLS
LD @MR302                ; 自動ｽﾀｰﾄPLS
OR @MR303                ; 自動ｽﾀｰﾄ　   確認
AND R50001               ; 自動中
ANB @MR304               ; 各ﾀｽｸ動作中
OUT @MR303               ; 自動ｽﾀｰﾄ　   確認
;停止信号入力待ち
;STOP signal wait
LD @MR303                ; 自動ｽﾀｰﾄ　   確認
LD R50500                ; AUTO動作     終了
OR R49800                ; ｻｲｸﾙ停止・払出し開始
ANL
OR @MR304                ; 各ﾀｽｸ動作中
AND R50001               ; 自動中
ANB @MR305               ; 自動終了
OUT @MR304               ; 各ﾀｽｸ動作中
;各ﾓｼﾞｭｰﾙ停止待ち
;Each modul STOP wait
LD @MR304                ; 各ﾀｽｸ動作中
ANF R49807               ; 全ﾕﾆｯﾄ払出し完了
RES MR1005               ; ﾊｰﾌｶｯﾄ前動作
CON
RES MR3004               ; CONVEYER1 POWER ON
CON
RES MR3005               ; CONVEYER2 POWER ON
CON
RES R50500               ; AUTO動作     終了
CON
OUT @MR305               ; 自動終了
;AUTO 終了処理
;AUTO END process
LD R49808
OR R51008                ; 自動終了   ﾌﾞｻﾞｰON
ANB T100                 ; 自動終了   ﾌﾞｻﾞｰON
MPS
RES R50100               ; 自動ﾓｰﾄﾞ停止FLG
CON
OUT R51008               ; 自動終了   ﾌﾞｻﾞｰON
MPP
TMR #100 #4              ; 自動終了   ﾌﾞｻﾞｰON
LD T100                  ; 自動終了   ﾌﾞｻﾞｰON
RES MR1500               ; 断続ﾓｰﾄﾞ
CON
RES LR200                ; 自動ﾓｰﾄﾞ　   選択
CON
ZRES R49800 R49811       ; ｻｲｸﾙ停止・払出し開始
LD @MR301                ; 自動ｽﾀｰﾄ　  SW ON
AND R55000               ; 即停止/TRAP 入力無し
AND R51100               ; 原点復帰完了
ANB @MR305               ; 自動終了
ANB R50001               ; 自動中
ANB MR100                ; ST原点FLG
SET R50507               ; 自動原点無し起動ｴﾗｰ
;ＳＴ 原点部
;
LD<=.L DM602 DM12290     ; RB1 ﾄﾚｲ1列1個目  軸1 現在座標
OR<=.L DM690 DM12290     ; RB1 ﾄﾚｲ1列3個目  軸1 現在座標
LD>=.L DM606 DM12290     ; RB1 ﾄﾚｲ3列7個目  軸1 現在座標
OR>=.L DM698 DM12290     ; RB1 ﾄﾚｲ3列9個目  軸1 現在座標
ANL
AND R1008                ; RB2 HOME END
ANB CR2002               ; 常時ON
OUT MR101                ; RB1原点領域
LD R38108                ; 給材上下 Z軸 P1 位置　表示
OUT MR102                ; RB2原点領域
LD R38208                ; 除材上下 Z軸 P1 位置　表示
OUT MR103                ; RB3原点領域
LD R38308                ; ﾕﾆｯﾄ前後 P1 位置　表示
OUT MR104                ; RB4原点領域
LD R4407                 ; ｽﾄｯﾊﾟｰ1上昇端
AND R4408                ; ｽﾄｯﾊﾟｰ2下降端
AND R4402                ; 給材ﾊﾞｯｸｱｯﾌﾟ下降端
AND R4405                ; 給材位置決め前進端
AND R4401                ; 除材押え前進端
OUT MR105                ; ｺﾝﾍﾞｱ原点
LD R4306                 ; 除材ﾁｬｯｸ開端
AND R4310                ; 除材後退端
AND R4313                ; 除材取る側端
AND R38208               ; 除材上下 Z軸 P1 位置　表示
OUT MR106                ; 除材P&P原点
LD R4308                 ; 給材ﾁｬｯｸ開端
AND R4314                ; 給材取る側端
AND R38108               ; 給材上下 Z軸 P1 位置　表示
OUT MR107                ; 給材P&P原点
LD R4300                 ; ﾊｰﾌｶｰｰﾀｰ下降端
AND R4302                ; ﾊｰﾌｶｰｰﾄ後退端
AND R4304                ; ACF剥離後退端
OUT MR108                ; ﾊｰﾌｶｯﾄ原点
LD R4200                 ; 貼付ﾕﾆｯﾄ下降端
AND R4207                ; ﾜｰｸ前後基準後退端
AND R4202                ; ﾜｰｸ左右基準戻し端
AND R4205                ; ﾜｰｸ横押し戻し端
AND R4208                ; ﾜｰｸ押付け戻し端
AND R38308               ; ﾕﾆｯﾄ前後 P1 位置　表示
OUT MR109                ; 貼付ﾕﾆｯﾄけ原点
LD MR101                 ; RB1原点領域
OR CR2002                ; 常時ON
AND MR105                ; ｺﾝﾍﾞｱ原点
AND MR106                ; 除材P&P原点
AND MR107                ; 給材P&P原点
AND MR108                ; ﾊｰﾌｶｯﾄ原点
AND MR109                ; 貼付ﾕﾆｯﾄけ原点
OUT MR100                ; ST原点FLG
LD MR105                 ; ｺﾝﾍﾞｱ原点
AND MR106                ; 除材P&P原点
AND MR107                ; 給材P&P原点
AND MR108                ; ﾊｰﾌｶｯﾄ原点
AND R38309               ; ﾕﾆｯﾄ前後 P2 位置　表示
OUT MR110                ; 背面使用_貼付動作FLG
LDF R50001               ; 自動中
SET R50008               ; ｻｲｸﾙ停止FLG
;ＭＡＮＵ
;
LD R49201                ; MAIN 画面表示中
AND R51000               ; ｽﾀｰﾄsw ON PLS
LD LR000                 ; 給材動作  ｽﾃｯﾌﾟ選択
OR LR001                 ; 除材動作  ｽﾃｯﾌﾟ選択
OR LR002                 ; 貼付動作  ｽﾃｯﾌﾟ選択
OR LR003                 ; ﾃｰﾌﾟ送り動作  ｽﾃｯﾌﾟ選択
OR LR004                 ; 動作  ｽﾃｯﾌﾟ選択
OR LR005                 ; 動作  ｽﾃｯﾌﾟ選択
OR LR006                 ; 動作  ｽﾃｯﾌﾟ選択
OR LR007                 ; 動作  ｽﾃｯﾌﾟ選択
ANL
AND LR201                ; 手動ﾓｰﾄﾞ　   選択
AND R1003                ; SAFETY COVER(ON=閉)
AND MR100                ; ST原点FLG
DIFU @MR401              ; 手動ｽﾀｰﾄ 　SW ON
LD @MR401                ; 手動ｽﾀｰﾄ 　SW ON
LD= DM3100 #1051
AND R1100                ; BOX START_SW
AND R205                 ; 背面表示無効
AND MR110                ; 背面使用_貼付動作FLG
LD LR002                 ; 貼付動作  ｽﾃｯﾌﾟ選択
OR LR003                 ; ﾃｰﾌﾟ送り動作  ｽﾃｯﾌﾟ選択
ANL
ORL
OR R50002                ; ステップ動作中
AND R55000               ; 即停止/TRAP 入力無し
AND R51100               ; 原点復帰完了
ANB @MR405               ; 手動終了
AND R50012               ; 手動   動作選択済
RES R50008               ; ｻｲｸﾙ停止FLG
CON
OUT R50002               ; ステップ動作中
LD R50002                ; ステップ動作中
DIFU @MR402              ; 手動STEPｽﾀｰﾄPLS
LD @MR402                ; 手動STEPｽﾀｰﾄPLS
OR @MR403                ; 手動ｽﾀｰﾄ　 確認
AND R50002               ; ステップ動作中
ANB @MR404               ; 各ﾀｽｸ動作中
OUT @MR403               ; 手動ｽﾀｰﾄ　 確認
LDB R50500               ; AUTO動作     終了
ANB R50700               ; 給材動作  ｽﾃｯﾌﾟ選択
ANB R50701               ; 除材動作  ｽﾃｯﾌﾟ選択
ANB R50702               ; 貼付動作  ｽﾃｯﾌﾟ選択
ANB R50703               ; ﾃｰﾌﾟ送り動作  ｽﾃｯﾌﾟ選択
ANB R50704               ; 動作  ｽﾃｯﾌﾟ選択
ANB R50705               ; 動作  ｽﾃｯﾌﾟ選択
ANB R50706               ; 動作  ｽﾃｯﾌﾟ選択
ANB R50707               ; 動作  ｽﾃｯﾌﾟ選択
OUB @MR406               ; STEP実行中
LD @MR403                ; 手動ｽﾀｰﾄ　 確認
AND @MR406               ; STEP実行中
OR @MR404                ; 各ﾀｽｸ動作中
AND R50002               ; ステップ動作中
ANB @MR405               ; 手動終了
OUT @MR404               ; 各ﾀｽｸ動作中
LD @MR404                ; 各ﾀｽｸ動作中
ANB @MR406               ; STEP実行中
OUT @MR405               ; 手動終了
;*****　連続手動ﾓｰﾄﾞ許可  /  Mnual mode Demand　*****
LD R42500                ; 連続STEP 許可sw
TMR #204 #30
LDP T204
ALT R42503               ; 連続STEP許可
;動作中
;Ｄｕｒｉｎｇ  Ｏｐｅｒａｔｉｏｎ
LD R50001                ; 自動中
OR R50002                ; ステップ動作中
OR R51101                ; 原点復帰
OUT R50004               ; 動作中
;手動ﾓｰﾄﾞ監視
LDB LR000                ; 給材動作  ｽﾃｯﾌﾟ選択
ANB LR001                ; 除材動作  ｽﾃｯﾌﾟ選択
ANB LR002                ; 貼付動作  ｽﾃｯﾌﾟ選択
ANB LR003                ; ﾃｰﾌﾟ送り動作  ｽﾃｯﾌﾟ選択
ANB LR004                ; 動作  ｽﾃｯﾌﾟ選択
ANB LR005                ; 動作  ｽﾃｯﾌﾟ選択
ANB LR006                ; 動作  ｽﾃｯﾌﾟ選択
ANB LR007                ; 動作  ｽﾃｯﾌﾟ選択
OUB @MR500               ; MEIN_STEPﾓｰﾄﾞ
;Ｃａｌｌ　Ｐｏｓｔ
LDB R50002               ; ステップ動作中
ZRES R50403 R50405       ; Call post   実行中  50405
STG R50400               ; Call Post
ANB R50403               ; Call post   実行中
AND R50002               ; ステップ動作中
SET R50403               ; Call post   実行中
CON
JMP R50404               ; 50404
STG R50404               ; 50404
AND @MR500               ; MEIN_STEPﾓｰﾄﾞ
LDP R51000               ; ｽﾀｰﾄsw ON PLS
ORP R1100                ; BOX START_SW
ANL
ANB R50301               ; MEIN部    Pause
ANB R50006               ; 異常発生中
AND R55000               ; 即停止/TRAP 入力無し
ANB R50103               ; 警報ｾｯﾄﾌﾗｸﾞ
ANB R50901               ; ｼﾘﾝﾀﾞｰｴﾗｰ
ANB R50902               ; Trapしない   異常
AND R1003                ; SAFETY COVER(ON=閉)
JMP R50405               ; 50405
STG R50405               ; 50405
ANB R50400               ; Call Post
RES R50403               ; Call post   実行中
CON
ENDS
LD R50400                ; Call Post
AND R50404               ; 50404
OUT R50401               ; Post Busy
LD R50002                ; ステップ動作中
AND R50405               ; 50405
ORB R50002               ; ステップ動作中
OR R50408                ; POST PASS
OR R50409                ; POST PASS 2
OR R51109                ; 転写部(L) 原点復帰中
OUT R50402               ; Post Ok
LD @MR500                ; MEIN_STEPﾓｰﾄﾞ
AND R51001               ; ｽﾀｰﾄsw ON
ANB R50301               ; MEIN部    Pause
TMR @8 #30
CON
AND @T8
OUT R50409               ; POST PASS 2
;サイクルタイム計算
;Cycle Time Measeremet 
LD R50001                ; 自動中
AND R51314               ; ｺﾝﾍﾞｱｻｲｸﾙ処理 実行中
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC DM15034             ; ｻｲｸﾙﾀｲﾑ
LD R51314                ; ｺﾝﾍﾞｱｻｲｸﾙ処理 実行中
ANF R36400               ; ｻｲｸﾙ時間
MOV DM15034 DM15018      ; ｻｲｸﾙﾀｲﾑ  装置ｻｲｸﾙ
DW #0 DM15034            ; ｻｲｸﾙﾀｲﾑ
LD R53201                ; ACF貼付時間
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC DM15002
LDF R53201               ; ACF貼付時間
MOV DM15002 DM15100
DW #0 DM15002
END
ENDH
