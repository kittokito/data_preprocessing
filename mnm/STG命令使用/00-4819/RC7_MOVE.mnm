DEVICE:53
;MODULE:RC7_MOVE
;MODULE_TYPE:1
;OneSystem SupportMacro
;
;-------------------------------------------
;Macro Name 	: 	RC7_MOVE
;Macro Type	:	Self Holding
;Ver.		:	1.0
;Function	:	EPSON RC7N系 簡易型移動ﾏｸﾛ
;-------------------------------------------
;引数：
;	V0	=	目標位置							V0	=	Target Position Value
;
;	P0	=	RB入力先頭番地	DM番号で指定		P0	=	Input Leading Address for RB
;	P1	=	RB出力先頭番地	DM番号で指定		P1	=	Output Leading Address for RB
;	P2	=	完了コード							P2	=	Macro Complete Code
;			#20			=	正常終了				#20 		=	No Error
;			#21			=	RB応答なし				#21 		=	Non Responce
;			#22			=	ｽﾃｰﾀｽ異常				#22   	  	=	Status Abnormal
;			#23         =   速度設定異常            #23         =   Speed Setting Abnormal
;			#24			=	ｱﾗｲﾒﾝﾄNG				#24			=	Alignment NG
;			Alarmcode	=	Alarm					#Alarmcode =	Alarm
;
;機能：RC7系をﾎﾟｼﾞｼｮﾝNo.を指定して移動させます。  簡易型です。
;IN
BLD P0:#2 #15            ; RB_IN
OUT RC7_MOVE             ; IN MOVE
BLD P0:#0 #0             ; RB_IN
OUT Ready                ; IN Ready
BLD P0:#0 #3             ; RB_IN
OUT Error                ; IN Error
BLD P0:#0 #4             ; RB_IN
OUT EstopOn              ; IN EstopOn
BLD P0:#0 #6             ; RB_IN
OUT SError               ; IN SError
BLD P0:#0 #7             ; RB_IN
OUT Warning              ; IN Warning
BLD P0:#0 #8             ; RB_IN
OUT SV                   ; IN SV
;OUT
LD CSTR                  ; OUT START
BOUT P1:#2 #15           ; RB_OUT
;初期化　/ Initialize
LD @CR2008
SET @R000
;RBｽﾃｰﾀｽﾓﾆﾀｰ / Status Monitoring
LDB Ready                ; IN Ready
ANB Error                ; IN Error
ANB EstopOn              ; IN EstopOn
ANB SError               ; IN SError
LDB Warning              ; IN Warning
OR CR2002                ; 常時ON
ANL
OUT @R014
;Move Parameter storage
STG @R000
MPS
LDA V0                   ; POS
CON
STA P1:#3                ; RB_OUT
MPP
TMH @0 #3
CON
AND @T0
JMP @R001
STG @R001
MPS
AND @R014
SET CSTR                 ; OUT START
CON
JMP @R002
MPP
ANB @R014
MPS
LDB Error                ; IN Error
ANB SError               ; IN SError
ANL
DW #22 @DM0
CON
JMP @R012
MPP
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
JMP @R011
STG @R002
MPS
LD RC7_MOVE              ; IN MOVE
LDB RC7_MOVE             ; IN MOVE
AND= P1:#3 P0:#3         ; RB_OUT  RB_IN
AND @T4
ORL
ANB Error                ; IN Error
ANB SError               ; IN SError
ANB @T2
ANL
JMP @R003
MRD
TMH @4 #20
MRD
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
RES CSTR                 ; OUT START
CON
JMP @R011
MPP
TMR @2 #20
CON
AND @T2
RES CSTR                 ; OUT START
CON
DW #21 @DM0
CON
JMP @R012
STG @R003
MPS
LDB RC7_MOVE             ; IN MOVE
AND= P1:#3 P0:#3         ; RB_OUT  RB_IN
ANL
RES CSTR                 ; OUT START
CON
DW #20 @DM0
CON
JMP @R012
MRD
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
RES CSTR                 ; OUT START
CON
JMP @R011
MPP
TMR @3 #300
CON
AND @T3
DW #21 @DM0
CON
JMP @R012
;Alarm code Stroge
STG @R011
LDA P0:#6                ; RB_IN
CON
STA @DM0
CON
JMP @R012
;終了処理
STG @R012
LDA @DM0
CON
STA P2                   ; C_CODE
CON
MPS
ZRES @R001 @R100
CON
ENDS
MPP
MEND
END
ENDH
