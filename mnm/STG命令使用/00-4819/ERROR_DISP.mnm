DEVICE:53
;MODULE:ERROR_DISP
;MODULE_TYPE:1
;SCRIPT_TYPE:
;OneSystem SupportMacro
;広丘
;-----------------------------------------
;Macro Name 	: 	ERROR_DISP
;Macro Type	:	Self Holding
;Function	:	通常ｴﾗｰ要求
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;引数：
;	V0	=	表示ｸﾞﾙｰﾌﾟ番号			#1〜8
;	V1	=	ﾀｽｸ番号					#0〜31
;	V2	=	通常ｴﾗｰ番号				#0〜4095
;	V7	=	表示ｾﾝｻ番号				#0〜4095
;	V3	=	復帰SW1名称登録番号		#0〜100
;	V4	=	復帰SW2名称登録番号		#0〜100
;	V5	=	復帰SW3名称登録番号		#0〜100
;	V6	=	復帰SW4名称登録番号		#0〜100
;	P9	=	選択SW名称登録番号		#0〜100		(戻り値)
;	P8	=	ｴﾗｰ完了番号				#0〜4095	(戻り値)	
;機能：
;	指定ｸﾞﾙｰﾌﾟのTPに指定ｴﾗｰ番号、指定復帰SW名称にてｱﾗｰﾑ画面表示、選択された復帰SW登録番号を戻り値に格納する
LD R59915                ; _運転ﾓｰﾄﾞ指定
MOV #0 P9                ; ReturnSw_No
MOV #0 P8                ; ReturnErr_No
MEND
LD R46104                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @R000 @R015
CON
MEND
;<h1/>Argument Range Check/引数範囲ﾁｪｯｸ
LD< V0 #1                ; Group_No
OR> V0 #8                ; Group_No
OR< V1 #0                ; Task_No
OR> V1 #100              ; Task_No
LD> V1 #31               ; Task_No
AND< V1 #100             ; Task_No
ORL
OR< V2 #0                ; Error_No
OR> V2 #4095             ; Error_No
OR< V7 #0                ; Sensor_No
OR> V7 #4095             ; Sensor_No
OR< V3 #0                ; SW1_No
OR> V3 #100              ; SW1_No
OR< V4 #0                ; SW2_No
OR> V4 #100              ; SW2_No
OR< V5 #0                ; SW3_No
OR> V5 #100              ; SW3_No
OR< V6 #0                ; SW4_No
OR> V6 #100              ; SW4_No
SET R40008               ; _ｻﾎﾟｰﾄﾏｸﾛ引数異常発生
CON
SET @R015
;<h1/>Init/初期化
;戻り値初期化
;ｴﾗｰ計測ﾀｲﾏ起動
LD @CR2008
ORB @R000
ANB R59915               ; _運転ﾓｰﾄﾞ指定
SET @R014
MOV #0 P9                ; ReturnSw_No
MOV #0 P8                ; ReturnErr_No
MOV #0 @EM6
FMOV #0 @EM7 #4
;<h1/>Exclusive/排他占有
;同一TPｴﾗｰ画面表示排他解放待ちで処理開始
LDB R59915               ; _運転ﾓｰﾄﾞ指定
ANB R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB R48000               ; _Group1　ｴﾗｰ処理排他
ANB R46208               ; _Group1　ｴﾗｰ発生中
SET R48000               ; _Group1　ｴﾗｰ処理排他
SET @R000
CON
SET @R001
;<h1/>Error Code Making/ｴﾗｰｺｰﾄﾞ作成
;・ｴﾗｰｺｰﾄﾞはｸﾞﾙｰﾌﾟ番号＋指定ｴﾗｰｺｰﾄﾞ(0000〜4096）の5桁構成とする
STG @R001
MPS
LDA V0                   ; Group_No
CON
MUL #10000
CON
STA @EM1
MRD
LDA @EM1
CON
ADD V2                   ; Error_No
CON
STA @EM2
MRD
FMOV #0 DM5500 #256      ; _通常ｴﾗｰ(ID0)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
MRD
FMOV #0 DM5800 #256      ; _ﾀｲﾑｱｯﾌﾟｴﾗｰ(ID1)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
MPP
JMP @R002
;<h1/>Return Choise Switch/復帰選択肢SW名称番号制御
;・指定された選択肢SW名称番号をSW1〜SW4の指定ﾃﾞﾊﾞｲｽに格納
;・名称番号が0ならばSWは非表示とする
STG @R002
MPS
AND= V3 #0               ; SW1_No
RES R53101               ; _復帰SW1表示／非表示
CON
MOV #0 DM8002            ; _通常ｴﾗｰ　復帰SW1名称番号
MRD
AND<> V3 #0              ; SW1_No
SET R53101               ; _復帰SW1表示／非表示
CON
MOV V3 DM8002            ; SW1_No  _通常ｴﾗｰ　復帰SW1名称番号
MRD
AND= V4 #0               ; SW2_No
RES R53102               ; _復帰SW2表示／非表示
CON
MOV #0 DM8003            ; _通常ｴﾗｰ　復帰SW2名称番号
MRD
AND<> V4 #0              ; SW2_No
SET R53102               ; _復帰SW2表示／非表示
CON
MOV V4 DM8003            ; SW2_No  _通常ｴﾗｰ　復帰SW2名称番号
MRD
AND= V5 #0               ; SW3_No
RES R53103               ; _復帰SW3表示／非表示
CON
MOV #0 DM8004            ; _通常ｴﾗｰ　復帰SW3名称番号
MRD
AND<> V5 #0              ; SW3_No
SET R53103               ; _復帰SW3表示／非表示
CON
MOV V5 DM8004            ; SW3_No  _通常ｴﾗｰ　復帰SW3名称番号
MRD
AND= V6 #0               ; SW4_No
RES R53104               ; _復帰SW4表示／非表示
CON
MOV #0 DM8005            ; _通常ｴﾗｰ　復帰SW4名称番号
MRD
AND<> V6 #0              ; SW4_No
SET R53104               ; _復帰SW4表示／非表示
CON
MOV V6 DM8005            ; SW4_No  _通常ｴﾗｰ　復帰SW4名称番号
MPP
JMP @R003
;<h1/>Error Message Alarm Bit Making/ｴﾗｰﾒｯｾｰｼﾞｱﾗｰﾑﾋﾞｯﾄ作成
;・ｴﾗｰｺｰﾄﾞ格納
;・ｴﾗｰｺｰﾄﾞ、ｾﾝｻｺｰﾄﾞに対応したｱﾗｰﾑﾋﾞｯﾄ作成(256word-bit出力)
STG @R003
MOV @EM2 DM8000          ; _通常ｴﾗｰ　ｴﾗｰ番号
MPS
MOV V2 DM8001            ; Error_No  _通常ｴﾗｰ　ﾒｯｾｰｼﾞ番号
MPP
AND @R010
RES @R010
CON
JMP @R004
;
;'エラー番号(アラームID0)ONビット生成
;
;@EM7 = V2 / 16
;@EM8 = V2 MOD 16
;
;BSET (KtoV_ERR_ALM:@EM7,@EM8)
;
;
;'センサ番号(アラームID1)ONビット生成
;
;IF V7 > 0 THEN
;
;@EM9 = (V7-4000) / 100
;@EM10 = (V7-4000) -(@EM9*100)
;
;ELSE
;
;@EM9 = 0
;@EM10 = 0
;
;END IF
;
;BSET (KtoV_TERR_ALM:@EM9,@EM10)
;
;SET (@R010)
LD @R003
NCJ #1001
;'エラー番号(アラームID0)ONビット生成
;
;@EM7 = V2 / 16
;
LD CR2002                ; 常時ON
LDA V2                   ; Error_No
CON
EXT
CON
EXT.L
CON
DIV.L +16
CON
STA @EM7
;@EM8 = V2 MOD 16
;
LD CR2002                ; 常時ON
LDA V2                   ; Error_No
CON
EXT
CON
EXT.L
CON
DIV.L +16
CON
LDA.L TM2
CON
STA @EM8
;BSET (KtoV_ERR_ALM:@EM7,@EM8)
;
LD CR2002                ; 常時ON
LDA @EM7
CON
EXT
CON
STA.L Z12
BSET DM5500:Z12 @EM8     ; _通常ｴﾗｰ(ID0)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
;'センサ番号(アラームID1)ONビット生成
;
;IF V7 > 0 THEN
;
LD> V7 #0                ; Sensor_No
NCJ #2000
;@EM9 = (V7-4000) / 100
;
LD CR2002                ; 常時ON
LDA V7                   ; Sensor_No
CON
EXT
CON
SUB.L +4000
CON
EXT.L
CON
DIV.L +100
CON
STA @EM9
;@EM10 = (V7-4000) -(@EM9*100)
;
LD CR2002                ; 常時ON
LDA @EM9
CON
EXT
CON
MUL.L +100
CON
STA.L @VM0
CON
LDA V7                   ; Sensor_No
CON
EXT
CON
SUB.L +4000
CON
SUB.L @VM0
CON
STA @EM10
;ELSE
;
LD CR2002                ; 常時ON
CJ #2001
LABEL #2000
;@EM9 = 0
;
LD CR2002                ; 常時ON
MOV #0 @EM9
;@EM10 = 0
;
LD CR2002                ; 常時ON
MOV #0 @EM10
;END IF
;
LABEL #2001
;BSET (KtoV_TERR_ALM:@EM9,@EM10)
;
LD CR2002                ; 常時ON
LDA @EM9
CON
EXT
CON
STA.L Z12
BSET DM5800:Z12 @EM10    ; _ﾀｲﾑｱｯﾌﾟｴﾗｰ(ID1)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
;SET (@R010)
LD CR2002                ; 常時ON
SET @R010
LABEL #1001
;
;<h1/>Error Display/通常ｴﾗｰ画面表示
STG @R004
SET R46208               ; _Group1　ｴﾗｰ発生中
TMH @1 #5
CON
AND @T1
SET R53100               ; _通常ｴﾗｰ画面表示
CON
JMP @R005
;<h1/>Error Display Release Waiting/通常ｴﾗｰ画面解除待ち
;・ｴﾗｰ復帰選択肢→STARTｽｲｯﾁ待ち
;・安全ｶﾊﾞｰ開、元圧低下、ｴﾘｱｾﾝｻ遮断全復帰待ち(ﾒｲﾝﾀｽｸの場合)
;・画面表示中のﾎﾟｰｽﾞ要求監視
STG @R005
LD<> V1 #100             ; Task_No
ANB R46200               ; _安全ｶﾊﾞｰ1開中
ANB R50700               ; _Group1 ｴｱｰ元圧状態
ANB R46300               ; _Group1　ｴﾘｱｾﾝｻ遮断中
OR= V1 #100              ; Task_No
ANL
AND<> DM8006 #0          ; _通常ｴﾗｰ　復帰選択肢
LD= V0 #1                ; Group_No
ANP R44400               ; *Group1　ｽﾀｰﾄSW
LD= V0 #2                ; Group_No
ANP R44600               ; *Group2　ｽﾀｰﾄSW
ORL
LD= V0 #3                ; Group_No
ANP R44800               ; *Group3　ｽﾀｰﾄSW
ORL
LD= V0 #4                ; Group_No
ANP R45000               ; *Group4　ｽﾀｰﾄSW
ORL
LD= V0 #5                ; Group_No
ANP R45200               ; *Group5　ｽﾀｰﾄSW
ORL
LD= V0 #6                ; Group_No
ANP R45400               ; *Group6　ｽﾀｰﾄSW
ORL
LD= V0 #7                ; Group_No
ANP R45600               ; *Group7　ｽﾀｰﾄSW
ORL
LD= V0 #8                ; Group_No
ANP R45800               ; *Group8　ｽﾀｰﾄSW
ORL
ANL
JMP @R006
;<h1/>Return Choise Switch Check/復帰選択肢SW確認
;・選択された復帰SW番号を戻り値に格納
;・ｴﾗｰ復帰選択肢は選択番号では無く、[名称登録番号]とする
LD @R006
MPS
AND= DM8006 #1           ; _通常ｴﾗｰ　復帰選択肢
MOV V3 @EM6              ; SW1_No
MRD
AND= DM8006 #2           ; _通常ｴﾗｰ　復帰選択肢
MOV V4 @EM6              ; SW2_No
MRD
AND= DM8006 #3           ; _通常ｴﾗｰ　復帰選択肢
MOV V5 @EM6              ; SW3_No
MPP
AND= DM8006 #4           ; _通常ｴﾗｰ　復帰選択肢
MOV V6 @EM6              ; SW4_No
STG @R006
AND<> @EM6 #0
RES R53100               ; _通常ｴﾗｰ画面表示
JMP @R007
;<h1/>Data Collection Making/ﾃﾞｰﾀ収集用ﾃﾞｰﾀ作成
;・ｴﾗｰｺｰﾄﾞ
;・ｴﾗｰ停止時間
;・復帰選択肢
;///Ver1.0.2///
;・データ収集エラー復帰選択肢格納(DC_Err*Ret)に選択肢が格納されない不具合修正
STG @R007
MOV @EM2 DM5080          ; _ﾃﾞｰﾀ収集　ｴﾗｰｺｰﾄﾞ
MOV.D @EM4 DM5081        ; _ﾃﾞｰﾀ収集　停止時間
MOV @EM6 DM5083          ; _ﾃﾞｰﾀ収集　復帰ｽｲｯﾁ番号
MOV #0 DM5084            ; _ﾃﾞｰﾀ収集　ｾﾝｻｱﾗｰﾑ番号1
MOV #0 DM5085            ; _ﾃﾞｰﾀ収集　ｾﾝｻｱﾗｰﾑ番号2
MOV #0 DM5086            ; _ﾃﾞｰﾀ収集　ｾﾝｻｱﾗｰﾑ番号3
SET R46308               ; _ﾃﾞｰﾀ収集　通常ｴﾗｰ解除
RES @R014
CON
JMP @R008
;<h1/>End Process/終了処理
;通常ｴﾗｰ排他開放、初期化
STG @R008
AND<> DM3000 #1010       ; _MainTP　表示中ﾍﾟｰｼﾞNo
FMOV #0 DM5500 #256      ; _通常ｴﾗｰ(ID0)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
FMOV #0 DM5500 #256      ; _通常ｴﾗｰ(ID0)ｱﾗｰﾑ検出ﾜｰﾄﾞﾋﾞｯﾄ
FMOV #0 DM8000 #7        ; _通常ｴﾗｰ　ｴﾗｰ番号
MPS
ZRES R53101 R53104       ; _復帰SW1表示／非表示  _復帰SW4表示／非表示
CON
RES R46208               ; _Group1　ｴﾗｰ発生中
CON
RES R48000               ; _Group1　ｴﾗｰ処理排他
MRD
MOV @EM6 P9              ; ReturnSw_No
MRD
MOV V2 P8                ; Error_No  ReturnErr_No
MRD
MEND
MPP
RES @R000
CON
ENDS
;<h1/>Error M/C Stop Time/ｴﾗｰ停止時間計測
LD @R014
MPS
ANB @T0
TMS @0 #4294967295
MPP
LDA.D #4294967295
CON
SUB.D @T0
CON
STA.D @EM4
END
ENDH
