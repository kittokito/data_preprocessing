DEVICE:53
;MODULE:OneSystem5_Cover
;MODULE_TYPE:0
;OneSystem Master-Module
;
;-----------------------------------------
;ModuleName	:	OneSystem5_Cover
;ModuleType	:	Master Module
;Function	:	Safety Cover/Pause
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;<h1/>Safety Cover1/安全ｶﾊﾞｰ1処理
;●安全ｶﾊﾞｰ有効sig出力
LD>= DM9200 #1           ; _Group1ﾀｽｸ登録数
AND R59800               ; _安全ｶﾊﾞｰ1使用有無
MPS
OUT R40200               ; _安全ｶﾊﾞｰ1使用時出力
MPP
AND CR2008               ; 運転開始時1ｽｷｬﾝON
SET R46900               ; _安全ｶﾊﾞｰ閉要求sig
CON
SET @R009
;●電源投入時安全ｶﾊﾞｰﾛｯｸ&確認
;(ﾛｯｸしたのにﾛｯｸ入力が入らなければﾛｯｸ要求処理へ)
STG @R009
MPS
ANB R44303               ; *Group1　安全ｶﾊﾞｰ入力
SET R46700               ; _安全ｶﾊﾞｰ1開処理中
CON
SET R46200               ; _安全ｶﾊﾞｰ1開中
CON
JMP @R005
MPP
AND R44303               ; *Group1　安全ｶﾊﾞｰ入力
ENDS
;●ｶﾊﾞｰ開要求ｽｲｯﾁ(COVER_SW)待ち
;●安全ｶﾊﾞｰ不正開監視
;	安全ｶﾊﾞｰ開要求が出ていないのに安全ｶﾊﾞｰ入力がOFFした
LD R40200                ; _安全ｶﾊﾞｰ1使用時出力
ANB R46700               ; _安全ｶﾊﾞｰ1開処理中
ANB R44400               ; *Group1　ｽﾀｰﾄSW
ANB R44401               ; *Group1　ｽﾄｯﾌﾟSW
MPS
LD R44303                ; *Group1　安全ｶﾊﾞｰ入力
ANP R44402               ; *Group1　ｶﾊﾞｰ開要求SW
ANL
SET R46700               ; _安全ｶﾊﾞｰ1開処理中
CON
SET R46800               ; _安全ｶﾊﾞｰ1開待ち状態
CON
SET @R000
MRD
LD @R010
ANB R44303               ; *Group1　安全ｶﾊﾞｰ入力
ANL
SET R46700               ; _安全ｶﾊﾞｰ1開処理中
CON
SET R46200               ; _安全ｶﾊﾞｰ1開中
CON
SET @R005
MPP
LDB @R010
ORB R46103               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB R44303               ; *Group1　安全ｶﾊﾞｰ入力
ANL
MOV #65 DM2003           ; _ｼｽﾃﾑｴﾗｰｺｰﾄﾞ
SET @R010
CON
SET R40009               ; _OneSystem優先ｴﾗｰ要求
LDF R46103               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
RES @R010
;●各Taskへﾎﾟｰｽﾞ要求通知(PAUSE)
STG @R000
MPS
LDB R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB R46103               ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB R50700               ; _Group1 ｴｱｰ元圧状態
ANL
SET R44200               ; Grp1 ﾎﾟｰｽﾞ要求
CON
JMP @R001
MPP
LD R46103                ; _停止中　ﾓｰﾄﾞﾌﾗｸﾞ
OR R46104                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
OR R50700                ; _Group1 ｴｱｰ元圧状態
ANL
SET R40500               ; _安全ｶﾊﾞｰ1解除ﾕｰｻﾞｰSTG
CON
JMP @R002
;●各TaskからのHALT出力待ち
;・起動中全TaskからのHALT待ち
STG @R001
AND=.D R42600 R41000     ; Grp1_Task0_ﾎﾟｰｽﾞ許可  _Grp1_Task0_起動中ﾌﾗｸﾞ
SET R40500               ; _安全ｶﾊﾞｰ1解除ﾕｰｻﾞｰSTG
CON
JMP @R002
;●SC_Before
;ｶﾊﾞｰﾛｯｸ解除可／不可確認(ﾕｰｻﾞｰ解放)
STG @R002
ANB R40500               ; _安全ｶﾊﾞｰ1解除ﾕｰｻﾞｰSTG
MPS
AND R40600               ; _安全ｶﾊﾞｰ1ﾛｯｸ解除OK
MPS
LD DM11150.1             ; Running
OR DM11350.5             ; Pause
ANL
SET DM11350.5            ; Pause
CON
AND DM11150.2            ; Paused
RES DM11350.5            ; Pause
CON
RES R40600               ; _安全ｶﾊﾞｰ1ﾛｯｸ解除OK
CON
JMP @R003
MPP
LDB DM11150.1            ; Running
ANB DM11350.5            ; Pause
ANL
RES R40600               ; _安全ｶﾊﾞｰ1ﾛｯｸ解除OK
CON
JMP @R003
MPP
AND R40601               ; _安全ｶﾊﾞｰ1ﾛｯｸ解除NG
RES R40601               ; _安全ｶﾊﾞｰ1ﾛｯｸ解除NG
CON
ZRES R42600:#0 R42600:#31; Grp1_Task0_ﾎﾟｰｽﾞ許可
CON
RES R46800               ; _安全ｶﾊﾞｰ1開待ち状態
CON
RES R44200               ; Grp1 ﾎﾟｰｽﾞ要求
CON
RES R46700               ; _安全ｶﾊﾞｰ1開処理中
CON
ENDS
;●安全ｶﾊﾞｰﾛｯｸ解除
STG @R003
RES R46900               ; _安全ｶﾊﾞｰ閉要求sig
CON
JMP @R004
STG @R004
RES R46800               ; _安全ｶﾊﾞｰ1開待ち状態
CON
SET R46200               ; _安全ｶﾊﾞｰ1開中
CON
JMP @R005
;●安全ｶﾊﾞｰﾛｯｸ要求(操作BOXｽﾀｰﾄSW or 3連BOXｽﾀｰﾄSW)待ち
STG @R005
LDP R44400               ; *Group1　ｽﾀｰﾄSW
ORP R44406               ; *Group1　ｶﾊﾞｰ継続実行SW
ANL
SET R46900               ; _安全ｶﾊﾞｰ閉要求sig
CON
JMP @R006
;●安全ｶﾊﾞｰﾛｯｸ〜入力確認
;(ﾛｯｸ入力入らなければ再度ﾛｯｸ要求待ちへ)
STG @R006
MPS
AND R44303               ; *Group1　安全ｶﾊﾞｰ入力
SET R40501               ; _安全ｶﾊﾞｰ1ﾛｯｸﾕｰｻﾞｰSTG
CON
JMP @R007
MPP
ANB R44303               ; *Group1　安全ｶﾊﾞｰ入力
TMR @0 DM9352            ; _安全ｶﾊﾞｰﾛｯｸ確認時間
CON
AND @T0
RES R46900               ; _安全ｶﾊﾞｰ閉要求sig
CON
JMP @R005
;●SC_After
;ｶﾊﾞｰﾛｯｸ可／不可確認(ﾕｰｻﾞｰ解放)
STG @R007
ANB R40501               ; _安全ｶﾊﾞｰ1ﾛｯｸﾕｰｻﾞｰSTG
MPS
AND R40700               ; _安全ｶﾊﾞｰ1ﾛｯｸOK
SET DM11350.6            ; Continue
CON
ANB DM11150.2            ; Paused
RES DM11350.6            ; Continue
CON
RES R46200               ; _安全ｶﾊﾞｰ1開中
CON
RES R40700               ; _安全ｶﾊﾞｰ1ﾛｯｸOK
CON
JMP @R008
MPP
AND R40701               ; _安全ｶﾊﾞｰ1ﾛｯｸNG
RES R40701               ; _安全ｶﾊﾞｰ1ﾛｯｸNG
CON
JMP @R003
STG @R008
ANB R44400               ; *Group1　ｽﾀｰﾄSW
ANB R50600               ; _元圧検出1無効宣言
RES R44200               ; Grp1 ﾎﾟｰｽﾞ要求
CON
ZRES R42600:#0 R42600:#31; Grp1_Task0_ﾎﾟｰｽﾞ許可
CON
RES R46700               ; _安全ｶﾊﾞｰ1開処理中
CON
ENDS
;<h1/>Safety Cover Open Sig Logic/安全ｶﾊﾞｰ出力論理設定
;LockRequest = 安全ｶﾊﾞｰ処理ｼｰｹﾝｽ内でｶﾊﾞｰ閉時にSET/ｶﾊﾞｰ開時にRESする中間sig
;
;ﾕｰｻﾞｰ設定(CoverOUT_Logic)の設定により、実ｺｲﾙ(COVER_LOCK*)出力の反転を行う
;OFF：A接点でｶﾊﾞｰ開出力
;ON：B接点でｶﾊﾞｰ開出力
;ｶﾊﾞｰ開出力をA接点に(ONでｶﾊﾞｰ開)
LDB R59408               ; _安全ｶﾊﾞｰ出力論理設定
MPS
AND R46900               ; _安全ｶﾊﾞｰ閉要求sig
SET R44310               ; *Group1　安全ｶﾊﾞｰﾛｯｸ
MPP
ANB R46900               ; _安全ｶﾊﾞｰ閉要求sig
RES R44310               ; *Group1　安全ｶﾊﾞｰﾛｯｸ
;ｶﾊﾞｰ開出力をB接点に(OFFでｶﾊﾞｰ開)
LD R59408                ; _安全ｶﾊﾞｰ出力論理設定
MPS
AND R46900               ; _安全ｶﾊﾞｰ閉要求sig
RES R44310               ; *Group1　安全ｶﾊﾞｰﾛｯｸ
MPP
ANB R46900               ; _安全ｶﾊﾞｰ閉要求sig
SET R44310               ; *Group1　安全ｶﾊﾞｰﾛｯｸ
END
ENDH
