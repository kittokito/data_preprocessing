DEVICE:53
;MODULE:Slave
;MODULE_TYPE:0
LD R40013                ; 非常停止
ZRES @MR000 @MR1015      ; PAUSE条件確認
;ミューティング処理
;Muting processing
LDP R4102                ; INDEX 1ST 治具ｴｱｰ上下 上昇端
OR MR2102                ; ﾐｭｰﾃｨﾝｸﾞ中
ANB @MR500               ; ﾐｭｰﾃｨﾝｸﾞ禁止
ANB R40104               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
AND R4102                ; INDEX 1ST 治具ｴｱｰ上下 上昇端
OUT MR2102               ; ﾐｭｰﾃｨﾝｸﾞ中
TMH @10 #4               ; ｳｴｲﾄﾀｲﾏｰ010
CON
AND @T10                 ; ｳｴｲﾄﾀｲﾏｰ010
OUT R40008               ; ﾐｭｰﾃｨﾝｸﾞ中
LDB MR2102               ; ﾐｭｰﾃｨﾝｸﾞ中
AND R40104               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
RES R40104               ; ﾐｭｰﾃｨﾝｸﾞﾘｾｯﾄ
;Reset Light Curtain / ライトカーテンリセット
LD CR2002                ; 常時ON
TMH @7 #100              ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ遅延
LDP R40003               ; 原点復帰中
ORP R40001               ; 自動動作中
ORP R40002               ; ﾒﾝﾃﾅﾝｽ動作中
ORP MR2110               ; INDEX 非常停止
AND R42202               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
ORP @T7                  ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ遅延
LDF R40005               ; ﾎﾟｰｽﾞ中
AND R42202               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
AND R1101                ; ﾗｲﾄｶｰﾃﾝ
ORL
RES R42202               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
CON
SHOT #10 MR2101          ; ﾗｲﾄｶｰﾃﾝ ﾘｾｯﾄ
LD MR2101                ; ﾗｲﾄｶｰﾃﾝ ﾘｾｯﾄ
SHOT #20 R42206          ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ中
LDB R40008               ; ﾐｭｰﾃｨﾝｸﾞ中
ANB R1101                ; ﾗｲﾄｶｰﾃﾝ
ANB MR2101               ; ﾗｲﾄｶｰﾃﾝ ﾘｾｯﾄ
ANB R42206               ; ﾗｲﾄｶｰﾃﾝﾘｾｯﾄ中
SET R42202               ; ﾗｲﾄｶｰﾃﾝ遮光記憶
LD MR2101                ; ﾗｲﾄｶｰﾃﾝ ﾘｾｯﾄ
RES R42210               ; INDEX非常停止遮断
CON
RES R42209               ; HOME完了前LC遮光
CON
RES R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
CON
SHOT #30 @MR506          ; LCﾘｾｯﾄﾃﾞｨﾚｲ?
LDB R40008               ; ﾐｭｰﾃｨﾝｸﾞ中
ANB R1101                ; ﾗｲﾄｶｰﾃﾝ
ANB MR2101               ; ﾗｲﾄｶｰﾃﾝ ﾘｾｯﾄ
ANB @MR506               ; LCﾘｾｯﾄﾃﾞｨﾚｲ?
ANB R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
LD R42212                ; INDEX監視1
OR R42213                ; INDEX監視2
ANL
RES MR2110               ; INDEX 非常停止
CON
RES MR2103               ; INDEX Power ON
CON
SET R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
;一時停止条件
;１、Ｍａｉｎ　Ａｉｒ
;２、ＰＡＵＳＥ ＳＷ
;３、動作中のカバー開
LDB R40013               ; 非常停止
TMR @0 #80               ; 起動遅延
CON
ANP @T0                  ; 起動遅延
SET @MR000               ; PAUSE条件確認
STG @MR000               ; PAUSE条件確認
LDB R42008               ; MAIN AIR
OR R42001                ; PAUSE sw
OR R40006                ; PAUSE要求
OR R42202                ; ﾗｲﾄｶｰﾃﾝ遮光記憶
OR R40115                ; ST1 PAUSE要求
ANL
LDB LR208                ; ｴﾗｰﾘｾｯﾄｷｰ要求
LD LR208                 ; ｴﾗｰﾘｾｯﾄｷｰ要求
AND LR209                ; ｴﾗｰﾘｾｯﾄｷｰ動作OK
ORL
ANL
RES R40115               ; ST1 PAUSE要求
CON
RES R40006               ; PAUSE要求
CON
SET R40005               ; ﾎﾟｰｽﾞ中
CON
JMP @MR001               ; PAUSE開始
;一時停止確認
LDB R40200               ; ﾎﾟｰｽﾞ禁止1
ANB R40201               ; ﾎﾟｰｽﾞ禁止2
ANB R40202               ; ﾎﾟｰｽﾞ禁止3
ANB R40203               ; ﾎﾟｰｽﾞ禁止4
ANB R40215               ; ﾎﾟｰｽﾞ禁止16
ANB R53900               ; RB動作中
OUT @MR505               ; 全STPack無し
STG @MR001               ; PAUSE開始
MPS
AND @MR505               ; 全STPack無し
TMH @18 #5               ; ｳｴｲﾄﾀｲﾏｰ018
CON
AND @T18                 ; ｳｴｲﾄﾀｲﾏｰ018
JMP @MR002               ; ST1-4停止要求
MPP
LDB @MR505               ; 全STPack無し
AND R42008               ; MAIN AIR
ANB R42001               ; PAUSE sw
AND R42000               ; START sw
ANL
JMP @MR004               ; 004
STG @MR004               ; 004
RES R40005               ; ﾎﾟｰｽﾞ中
CON
JMP @MR000               ; PAUSE条件確認
STG @MR002               ; ST1-4停止要求
JMP @MR003               ; 003
STG @MR003               ; 003
SET R40007               ; ﾎﾟｰｽﾞOK
CON
SET R42108               ; COVER_OPEN
CON
JMP @MR100               ; ﾎﾟｰｽﾞ復帰StartSW待ち
;RBなどでﾎﾟｰｽﾞ前の状態を記憶したい場合はここで行う
;カバー閉じ後、ＳＴＡＲＴ ｓｗ　ＯＮ
STG @MR100               ; ﾎﾟｰｽﾞ復帰StartSW待ち
AND R42008               ; MAIN AIR
ANB R42001               ; PAUSE sw
AND R42000               ; START sw
RES R42108               ; COVER_OPEN
CON
JMP @MR101               ; 101
STG @MR101               ; 101
TMH @1 #50               ; ｶﾊﾞｰ閉確認遅延
CON
AND @T1                  ; ｶﾊﾞｰ閉確認遅延
JMP @MR102               ; 102
STG @MR102               ; 102
MPS
LDB R42008               ; MAIN AIR
OR R42001                ; PAUSE sw
ORB R1101                ; ﾗｲﾄｶｰﾃﾝ
ANL
JMP @MR103               ; 103
MPP
LD R42008                ; MAIN AIR
ANB R42001               ; PAUSE sw
AND R1101                ; ﾗｲﾄｶｰﾃﾝ
ANL
JMP @MR104               ; 復帰処理
;ｷｬﾝｾﾙされた場合は復帰待ちへ戻る / Return to wait start sw on when Air conditon was NG.
STG @MR103               ; 103
SET R42108               ; COVER_OPEN
CON
JMP @MR100               ; ﾎﾟｰｽﾞ復帰StartSW待ち
;ｶﾊﾞｰ閉確認
;Cover Close Detect
;RB復帰が居る場合はここでRB復帰のｻﾌﾞﾙｰﾁﾝを呼ぶ
STG @MR104               ; 復帰処理
MPS
AND R40001               ; 自動動作中
JMP @MR105               ; 105
MPP
ANB R40001               ; 自動動作中
JMP @MR105               ; 105
STG @MR105               ; 105
MPS
LD R40009                ; 原点復帰完了
OR R40003                ; 原点復帰中
ANL
JMP @MR200
MPP
LDB R40009               ; 原点復帰完了
ANB R40003               ; 原点復帰中
ANL
JMP @MR615               ; Pause処理終了
;RBなど、ﾎﾟｰｽﾞ復帰時に処理が必要な場合はここで行う
;
STG @MR200
LD @MR114                ; RB復帰有り
AND @MR115               ; RB復帰完了
ORB @MR114               ; RB復帰有り
ANL
JMP @MR201               ; 201
STG @MR201               ; 201
RES @MR114               ; RB復帰有り
CON
RES @MR115               ; RB復帰完了
CON
JMP @MR300               ; 300
;Index復帰処理 / Index Recovery
STG @MR300               ; 300
MPS
LD R40009                ; 原点復帰完了
LD R40003                ; 原点復帰中
AND R42214               ; INDEX原点復帰完了
ORL
ANL
JMP @MR301               ; 301
MPP
LDB R40009               ; 原点復帰完了
LDB R40003               ; 原点復帰中
ORB R42214               ; INDEX原点復帰完了
ANL
ANL
JMP @MR615               ; Pause処理終了
STG @MR301               ; 301
MPS
AND<> DM1200 #287        ; 通常ｴﾗｰ番号
MPS
ANB R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
JMP @MR302               ; 通常時INDEX処理
MPP
AND R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
SET @MR900               ; Index遮光復帰
CON
JMP @MR614               ; LC遮光時INDEX処理
MPP
AND= DM1200 #287         ; 通常ｴﾗｰ番号
SET R40014               ; TRAP
;<h1/>①通常時INDEX復帰動作
;Index復帰
LD R42213                ; INDEX監視2
AND R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
RES R42213               ; INDEX監視2
CON
ZRES MR2108 MR2115       ; INDEX 起動  INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 4ﾋﾞｯﾄ　16
CON
RES MR2103               ; INDEX Power ON
CON
RES MR2106               ; INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 1桁目
CON
@SET @MR001              ; PAUSE開始
CON
ZRES @MR104 @MR615       ; 復帰処理  Pause処理終了
STG @MR302               ; 通常時INDEX処理
SET MR2110               ; INDEX 非常停止
CON
ANB R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
SET R42213               ; INDEX監視2
CON
TMH @6 #30               ; ｳｴｲﾄﾀｲﾏｰ006
CON
AND @T6                  ; ｳｴｲﾄﾀｲﾏｰ006
LD T200                  ; Index電源再投入ﾃﾞｨﾚｲ
OR R2103                 ; INDEX Power ON
ANL
JMP @MR303               ; 303
STG @MR303               ; 303
SET MR2103               ; INDEX Power ON
CON
SET MR2107               ; INDEX ﾘｾｯﾄ
CON
AND R1105                ; INDEX 起動入力待ち
RES MR2107               ; INDEX ﾘｾｯﾄ
CON
JMP @MR304               ; 304
;位置決め下降
;通常復帰時はIndex戻し動作は行わない。
STG @MR304               ; 304
RES MR6600               ; INDEX 位置決め上下 上昇
CON
SET MR6601               ; INDEX 位置決め上下 下降
CON
AND R4101                ; INDEX 位置決め上下 下降端
JMP @MR305               ; 305
;シリンダーの復帰処理などを記述
STG @MR305               ; 305
JMP @MR306               ; 306
STG @MR306               ; 306
JMP @MR615               ; Pause処理終了
;<h1/>②LC遮光時INDEX復帰動作
;LC遮光時復帰完了待ち
STG @MR614               ; LC遮光時INDEX処理
AND @MR915               ; Index復帰処理終了
RES @MR915               ; Index復帰処理終了
CON
JMP @MR615               ; Pause処理終了
;<h1/>PAUSE終了処理
STG @MR615               ; Pause処理終了
RES R42213               ; INDEX監視2
CON
RES R40007               ; ﾎﾟｰｽﾞOK
CON
RES R40005               ; ﾎﾟｰｽﾞ中
CON
JMP @MR000               ; PAUSE条件確認
;<h1/>Sub Routing / ｻﾌﾞﾙｰﾁﾝ
;ﾗｲﾄｶｰﾃﾝ遮光時 Index復帰処理
STG @MR900               ; Index遮光復帰
SET MR2110               ; INDEX 非常停止
CON
RES R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
CON
ANB R42211               ; ﾗｲﾄｶｰﾃﾝ遮光
SET R42213               ; INDEX監視2
CON
TMH @11 #30              ; ｳｴｲﾄﾀｲﾏｰ011
CON
AND @T11                 ; ｳｴｲﾄﾀｲﾏｰ011
LD R2103                 ; INDEX Power ON
ORB R18500               ; INDEX Power Cool down
ANL
JMP @MR901
STG @MR901
SET MR2103               ; INDEX Power ON
CON
SET MR2107               ; INDEX ﾘｾｯﾄ
CON
AND R1105                ; INDEX 起動入力待ち
RES MR2107               ; INDEX ﾘｾｯﾄ
CON
JMP @MR902
;位置決め下降　治具ｴｱ供給下降
;Positioner Decent and Jig air supply head Decent
STG @MR902
JMP @MR903
STG @MR903
RES MR6600               ; INDEX 位置決め上下 上昇
CON
SET MR6601               ; INDEX 位置決め上下 下降
CON
RES MR6612               ; 1ST 治具  Close
CON
RES MR6610               ; 1ST 治具  Open 
CON
RES MR6608               ; 2ST 治具  Close
CON
RES MR6606               ; 3ST 治具  Close
CON
RES MR6604               ; 4ST 治具  Close
CON
RES MR6602               ; 4ST 治具  Open 
CON
JMP @MR904
STG @MR904
SET @MR500               ; ﾐｭｰﾃｨﾝｸﾞ禁止
CON
RES MR6614               ; INDEX 治具ｴｱｰ上下 上昇
CON
AND R4101                ; INDEX 位置決め上下 下降端
AND R4103                ; INDEX 1ST 治具ｴｱｰ上下 下降端
AND R4105                ; INDEX 2ST 治具ｴｱｰ上下 下降端
AND R4107                ; INDEX 3ST 治具ｴｱｰ上下 下降端
AND R4109                ; INDEX 4ST 治具ｴｱｰ上下 下降端
JMP @MR905
STG @MR905
TMH @29 #20              ; ｳｴｲﾄﾀｲﾏｰ029
CON
AND @T29                 ; ｳｴｲﾄﾀｲﾏｰ029
RES @MR500               ; ﾐｭｰﾃｨﾝｸﾞ禁止
CON
JMP @MR909
;INDEXをLC遮断前の位置へ回転
;INDEX Rotation Before LC Cut Position
STG @MR909
AND R1105                ; INDEX 起動入力待ち
PMOV DM1359 #0 MR2111 #0 #5;INDEX位置記憶  INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 0ﾋﾞｯﾄ  1
JMP @MR911
STG @MR911
TMH @23 #20              ; ｳｴｲﾄﾀｲﾏｰ023
CON
AND @T23                 ; ｳｴｲﾄﾀｲﾏｰ023
SET MR2106               ; INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 1桁目
CON
TMH @24 #20              ; ｳｴｲﾄﾀｲﾏｰ024
CON
AND @T24                 ; ｳｴｲﾄﾀｲﾏｰ024
SET R42203               ; INDEX回転中（SET/RST）
CON
SET MR2108               ; INDEX 起動
CON
JMP @MR912
STG @MR912
ANB R1105                ; INDEX 起動入力待ち
RES MR2108               ; INDEX 起動
CON
ZRES MR2111 MR2115       ; INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 0ﾋﾞｯﾄ  1  INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 4ﾋﾞｯﾄ　16
CON
RES MR2106               ; INDEX ﾌﾟﾛｸﾞﾗﾑNo.指定 1桁目
CON
JMP @MR913
STG @MR913
AND R1104                ; INDEX 位置決め完了
SET MR2109               ; INDEX ｱﾝｻ
CON
JMP @MR914
STG @MR914
ANB R1104                ; INDEX 位置決め完了
RES MR2109               ; INDEX ｱﾝｻ
CON
RES R42203               ; INDEX回転中（SET/RST）
CON
SET @MR915               ; Index復帰処理終了
CON
ENDS
END
ENDH
