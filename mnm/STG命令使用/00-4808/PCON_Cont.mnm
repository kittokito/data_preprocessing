DEVICE:53
;MODULE:PCON_Cont
;MODULE_TYPE:0
;SCRIPT_TYPE:
;<h1/>ﾃﾞｨﾚｲﾀｲﾏ
LD CR2002                ; 常時ON
TMR #1000 #50            ; 電源ON後ﾃﾞｨﾚｲﾀｲﾏ
;<h1/>ﾌﾘｯｶ制御
LD CR2002                ; 常時ON
FLIK #30 #30 @MR000
;<h1/>非常停止処理
LDP R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @MR100 @MR115       ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
;<h1/>RB Controller ﾃﾞｰﾀｾｯﾄ
LD CR2002                ; 常時ON
DW.D #10 DM10710         ; 搬送ｽﾗｲﾀﾞRB 位置決め幅
CON
DW.D #10 DM10718         ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 位置決め幅
LD CR2002                ; 常時ON
DW.D #30 DM10713         ; 搬送ｽﾗｲﾀﾞRB 加減速度
CON
DW.D #30 DM10721         ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 加減速度
LD CR2008                ; 運転開始時1ｽｷｬﾝON
SET MR9500               ; 分離ｽﾗｲﾀﾞRB ｻｰﾎﾞON
CON
SET MR9700               ; ﾄﾚｲｴﾚﾍﾞｰﾀRB ｻｰﾎﾞON
;<h1/>RB1  搬送ｽﾗｲﾀﾞRB
;状態確認
LD T1000                 ; 電源ON後ﾃﾞｨﾚｲﾀｲﾏ
BAND DM10515 #3          ; 搬送ｽﾗｲﾀﾞRB 状態信号
ANB MR18715              ; ｴﾚﾍﾞｰﾀRB ｱﾗｰﾑ発生中
OUT MR18515              ; 搬送ｽﾗｲﾀﾞRB ｱﾗｰﾑ発生中
CON
OUT @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
LDP MR18515              ; 搬送ｽﾗｲﾀﾞRB ｱﾗｰﾑ発生中
ANB R39004               ; ｴﾗｰ中
SET @MR005
CON
SET R39004               ; ｴﾗｰ中
CON
DW #8 @DM1
CON
DW #1000 @DM9
CON
LDA DM60009              ; Sensor ERROR No.
CON
STA @DM2
CON
SET @R100                ; ｱﾗｰﾑ発生
CON
LDA DM10506
CON
STA DM11782              ; ｱﾗｰﾑ画面　軸ｴﾗｰｺｰﾄﾞ
CON
SET @R000
STG @R000
AND @R101                ; ｱﾗｰﾑ解除
DW #0 DM60009            ; Sensor ERROR No.
CON
AND @R105                ; SW1
SET MR24300              ; RB1　ｱﾗｰﾑﾘｾｯﾄ要求
CON
JMP @R001
STG @R001
TMR #530 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T530                 ; ｳｪｲﾄﾀｲﾏ
AND MR24303              ; ｴﾗｰﾘｾｯﾄ処理完了
RES @MR005
CON
RES R39004               ; ｴﾗｰ中
CON
RES MR24303              ; ｴﾗｰﾘｾｯﾄ処理完了
CON
RES @R101                ; ｱﾗｰﾑ解除
CON
ENDS
STG MR24300              ; RB1　ｱﾗｰﾑﾘｾｯﾄ要求
BSET DM10715 #3          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
MPS
BANB DM10515 #3          ; 搬送ｽﾗｲﾀﾞRB 状態信号
JMP MR24301
MPP
BAND DM10515 #3          ; 搬送ｽﾗｲﾀﾞRB 状態信号
TMR #532 #30
CON
AND T532
JMP MR24302
STG MR24301
BAND DM10515 #4          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BAND DM10515 #14         ; 搬送ｽﾗｲﾀﾞRB 状態信号
JMP MR24302
STG MR24302
SET MR24303              ; ｴﾗｰﾘｾｯﾄ処理完了
CON
ENDS
BLD DM10515 #4           ; 搬送ｽﾗｲﾀﾞRB 状態信号
BAND DM10515 #14         ; 搬送ｽﾗｲﾀﾞRB 状態信号
BANB DM10515 #15         ; 搬送ｽﾗｲﾀﾞRB 状態信号
OUT @MR101
;原点復帰
LD MR18500               ; 搬送ｽﾗｲﾀﾞRB 原点復帰要求
AND MR16714              ; 搬送ｽﾗｲﾀﾞ ﾄﾚｲ分離位置移動可
ANB @MR1001              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰中
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
RES MR18502              ; 搬送ｽﾗｲﾀﾞRB 原点復帰完了
CON
SET @MR1001              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰中
CON
SET MR18501              ; 搬送ｽﾗｲﾀﾞRB 原点復帰中
CON
RES MR18500              ; 搬送ｽﾗｲﾀﾞRB 原点復帰要求
CON
SET @MR1000              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰開始
STG @MR1000              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰開始
RES MR18500              ; 搬送ｽﾗｲﾀﾞRB 原点復帰要求
CON
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BRES DM10715 #0          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
BRES DM10715 #2          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
BRES DM10715 #3          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
BSET DM10715 #4          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR1003
STG @MR1003
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #2          ; 搬送ｽﾗｲﾀﾞRB 状態信号
TMR #600 #5              ; ｳｪｲﾄ
CON
AND T600                 ; ｳｪｲﾄ
JMP @MR1004
STG @MR1004
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BSET DM10715 #1          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR1005
STG @MR1005
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #0          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BRES DM10715 #1          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR1006
STG @MR1006
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #2          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BAND DM10515 #0          ; 搬送ｽﾗｲﾀﾞRB 状態信号
JMP @MR1007
STG @MR1007
RES @MR1001              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰中
CON
RES MR18501              ; 搬送ｽﾗｲﾀﾞRB 原点復帰中
CON
SET MR18502              ; 搬送ｽﾗｲﾀﾞRB 原点復帰完了
CON
ENDS
;ﾎﾟｲﾝﾄ移動
;  Ｐ１  トレイ分離位置
;  Ｐ２  ワーク受取り位置
;  Ｐ３  エレベータ載替え位置
LD MR18503               ; 搬送ｽﾗｲﾀﾞRB P1移動要求
OR MR18504               ; 搬送ｽﾗｲﾀﾞRB P2移動要求
OR MR18505               ; 搬送ｽﾗｲﾀﾞRB P3移動要求
AND MR18502              ; 搬送ｽﾗｲﾀﾞRB 原点復帰完了
ANB @MR1001              ; RB1(搬送ｽﾗｲﾀﾞ) 原点復帰中
ANP MR18508              ; 搬送ｽﾗｲﾀﾞRB 移動指令
ANB @MR2001              ; RB1(搬送ｽﾗｲﾀﾞ) 動作中
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
ZRES MR17304 MR17307     ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
CON
SET @MR2001              ; RB1(搬送ｽﾗｲﾀﾞ) 動作中
CON
SET @MR2000              ; RB1(ﾄﾚｲ搬送) 動作開始
STG @MR2000              ; RB1(ﾄﾚｲ搬送) 動作開始
RES MR18508              ; 搬送ｽﾗｲﾀﾞRB 移動指令
CON
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #2          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BRES DM10715 #2          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
BRES DM10715 #3          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR2003
STG @MR2003
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
MPS
AND MR18503              ; 搬送ｽﾗｲﾀﾞRB P1移動要求
MOV.L DM4200 DM10708     ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 目標位置
MOV DM4202 DM10712       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 速度
MOV DM4204 DM10713       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 加減速度
MRD
AND MR18504              ; 搬送ｽﾗｲﾀﾞRB P2移動要求
MOV.L DM4206 DM10708     ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 目標位置
MOV DM4208 DM10712       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 速度
MOV DM4210 DM10713       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 加減速度
MRD
AND MR18505              ; 搬送ｽﾗｲﾀﾞRB P3移動要求
MOV.L DM4212 DM10708     ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 目標位置
MOV DM4214 DM10712       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 速度
MOV DM4216 DM10713       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  搬送ｽﾗｲﾀﾞRB 加減速度
MPP
TMR #884 #2              ; ｳｪｲﾄﾀｲﾏ
CON
AND T884                 ; ｳｪｲﾄﾀｲﾏ
JMP @MR2004
STG @MR2004
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
MPS
AND MR18503              ; 搬送ｽﾗｲﾀﾞRB P1移動要求
SET MR18509              ; 搬送ｽﾗｲﾀﾞRB 移動中
CON
MPS
ANB MR17304              ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
BSET DM10715 #0          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR2005
MPP
AND MR17304              ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
JMP @MR2007
MRD
AND MR18504              ; 搬送ｽﾗｲﾀﾞRB P2移動要求
SET MR18509              ; 搬送ｽﾗｲﾀﾞRB 移動中
CON
MPS
ANB MR17305              ; TPLP_搬送ｽﾗｲﾀﾞ P2表示
BSET DM10715 #0          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR2005
MPP
AND MR17305              ; TPLP_搬送ｽﾗｲﾀﾞ P2表示
JMP @MR2007
MPP
AND MR18505              ; 搬送ｽﾗｲﾀﾞRB P3移動要求
SET MR18509              ; 搬送ｽﾗｲﾀﾞRB 移動中
CON
MPS
ANB MR17306              ; TPLP_搬送ｽﾗｲﾀﾞ P3表示
BSET DM10715 #0          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR2005
MPP
AND MR17306              ; TPLP_搬送ｽﾗｲﾀﾞ P3表示
JMP @MR2007
STG @MR2005
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #0          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BRES DM10715 #0          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
JMP @MR2006
STG @MR2006
ANB @MR100               ; 搬送ｽﾗｲﾀﾞｱﾗｰﾑ発生中
BANB DM10515 #2          ; 搬送ｽﾗｲﾀﾞRB 状態信号
BAND DM10515 #0          ; 搬送ｽﾗｲﾀﾞRB 状態信号
TMH #885 #10             ; ｳｪｲﾄﾀｲﾏ
CON
AND T885                 ; ｳｪｲﾄﾀｲﾏ
JMP @MR2007
STG @MR2007
MPS
AND MR18503              ; 搬送ｽﾗｲﾀﾞRB P1移動要求
ZRES MR17304 MR17307     ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
CON
SET MR17304              ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
CON
JMP @MR2008
MRD
AND MR18504              ; 搬送ｽﾗｲﾀﾞRB P2移動要求
ZRES MR17304 MR17307     ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
CON
SET MR17305              ; TPLP_搬送ｽﾗｲﾀﾞ P2表示
CON
JMP @MR2008
MPP
AND MR18505              ; 搬送ｽﾗｲﾀﾞRB P3移動要求
ZRES MR17304 MR17307     ; TPLP_搬送ｽﾗｲﾀﾞ P1表示
CON
SET MR17306              ; TPLP_搬送ｽﾗｲﾀﾞ P3表示
CON
JMP @MR2008
STG @MR2008
RES MR18508              ; 搬送ｽﾗｲﾀﾞRB 移動指令
CON
BRES DM10715 #2          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
BRES DM10715 #3          ; 搬送ｽﾗｲﾀﾞRB 制御信号
CON
ZRES MR18503 MR18506     ; 搬送ｽﾗｲﾀﾞRB P1移動要求  予備
CON
RES MR18509              ; 搬送ｽﾗｲﾀﾞRB 移動中
CON
RES @MR2001              ; RB1(搬送ｽﾗｲﾀﾞ) 動作中
CON
ENDS
;<h1/>RB2  ｴﾚﾍﾞｰﾀRB
;状態確認
LD T1000                 ; 電源ON後ﾃﾞｨﾚｲﾀｲﾏ
BAND DM10523 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
ANB MR18515              ; 搬送ｽﾗｲﾀﾞRB ｱﾗｰﾑ発生中
OUT MR18715              ; ｴﾚﾍﾞｰﾀRB ｱﾗｰﾑ発生中
CON
OUT @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
LDP MR18715              ; ｴﾚﾍﾞｰﾀRB ｱﾗｰﾑ発生中
ANB R39004               ; ｴﾗｰ中
SET @MR006
CON
SET R39004               ; ｴﾗｰ中
CON
DW #7 @DM1
CON
DW #1000 @DM9
CON
LDA DM60009              ; Sensor ERROR No.
CON
STA @DM2
CON
SET @R100                ; ｱﾗｰﾑ発生
CON
LDA DM10522              ; ﾄﾚｲｴﾚﾍﾞｰﾀRB ｱﾗｰﾑｺｰﾄﾞ
CON
STA DM11782              ; ｱﾗｰﾑ画面　軸ｴﾗｰｺｰﾄﾞ
CON
SET @R008
STG @R008
AND @R101                ; ｱﾗｰﾑ解除
DW #0 DM60009            ; Sensor ERROR No.
CON
AND @R105                ; SW1
SET MR24308              ; RB2　ｱﾗｰﾑﾘｾｯﾄ要求
CON
JMP @R009
STG @R009
TMR #531 #5
CON
AND T531
AND MR24311              ; ｴﾗｰﾘｾｯﾄ処理完了
RES @MR006
CON
RES R39004               ; ｴﾗｰ中
CON
RES MR24311              ; ｴﾗｰﾘｾｯﾄ処理完了
CON
RES @R101                ; ｱﾗｰﾑ解除
CON
ENDS
STG MR24308              ; RB2　ｱﾗｰﾑﾘｾｯﾄ要求
BSET DM10723 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
MPS
BANB DM10523 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
JMP MR24309
MPP
BAND DM10523 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
TMR #534 #30
CON
AND T534
JMP MR24310
STG MR24309
BAND DM10523 #4          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BAND DM10523 #14         ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
JMP MR24310
STG MR24310
SET MR24311              ; ｴﾗｰﾘｾｯﾄ処理完了
CON
ENDS
BLD DM10523 #4           ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BAND DM10523 #14         ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BANB DM10523 #15         ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
OUT @MR201
;原点復帰
LDP MR18700              ; ｴﾚﾍﾞｰﾀRB 原点復帰要求
AND MR16805              ; ﾄﾚｲｴﾚﾍﾞｰﾀ ﾄﾚｲ受渡し位置移動可
ANB @MR1501              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰中
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
ZRES MR17604 MR17606     ; TPSW_ｴﾚﾍﾞｰﾀ P1表示  TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
RES MR18702              ; ｴﾚﾍﾞｰﾀRB 原点復帰完了
CON
SET @MR1501              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰中
CON
SET MR18701              ; ｴﾚﾍﾞｰﾀRB 原点復帰中
CON
SET @MR1500              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰開始
STG @MR1500              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰開始
RES MR18700              ; ｴﾚﾍﾞｰﾀRB 原点復帰要求
CON
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BRES DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
BRES DM10723 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
BRES DM10723 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
BSET DM10723 #4          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR1503
STG @MR1503
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
JMP @MR1504
STG @MR1504
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BSET DM10723 #1          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR1505
STG @MR1505
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BRES DM10723 #1          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR1506
STG @MR1506
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BAND DM10523 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
JMP @MR1507
STG @MR1507
RES @MR1501              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰中
CON
RES MR18701              ; ｴﾚﾍﾞｰﾀRB 原点復帰中
CON
SET MR18702              ; ｴﾚﾍﾞｰﾀRB 原点復帰完了
CON
ENDS
;ﾎﾟｲﾝﾄ移動
;  Ｐ１  最下降（トレイ受渡し）位置
;  Ｐ２  トレイ受け取り位置
;  Ｐ１０〜２２  トレイ検出位置
;  Ｐ３  トレイ検出位置戻し位置（トレーサーチ位置 
;    
LD MR18703               ; ｴﾚﾍﾞｰﾀRB P1移動要求
OR MR18704               ; ｴﾚﾍﾞｰﾀRB P10-22 ﾄﾚｲ検出位置
OR MR18705               ; ｴﾚﾍﾞｰﾀRB P2 受取り位置移動要求
OR MR18706               ; ｴﾚﾍﾞｰﾀRB P3移動要求
AND MR18502              ; 搬送ｽﾗｲﾀﾞRB 原点復帰完了
ANB @MR1501              ; RB2(ｴﾚﾍﾞｰﾀ) 原点復帰中
ANP MR18708              ; ｴﾚﾍﾞｰﾀRB 移動指令
ANB @MR2501              ; RB2(ｴﾚﾍﾞｰﾀ) 動作中
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
ZRES MR17604 MR17606     ; TPSW_ｴﾚﾍﾞｰﾀ P1表示  TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
RES MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
SET MR18709              ; ｴﾚﾍﾞｰﾀRB 移動中
CON
SET @MR2501              ; RB2(ｴﾚﾍﾞｰﾀ) 動作中
CON
SET @MR2500              ; RB2(ｴﾚﾍﾞｰﾀ) 動作開始
STG @MR2500              ; RB2(ｴﾚﾍﾞｰﾀ) 動作開始
RES MR18708              ; ｴﾚﾍﾞｰﾀRB 移動指令
CON
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BRES DM10723 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
BRES DM10723 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2503
STG @MR2503
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
MPS
AND MR18703              ; ｴﾚﾍﾞｰﾀRB P1移動要求
MOV.L DM4220 DM10716     ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  ﾄﾚｲｴﾚﾍﾞｰﾀRB 目標位置
MOV DM4222 DM10720       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  ﾄﾚｲｴﾚﾍﾞｰﾀRB 速度
MOV DM4224 DM10721       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  ﾄﾚｲｴﾚﾍﾞｰﾀRB 加減速度
MRD
LD MR18705               ; ｴﾚﾍﾞｰﾀRB P2 受取り位置移動要求
OR MR18706               ; ｴﾚﾍﾞｰﾀRB P3移動要求
ANL
MOV DM4222 DM10720       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  ﾄﾚｲｴﾚﾍﾞｰﾀRB 速度
MOV DM4224 DM10721       ; _機種別ﾃﾞｰﾀ　ﾃﾝﾎﾟﾗﾘ  ﾄﾚｲｴﾚﾍﾞｰﾀRB 加減速度
MPP
TMH #886 #5              ; ｳｪｲﾄﾀｲﾏ
CON
AND T886                 ; ｳｪｲﾄﾀｲﾏ
JMP @MR2504
STG @MR2504
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
MPS
LD MR18703               ; ｴﾚﾍﾞｰﾀRB P1移動要求
AND MR16805              ; ﾄﾚｲｴﾚﾍﾞｰﾀ ﾄﾚｲ受渡し位置移動可
ANL
MPS
ANB MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
BSET DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2505
MPP
AND MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
JMP @MR2507
MRD
AND MR18704              ; ｴﾚﾍﾞｰﾀRB P10-22 ﾄﾚｲ検出位置
MPS
ANB MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
BSET DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2505
MPP
AND MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
JMP @MR2507
MRD
LD MR18705               ; ｴﾚﾍﾞｰﾀRB P2 受取り位置移動要求
AND MR16804
ANL
MPS
ANB MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
BSET DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2505
MPP
AND MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
JMP @MR2507
MPP
LD MR18706               ; ｴﾚﾍﾞｰﾀRB P3移動要求
AND MR16806              ; ｴﾚﾍﾞｰﾀRB P3 ｻｰﾁ位置動作可
ANL
MPS
ANB MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
BSET DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2505
MPP
AND MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
JMP @MR2507
STG @MR2505
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BRES DM10723 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
JMP @MR2506
STG @MR2506
ANB @MR200               ; ｴﾚﾍﾞｰﾀｱﾗｰﾑ発生中
BANB DM10523 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
BAND DM10523 #0          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 状態信号
TMH #887 #10             ; ｳｪｲﾄﾀｲﾏ
CON
AND T887                 ; ｳｪｲﾄﾀｲﾏ
JMP @MR2507
STG @MR2507
MPS
AND MR18703              ; ｴﾚﾍﾞｰﾀRB P1移動要求
RES MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
CON
RES MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
RES MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
CON
RES MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
SET MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
CON
JMP @MR2508
MRD
AND MR18704              ; ｴﾚﾍﾞｰﾀRB P10-22 ﾄﾚｲ検出位置
RES MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
CON
RES MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
RES MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
CON
RES MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
SET MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
JMP @MR2508
MRD
AND MR18705              ; ｴﾚﾍﾞｰﾀRB P2 受取り位置移動要求
RES MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
CON
RES MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
RES MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
CON
RES MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
SET MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
CON
JMP @MR2508
MPP
AND MR18706              ; ｴﾚﾍﾞｰﾀRB P3移動要求
RES MR17604              ; TPSW_ｴﾚﾍﾞｰﾀ P1表示
CON
RES MR17612              ; ﾄﾚｲｻｰﾁ完了
CON
RES MR17605              ; TPSW_ｴﾚﾍﾞｰﾀ 受渡し位置表示
CON
RES MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
SET MR17606              ; TPSW_ｴﾚﾍﾞｰﾀ P3表示
CON
JMP @MR2508
STG @MR2508
RES MR18708              ; ｴﾚﾍﾞｰﾀRB 移動指令
CON
BRES DM10723 #2          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
BRES DM10723 #3          ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 制御信号
CON
ZRES MR18703 MR18706     ; ｴﾚﾍﾞｰﾀRB P1移動要求  ｴﾚﾍﾞｰﾀRB P3移動要求
CON
RES MR18709              ; ｴﾚﾍﾞｰﾀRB 移動中
CON
RES @MR2501              ; RB2(ｴﾚﾍﾞｰﾀ) 動作中
CON
ENDS
;取得ﾃﾞｰﾀ
LDP MR9200               ; 取込みSW-1
LDA.L DM10516            ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 現在位置
CON
STA.L EM31000            ; ﾄﾚｲ1枚時 ﾄﾚｲ有無位置座標ﾊﾞｯﾌｧ
CON
STA.L DM21000            ; 切出し位置ﾃﾞｰﾀ（取込）
LDP MR9201               ; 取込みSW-2
LDA.L DM10516            ; ﾄﾚｲｴﾚﾍﾞｰﾀRB 現在位置
CON
STA.L EM31100
CON
STA.L DM21002            ; ﾄﾚｲ無し ﾃｰﾌﾞﾙ停止位ﾃﾞｰﾀ（取込）
;取得ﾃﾞｰﾀ　CF書込み
;'ﾄﾚｲ切出し位置への移動量算出
;EM31200.L = EM31100.L - EM31000.L
;DM21004.L = EM31200.L
LDP MR9202               ; 取込みSW-3
NCJ #1000
;'ﾄﾚｲ切出し位置への移動量算出
;
;EM31200.L = EM31100.L - EM31000.L
;
LD CR2002                ; 常時ON
LDA.L EM31100
CON
SUB.L EM31000            ; ﾄﾚｲ1枚時 ﾄﾚｲ有無位置座標ﾊﾞｯﾌｧ
CON
STA.L EM31200
;DM21004.L = EM31200.L
LD CR2002                ; 常時ON
MOV.L EM31200 DM21004    ; 切出し移動量（算出）
LABEL #1000
;
;MyTask Error Process Substance/エラー処理実体
STG @R100                ; ｱﾗｰﾑ発生
ANB R39002               ; ERROR_DISP Busy
MPS
ZRES @R105 @R108         ; SW1
CON
LDA @DM9
CON
TBCD
CON
STA @DM10
CON
DISN @DM10 @DM3 #4
MRD
DW #5 @DM0
CON
BMOV @DM0 DM60000 #7     ; TaskNo /ERROR_DISP
MPP
SET R39000               ; ERROR_DISP Start
CON
SET @R102
CON
JMP @R103
STG @R103
AND R39002               ; ERROR_DISP Busy
AND= DM60008 @DM1        ; ReturnNo /ERROR_DISP
AND= DM60007 @DM6        ; ReturnSW /ERROR_DISP
SET @R105                ; SW1
CON
JMP @R104
STG @R104
ANB R39002               ; ERROR_DISP Busy
RES @R102
CON
SET @R101                ; ｱﾗｰﾑ解除
CON
ENDS
END
ENDH
