DEVICE:53
;MODULE:RB_W_H_WDATA
;MODULE_TYPE:1
;***** RB Control Macro *****
;RB Type      : Weld
;RB Controler : PSEL / ASEL
;Version      : 1.00
;Date         : 2011/08/12
;ACTION 		 : 溶着作業原点設定
;<引数>
;P0 : RB_W DeviceNet入力割付先頭
;P1 : RB_W DeviceNet出力割付先頭
;P2 : ﾏｸﾛ実行完了ﾌﾗｸﾞ
;P3 : 結果ｺｰﾄﾞ
;P4 : ｿﾌﾄ非常停止信号
;V0 : ﾀｲﾑｱｳﾄ設定(ms)
;V1 : 軸番号
;P5 : 作業原点ﾃﾞｰﾀ格納DM番号
;
;<結果ｺｰﾄﾞ一覧>
;#20 : 正常完了
;#21 : 応答なし
;#22 : RBステータス異常
;#23 : 設定異常
LD @CR2008
DW #0 P3                 ; Result_Code
CON
SET @MR000
STG @MR000
MPS
LD P0:#0                 ; RB_DV_IN
AND P0:#1                ; RB_DV_IN
ANL
JMP @MR001
MPP
LDB P0:#0                ; RB_DV_IN
ORB P0:#1                ; RB_DV_IN
ANL
TMR @0 V0                ; TIMEOUT
CON
AND @T0
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR001
MOV P5:#0 P1:#32         ; WELD_DATA  RB_DV_OUT
JMP @MR002
STG @MR002
MPS
AND= V1 #1               ; Axis_No
RES P1:#14               ; RB_DV_OUT
CON
SET P1:#17               ; RB_DV_OUT
CON
JMP @MR003
MRD
AND= V1 #2               ; Axis_No
SET P1:#14               ; RB_DV_OUT
CON
SET P1:#17               ; RB_DV_OUT
CON
JMP @MR003
MPP
LD<> V1 #1               ; Axis_No
AND<> V1 #2              ; Axis_No
ANL
DW #23 P3                ; Result_Code
CON
JMP @MR015
STG @MR003
MPS
AND P0:#7                ; RB_DV_IN
RES P1:#17               ; RB_DV_OUT
CON
JMP @MR004
MPP
TMR @1 V0                ; TIMEOUT
CON
AND @T1
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR004
MPS
LDB P0:#7                ; RB_DV_IN
AND P0:#6                ; RB_DV_IN
ANL
JMP @MR005
MPP
TMR @2 V0                ; TIMEOUT
CON
AND @T2
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR005
DW #20 P3                ; Result_Code
CON
JMP @MR015
STG @MR015
SET P2                   ; End_Flag
CON
ENDS
;終了処理
LD P4                    ; EMG_Flag
ZRES @MR000 @MR115
LD P2                    ; End_Flag
OR P4                    ; EMG_Flag
MEND
END
ENDH
