DEVICE:53
;MODULE:RB_W_R_WDATA
;MODULE_TYPE:1
;***** RB Control Macro *****
;RB Type      : Weld
;RB Controler : PSEL / ASEL
;Version      : 1.00
;Date         : 2011/08/
;ACTION 		 : 溶着ﾃﾞｰﾀ読込み
;<引数>
;P0 : RB_W DeviceNet入力割付先頭
;P1 : RB_W DeviceNet出力割付先頭
;P2 : ﾏｸﾛ実行完了ﾌﾗｸﾞ
;P3 : 結果ｺｰﾄﾞ
;P4 : ｿﾌﾄ非常停止信号
;V0 : ﾀｲﾑｱｳﾄ設定(ms)
;V1 : 軸番号
;P5 : 溶着ﾃﾞｰﾀ格納先頭DM番号　*連続する6個のDMにﾃﾞｰﾀ格納します。
;
;<P5> 
;#0 : 接触点ﾃﾞｰﾀ
;#1 : 溶融点ﾃﾞｰﾀ
;#2 : 押込み完了点ﾃﾞｰﾀ
;#3 : 接触点検出時間
;#4 : 溶融点検出時間
;#5 : 押込み完了点検出時間
;
;<結果ｺｰﾄﾞ一覧>
;#20 : 正常完了
;#21 : 応答なし
;#22 : RBステータス異常
;#23 : 設定異常
LD @CR2008
DW #0 P3                 ; Result_Code
CON
SET @MR000
STG @MR000
MPS
LD P0:#0                 ; RB_DV_IN
AND P0:#1                ; RB_DV_IN
ANL
JMP @MR001
MPP
LDB P0:#0                ; RB_DV_IN
ORB P0:#1                ; RB_DV_IN
ANL
TMR @0 V0                ; TIMEOUT
CON
AND @T0
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR001
MPS
AND= V1 #1               ; Axis_No
RES P1:#14               ; RB_DV_OUT
CON
SET P1:#15               ; RB_DV_OUT
CON
JMP @MR002
MRD
AND= V1 #2               ; Axis_No
SET P1:#14               ; RB_DV_OUT
CON
SET P1:#15               ; RB_DV_OUT
CON
JMP @MR002
MPP
LD<> V1 #1               ; Axis_No
AND<> V1 #2              ; Axis_No
ANL
DW #23 P3                ; Result_Code
CON
JMP @MR015
STG @MR002
MPS
AND P0:#7                ; RB_DV_IN
RES P1:#15               ; RB_DV_OUT
CON
JMP @MR003
MPP
TMR @1 V0                ; TIMEOUT
CON
AND @T1
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR003
MPS
LDB P0:#7                ; RB_DV_IN
AND P0:#6                ; RB_DV_IN
ANL
MOV P0:#16 P5:#0         ; RB_DV_IN  WELD_DATA
MOV P0:#32 P5:#1         ; RB_DV_IN  WELD_DATA
MOV P0:#48 P5:#2         ; RB_DV_IN  WELD_DATA
MOV P0:#64 P5:#3         ; RB_DV_IN  WELD_DATA
MOV P0:#80 P5:#4         ; RB_DV_IN  WELD_DATA
MOV P0:#96 P5:#5         ; RB_DV_IN  WELD_DATA
JMP @MR005
MPP
TMH @2 V0                ; TIMEOUT
CON
AND @T2
DW #22 P3                ; Result_Code
CON
JMP @MR015
STG @MR005
DW #20 P3                ; Result_Code
CON
JMP @MR015
STG @MR015
SET P2                   ; End_Flag
CON
ENDS
;終了処理
LD P4                    ; EMG_Flag
ZRES @MR000 @MR115
LD P2                    ; End_Flag
OR P4                    ; EMG_Flag
MEND
END
ENDH
