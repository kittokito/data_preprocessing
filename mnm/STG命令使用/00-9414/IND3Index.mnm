DEVICE:53
;MODULE:IND3Index
;MODULE_TYPE:0
;***************************************
; 非常停止
; Emergency Stop 
;***************************************
LD R50010                ; Emergency　Stop
OR @MR000                ; Emergency STOP
ZRES @MR001 @MR2715      ; ST1ｼﾘﾝﾀﾞ 干渉外  4708
CON
OUT @MR000               ; Emergency STOP
TMR @0 #5
LD @T0
MDSTOP IND3Index
;***************************************
; ﾎﾟｰｽﾞ ･ ｴﾗｰ 監視
; Pause ･ Error Check 
;***************************************
LDB R50300               ; Pause
ANB R51600               ; Err IND3
OUT @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
;***************************************
; INDEXｼﾘﾝﾀﾞｲﾝﾀｰﾛｯｸﾁｪｯｸ
; INDEX Cylinder Interlock Check 
;***************************************
LDB R6100                ; IND3 INDEX位置決め 上限
AND R6101                ; IND3 INDEX位置決め 下限
OUT @MR001               ; ST1ｼﾘﾝﾀﾞ 干渉外
LDB R6010                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6011                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 下限
LD R11913                ; 除材P&P-IND3 Z軸干渉外
ORB R11908               ; 除材P&P干渉外
ANL
OUT @MR002               ; ST2ｼﾘﾝﾀﾞ 干渉外
LDB R6012                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6013                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 下限
ANB R6108                ; ﾊﾞｲﾊﾟｽ ﾜｰｸ押さえ　下降限
AND R6109                ; ﾊﾞｲﾊﾟｽ ﾜｰｸ押さえ　上昇限
ANB R6110                ; ﾊﾞｲﾊﾟｽ ﾉｽﾞﾙ下降限
AND R6111                ; ﾊﾞｲﾊﾟｽ ﾉｽﾞﾙ上昇限
ANB R13108               ; RB4_PSEL_軸1Busy
AND R13110               ; RB4_PSEL_ｿﾞｰﾝ出力設定用1
LDB R13109               ; RB4_PSEL_軸2Busy
OR CR2002                ; 常時ON
ANL
LD R13111                ; RB4_PSEL_ｿﾞｰﾝ出力設定用2
OR CR2002                ; 常時ON
ANL
ANB R13908               ; RB5_PSEL_軸1Busy
LD R13910                ; RB5_PSEL_ｿﾞｰﾝ出力設定用1
OR MR914                 ; 使用注意！ 浮き無視
ANL
ANB R13909               ; RB5_PSEL_軸2Busy
LD R13911                ; RB5_PSEL_ｿﾞｰﾝ出力設定用2
OR MR914                 ; 使用注意！ 浮き無視
ANL
OUT @MR003               ; ST3ｼﾘﾝﾀﾞ 干渉外
LDB R6014                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6015                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 下限
LDB R6204                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞ 下降限
AND R6205                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞ 上昇限
OR R6202                 ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞｽﾗｲﾄﾞ 天秤側限
ANL
OUT @MR004               ; ST4ｼﾘﾝﾀﾞ 干渉外
LD @MR001                ; ST1ｼﾘﾝﾀﾞ 干渉外
AND @MR002               ; ST2ｼﾘﾝﾀﾞ 干渉外
AND @MR003               ; ST3ｼﾘﾝﾀﾞ 干渉外
AND @MR004               ; ST4ｼﾘﾝﾀﾞ 干渉外
OUT @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
;<h1/>原点復帰動作
;HOME RETURN OPERATION 
LD R50003                ; HOME
ANB R51506               ; IND3 DO
SET R51506               ; IND3 DO
CON
SET @MR1000              ; INDEX 原点復帰開始
STG @MR1000              ; INDEX 原点復帰開始
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
SET @MR1001              ; INDEX 原点復帰中
CON
ZRES DM3200 DM3799       ; IND2-ST2 QRID-1  排出時読込 
CON
DW #4 DM688              ; 原点復帰時IND3回転回数
CON
JMP @MR1002              ; 1002
STG @MR1002              ; 1002
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND R50003               ; HOME
RES MR1203               ; IND3 CW回転Fin
CON
JMP @MR1003              ; 1003
STG @MR1003              ; 1003
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8412               ; IND3 INDEX位置決め 上昇
CON
SET MR8413               ; IND3 INDEX位置決め 下降
CON
SET MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
RES MR8509               ; IND3 ST2治具1(縦) 開き
CON
SET MR8510               ; IND3 ST2治具2(横) 閉じ
CON
RES MR8511               ; IND3 ST2治具2(横) 開き
CON
SET MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8513               ; IND3 ST3治具 開き
CON
SET MR8514               ; IND3 ST4治具 閉じ
CON
RES MR8515               ; IND3 ST4治具 開き
CON
JMP @MR1004              ; 1004
STG @MR1004              ; 1004
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
TMH @19 #10
CON
AND @T19
RES MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
RES MR8510               ; IND3 ST2治具2(横) 閉じ
CON
RES MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8514               ; IND3 ST4治具 閉じ
CON
JMP @MR1005              ; 1005
STG @MR1005              ; 1005
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6011                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6013                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6015                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6101                ; IND3 INDEX位置決め 下限
JMP @MR1006              ; 1006
STG @MR1006              ; 1006
TMH @6 #3
CON
AND @T6
AND MR15003              ; 除材P&P原点復帰完了
MPS
AND @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
JMP @MR1100              ; 1100
MPP
ANB @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
SET @MR304               ; Err160 INDEX Err10
CON
JMP @MR1014              ; 1014
;Err180
STG @MR1014              ; 1014
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR307               ; Err160 INDEX Err10解除完了
JMP @MR1015              ; 1015
STG @MR1015              ; 1015
RES @MR307               ; Err160 INDEX Err10解除完了
CON
JMP @MR1003              ; 1003
;
STG @MR1100              ; 1100
RES MR15003              ; 除材P&P原点復帰完了
CON
SET R51606               ; PACK INDEX3
CON
JMP @MR1101              ; 1101
STG @MR1101              ; 1101
LD T724                  ; IND3電源再投入ﾃﾞｨﾚｲ
OR R15906                ; IND3 電源ON
ANL
SET MR1200               ; IND3 原点復帰SUB
CON
JMP @MR1102              ; 1102
STG @MR1102              ; 1102
AND MR1201               ; IND3 原点復帰完了
SET MR1015               ; IND3原点復帰完了
CON
JMP @MR1103              ; 1103
STG @MR1103              ; 1103
RES MR1201               ; IND3 原点復帰完了
CON
RES R51606               ; PACK INDEX3
CON
JMP @MR1300              ; ST3以外にｹｰｽ有るかも
;
STG @MR1108              ; 1108
TMH @1 #10
CON
AND @T1
RES R51606               ; PACK INDEX3
CON
JMP @MR1109              ; 1109
STG @MR1109              ; 1109
JMP @MR1200              ; 1200
;ケースチェック
;Check the Case
;UT1のﾜｰｸﾁｪｯｸﾌﾗｸﾞ
;UT1 Work Check Flag
LDB R6008                ; IND3 ST4 ﾜｰｸ有無検出
OUT @MR010               ; ST4にｹｰｽ無し
;
STG @MR1200              ; 1200
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
MPS
AND @MR010               ; ST4にｹｰｽ無し
MPS
AND= DM688 #0            ; 原点復帰時IND3回転回数
JMP @MR1201              ; INDEX上にｹｰｽ無し
MPP
AND<> DM688 #0           ; 原点復帰時IND3回転回数
RES R51606               ; PACK INDEX3
CON
JMP @MR1300              ; ST3以外にｹｰｽ有るかも
MPP
ANB @MR010               ; ST4にｹｰｽ無し
JMP @MR1208              ; ST1 ｹｰｽ有り
STG @MR1201              ; INDEX上にｹｰｽ無し
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
SET MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6010                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6012                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6014                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 上限
JMP @MR1202              ; 1202
STG @MR1202              ; 1202
RES MR1203               ; IND3 CW回転Fin
CON
JMP @MR1203              ; 1203
STG @MR1203              ; 1203
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
SET MR8509               ; IND3 ST2治具1(縦) 開き
CON
RES MR8510               ; IND3 ST2治具2(横) 閉じ
CON
SET MR8511               ; IND3 ST2治具2(横) 開き
CON
SET MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8513               ; IND3 ST3治具 開き
CON
RES MR8514               ; IND3 ST4治具 閉じ
CON
SET MR8515               ; IND3 ST4治具 開き
CON
TMH @9 #50
CON
AND @T9
JMP @MR1204
STG @MR1204
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8509               ; IND3 ST2治具1(縦) 開き
CON
RES MR8511               ; IND3 ST2治具2(横) 開き
CON
RES MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8515               ; IND3 ST4治具 開き
CON
RES @MR1001              ; INDEX 原点復帰中
CON
SET R51505               ; IND3INDEX  1ｻｲｸﾙ終了
CON
ENDS
;INDEX ST1ワーク残り　エラー解除・除材
STG @MR1208              ; ST1 ｹｰｽ有り
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND R6202                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞｽﾗｲﾄﾞ 天秤側限
SET @MR108               ; Err153 INDEX Err3
CON
JMP @MR1209              ; 2301
STG @MR1209              ; 2301
AND @MR111               ; Err153 INDEX Err3解除完了
RES @MR111               ; Err153 INDEX Err3解除完了
CON
JMP @MR1210
;在荷時ﾊﾝﾄﾞ逃がし
LDP @MR1208              ; ST1 ｹｰｽ有り
RES MR8403               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ ｲﾝﾃﾞｯｸｽ側移動
CON
SET MR8402               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ 天秤側移動
;ﾊﾝﾄﾞ戻し
STG @MR1210
RES MR8402               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ 天秤側移動
CON
SET MR8403               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ ｲﾝﾃﾞｯｸｽ側移動
CON
AND R6203                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞｽﾗｲﾄﾞ ｲﾝﾃﾞｯｸｽ側限
JMP @MR1108              ; 1108
;INDEX ワーク残りの為､INDEX回転
STG @MR1300              ; ST3以外にｹｰｽ有るかも
SET @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
CON
JMP @MR1301              ; 1301
STG @MR1301              ; 1301
AND @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
RES @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
CON
JMP @MR1302              ; 1302
STG @MR1302              ; 1302
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
JMP @MR1303              ; 1303
STG @MR1303              ; 1303
MPS
AND @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
SET R51606               ; PACK INDEX3
CON
SET MR1202               ; IND3 90°CW回転SUB
CON
DEC DM688                ; 原点復帰時IND3回転回数
CON
JMP @MR1400              ; 1400
MPP
ANB @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
SET @MR304               ; Err160 INDEX Err10
CON
JMP @MR1308              ; 1308
;Err180
STG @MR1308              ; 1308
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR307               ; Err160 INDEX Err10解除完了
JMP @MR1309              ; 1309
STG @MR1309              ; 1309
RES @MR307               ; Err160 INDEX Err10解除完了
CON
JMP @MR1303              ; 1303
;
STG @MR1400              ; 1400
AND MR1203               ; IND3 CW回転Fin
JMP @MR1401              ; 1401
STG @MR1401              ; 1401
RES MR1203               ; IND3 CW回転Fin
CON
JMP @MR1108              ; 1108
;<h1/>自動運転
;AUTO OERATION
LD R50001                ; AUTO
ANB R51506               ; IND3 DO
ANB @MR1500              ; INDEX AUTO
SET R51506               ; IND3 DO
CON
SET @MR1500              ; INDEX AUTO
LDB LR41100              ; AUTO-0 全ﾕﾆｯﾄ動作
ANB LR41101              ; AUTO-1 IND1 2UT
ANB LR41102              ; AUTO-2 IND1 3UT
ANB LR41103              ; AUTO-3 IND1 4UT
ANB LR41104              ; AUTO-4 IND2 3UT
ANB LR41105              ; AUTO-5 IND3 3UT
ANB LR41106              ; AUTO-6
ANB LR41107              ; AUTO-7
ANB LR41108              ; AUTO-8
OUB @MR013               ; INDEX AUTO Parameter OK
;AUTO Operation Parameter No? / auto動作Ｎｏ．は？
STG @MR1500              ; INDEX AUTO
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
MPS
AND @MR013               ; INDEX AUTO Parameter OK
JMP @MR1506              ; 4010
MPP
ANB @MR013               ; INDEX AUTO Parameter OK
JMP @MR1501              ; 4003
;動作パラメーター設定不良
;Parameter Set NG
STG @MR1501              ; 4003
SET R50009               ; HOLDRQ
CON
SET R51505               ; IND3INDEX  1ｻｲｸﾙ終了
CON
ENDS
;
STG @MR1506              ; 4010
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8412               ; IND3 INDEX位置決め 上昇
CON
SET MR8413               ; IND3 INDEX位置決め 下降
CON
RES MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
RES MR8509               ; IND3 ST2治具1(縦) 開き
CON
RES MR8510               ; IND3 ST2治具2(横) 閉じ
CON
RES MR8511               ; IND3 ST2治具2(横) 開き
CON
RES MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8513               ; IND3 ST3治具 開き
CON
RES MR8514               ; IND3 ST4治具 閉じ
CON
RES MR8515               ; IND3 ST4治具 開き
CON
SET @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
CON
JMP @MR1507
STG @MR1507
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6011                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6013                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6015                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6101                ; IND3 INDEX位置決め 下限
JMP @MR1508
STG @MR1508
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
RES @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
CON
MPS
AND @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
JMP @MR1600              ; 4011
MPP
ANB @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
SET @MR304               ; Err160 INDEX Err10
CON
JMP @MR1514              ; 6908
;Err180 Release
STG @MR1514              ; 6908
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR307               ; Err160 INDEX Err10解除完了
JMP @MR1515              ; 6909
STG @MR1515              ; 6909
RES @MR307               ; Err160 INDEX Err10解除完了
CON
JMP @MR1506              ; 4010
;
STG @MR1600              ; 4011
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
SET R51606               ; PACK INDEX3
CON
SET @MR2300              ; IND3 上ｹｰｽ記憶開始
CON
JMP @MR1601              ; 4012
STG @MR1601              ; 4012
AND @MR2301              ; IND3 上ｹｰｽ記憶完了
JMP @MR1602              ; 4013
STG @MR1602              ; 4013
RES @MR2301              ; IND3 上ｹｰｽ記憶完了
CON
SET MR1202               ; IND3 90°CW回転SUB
CON
JMP @MR1603              ; 4014
STG @MR1603              ; 4014
AND MR1203               ; IND3 CW回転Fin
JMP @MR1700              ; 4100
;
STG @MR1700              ; 4100
RES MR1203               ; IND3 CW回転Fin
CON
JMP @MR1701              ; 4101
STG @MR1701              ; 4101
TMH @7 #5
CON
AND @T7
RES R51606               ; PACK INDEX3
CON
AND R11702               ; IND3 Alarm1 正常=ON
AND R11703               ; IND3 Alarm2 正常=ON
JMP @MR1702              ; 4102
STG @MR1702              ; 4102
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
SET MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6010                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6012                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6014                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 上限
JMP @MR1703
STG @MR1703
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
SET MR8509               ; IND3 ST2治具1(縦) 開き
CON
RES MR8510               ; IND3 ST2治具2(横) 閉じ
CON
SET MR8511               ; IND3 ST2治具2(横) 開き
CON
SET MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8513               ; IND3 ST3治具 開き
CON
RES MR8514               ; IND3 ST4治具 閉じ
CON
SET MR8515               ; IND3 ST4治具 開き
CON
JMP @MR1704
STG @MR1704
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
MPS
ANB R6008                ; IND3 ST4 ﾜｰｸ有無検出
JMP @MR1800              ; UT4 ｹｰｽ確認終了
MPP
AND R6008                ; IND3 ST4 ﾜｰｸ有無検出
MPS
AND= DM3751 #0           ; IND3-ST4 NGﾜｰｸ 0=OK
JMP @MR1800              ; UT4 ｹｰｽ確認終了
MPP
AND<> DM3751 #0          ; IND3-ST4 NGﾜｰｸ 0=OK
SET @MR112               ; Err154 INDEX Err4
CON
JMP @MR1708              ; 2109
;ＩＮＤ３　ＳＴ４到着ワーク不良品 エラー・除材
STG @MR1708              ; 2109
TMH @8 #20
CON
AND @T8
AND @MR115               ; Err154 INDEX Err4解除完了
JMP @MR1709              ; 2110
STG @MR1709              ; 2110
RES @MR115               ; Err154 INDEX Err4解除完了
CON
JMP @MR1701              ; 4101
;IND3 ST4 初期確認ﾜｰｸ
STG @MR1710
AND @MR315               ; Err142 INDEX Err12解除完了
JMP @MR1711
STG @MR1711
MPS
RES @MR315               ; Err142 INDEX Err12解除完了
CON
JMP @MR1701              ; 4101
MPP
ANB R6008                ; IND3 ST4 ﾜｰｸ有無検出
DW #0 DM693              ; IND3_ST4 Work Memory
;動作終了
STG @MR1800              ; UT4 ｹｰｽ確認終了
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
SET MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
SET @MR2400              ; 到着ｹｰｽﾁｪｯｸ
CON
JMP @MR1801              ; 4111
STG @MR1801              ; 4111
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR2401              ; 到着ｹｰｽﾁｪｯｸ完了
AND R6010                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6012                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6014                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 上限
JMP @MR1802              ; 4112
STG @MR1802              ; 4112
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
TMH @17 #5
CON
AND @T17
SET R51505               ; IND3INDEX  1ｻｲｸﾙ終了
CON
RES @MR2401              ; 到着ｹｰｽﾁｪｯｸ完了
CON
ENDS
;サブルーチン
;Subroutine
;INDEX 原点復帰動作
;INDEX HOME
;The starting point return of an index is program No.5.  
;By 4 division specification, it moves to a starting point position by the shortest. 
;/ ｲﾝﾃﾞｯｸｽの原点復帰は、ﾌﾟﾛｸﾞﾗﾑNo.５　4分割指定で、近周りで原点位置へ移動する
STG MR1200               ; IND3 原点復帰SUB
SET MR26115              ; IND3     非常停止
CON
TMH @3 #10
CON
AND @T3
SET MR26106              ; IND3 電源ON
CON
JMP @MR2000              ; 6003
STG @MR2000              ; 6003
AND R15906               ; IND3 電源ON
SET MR26112              ; IND3 ﾘｾｯﾄ
CON
JMP @MR2001              ; 6004
STG @MR2001              ; 6004
AND R11702               ; IND3 Alarm1 正常=ON
AND R11703               ; IND3 Alarm2 正常=ON
RES MR26112              ; IND3 ﾘｾｯﾄ
CON
JMP @MR2002              ; 6005
STG @MR2002              ; 6005
AND R11701               ; IND3 起動入力待ち
RES MR26107              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit
CON
SET MR26108              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1bit
CON
SET MR26109              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 2bit
CON
TMH @12 #5
CON
AND @T12
SET MR26111              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1行目
CON
JMP @MR2003              ; 6006
STG @MR2003              ; 6006
TMH @13 #5
CON
AND @T13
SET R51605               ; IND3回転中(SET/RESET)
CON
SET MR26113              ; IND3 起動
CON
ANB R11701               ; IND3 起動入力待ち
JMP @MR2004              ; 6007
STG @MR2004              ; 6007
RES MR26113              ; IND3 起動
CON
ZRES MR26107 MR26111     ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit  IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1行目
CON
AND R11700               ; IND3 位置決め完了
SET MR26114              ; IND3 ｱﾝｻ
CON
JMP @MR2005
STG @MR2005
ANB R11700               ; IND3 位置決め完了
RES MR26114              ; IND3 ｱﾝｻ
CON
RES R51605               ; IND3回転中(SET/RESET)
CON
DW #0 DM689              ; IND3位置記憶
CON
JMP @MR2006
STG @MR2006
SET MR1201               ; IND3 原点復帰完了
CON
ENDS
;90 degrees positive rotation of an index chooses program No.0 to 3 in order, and starts it. 
;By 4 division specification, it moves to the following position of four positions. 
;If movement is completed, the following specification will be written in DM. 
; / ｲﾝﾃﾞｯｸｽの90度正回転は、ﾌﾟﾛｸﾞﾗﾑNo．０から３を順に選択し起動する
;4分割指定で、4位置の次位置へ移動する、移動が完了したらDMに次の指定を書込む
STG MR1202               ; IND3 90°CW回転SUB
SET R51514
CON
SET MR1211               ; IND3 監視
CON
SET MR26115              ; IND3     非常停止
CON
JMP @MR2100              ; IND3  　　 CW回転sig
STG @MR2100              ; IND3  　　 CW回転sig
AND R11701               ; IND3 起動入力待ち
MPS
AND= DM689 #0            ; IND3位置記憶
SET MR26107              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit
CON
RES MR26108              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1bit
CON
RES MR26109              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 2bit
CON
JMP @MR2101              ; 6103
MRD
AND= DM689 #1            ; IND3位置記憶
RES MR26107              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit
CON
SET MR26108              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1bit
CON
RES MR26109              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 2bit
CON
JMP @MR2101              ; 6103
MRD
AND= DM689 #2            ; IND3位置記憶
SET MR26107              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit
CON
SET MR26108              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1bit
CON
RES MR26109              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 2bit
CON
JMP @MR2101              ; 6103
MPP
AND= DM689 #3            ; IND3位置記憶
RES MR26107              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit
CON
RES MR26108              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1bit
CON
RES MR26109              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 2bit
CON
JMP @MR2101              ; 6103
STG @MR2101              ; 6103
TMH @14 #3
CON
AND @T14
SET MR26111              ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1行目
CON
JMP @MR2102              ; 6104
STG @MR2102              ; 6104
TMH @15 #3
CON
AND @T15
SET R51605               ; IND3回転中(SET/RESET)
CON
SET MR26113              ; IND3 起動
CON
JMP @MR2103              ; 6200
STG @MR2103              ; 6200
ANB R11701               ; IND3 起動入力待ち
RES MR26113              ; IND3 起動
CON
ZRES MR26107 MR26111     ; IND3 ﾌﾟﾛｸﾞﾗﾑNo. 0bit  IND3 ﾌﾟﾛｸﾞﾗﾑNo. 1行目
CON
JMP @MR2104              ; 6201
STG @MR2104              ; 6201
AND R11700               ; IND3 位置決め完了
SET MR26114              ; IND3 ｱﾝｻ
CON
JMP @MR2105              ; 6202
STG @MR2105              ; 6202
ANB R11700               ; IND3 位置決め完了
RES MR26114              ; IND3 ｱﾝｻ
CON
RES R51605               ; IND3回転中(SET/RESET)
CON
RES MR1211               ; IND3 監視
CON
MPS
AND<= DM689 #2           ; IND3位置記憶
JMP @MR2106              ; 6204
MPP
AND>= DM689 #3           ; IND3位置記憶
JMP @MR2107              ; 6205
STG @MR2106              ; 6204
INC DM689                ; IND3位置記憶
CON
JMP @MR2108              ; 6206
STG @MR2107              ; 6205
DW #0 DM689              ; IND3位置記憶
CON
JMP @MR2108              ; 6206
STG @MR2108              ; 6206
MPS
RES R51514
CON
SET MR1203               ; IND3 CW回転Fin
CON
ENDS
MRD
WSR DM690 #3 #1          ; IND3_ST1 Work Memory
MPP
WSR DM3700 #40 #20       ; IND3-ST2 QRID-1
;Floating check
;
STG @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
MPS
ANB MR914                ; 使用注意！ 浮き無視
MPS
AND R6005                ; IND3 ST3 ﾜｰｸ有無検出
MPS
LD R6006                 ; IND3 ST3 ﾜｰｸ浮き検出1(供給孔側)
AND R6007                ; IND3 ST3 ﾜｰｸ浮き検出2(ﾌｨﾙﾑ側)
ANL
JMP @MR2201              ; 3101
MPP
LDB R6006                ; IND3 ST3 ﾜｰｸ浮き検出1(供給孔側)
ORB R6007                ; IND3 ST3 ﾜｰｸ浮き検出2(ﾌｨﾙﾑ側)
ANL
SET @MR308               ; Err141 INDEX Err11
CON
JMP @MR2204              ; 3104
MPP
ANB R6005                ; IND3 ST3 ﾜｰｸ有無検出
JMP @MR2201              ; 3101
MPP
AND MR914                ; 使用注意！ 浮き無視
JMP @MR2203              ; 3103
STG @MR2201              ; 3101
MPS
AND R6000                ; IND3 ST2 ﾜｰｸ有無検出
MPS
LD R6001                 ; IND3 ST2 ﾜｰｸ浮き検出1(供給孔側)
AND R6002                ; IND3 ST2 ﾜｰｸ浮き検出2(ﾌｨﾙﾑ側)
ANL
JMP @MR2203              ; 3103
MPP
LDB R6001                ; IND3 ST2 ﾜｰｸ浮き検出1(供給孔側)
ORB R6002                ; IND3 ST2 ﾜｰｸ浮き検出2(ﾌｨﾙﾑ側)
ANL
SET @MR212               ; Err124
CON
JMP @MR2205
MPP
ANB R6000                ; IND3 ST2 ﾜｰｸ有無検出
JMP @MR2203              ; 3103
STG @MR2203              ; 3103
SET @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
CON
ENDS
;Err141 IND2_ST3 Cartridge Floating Detection Before turn
STG @MR2204              ; 3104
ANB R51600               ; Err IND3
AND @MR311               ; Err141 INDEX Err11解除完了
RES @MR311               ; Err141 INDEX Err11解除完了
CON
JMP @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
;Err124 IND3_ST3 Cartridge Floating Detection Before turn
STG @MR2205
ANB R51600               ; Err IND3
AND @MR215               ; Err124解除完了
RES @MR215               ; Err124解除完了
CON
JMP @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
;
;<h1/>ｽﾃｯﾌﾟ運転
;STEP OERATION
LD R50002                ; STEP
ANB R51513               ; IND3_INDEX DO (STEP)
ANB LR40600              ; INDEX STEP-01 flag
ANB LR40601              ; INDEX STEP-02 flag
AND LR40602              ; INDEX STEP-IND2 flag
SET R51513               ; IND3_INDEX DO (STEP)
CON
SET @MR2500              ; IND3  STEP開始
LDB R50003               ; HOME
ANB R50001               ; AUTO
ANB R50002               ; STEP
LD @MR2501               ; IND3  STEP完了
OR R51505                ; IND3INDEX  1ｻｲｸﾙ終了
ANL
RES R51513               ; IND3_INDEX DO (STEP)
CON
RES @MR2501              ; IND3  STEP完了
;動作パラメーター確認
;No.0　90°正転
STG @MR2500              ; IND3  STEP開始
ANB R50300               ; Pause
ANB R51600               ; Err IND3
MPS
AND LR40602              ; INDEX STEP-IND2 flag
JMP @MR2600              ; STEP
MPP
ANB LR40602              ; INDEX STEP-IND2 flag
JMP @MR2502              ; ﾊﾟﾗﾒｰﾀｰNo.NG
;動作パラメーター設定不良
STG @MR2502              ; ﾊﾟﾗﾒｰﾀｰNo.NG
SET R51505               ; IND3INDEX  1ｻｲｸﾙ終了
CON
ENDS
;STEP通常動作
STG @MR2600              ; STEP
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
ANB _IND3ST2
ANB _IND3ST3
ANB _IND3ST4
JMP @MR2601              ; 4608
STG @MR2601              ; 4608
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
RES MR8508               ; IND3 ST2治具1(縦) 閉じ
CON
RES MR8509               ; IND3 ST2治具1(縦) 開き
CON
RES MR8510               ; IND3 ST2治具2(横) 閉じ
CON
RES MR8511               ; IND3 ST2治具2(横) 開き
CON
RES MR8512               ; IND3 ST3治具 閉じ
CON
RES MR8513               ; IND3 ST3治具 開き
CON
TMH @18 #0
CON
AND @T18
SET @MR2200              ; ﾜｰｸ浮きﾁｪｯｸ開始
CON
JMP @MR2602              ; 4609
STG @MR2602              ; 4609
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
RES MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6011                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6013                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 下限
AND R6015                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 下限
RES @MR2215              ; ﾜｰｸ浮きﾁｪｯｸ終了
CON
JMP @MR2603              ; 4701
STG @MR2603              ; 4701
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
MPS
AND @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
JMP @MR2700              ; 4702
MPP
ANB @MR005               ; INDEXｼﾘﾝﾀﾞ全干渉外
SET @MR304               ; Err160 INDEX Err10
CON
JMP @MR2608              ; 4712
;Err180 Release
STG @MR2608              ; 4712
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
AND @MR307               ; Err160 INDEX Err10解除完了
JMP @MR2609              ; 4713
STG @MR2609              ; 4713
RES @MR307               ; Err160 INDEX Err10解除完了
CON
JMP @MR2603              ; 4701
;
STG @MR2700              ; 4702
SET R51606               ; PACK INDEX3
CON
JMP @MR2701              ; 4703
STG @MR2701              ; 4703
SET MR1202               ; IND3 90°CW回転SUB
CON
JMP @MR2702              ; 4704
STG @MR2702              ; 4704
AND MR1203               ; IND3 CW回転Fin
JMP @MR2708              ; 4705
;
STG @MR2708              ; 4705
RES MR1203               ; IND3 CW回転Fin
CON
JMP @MR2709              ; 4706
STG @MR2709              ; 4706
TMH @10 #5
CON
AND @T10
RES R51606               ; PACK INDEX3
CON
AND R11702               ; IND3 Alarm1 正常=ON
AND R11703               ; IND3 Alarm2 正常=ON
JMP @MR2710              ; 4707
STG @MR2710              ; 4707
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
MPS
ANB R6008                ; IND3 ST4 ﾜｰｸ有無検出
JMP @MR2715              ; 4708
MPP
AND R6008                ; IND3 ST4 ﾜｰｸ有無検出
MPS
AND= DM3751 #0           ; IND3-ST4 NGﾜｰｸ 0=OK
JMP @MR2715              ; 4708
MPP
AND<> DM3751 #0          ; IND3-ST4 NGﾜｰｸ 0=OK
SET @MR112               ; Err154 INDEX Err4
CON
JMP @MR2711              ; 4709
;INDEX UT1到着ワーク不良品 エラー・除材
STG @MR2711              ; 4709
TMH @11 #10
CON
AND @T11
AND @MR115               ; Err154 INDEX Err4解除完了
JMP @MR2712              ; 4710
STG @MR2712              ; 4710
RES MR8402               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ 天秤側移動
CON
SET MR8403               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ ｲﾝﾃﾞｯｸｽ側移動
CON
RES MR8406               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ　旋回
CON
SET MR8407               ; ﾊﾞｲﾊﾟｽ封止 ﾊﾝﾄﾞ　旋回戻し
CON
AND R6203                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞｽﾗｲﾄﾞ ｲﾝﾃﾞｯｸｽ側限
AND R6207                ; ﾊﾞｲﾊﾟｽ ﾊﾝﾄﾞ 戻り限
RES @MR115               ; Err154 INDEX Err4解除完了
CON
JMP @MR2709              ; 4706
;
STG @MR2715              ; 4708
AND @MR015               ; IND3 ﾎﾟｰｽﾞ&ｴﾗｰ無
DW #0 DM3751             ; IND3-ST4 NGﾜｰｸ 0=OK
CON
SET MR8410               ; IND3 治具ｴｱｰ供給ﾉｽﾞﾙ 上昇
CON
AND R6010                ; IND3 ST2治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6012                ; IND3 ST3治具ｴｱｰ供給ﾉｽﾞﾙ 上限
AND R6014                ; IND3 ST4治具ｴｱｰ供給ﾉｽﾞﾙ 上限
SET R50009               ; HOLDRQ
CON
SET R51505               ; IND3INDEX  1ｻｲｸﾙ終了
CON
ENDS
;INDEX上ｹｰｽ記憶
;INDEX Case Memory
STG @MR2300              ; IND3 上ｹｰｽ記憶開始
SET @MR2302              ; IND3 上ｹｰｽ記憶中
CON
MPS
AND CR2003               ; 常時OFF
BSET DM690 #0            ; IND3_ST1 Work Memory
CON
JMP @MR2303              ; 6403
MPP
ANB CR2003               ; 常時OFF
BRES DM690 #0            ; IND3_ST1 Work Memory
CON
JMP @MR2303              ; 6403
STG @MR2303              ; 6403
MPS
AND R6000                ; IND3 ST2 ﾜｰｸ有無検出
BSET DM691 #0            ; IND3_ST2 Work Memory
CON
JMP @MR2304              ; 6404
MPP
ANB R6000                ; IND3 ST2 ﾜｰｸ有無検出
BRES DM691 #0            ; IND3_ST2 Work Memory
CON
JMP @MR2304              ; 6404
STG @MR2304              ; 6404
MPS
AND R6005                ; IND3 ST3 ﾜｰｸ有無検出
BSET DM692 #0            ; IND3_ST3 Work Memory
CON
JMP @MR2305              ; 6405
MPP
ANB R6005                ; IND3 ST3 ﾜｰｸ有無検出
BRES DM692 #0            ; IND3_ST3 Work Memory
CON
JMP @MR2305              ; 6405
STG @MR2305              ; 6405
MPS
AND R6008                ; IND3 ST4 ﾜｰｸ有無検出
BSET DM693 #0            ; IND3_ST4 Work Memory
CON
JMP @MR2306              ; 6406
MPP
ANB R6008                ; IND3 ST4 ﾜｰｸ有無検出
BRES DM693 #0            ; IND3_ST4 Work Memory
CON
JMP @MR2306              ; 6406
STG @MR2306              ; 6406
RES @MR2302              ; IND3 上ｹｰｽ記憶中
CON
SET @MR2301              ; IND3 上ｹｰｽ記憶完了
CON
ENDS
;
;到着ｹｰｽﾁｪｯｸ
;Arrival Case Check
STG @MR2400              ; 到着ｹｰｽﾁｪｯｸ
MPS
AND R6000                ; IND3 ST2 ﾜｰｸ有無検出
MPS
AND= DM691 #1            ; IND3_ST2 Work Memory
JMP @MR2402              ; 6503
MPP
AND= DM691 #0            ; IND3_ST2 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
@SET @MR100              ; ﾜｰｸ有り異常
CON
SET R51600               ; Err IND3
CON
JMP @MR2408              ; 6512
MPP
ANB R6000                ; IND3 ST2 ﾜｰｸ有無検出
MPS
AND= DM691 #0            ; IND3_ST2 Work Memory
JMP @MR2402              ; 6503
MPP
AND= DM691 #1            ; IND3_ST2 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
SET R51600               ; Err IND3
CON
JMP @MR2408              ; 6512
STG @MR2402              ; 6503
MPS
AND R6005                ; IND3 ST3 ﾜｰｸ有無検出
MPS
AND= DM692 #1            ; IND3_ST3 Work Memory
JMP @MR2403              ; 6504
MPP
AND= DM692 #0            ; IND3_ST3 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
@SET @MR100              ; ﾜｰｸ有り異常
CON
SET R51600               ; Err IND3
CON
JMP @MR2409              ; 6513
MPP
ANB R6005                ; IND3 ST3 ﾜｰｸ有無検出
MPS
AND= DM692 #0            ; IND3_ST3 Work Memory
JMP @MR2403              ; 6504
MPP
AND= DM692 #1            ; IND3_ST3 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
SET R51600               ; Err IND3
CON
JMP @MR2409              ; 6513
STG @MR2403              ; 6504
MPS
AND R6008                ; IND3 ST4 ﾜｰｸ有無検出
MPS
AND= DM693 #1            ; IND3_ST4 Work Memory
JMP @MR2404              ; 6505
MPP
AND= DM693 #0            ; IND3_ST4 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
@SET @MR100              ; ﾜｰｸ有り異常
CON
SET R51600               ; Err IND3
CON
JMP @MR2410              ; 6514
MPP
ANB R6008                ; IND3 ST4 ﾜｰｸ有無検出
MPS
AND= DM693 #0            ; IND3_ST4 Work Memory
JMP @MR2404              ; 6505
MPP
AND= DM693 #1            ; IND3_ST4 Work Memory
@SET @MR208              ; Err157 INDEX Err7
CON
SET R51600               ; Err IND3
CON
JMP @MR2410              ; 6514
STG @MR2404              ; 6505
SET @MR2401              ; 到着ｹｰｽﾁｪｯｸ完了
CON
ENDS
;
STG @MR2408              ; 6512
AND @MR211               ; Err157 INDEX Err7解除完了
DW #5 DM3711             ; IND3-ST2 NGﾜｰｸ 0=OK
CON
RES @MR211               ; Err157 INDEX Err7解除完了
CON
JMP @MR2402              ; 6503
STG @MR2409              ; 6513
AND @MR211               ; Err157 INDEX Err7解除完了
DW #6 DM3731             ; IND3-ST3 NGﾜｰｸ 0=OK
CON
RES @MR211               ; Err157 INDEX Err7解除完了
CON
JMP @MR2403              ; 6504
STG @MR2410              ; 6514
AND @MR211               ; Err157 INDEX Err7解除完了
DW #7 DM3751             ; IND3-ST4 NGﾜｰｸ 0=OK
CON
RES @MR211               ; Err157 INDEX Err7解除完了
CON
JMP @MR2404              ; 6505
;
;<h1/>Err
;Err153
;原点復帰時ﾜｰｸ有り
STG @MR108               ; Err153 INDEX Err3
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
JMP @MR109               ; 109
MOV #153 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR109               ; 109
AND R50008               ; Error
JMP @MR110               ; 110
STG @MR110               ; 110
ANB R50008               ; Error
SET @MR111               ; Err153 INDEX Err3解除完了
CON
ENDS
;Error154-155
;到着ワーク不良・除材確認 / Arrival Work NG, Remove Check
STG @MR112               ; Err154 INDEX Err4
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
MPS
AND= DM3751 #2           ; IND3-ST4 NGﾜｰｸ 0=OK
SET LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
CON
JMP @MR113               ; 113
MOV #174 DM400           ; ﾒｯｾｰｼﾞNo.
MRD
AND= DM3751 #3           ; IND3-ST4 NGﾜｰｸ 0=OK
SET LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
CON
JMP @MR113               ; 113
MOV #175 DM400           ; ﾒｯｾｰｼﾞNo.
MRD
AND= DM3751 #4           ; IND3-ST4 NGﾜｰｸ 0=OK
SET LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
CON
JMP @MR113               ; 113
MOV #176 DM400           ; ﾒｯｾｰｼﾞNo.
MRD
AND= DM3751 #5           ; IND3-ST4 NGﾜｰｸ 0=OK
SET LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
CON
JMP @MR113               ; 113
MOV #154 DM400           ; ﾒｯｾｰｼﾞNo.
MPP
AND= DM3751 #6           ; IND3-ST4 NGﾜｰｸ 0=OK
SET LR49914              ; ｴﾗｰﾘｾｯﾄｷｰ要求
CON
JMP @MR113               ; 113
MOV #155 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR113               ; 113
AND R50008               ; Error
JMP @MR114               ; 114
STG @MR114               ; 114
ANB R50008               ; Error
BRES DM693 #0            ; IND3_ST4 Work Memory
CON
SET @MR115               ; Err154 INDEX Err4解除完了
CON
ENDS
;Error157,158
;ﾜｰｸ検出異常 / Work Detect is Abnormal
STG @MR208               ; Err157 INDEX Err7
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
JMP @MR209               ; 209
MPS
ANB @MR100               ; ﾜｰｸ有り異常
MOV #157 DM400           ; ﾒｯｾｰｼﾞNo.
MPP
AND @MR100               ; ﾜｰｸ有り異常
MOV #158 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR209               ; 209
AND R50008               ; Error
JMP @MR210               ; 210
STG @MR210               ; 210
ANB R50008               ; Error
RES @MR100               ; ﾜｰｸ有り異常
CON
SET @MR211               ; Err157 INDEX Err7解除完了
CON
ENDS
;Error160
;INDEX 干渉ｼﾘﾝﾀﾞ有り
;INDEX interference CY Detect
STG @MR304               ; Err160 INDEX Err10
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
JMP @MR305               ; 305
MOV #160 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR305               ; 305
AND R50008               ; Error
JMP @MR306               ; 306
STG @MR306               ; 306
ANB R50008               ; Error
SET @MR307               ; Err160 INDEX Err10解除完了
CON
ENDS
;Err137 
;Err137 IND3_ST3 Cartridge Floating Detection Before turn
STG @MR308               ; Err141 INDEX Err11
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
JMP @MR309               ; 309
MOV #137 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR309               ; 309
AND R50008               ; Error
JMP @MR310               ; 310
STG @MR310               ; 310
ANB R50008               ; Error
SET @MR311               ; Err141 INDEX Err11解除完了
CON
ENDS
;Err124 ｶｰﾄﾘｯｼﾞ浮き
;Err124 IND3_ST3 Cartridge Floating Detection Before turn
STG @MR212               ; Err124
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R51600               ; Err IND3
CON
JMP @MR213               ; 213
MOV #124 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR213               ; 213
AND R50008               ; Error
JMP @MR214               ; 214
STG @MR214               ; 214
ANB R50008               ; Error
SET @MR215               ; Err124解除完了
CON
ENDS
END
ENDH
