DEVICE:53
;MODULE:Monitor
;MODULE_TYPE:0
LD R50010                ; Emergency　Stop
ZRES @MR000 @MR915       ; ﾋｰﾀｰ適温外  915
;原点復帰終了したので､ ヒーターON
;After the Home Position of All Unit, Heater1 Power ON
LDP R50007               ; 原点復帰　完了
SET MR2000               ; ﾋｰﾀｰ1 ON
CON
SET MR2001               ; ﾋｰﾀｰ2 ON
;If Emergency Stop then Heater Power OFF
;Now no use this curcuit (Prevent for a fall of Temperture) 
;非常停止でヒーターOFF
;実際は使用しない。（ヒーターの温度低下防止策）
LD R50011                ; TRAP
AND CR2003               ; 常時OFF
RES MR2000               ; ﾋｰﾀｰ1 ON
CON
RES MR2001               ; ﾋｰﾀｰ2 ON
;ヒーター1状態出力
;Heater1 Output of State
LD R2000                 ; ﾋｰﾀｰ1 ON
MPS
TMR @0 #10               ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
CON
AND @T0                  ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
ANB R1010                ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄ
TMR @1 #1                ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
CON
AND @T1                  ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
OUT @MR006               ; ﾋｰﾀｰ1過昇温
MPP
LD R1010                 ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄ
ANB R1000                ; ﾋｰﾀｰ1 ALM1 低温
AND R1001                ; ﾋｰﾀｰ1 ALM2 適温
ANL
OUT @MR005               ; ﾋｰﾀｰ1適温
;ヒーター2状態出力
;Heater2 Output of State
LD R2001                 ; ﾋｰﾀｰ2 ON
MPS
TMR @3 #10               ; ﾋｰﾀｰ2 ON後ﾃﾞｨﾚｲ
CON
AND @T3                  ; ﾋｰﾀｰ2 ON後ﾃﾞｨﾚｲ
ANB R1011                ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄ
TMR @4 #1                ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
CON
AND @T4                  ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
OUT @MR008               ; ﾋｰﾀｰ2過昇温
MPP
LD R1011                 ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄ
ANB R1002                ; ﾋｰﾀｰ2 ALM1 低温
AND R1003                ; ﾋｰﾀｰ2 ALM2 適温
ANL
OUT @MR007               ; ﾋｰﾀｰ2適温
;ヒーター適温
;Heater Fit Temperature
LD @MR005                ; ﾋｰﾀｰ1適温
AND @MR007               ; ﾋｰﾀｰ2適温
OUT R50212               ; ﾋｰﾀｰ適温
;Err189 ヒーター適温外エラー				 --> 継続処理
;Err189 Heater Error of Not Fit Temperture --> Deal with Continuity 
LD R50001                ; AUTO
LD R50002                ; STEP
AND LR40203              ; ST1 STEP-04 溶着
ORL
ANB R50212               ; ﾋｰﾀｰ適温
ANB @MR000               ; ﾋｰﾀｰ適温外
ANB MR002                ; ﾋｰﾀｰ無効=ON
ANB LR41101              ; AUTO PARA02 flag_日常点検
TMR @15 #1               ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
LD @T15                  ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
ANB R50008               ; Error
SET @MR000               ; ﾋｰﾀｰ適温外
CON
SET R50110               ; Err INDEX
CON
SET R50008               ; Error
CON
SET @MR001               ; 001
MOV #189 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR001               ; 001
AND R50008               ; Error
JMP @MR002               ; 002
STG @MR002               ; 002
ANP R40006               ; ｴﾗｰﾘｾｯﾄSW
SET R42103               ; ﾌﾞｻﾞｰ停止ﾌﾗｸﾞ
CON
JMP @MR003               ; 003
STG @MR003               ; 003
ANB R50008               ; Error
RES R42103               ; ﾌﾞｻﾞｰ停止ﾌﾗｸﾞ
CON
RES @MR000               ; ﾋｰﾀｰ適温外
CON
ENDS
;Err191 ヒーター1 過昇温　  --> 非常停止処理
;Err191 Heater1 Over heat   --> Emergency STOP
LD @MR006                ; ﾋｰﾀｰ1過昇温
TMR @20 #1               ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
LD @T20                  ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2000               ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR101               ; 101
MOV #191 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR101               ; 101
AND R50008               ; Error
JMP @MR102               ; 102
STG @MR102               ; 102
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err192 ヒーター2 過昇温　  --> 非常停止処理
;Err192 Heater2 Over heat   --> Emergency STOP
LD @MR008                ; ﾋｰﾀｰ2過昇温
TMR @21 #1               ; ﾋｰﾀｰ2過昇温ﾃﾞｨﾚｲ
LD @T21                  ; ﾋｰﾀｰ2過昇温ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2001               ; ﾋｰﾀｰ2 ON
CON
SET R50008               ; Error
CON
SET @MR201               ; 201
MOV #192 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR201               ; 201
AND R50008               ; Error
JMP @MR202               ; 202
STG @MR202               ; 202
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err196 ヒーター1 断線		   --> 非常停止処理
;Err196 Heater1 Snapping of Wire --> Emergency Stop
LD R50301                ; ﾋｰﾀｰ1断線
ANB @MR006               ; ﾋｰﾀｰ1過昇温
TMR @25 #1               ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
LD @T25                  ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2000               ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR109               ; 109
MOV #196 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR109               ; 109
AND R50008               ; Error
JMP @MR110               ; 110
STG @MR110               ; 110
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err197 ヒーター2 断線		   --> 非常停止処理
;Err197 Heater2 Snapping of Wire --> Emergency Stop
LD R50302                ; ﾋｰﾀｰ2断線
ANB @MR008               ; ﾋｰﾀｰ2過昇温
TMR @26 #1               ; ﾋｰﾀｰ2断線ﾃﾞｨﾚｲ
LD @T26                  ; ﾋｰﾀｰ2断線ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR2001               ; ﾋｰﾀｰ2 ON
CON
SET R50008               ; Error
CON
SET @MR209
MOV #197 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR209
AND R50008               ; Error
JMP @MR210               ; 210
STG @MR210               ; 210
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;温調器ステータス読出し
;Temperature controller status reading
LD R50007                ; 原点復帰　完了
TMR @17 #20              ; 原点復帰ﾃﾞｨﾚｲ
LD @T17                  ; 原点復帰ﾃﾞｨﾚｲ
@SET @MR700              ; 温調器ST読み出し開始
STG @MR700               ; 温調器ST読み出し開始
ANB R50214               ; 通信中
JMP @MR701               ; 701
STG @MR701               ; 701
SET R50214               ; 通信中
CON
MPS
AND R75000               ; 通信実行中
JMP @MR702               ; 702
MPP
ANB R75000               ; 通信実行中
MPS
AND @MR004
SET @MR015
CON
MSTRT TEMP_ST #21
MPP
ANB @MR004
SET @MR004
CON
MSTRT TEMP_ST #22
STG @MR702               ; 702
RES R75000               ; 通信実行中
CON
RES R50214               ; 通信中
CON
MPS
ANB R75001               ; 通信異常
JMP @MR703               ; 703
MPP
AND R75001               ; 通信異常
SET @MR708               ; 温調器通信ｴﾗｰ
CON
RES R75001               ; 通信異常
CON
JMP @MR704               ; 704
STG @MR704               ; 704
AND @MR712               ; 温調器通信ｴﾗｰ解除
RES @MR712               ; 温調器通信ｴﾗｰ解除
CON
JMP @MR703               ; 703
STG @MR703               ; 703
TMR @18 #100             ; Port2 繰り返しﾃﾞｨﾚｲ
CON
AND @T18                 ; Port2 繰り返しﾃﾞｨﾚｲ
MPS
LD @MR004
AND @MR015
ANL
RES @MR004
CON
RES @MR015
MPP
JMP @MR700               ; 温調器ST読み出し開始
;Err190 温調器通信エラー 
;Err190 Heater Controller communication error
STG @MR708               ; 温調器通信ｴﾗｰ
ANB R50008               ; Error
SET R50008               ; Error
CON
JMP @MR709               ; 709
MOV #190 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR709               ; 709
AND R50008               ; Error
JMP @MR710               ; 710
STG @MR710               ; 710
ANB R50008               ; Error
SET @MR712               ; 温調器通信ｴﾗｰ解除
CON
ENDS
;温調器PV値読出し
;Temperature controller PV value reading
LD R50312                ; ﾋｰﾀｰ1溶着開始
LDB R50313               ; ﾋｰﾀｰ2溶着開始
OR R50313                ; ﾋｰﾀｰ2溶着開始
ANL
AND= @DM0 #0
DW #21 @DM0
LDB R50312               ; ﾋｰﾀｰ1溶着開始
AND R50313               ; ﾋｰﾀｰ2溶着開始
AND= @DM0 #0
DW #22 @DM0
LD R50312                ; ﾋｰﾀｰ1溶着開始
OR R50313                ; ﾋｰﾀｰ2溶着開始
ANB @MR515               ; ﾋｰﾀｰ溶着時間転送中
@SET @MR600              ; ﾋｰﾀｰ溶着時間転送開始
STG @MR600               ; ﾋｰﾀｰ溶着時間転送開始
SET @MR515               ; ﾋｰﾀｰ溶着時間転送中
CON
ANB R50214               ; 通信中
JMP @MR601               ; 601
STG @MR601               ; 601
SET R50214               ; 通信中
CON
MPS
AND R75000               ; 通信実行中
JMP @MR602               ; 602
MPP
ANB R75000               ; 通信実行中
MSTRT TEMP_PV @DM0
STG @MR602               ; 602
RES R75000               ; 通信実行中
CON
MPS
ANB R75001               ; 通信異常
MPS
AND= #21 @DM0
RES R50312               ; ﾋｰﾀｰ1溶着開始
CON
DW #0 @DM0
CON
JMP @MR603               ; 603
MPP
AND= #22 @DM0
RES R50313               ; ﾋｰﾀｰ2溶着開始
CON
DW #0 @DM0
CON
JMP @MR603               ; 603
MPP
AND R75001               ; 通信異常
SET @MR708               ; 温調器通信ｴﾗｰ
CON
RES R75001               ; 通信異常
CON
MPS
ANB R50000               ; HOLD
SET R50009               ; HOLDRQ
CON
JMP @MR604               ; 604
MPP
AND R50000               ; HOLD
JMP @MR604               ; 604
STG @MR603               ; 603
RES R75000               ; 通信実行中
CON
MPS
LDB R50312               ; ﾋｰﾀｰ1溶着開始
ANB R50313               ; ﾋｰﾀｰ2溶着開始
AND @MR514               ; ﾋｰﾀｰ動作STｻｲｸﾙ停止中
ANL
MPS
RES R50214               ; 通信中
CON
RES @MR515               ; ﾋｰﾀｰ溶着時間転送中
CON
ENDS
MPP
SET @MR609               ; 温度ﾃﾞｰﾀ比較
CON
ENDS
MRD
AND R50312               ; ﾋｰﾀｰ1溶着開始
JMP @MR601               ; 601
MPP
AND R50313               ; ﾋｰﾀｰ2溶着開始
JMP @MR601               ; 601
STG @MR604               ; 604
AND @MR712               ; 温調器通信ｴﾗｰ解除
RES @MR712               ; 温調器通信ｴﾗｰ解除
CON
JMP @MR603               ; 603
LD R50412                ; ST2 1ｻｲｸﾙ終了
AND R50413               ; ST3 1ｻｲｸﾙ終了
AND R50414               ; ST4 1ｻｲｸﾙ終了
OUT @MR514               ; ﾋｰﾀｰ動作STｻｲｸﾙ停止中
;温度データ比較・出力・格納
;Temperature Data Compare / Output / Store
STG @MR609               ; 温度ﾃﾞｰﾀ比較
MPS
LD<> DM290 DM280         ; 温度1  前回温度記憶1
OR<> DM292 DM282         ; 温度2  前回温度記憶2
OR<> DM294 DM284         ; 温度3  前回温度記憶3
OR<> DM296 DM286         ; 温度4  前回温度記憶4
ANL
SET R50305               ; 温度ﾃﾞｰﾀ出力
CON
JMP @MR610               ; 610
MPP
LD= DM290 DM280          ; 温度1  前回温度記憶1
AND= DM292 DM282         ; 温度2  前回温度記憶2
AND= DM294 DM284         ; 温度3  前回温度記憶3
AND= DM296 DM286         ; 温度4  前回温度記憶4
ANL
JMP @MR610               ; 610
STG @MR610               ; 610
BMOV DM290 DM280 #8      ; 温度1  前回温度記憶1
ENDS
LDP R50001               ; AUTO
FMOV #0 DM280 #8         ; 前回温度記憶1
;Error161
;動作中MAIN AIR OFFｴﾗｰ
;Main AIR off during operation error
LD R50004                ; TASKDO
AND MR607                ; 動作中MainAirOff
ANB R50008               ; Error
@SET @MR800              ; 動作中Main Air OFF
STG @MR800               ; 動作中Main Air OFF
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50110               ; Err INDEX
CON
JMP @MR802               ; 802
MOV #161 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR802               ; 802
AND R50008               ; Error
RES MR607                ; 動作中MainAirOff
CON
JMP @MR803               ; 803
STG @MR803               ; 803
ANB R50008               ; Error
RES @MR801               ; 801
CON
ENDS
;Error162 動作中ﾗｲﾄｶｰﾃﾝ遮光
LD MR10003               ; ﾗｲﾄｶｰﾃﾝ監視中(ｼｬｯﾀｰ動作)
ANB R1107                ; ﾗｲﾄｶｰﾃﾝ遮光無し=ON
OR R50208                ; RB動作中LC遮光
ANB @MR805               ; 動作中ﾗｲﾄｶｰﾃﾝ遮光ﾌﾗｸﾞ
SET MR10007              ; ﾗｲﾄｶｰﾃﾝ遮光検出
CON
SET @MR804
STG @MR804
ANB R50008               ; Error
SET @MR805               ; 動作中ﾗｲﾄｶｰﾃﾝ遮光ﾌﾗｸﾞ
CON
SET R50008               ; Error
CON
SET R50110               ; Err INDEX
CON
JMP @MR806
MOV #162 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR806
AND R50008               ; Error
RES MR607                ; 動作中MainAirOff
CON
JMP @MR807
STG @MR807
ANB R50008               ; Error
RES DM2207.2             ; RB1_PCON 一時停止=ON
CON
RES R50208               ; RB動作中LC遮光
CON
RES MR10007              ; ﾗｲﾄｶｰﾃﾝ遮光検出
CON
RES @MR805               ; 動作中ﾗｲﾄｶｰﾃﾝ遮光ﾌﾗｸﾞ
CON
ENDS
;Error163
;動作中MAIN 真空 OFFｴﾗｰ
LD R50004                ; TASKDO
ANB R4312                ; 元真空圧(ONOK)
ANB R50008               ; Error
TMH @24 #30              ; 元真空圧低下検知ﾃﾞｨﾚｲ
CON
AND @T24                 ; 元真空圧低下検知ﾃﾞｨﾚｲ
@SET @MR808
STG @MR808
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50110               ; Err INDEX
CON
JMP @MR809
MOV #163 DM400           ; ﾒｯｾｰｼﾞNo.
STG @MR809
AND R50008               ; Error
RES MR607                ; 動作中MainAirOff
CON
JMP @MR810
STG @MR810
ANB R50008               ; Error
RES @MR808
CON
ENDS
;
;Sub Information Window
LD R50004                ; TASKDO
MC
LD MR15100               ; SubInfo ﾎﾟｰｽﾞ処理中
DW #0 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
LDB R50212               ; ﾋｰﾀｰ適温
ANB R50003               ; HOME
ANB MR002                ; ﾋｰﾀｰ無効=ON
ANB LR41101              ; AUTO PARA02 flag_日常点検
OUT MR15101              ; SubInfo ﾋｰﾀｰ適温待ち
CON
DW #1 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
;R15100〜16点をSub Information 表示用
LD<> $0000 MR15100       ; SubInfo ﾎﾟｰｽﾞ処理中
OUT R42100               ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示
MCR
;
LD MR2000                ; ﾋｰﾀｰ1 ON
AND MR2001               ; ﾋｰﾀｰ2 ON
OUT R42104               ; ﾋｰﾀｰ電源OK
;Index 電源再投入ﾃﾞｨﾚｲ
;Index Re-Power Delay 
LDB R2009                ; RB1 電源ON
TMR #200 #60             ; PCON電源再投入ﾃﾞｨﾚｲ
END
ENDH
