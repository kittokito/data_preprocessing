DEVICE:51
;MODULE:Monitor
;MODULE_TYPE:0
LD R50010                ; Emergency　Stop
ZRES @MR000 @MR915       ; ﾋｰﾀｰ適温外
;原点復帰終了したので､ ヒーターON
;After the Home Position of All Unit, Heater1 Power ON
LDP R50007               ; 原点復帰　完了
MPS
ANB MR000                ; ﾋｰﾀｰ無し=ON
MPS
SET MR500                ; ﾋｰﾀｰ1 ON
MPP
AND CR2003               ; 常時OFF
SET MR501                ; ﾋｰﾀｰ2 ON
MPP
AND MR000                ; ﾋｰﾀｰ無し=ON
RES MR500                ; ﾋｰﾀｰ1 ON
CON
RES MR501                ; ﾋｰﾀｰ2 ON
;If Emergency Stop then Heater Power OFF
;Now no use this curcuit (Prevent for a fall of Temperture) 
;非常停止でヒーターOFF
;実際は使用しない。（ヒーターの温度低下防止策）
LD R50011                ; TRAP
AND CR2003               ; 常時OFF
RES MR500                ; ﾋｰﾀｰ1 ON
CON
RES MR501                ; ﾋｰﾀｰ2 ON
;ヒーター1状態出力
;Heater1 Output of State
LD MR500                 ; ﾋｰﾀｰ1 ON
MPS
TMR @0 #10               ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
CON
AND @T0                  ; ﾋｰﾀｰ1 ON後ﾃﾞｨﾚｲ
ANB R000                 ; ﾋｰﾀｰ1 過昇=OFF
TMR @1 #1                ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
CON
AND @T1                  ; ﾋｰﾀｰ1 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
OUT @MR006               ; ﾋｰﾀｰ1過昇温
MPP
LD R000                  ; ﾋｰﾀｰ1 過昇=OFF
ANB R001                 ; ﾋｰﾀｰ1 断線=ON
AND R002                 ; ﾋｰﾀｰ1 適温=ON
ANL
OUT @MR005               ; ﾋｰﾀｰ1適温
;ヒーター2状態出力
;Heater2 Output of State
LD MR501                 ; ﾋｰﾀｰ2 ON
MPS
TMR @3 #10               ; ﾋｰﾀｰ2 ON後ﾃﾞｨﾚｲ
CON
AND @T3                  ; ﾋｰﾀｰ2 ON後ﾃﾞｨﾚｲ
ANB R003                 ; ﾋｰﾀｰ2 過昇=OFF
TMR @4 #1                ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
CON
AND @T4                  ; ﾋｰﾀｰ2 ｵｰﾊﾞｰﾋｰﾄﾃﾞｨﾚｲ
OUT @MR008               ; ﾋｰﾀｰ2過昇温
MPP
LD R003                  ; ﾋｰﾀｰ2 過昇=OFF
ANB R004                 ; ﾋｰﾀｰ2 断線=ON
AND R005                 ; ﾋｰﾀｰ2 適温=ON
ANL
OUT @MR007               ; ﾋｰﾀｰ2適温
;ヒーター適温
;Heater Fit Temperature
LD @MR005                ; ﾋｰﾀｰ1適温
OR MR000                 ; ﾋｰﾀｰ無し=ON
OUT R50212               ; ﾋｰﾀｰ適温
;Err40 ヒーター適温外エラー				 --> 継続処理
;Err40 Heater Error of Not Fit Temperture --> Deal with Continuity 
LD R50001                ; AUTO
OR R50002                ; STEP
ANB R50212               ; ﾋｰﾀｰ適温
TMR @15 #1               ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
LD @T15                  ; 全ﾋｰﾀｰ適温外ﾃﾞｨﾚｲ
OR R50213                ; ﾋｰﾀ適温外運転開始
ANB R50008               ; Error
SET R51403               ; Err IND2ST3
CON
SET @MR000               ; ﾋｰﾀｰ適温外
CON
SET R50008               ; Error
CON
JMP @MR001               ; 001
MOV #40 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No40" DM100
STG @MR001               ; 001
AND R50008               ; Error
JMP @MR002               ; 002
STG @MR002               ; 002
ANB R50008               ; Error
RES @MR000               ; ﾋｰﾀｰ適温外
CON
ENDS
;Err41 ヒーター1 過昇温　  --> 非常停止処理
;Err41 Heater1 Over heat   --> Emergency STOP
LD @MR006                ; ﾋｰﾀｰ1過昇温
TMR @20 #1               ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
LD @T20                  ; ﾋｰﾀｰ1過昇温ﾃﾞｨﾚｲ
ANB R50008               ; Error
SET R51403               ; Err IND2ST3
CON
RES MR500                ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR101               ; 101
MOV #41 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No41" DM100
STG @MR101               ; 101
AND R50008               ; Error
JMP @MR102               ; 102
STG @MR102               ; 102
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err42 ヒーター1 断線		   --> 非常停止処理
;Err42 Heater1 Snapping of Wire --> Emergency Stop
LD R500                  ; ﾋｰﾀｰ1 ON
AND R001                 ; ﾋｰﾀｰ1 断線=ON
ANB @MR006               ; ﾋｰﾀｰ1過昇温
TMR @25 #20              ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
LD @T25                  ; ﾋｰﾀｰ1断線ﾃﾞｨﾚｲ
ANB R50008               ; Error
SET R51403               ; Err IND2ST3
CON
RES MR500                ; ﾋｰﾀｰ1 ON
CON
SET R50008               ; Error
CON
SET @MR109               ; 109
MOV #42 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No42" DM100
STG @MR109               ; 109
AND R50008               ; Error
JMP @MR110               ; 110
STG @MR110               ; 110
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err43 ヒーター2 過昇温　  --> 非常停止処理
;Err43 Heater2 Over heat   --> Emergency STOP
LD @MR008                ; ﾋｰﾀｰ2過昇温
TMR @21 #1               ; ﾋｰﾀｰ2過昇温ﾃﾞｨﾚｲ
LD @T21                  ; ﾋｰﾀｰ2過昇温ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR501                ; ﾋｰﾀｰ2 ON
CON
SET R50008               ; Error
CON
SET @MR201               ; 201
MOV #43 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No43" DM100
STG @MR201               ; 201
AND R50008               ; Error
JMP @MR202               ; 202
STG @MR202               ; 202
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err44 ヒーター2 断線		   --> 非常停止処理
;Err44 Heater2 Snapping of Wire --> Emergency Stop
LD R501                  ; ﾋｰﾀｰ2 ON
AND R004                 ; ﾋｰﾀｰ2 断線=ON
ANB @MR008               ; ﾋｰﾀｰ2過昇温
TMR @26 #20              ; ﾋｰﾀｰ2断線ﾃﾞｨﾚｲ
LD @T26                  ; ﾋｰﾀｰ2断線ﾃﾞｨﾚｲ
ANB R50008               ; Error
RES MR501                ; ﾋｰﾀｰ2 ON
CON
SET R50008               ; Error
CON
SET @MR209               ; 109
MOV #44 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No44" DM100
STG @MR209               ; 109
AND R50008               ; Error
JMP @MR210               ; 110
STG @MR210               ; 110
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;Err50 メインエアーＯＦＦ --> 非常停止処理
;Err50 Main air off       --> Emergency Stop
LD R50001                ; AUTO
OR R50002                ; STEP
AND R1014                ; 元圧確認
ANB R50008               ; Error
SET R50008               ; Error
CON
SET R50111               ; Err ST1
CON
SET @MR401
MOV #50 DM400            ; ﾒｯｾｰｼﾞNo.
SMOV "Err_No50" DM100
STG @MR401
AND R50008               ; Error
JMP @MR402
STG @MR402
ANB R50008               ; Error
SET R50011               ; TRAP
CON
ENDS
;ﾌｨﾙﾑ切れ監視　
LD CR2002                ; 常時ON
ANB R5414                ; 剥離ﾌｨﾙﾑ切れ　銀紙検出
ANB MR15007              ; 剥離F銀紙検出
SET MR15007              ; 剥離F銀紙検出
;ﾌｨﾙﾑﾘｰﾙ交換監視
LD MR15007               ; 剥離F銀紙検出
AND R5400                ; 剥離F ｸﾗﾝﾌﾟ逃がし上下 上昇限
AND R5414                ; 剥離ﾌｨﾙﾑ切れ　銀紙検出
RES MR15007              ; 剥離F銀紙検出
;Sub Information Window
LD R50004                ; TASKDO
MC
LD MR15100               ; SubInfo ﾎﾟｰｽﾞ処理中
DW #0 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
LDB R50212               ; ﾋｰﾀｰ適温
ANB R50003               ; HOME
OUT MR15105              ; SubInfo 05
CON
DW #5 DM621              ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示内容
;R15100〜16点をSub Information 表示用
LD<> $0000 MR15100       ; SubInfo ﾎﾟｰｽﾞ処理中
OUT R42100               ; ﾒｲﾝ画面ｳｨﾝﾄﾞｳ表示
MCR
;Weld head Cleaning Request: Removable Film W/H
LD CR2002                ; 常時ON
MPS
LDB MR22008
AND MR22004
ANL
TMR @42 #20
CON
AND @T42
MPS
AND= DM543 #0            ; ST5 Data03
LDA DM541                ; ST5 Data01
CON
STA DM1101               ; 剥離溶着CNT
CON
AND= DM541 DM1101        ; ST5 Data01  剥離溶着CNT
SET MR22008
MPP
AND<> DM543 #0           ; ST5 Data03
SET MR22002
MPP
LD MR22008
AND= DM1101 #0           ; 剥離溶着CNT
ANL
RES MR22000
CON
RES MR22004
CON
RES MR22008
LD MR22002
AND MR22003
RES MR22002
CON
RES MR22003
CON
RES MR22000
CON
RES MR22004
END
ENDH
