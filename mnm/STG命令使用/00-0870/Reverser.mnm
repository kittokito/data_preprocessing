DEVICE:52
;MODULE:Reverser
;MODULE_TYPE:0
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50914                ; HOMEsw ON
ZRES @MR000 @MR1915
;状態ﾌﾗｸﾞ  /  State FLG
;原点
;Home Position
LDB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
ANB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
OUT R54001               ; 反転部原点位置
;反転ﾊﾝﾄﾞ吸着確認
;Reverser Hand Adsorp Confirmation
LDB R2313                ; 反転ﾊﾝﾄﾞ 真空破壊
AND R2312                ; 反転ﾊﾝﾄﾞ 吸着
MPS
AND R4804                ; 反転ﾊﾝﾄﾞ吸着 OUT1
TMR @0 #5
CON
AND @T0
OUT @MR100               ; 反転ﾊﾝﾄﾞ 吸着検出
MRD
ANB R4804                ; 反転ﾊﾝﾄﾞ吸着 OUT1
TMR @1 #20
CON
AND @T1
OUT @MR101               ; 反転ﾊﾝﾄﾞ 未吸着検出
MRD
AND R4805                ; 反転ﾊﾝﾄﾞ吸着 OUT2
TMR @2 #5
CON
AND @T2
OUT @MR102               ; 反転ﾊﾝﾄﾞ 除材検出
MPP
ANB R4805                ; 反転ﾊﾝﾄﾞ吸着 OUT2
TMR @3 #50
CON
AND @T3
OUT @MR103               ; 反転ﾊﾝﾄﾞ 未除材検出
;反転ﾊﾝﾄﾞ状態確認
;Reverser Hand States Confirmation
LDB R6310                ; 反転上下 上昇
AND R6311                ; 反転上下 下降
ANB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
ANB R6314                ; 反転 CW((R)ｾｯﾄ側)
AND R6315                ; 反転 CCW((L)取出側)
ANB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
OUT @MR200               ; 反転ﾊﾝﾄﾞ右行OK
LDB R6311                ; 反転上下 下降
AND R6310                ; 反転上下 上昇
ANB R4511                ; 反転 下降限
AND R4510                ; 反転 上昇限
ANB R6312                ; 反転左右　左行((L)側)
AND R6313                ; 反転左右　右行((R)側)
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
AND R6200                ; ｳｪﾊｰ治具(R) 前進(転写側)
AND R4400                ; 治具(R) 前進限(転写側)
AND R6202                ; ｳｪﾊｰ治具(R) 左行(転写側)
AND R4402                ; 治具(R) 転写限（左限）
OUT @MR201               ; 反転ﾊﾝﾄﾞ旋回可能位置
LD R4804                 ; 反転ﾊﾝﾄﾞ吸着 OUT1
OR R4805                 ; 反転ﾊﾝﾄﾞ吸着 OUT2
AND @MR201               ; 反転ﾊﾝﾄﾞ旋回可能位置
OR R4514                 ; 反転 CW側限（(R)ｾｯﾄ側)
OUT @MR202               ; 反転ﾊﾝﾄﾞ旋回OK
;原点復帰
;HOME
STG R54000               ; 反転部原点復帰要求
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
MPS
LDB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
ANL
RES MR6314               ; 反転 CW((R)ｾｯﾄ側)
CON
SET MR6315               ; 反転 CCW((L)取出側)
CON
JMP @MR1000              ; 1000
MPP
LD R4514                 ; 反転 CW側限（(R)ｾｯﾄ側)
ORB R4515                ; 反転 CCW側限（(L)取出し側)
AND R52110               ; 転写RB(R)原点復帰完
ANL
MPS
LDB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
ANL
RES MR6312               ; 反転左右　左行((L)側)
CON
SET MR6313               ; 反転左右　右行((R)側)
CON
JMP @MR1001              ; 1001
MPP
LD R4512                 ; 反転 (L)側限
ORB R4513                ; 反転 (R)側限
ANL
JMP @MR1002              ; 1002
STG @MR1000              ; 1000
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6312               ; 反転左右　左行((L)側)
CON
SET MR6313               ; 反転左右　右行((R)側)
CON
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
JMP @MR1009
STG @MR1001              ; 1001
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6314               ; 反転 CW((R)ｾｯﾄ側)
CON
SET MR6315               ; 反転 CCW((L)取出側)
CON
ANB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
JMP @MR1009
STG @MR1002              ; 1002
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6311               ; 反転上下 下降
CON
SET MR6310               ; 反転上下 上昇
CON
ANB R4511                ; 反転 下降限
AND R4510                ; 反転 上昇限
JMP @MR1003              ; 1003
STG @MR1003              ; 1003
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6312               ; 反転左右　左行((L)側)
CON
SET MR6313               ; 反転左右　右行((R)側)
CON
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
JMP @MR1004              ; 1004
STG @MR1004              ; 1004
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6314               ; 反転 CW((R)ｾｯﾄ側)
CON
SET MR6315               ; 反転 CCW((L)取出側)
CON
ANB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
JMP @MR1009
STG @MR1009
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6310               ; 反転上下 上昇
CON
SET MR6311               ; 反転上下 下降
CON
ANB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
JMP @MR1010
STG @MR1010
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
SET MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
MPS
AND @MR102               ; 反転ﾊﾝﾄﾞ 除材検出
JMP @MR1015
MPP
AND @MR103               ; 反転ﾊﾝﾄﾞ 未除材検出
JMP @MR1011
STG @MR1011
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
SET MR802                ; 反転ﾊﾝﾄﾞ上WF残
CON
JMP @MR1012
STG @MR1012
ANB MR802                ; 反転ﾊﾝﾄﾞ上WF残
AND R50110               ; Err 転写(L)UT
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51006               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR1013              ; 1013
STG @MR1013              ; 1013
AND R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
JMP @MR1014              ; 1014
STG @MR1014              ; 1014
ANB R50110               ; Err 転写(L)UT
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
SET R51007               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
JMP @MR1010
STG @MR1015
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
SET R54015               ; 反転部原点復帰完了
CON
ENDS
;**********  STEP動作  /  STEP Operation  **********
;
LDP R50002               ; 手動中
MPS
AND LR108                ; 反転ﾊﾝﾄﾞ  ｳｪﾊｰ受取
@SET @MR1500             ; 受取開始
MPP
AND LR109                ; 反転部よりWF受取ｽﾃｯﾌﾟ選択
@SET @MR1300             ; 1300
STG @MR1300              ; 1300
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6311               ; 反転上下 下降
CON
SET MR6310               ; 反転上下 上昇
CON
AND R4510                ; 反転 上昇限
JMP @MR1702              ; 1702
;**********  AUTO動作  /  AUTO Operation  **********
;
LDP R50001               ; 自動中
SET @MR1400              ; 受取準備開始
;反転ﾊﾝﾄﾞ上ｳｪﾊｰ確認
;Wafer Check on Reverser hand
STG @MR1400              ; 受取準備開始
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
SET MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
MPS
AND @MR102               ; 反転ﾊﾝﾄﾞ 除材検出
JMP @MR1401              ; 1401
MPP
AND @MR103               ; 反転ﾊﾝﾄﾞ 未除材検出
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
SET MR802                ; 反転ﾊﾝﾄﾞ上WF残
CON
JMP @MR1402              ; 1402
STG @MR1401              ; 1401
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
JMP @MR1500              ; 受取開始
;***  ERROR ｳｪﾊｰ残りの時        ***
;***  ERROR when Wafer remain   ***
STG @MR1402              ; 1402
ANB MR802                ; 反転ﾊﾝﾄﾞ上WF残
AND R50110               ; Err 転写(L)UT
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51006               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR1403              ; 1403
STG @MR1403              ; 1403
AND R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
JMP @MR1404              ; 1404
STG @MR1404              ; 1404
ANB R50110               ; Err 転写(L)UT
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
SET R51007               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
JMP @MR1400              ; 受取準備開始
;***      ***
;(L)より受取開始
;Pick up from Jig(L) Start
STG @MR1500              ; 受取開始
MPS
LD R54100                ; 反転部取出し要求
AND R54001               ; 反転部原点位置
ANB R54114               ; 反転部取出し実行中
ANB R54214               ; 反転部給材実行中
ANL
SET R54114               ; 反転部取出し実行中
CON
JMP @MR1501              ; 1501
MPP
LDB R54100               ; 反転部取出し要求
AND R49802               ; （L)払出し完了
ANL
SET R49804               ; 反転払出し完了
CON
JMP @MR1803              ; 1803
STG @MR1501              ; 1501
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
AND R54114               ; 反転部取出し実行中
RES R54100               ; 反転部取出し要求
CON
JMP @MR1502              ; 1502
STG @MR1502              ; 1502
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2310               ; ｳｪﾊｰ治具(L)吸着
CON
SET MR2311               ; ｳｪﾊｰ治具(L)真空破壊
CON
MPS
AND R4803                ; 治具(L)吸着 OUT2
JMP @MR1503              ; 1503
MPP
TMR @6 #30
CON
AND @T6
JMP @MR1503              ; 1503
STG @MR1503              ; 1503
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
TMR @10 #5
CON
AND @T10
RES MR2310               ; ｳｪﾊｰ治具(L)吸着
CON
RES MR2311               ; ｳｪﾊｰ治具(L)真空破壊
CON
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1504              ; 1504
;反転　左行
;Reverser Go to Left
STG @MR1504              ; 1504
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
AND @MR200               ; 反転ﾊﾝﾄﾞ右行OK
RES MR6313               ; 反転左右　右行((R)側)
CON
SET MR6312               ; 反転左右　左行((L)側)
CON
AND R4512                ; 反転 (L)側限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1505              ; 1505
;吸着切り替え
;Adsorption Change
STG @MR1505              ; 1505
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
TMR @13 #5
CON
AND @T13
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
SET MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
JMP @MR1506              ; 1506
;反転　上昇
;Reverser Up
STG @MR1506              ; 1506
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6311               ; 反転上下 下降
CON
SET MR6310               ; 反転上下 上昇
CON
ANB R4511                ; 反転 下降限
AND R4510                ; 反転 上昇限
TMH @11 #20
CON
AND @T11
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1507              ; ｳｪﾊｰ吸着確認
;反転ﾊﾝﾄﾞ ｳｪﾊｰ吸着確認
;Reverser Hand Adsorption check
STG @MR1507              ; ｳｪﾊｰ吸着確認
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
MPS
LD R4804                 ; 反転ﾊﾝﾄﾞ吸着 OUT1
OR R50002                ; 手動中
ANL
JMP @MR1700              ; 1700
MPP
LDB R4804                ; 反転ﾊﾝﾄﾞ吸着 OUT1
ANB R50002               ; 手動中
ANL
TMR @5 #30
CON
AND @T5
SET MR800                ; 反転ﾊﾝﾄﾞ吸着異常
CON
JMP @MR1600              ; 1600
;*** ERROR 反転ﾊﾝﾄﾞ吸着異常の時    ***
;*** ERROR when Adsorption Error   ***
STG @MR1600              ; 1600
ANB MR800                ; 反転ﾊﾝﾄﾞ吸着異常
AND R50110               ; Err 転写(L)UT
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51006               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR1601              ; 1601
STG @MR1601              ; 1601
AND R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
JMP @MR1602              ; 1602
STG @MR1602              ; 1602
ANB R50110               ; Err 転写(L)UT
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
SET R51007               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
MPS
ANB R40009               ; ｴﾗｰｳｨﾝﾄﾞｩ YES sw
JMP @MR1507              ; ｳｪﾊｰ吸着確認
MPP
AND R40009               ; ｴﾗｰｳｨﾝﾄﾞｩ YES sw
JMP @MR1603              ; ｳｪﾊｰ除材確認
;*** 反転ﾊﾝﾄﾞ上ｳｪﾊｰ除材確認
;*** Wafer on Reverser hand remain check
STG @MR1603              ; ｳｪﾊｰ除材確認
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
SET MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
MPS
AND @MR102               ; 反転ﾊﾝﾄﾞ 除材検出
JMP @MR1607              ; 1607
MPP
AND @MR103               ; 反転ﾊﾝﾄﾞ 未除材検出
SET MR802                ; 反転ﾊﾝﾄﾞ上WF残
CON
JMP @MR1604              ; 1604
STG @MR1604              ; 1604
ANB MR802                ; 反転ﾊﾝﾄﾞ上WF残
AND R50110               ; Err 転写(L)UT
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51006               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR1605              ; 1605
STG @MR1605              ; 1605
AND R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
JMP @MR1606              ; 1606
STG @MR1606              ; 1606
ANB R50110               ; Err 転写(L)UT
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
SET R51007               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
JMP @MR1603              ; ｳｪﾊｰ除材確認
;*** 反転 右行
;*** Reverser Go to Right
STG @MR1607              ; 1607
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
RES MR6312               ; 反転左右　左行((L)側)
CON
SET MR6313               ; 反転左右　右行((R)側)
CON
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
JMP @MR1608              ; 1608
;*** 反転　下降
;*** Reverser Down
STG @MR1608              ; 1608
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6310               ; 反転上下 上昇
CON
SET MR6311               ; 反転上下 下降
CON
ANB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
JMP @MR1609              ; 1609
STG @MR1609              ; 1609
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES R54114               ; 反転部取出し実行中
CON
SET R54115               ; 反転部取出し完了
CON
JMP @MR1500              ; 受取開始
;***      ***
;反転 右行
;Reverser Go to Right
STG @MR1700              ; 1700
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES MR6312               ; 反転左右　左行((L)側)
CON
SET MR6313               ; 反転左右　右行((R)側)
CON
ANB R4512                ; 反転 (L)側限
AND R4513                ; 反転 (R)側限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1701              ; 1701
;反転ﾊﾝﾄﾞ受取完了
;Reverser Pick up Complete
STG @MR1701              ; 1701
ANB R50302               ; 転写部(L) Pause
ANB R50110               ; Err 転写(L)UT
RES R54114               ; 反転部取出し実行中
CON
SET R54115               ; 反転部取出し完了
CON
MPS
ANB R50002               ; 手動中
JMP @MR1702              ; 1702
MPP
LD R50002                ; 手動中
AND LR108                ; 反転ﾊﾝﾄﾞ  ｳｪﾊｰ受取
ANL
ENDS
;（R)受取準備完了待ち
;Waiting for Supply Request from (R) 
STG @MR1702              ; 1702
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
AND R54200               ; 反転部給材要求
ANB T7                   ; (R)ｳｪﾊｰ待ちﾀｲﾑｵｰﾊﾞ
ANB R54114               ; 反転部取出し実行中
ANB R54214               ; 反転部給材実行中
SET R54214               ; 反転部給材実行中
CON
RES R54200               ; 反転部給材要求
CON
JMP @MR1703              ; 1703
;反転 旋回（CW側）
;Reverser Turn
STG @MR1703              ; 1703
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
AND @MR202               ; 反転ﾊﾝﾄﾞ旋回OK
RES MR6315               ; 反転 CCW((L)取出側)
CON
SET MR6314               ; 反転 CW((R)ｾｯﾄ側)
CON
AND R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1704              ; 1704
;治具(R) 吸着ON
;Jig(R) Adsorb
STG @MR1704              ; 1704
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
TMR @4 #10
CON
AND @T4
RES MR2315               ; ｳｪﾊｰ治具(R)真空破壊
CON
SET MR2314               ; ｳｪﾊｰ治具(R)吸着
CON
JMP @MR1705              ; 1705
;反転　下降
;Reverser Down
STG @MR1705              ; 1705
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR6310               ; 反転上下 上昇
CON
SET MR6311               ; 反転上下 下降
CON
ANB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1706              ; 1706
;反転 真空破壊
;Reverser Release　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　破壊時間測定用↓
STG @MR1706              ; 1706
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
SET MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
MPS
LD R4805                 ; 反転ﾊﾝﾄﾞ吸着 OUT2
AND R4806                ; 治具(R)吸着 OUT1
ANL
JMP @MR1707              ; 1707
MRD
AND R50002               ; 手動中
TMH @7 #100
CON
AND @T7
ANB R4804                ; 反転ﾊﾝﾄﾞ吸着 OUT1
JMP @MR1707              ; 1707
MPP
LDB R4805                ; 反転ﾊﾝﾄﾞ吸着 OUT2
ORB R4806                ; 治具(R)吸着 OUT1
ANB R50002               ; 手動中
ANL
TMH @12 #100
CON
AND @T12
JMP @MR1707              ; 1707
;反転　上昇
;Reverser Up
STG @MR1707              ; 1707
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR6311               ; 反転上下 下降
CON
SET MR6310               ; 反転上下 上昇
CON
AND R4510                ; 反転 上昇限
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1708              ; 1708
;吸着確認
;Adsorption check
STG @MR1708              ; 1708
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR2313               ; 反転ﾊﾝﾄﾞ 真空破壊
CON
SET MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
MPS
LD @MR102                ; 反転ﾊﾝﾄﾞ 除材検出
OR R50002                ; 手動中
ANL
JMP @MR1800              ; 1800
MPP
LD @MR103                ; 反転ﾊﾝﾄﾞ 未除材検出
ANB R50002               ; 手動中
ANL
SET MR803                ; 反転ﾊﾝﾄﾞ受渡し異常
CON
JMP @MR1709              ; 1709
;*** ERROR 反転ﾊﾝﾄﾞ受渡し異常の時    ***
;*** ERROR when Transport Error   ***
STG @MR1709              ; 1709
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51006               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR1710              ; 1710
STG @MR1710              ; 1710
AND R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
JMP @MR1711              ; 1711
STG @MR1711              ; 1711
ANB R50113               ; Err 転写(R)UT
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51301               ; 治具(L)ｴﾗｰﾘｾｯﾄsw ON
SET R51007               ; 治具(L)ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
JMP @MR1708              ; 1708
;***      ***
;反転 旋回戻り（CCW側）
;Reverser Return
STG @MR1800              ; 1800
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR2312               ; 反転ﾊﾝﾄﾞ 吸着
CON
RES MR6314               ; 反転 CW((R)ｾｯﾄ側)
CON
SET MR6315               ; 反転 CCW((L)取出側)
CON
ANB R4514                ; 反転 CW側限（(R)ｾｯﾄ側)
AND R4515                ; 反転 CCW側限（(L)取出し側)
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1801              ; 1801
;反転　下降
;Reverser Down
STG @MR1801              ; 1801
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES MR6310               ; 反転上下 上昇
CON
SET MR6311               ; 反転上下 下降
CON
ANB R4510                ; 反転 上昇限
AND R4511                ; 反転 下降限
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR1802              ; 1802
;反転ﾊﾝﾄﾞ給材完了
;Reverser Supply Complete
STG @MR1802              ; 1802
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
RES R54214               ; 反転部給材実行中
CON
SET R54215               ; 反転部給材完了
CON
MPS
ANB R50002               ; 手動中
MPS
ANB R49802               ; （L)払出し完了
JMP @MR1400              ; 受取準備開始
MPP
AND R49802               ; （L)払出し完了
SET R49804               ; 反転払出し完了
CON
JMP @MR1803              ; 1803
MPP
AND R50002               ; 手動中
ENDS
STG @MR1803              ; 1803
ANB R50304               ; 転写部(R) Pause
ANB R50113               ; Err 転写(R)UT
MPS
AND R49807               ; 全ﾕﾆｯﾄ払出し完了
RES R49804               ; 反転払出し完了
CON
ENDS
MPP
LDB R49807               ; 全ﾕﾆｯﾄ払出し完了
ANB R49802               ; （L)払出し完了
ANL
RES R49804               ; 反転払出し完了
CON
JMP @MR1500              ; 受取開始
END
ENDH
