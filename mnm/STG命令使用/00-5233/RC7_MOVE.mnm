DEVICE:53
;MODULE:RC7_MOVE
;MODULE_TYPE:1
;OneSystem SupportMacro
;
;-------------------------------------------
;Macro Name 	: 	RC7_MOVE
;Macro Type	:	Self Holding
;Ver.		:	1.0
;Function	:	EPSON RC7N系 簡易型移動ﾏｸﾛ
;-------------------------------------------
;引数：
;	V0	=	目標位置							V0	=	Target Position Value
;
;	P0	=	RB入力先頭番地	DM番号で指定		P0	=	Input Leading Address for RB
;	P1	=	RB出力先頭番地	DM番号で指定		P1	=	Output Leading Address for RB
;	P2	=	完了コード							P2	=	Macro Complete Code
;			#20			=	正常終了				#20 		=	No Error
;			#21			=	RB応答なし				#21 		=	Non Responce
;			#22			=	ｽﾃｰﾀｽ異常				#22   	  	=	Status Abnormal
;			#23         =   速度設定異常            #23         =   Speed Setting Abnormal
;			#24			=	ｱﾗｲﾒﾝﾄNG				#24			=	Alignment NG
;			Alarmcode	=	Alarm					#Alarmcode =	Alarm
;
;機能：RC7系をﾎﾟｼﾞｼｮﾝNo.を指定して移動させます。  簡易型です。
;IN
BLD P0:#2 #15            ; RB_IN
OUT RC7_MOVE             ; IN MOVE
BLD P0:#0 #0             ; RB_IN
OUT Ready                ; IN Ready
BLD P0:#0 #3             ; RB_IN
OUT Error                ; IN Error
BLD P0:#0 #4             ; RB_IN
OUT EstopOn              ; IN EstopOn
BLD P0:#0 #6             ; RB_IN
OUT SError               ; IN SError
BLD P0:#0 #7             ; RB_IN
OUT Warning              ; IN Warning
BLD P0:#0 #8             ; RB_IN
OUT SV                   ; IN SV
;OUT
LD CSTR                  ; OUT START
BOUT P1:#2 #15           ; RB_OUT
;初期化　/ Initialize
LD @CR2008
SET @MR000
;RBｽﾃｰﾀｽﾓﾆﾀｰ / Status Monitoring
LDB Ready                ; IN Ready
ANB Error                ; IN Error
ANB EstopOn              ; IN EstopOn
ANB SError               ; IN SError
LDB Warning              ; IN Warning
OR CR2002                ; 常時ON
ANL
OUT @MR013               ; RB Status OK
;Move Parameter storage
;ﾎﾟｲﾝﾄNo.1は待機位置で低速度移動の為、ｴﾗｰ時間30s固定。
;それ以外はOVERRAID設定依存
STG @MR000
MPS
LDA V0                   ; POS
CON
STA P1:#3                ; RB_OUT
MRD
AND<> V0 #1              ; POS
LDA #1000
CON
DIV DM11357              ; SCRBC1へ 通常スピード
CON
STA.D @DM2
MRD
AND= V0 #1               ; POS
DW.D #300 @DM2
MPP
TMH @0 #3
CON
AND @T0
JMP @MR001
STG @MR001
MPS
AND @MR013               ; RB Status OK
SET CSTR                 ; OUT START
CON
JMP @MR002
MPP
ANB @MR013               ; RB Status OK
MPS
LDB Error                ; IN Error
ANB SError               ; IN SError
ANL
DW #22 @DM0
CON
JMP @MR011               ; 終了処理
MPP
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
JMP @MR010               ; Alarm
STG @MR002
MPS
LD RC7_MOVE              ; IN MOVE
LDB RC7_MOVE             ; IN MOVE
AND= P1:#3 P0:#3         ; RB_OUT  RB_IN
AND @T4
ORL
ANB Error                ; IN Error
ANB SError               ; IN SError
ANB @T2
ANL
JMP @MR003
MRD
TMH @4 #20
MRD
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
RES CSTR                 ; OUT START
CON
JMP @MR010               ; Alarm
MPP
TMR @2 #20
CON
AND @T2
RES CSTR                 ; OUT START
CON
DW #21 @DM0
CON
JMP @MR011               ; 終了処理
STG @MR003
MPS
LDB RC7_MOVE             ; IN MOVE
AND= P1:#3 P0:#3         ; RB_OUT  RB_IN
ANL
RES CSTR                 ; OUT START
CON
DW #20 @DM0
CON
JMP @MR011               ; 終了処理
MRD
LD Error                 ; IN Error
OR SError                ; IN SError
ANL
RES CSTR                 ; OUT START
CON
JMP @MR010               ; Alarm
MPP
TMR @3 #300
CON
AND @T3
DW #21 @DM0
CON
JMP @MR011               ; 終了処理
;Alarm code Stroge
STG @MR010               ; Alarm
LDA P0:#6                ; RB_IN
CON
STA @DM0
CON
JMP @MR011               ; 終了処理
;終了処理
STG @MR011               ; 終了処理
LDA @DM0
CON
STA P2                   ; C_CODE
CON
MPS
ZRES @MR001 @MR015       ; ﾏｸﾛ異常発生
CON
ENDS
MPP
MEND
END
ENDH
