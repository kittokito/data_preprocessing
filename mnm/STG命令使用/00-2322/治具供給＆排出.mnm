DEVICE:52
;MODULE:治具供給＆排出
;MODULE_TYPE:0
;**** 治具供給＆排出　入力処理 / 治具供給＆排出　入力処理  ****
LD R4600                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
TMR @0 #2                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
LD R4601                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ端
TMR @1 #2                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ端
LD R4602                 ; 加圧治具ﾌﾟｯｼｬー押込み後退端
TMR @2 #2                ; 加圧治具ﾌﾟｯｼｬー押込み後退端
LD R4603                 ; 加圧治具ﾌﾟｯｼｬー押込み前進端
TMR @3 #2                ; 加圧治具ﾌﾟｯｼｬー押込み前進端
LD R4604                 ; 加圧治具ﾌﾟｯｼｬー早送り後退端
TMR @4 #2                ; 加圧治具ﾌﾟｯｼｬー早送り後退端
LD R4605                 ; 加圧治具ﾌﾟｯｼｬー早送り前進端
TMR @5 #2                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
LD R4606                 ; 加圧治具下降端右側
AND R4608                ; 加圧治具下降端左側
TMR @6 #2                ; 加圧治具下降端
LD R4607                 ; 加圧治具上昇端右側
AND R4609                ; 加圧治具上昇端左側
TMR @7 #2                ; 加圧治具上昇端
LD R4614                 ; 加圧治具突き当て確認1
AND R4615                ; 加圧治具突き当て確認2
TMH @8 #15               ; 加圧治具突き当て確認
LD R4700                 ; ｲﾝﾃﾞｯｸｽ1ST下治具突当て確認
TMR @9 #2                ; ｲﾝﾃﾞｯｸｽ1ST下治具突当て確認
;**** 治具供給＆排出　手動操作 / 治具供給＆排出　MANUAL　Operation  ****
LD CR2002                ; 常時ON
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR100               ; 加圧治具ｾｯﾄ下降可
LDP R66000               ; 加圧治具ｾｯﾄ下降SW
AND @MR100               ; 加圧治具ｾｯﾄ下降可
SET MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
LD CR2002                ; 常時ON
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR101               ; 加圧治具ｾｯﾄ上昇可
LDP R66001               ; 加圧治具ｾｯﾄ上昇SW
AND @MR101               ; 加圧治具ｾｯﾄ上昇可
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
LDB R86102               ; 加圧治具J8軸治具1位置PL
ANB R86104               ; 加圧治具J8軸治具2位置PL
ANB R86106               ; 加圧治具J8軸治具3位置PL
ANB R86108               ; 加圧治具J8軸治具4位置PL
INV
AND R4600                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
AND R7002                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL
AND R4606                ; 加圧治具下降端右側
AND R4608                ; 加圧治具下降端左側
AND R7008                ; 加圧治具ｾｯﾄ下降SOL
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR102               ; 排出ﾌﾟｯｼｬー押込み後退可
LDP R66004               ; 排出ﾌﾟｯｼｬー押込み後退SW
AND @MR102               ; 排出ﾌﾟｯｼｬー押込み後退可
RES MR7005               ; 排出ﾌﾟｯｼｬー押込み前進SOL補助
CON
SET MR7004               ; 排出ﾌﾟｯｼｬー押込み後退SOL補助
LD R4600                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
AND R7002                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR103               ; 排出ﾌﾟｯｼｬー押込み前進可
LDP R66005               ; 排出ﾌﾟｯｼｬー押込み前進SW
AND @MR103               ; 排出ﾌﾟｯｼｬー押込み前進可
RES MR7004               ; 排出ﾌﾟｯｼｬー押込み後退SOL補助
CON
SET MR7005               ; 排出ﾌﾟｯｼｬー押込み前進SOL補助
LDB R86102               ; 加圧治具J8軸治具1位置PL
ANB R86104               ; 加圧治具J8軸治具2位置PL
ANB R86106               ; 加圧治具J8軸治具3位置PL
ANB R86108               ; 加圧治具J8軸治具4位置PL
INV
AND R4600                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
AND R7002                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL
AND R4606                ; 加圧治具下降端右側
AND R4608                ; 加圧治具下降端左側
AND R7008                ; 加圧治具ｾｯﾄ下降SOL
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR104               ; 排出ﾌﾟｯｼｬー早送り後退可
LDP R66006               ; 排出ﾌﾟｯｼｬー早送り後退SW
AND @MR104               ; 排出ﾌﾟｯｼｬー早送り後退可
RES MR7007               ; 排出ﾌﾟｯｼｬー早送り前進SOL補助
CON
SET MR7006               ; 排出ﾌﾟｯｼｬー早送り後退SOL補助
LD R4600                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
AND R7002                ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
OUT @MR105               ; 排出ﾌﾟｯｼｬー早送り前進可
LDP R66007               ; 排出ﾌﾟｯｼｬー早送り前進SW
AND @MR105               ; 排出ﾌﾟｯｼｬー早送り前進可
RES MR7006               ; 排出ﾌﾟｯｼｬー早送り後退SOL補助
CON
SET MR7007               ; 排出ﾌﾟｯｼｬー早送り前進SOL補助
;****加圧治具J8軸　ﾊﾟﾈﾙﾗﾝﾌﾟ /加圧治具J8軸　ﾊﾟﾈﾙﾗﾝﾌﾟ ****
LD R4606                 ; 加圧治具下降端右側
AND R4608                ; 加圧治具下降端左側
OUT R86000               ; 加圧治具下降端PL
LD R4607                 ; 加圧治具上昇端右側
AND R4609                ; 加圧治具上昇端左側
OUT R86001               ; 加圧治具下降端PL
LD R4602                 ; 加圧治具ﾌﾟｯｼｬー押込み後退端
OUT R86004               ; 加圧治具ﾌﾟｯｼｬー押込み後退端PL
LD R4603                 ; 加圧治具ﾌﾟｯｼｬー押込み前進端
OUT R86005               ; 加圧治具ﾌﾟｯｼｬー押込み前進端PL
LD R4604                 ; 加圧治具ﾌﾟｯｼｬー早送り後退端
OUT R86006               ; 加圧治具ﾌﾟｯｼｬー早送り後退端PL
LD R4605                 ; 加圧治具ﾌﾟｯｼｬー早送り前進端
OUT R86007               ; 加圧治具ﾌﾟｯｼｬー早送り前進端PL
LD R4600                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
OUT R86010               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端PL
LD R4601                 ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ端
OUT R86011               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ端PL
LD=.L DM12170 DM2900     ; 加圧治具J8軸現在位置座標  加圧治具供給位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86200               ; 加圧治具J8軸供給位置PL
OUT R86100               ; 加圧治具J8軸供給位置PL
LD=.L DM12170 DM2902     ; 加圧治具J8軸現在位置座標  加圧治具治具1位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86201               ; 加圧治具J8軸治具1位置PL
OUT R86102               ; 加圧治具J8軸治具1位置PL
LD=.L DM12170 DM2904     ; 加圧治具J8軸現在位置座標  加圧治具治具2位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86202               ; 加圧治具J8軸治具2位置PL
OUT R86104               ; 加圧治具J8軸治具2位置PL
LD=.L DM12170 DM2906     ; 加圧治具J8軸現在位置座標  加圧治具治具3位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86203               ; 加圧治具J8軸治具3位置PL
OUT R86106               ; 加圧治具J8軸治具3位置PL
LD=.L DM12170 DM2908     ; 加圧治具J8軸現在位置座標  加圧治具治具4位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86204               ; 加圧治具J8軸治具4位置PL
OUT R86108               ; 加圧治具J8軸治具4位置PL
LD=.L DM12170 +0         ; 加圧治具J8軸現在位置座標
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT R86101               ; 加圧治具J8軸原点位置PL
LD R48913                ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
OUT R86302               ; 加圧治具J8軸ｻｰﾎﾞON　PL
LD R1111                 ; 加圧治具　J8軸原点復帰完了HEND
OUT R86206               ; 加圧治具J8軸原点復帰完了PL
;**** 治具供給＆排出　J8軸操作 / 治具供給＆排出　J8軸 Operation ****
LD R44800                ; 位置決めﾕﾆｯﾄ2動作可能
AND R48913               ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
AND R4603                ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND R4605                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANB R7004                ; 加圧治具ﾌﾟｯｼｬー押込み後退SOL
ANB R7006                ; 加圧治具ﾌﾟｯｼｬー早送り後退SOL
OUT @MR110               ; 加圧治具J8軸動作可能
LD @MR110                ; 加圧治具J8軸動作可能
MPS
LD R66305                ; 加圧治具　J8軸JOG+SW
ANB R66304               ; 加圧治具　J8軸JOG-SW
ANL
OUT R48500               ; 加圧治具　J8軸JOG+
MPP
LD R66304                ; 加圧治具　J8軸JOG-SW
ANB R66305               ; 加圧治具　J8軸JOG+SW
ANL
OUT R48501               ; 加圧治具　J8軸JOG-
LD R66300                ; 加圧治具　J8軸位置取込可SW
DIFU @MR014              ; 加圧治具　J8軸位置取込可SWﾊﾟﾙｽ
LD @MR014                ; 加圧治具　J8軸位置取込可SWﾊﾟﾙｽ
ANB R86300               ; 加圧治具　J8軸位置取込可能PL
LDB @MR014               ; 加圧治具　J8軸位置取込可SWﾊﾟﾙｽ
AND R86300               ; 加圧治具　J8軸位置取込可能PL
ORL
AND= DM308 #62           ; 画面No.通知
OUT R86300               ; 加圧治具　J8軸位置取込可能PL
LD R66301                ; 加圧治具　J8軸書込み可SW
DIFU @MR015              ; 加圧治具　J8軸書込み可SWﾊﾟﾙｽ
LD @MR015                ; 加圧治具　J8軸書込み可SWﾊﾟﾙｽ
ANB R86301               ; 加圧治具　J8軸書込み可能PL
LDB @MR015               ; 加圧治具　J8軸書込み可SWﾊﾟﾙｽ
AND R86301               ; 加圧治具　J8軸書込み可能PL
ORL
AND= DM308 #62           ; 画面No.通知
OUT R86301               ; 加圧治具　J8軸書込み可能PL
LD R86300                ; 加圧治具　J8軸位置取込可能PL
MPS
AND R66210               ; 加圧治具供給位置選択SW
@MOV.L DM12170 DM2900    ; 加圧治具J8軸現在位置座標  加圧治具供給位置座標
MRD
AND R66211               ; 加圧治具治具1位置選択SW
@MOV.L DM12170 DM2902    ; 加圧治具J8軸現在位置座標  加圧治具治具1位置座標
MRD
AND R66212               ; 加圧治具治具2位置選択SW
@MOV.L DM12170 DM2904    ; 加圧治具J8軸現在位置座標  加圧治具治具2位置座標
MRD
AND R66213               ; 加圧治具治具3位置選択SW
@MOV.L DM12170 DM2906    ; 加圧治具J8軸現在位置座標  加圧治具治具3位置座標
MPP
AND R66214               ; 加圧治具治具4位置選択SW
@MOV.L DM12170 DM2908    ; 加圧治具J8軸現在位置座標  加圧治具治具4位置座標
LD R66303                ; 加圧治具J8軸JOG高速度選択SW
DIFU @MR111              ; 加圧治具J8軸JOG高速度選択SWﾊﾟﾙｽ
LD @MR111                ; 加圧治具J8軸JOG高速度選択SWﾊﾟﾙｽ
ANB R86303               ; 加圧治具J8軸JOG高速度選択PL
LDB @MR111               ; 加圧治具J8軸JOG高速度選択SWﾊﾟﾙｽ
AND R86303               ; 加圧治具J8軸JOG高速度選択PL
ORL
AND= DM308 #62           ; 画面No.通知
OUT R86303               ; 加圧治具J8軸JOG高速度選択PL
LD= DM308 #62            ; 画面No.通知
OUT R48502               ; 加圧治具J8軸JOG高速度選択
LDP R86303               ; 加圧治具J8軸JOG高速度選択PL
LDP R66315               ; 加圧治具J8軸JOG速度設定通知
AND R86303               ; 加圧治具J8軸JOG高速度選択PL
ORL
UWRIT.L #4 #14318 DM2940 #1
LDF R86303               ; 加圧治具J8軸JOG高速度選択PL
LD= DM308 #62            ; 画面No.通知
MEP
ORL
LDP R66315               ; 加圧治具J8軸JOG速度設定通知
ANB R86303               ; 加圧治具J8軸JOG高速度選択PL
ORL
UWRIT.L #4 #14318 DM2942 #1
LD= DM308 #62            ; 画面No.通知
MEF
ORP R66314               ; 加圧治具J8軸座標 速度設定通知
ORP T202                 ; RB動作中遅延
LDB R66210               ; 加圧治具供給位置選択SW
ANB R66211               ; 加圧治具治具1位置選択SW
ANB R66212               ; 加圧治具治具2位置選択SW
INV
MEF
ORL
LDB R66213               ; 加圧治具治具3位置選択SW
ANB R66214               ; 加圧治具治具4位置選択SW
INV
MEF
ORL
UWRIT.L #4 #18112 DM2900 #1;加圧治具供給位置座標
UWRIT.L #4 #18624 DM2902 #1;加圧治具治具1位置座標
UWRIT.L #4 #19136 DM2904 #1;加圧治具治具2位置座標
UWRIT.L #4 #19648 DM2906 #1;加圧治具治具3位置座標
UWRIT.L #4 #20160 DM2908 #1;加圧治具治具4位置座標
UWRIT.L #4 #22720 +0 #1
LD= DM308 #62            ; 画面No.通知
MEF
ORP R66314               ; 加圧治具J8軸座標 速度設定通知
ORP T202                 ; RB動作中遅延
LDB R66210               ; 加圧治具供給位置選択SW
ANB R66211               ; 加圧治具治具1位置選択SW
ANB R66212               ; 加圧治具治具2位置選択SW
INV
MEF
ORL
LDB R66213               ; 加圧治具治具3位置選択SW
ANB R66214               ; 加圧治具治具4位置選択SW
INV
MEF
ORL
UWRIT.L #4 #18124 DM2920 #1
UWRIT.L #4 #18636 DM2922 #1
UWRIT.L #4 #19148 DM2924 #1
UWRIT.L #4 #19660 DM2926 #1
UWRIT.L #4 #20172 DM2928 #1
UWRIT.L #4 #22732 DM1020 #1
LD R44800                ; 位置決めﾕﾆｯﾄ2動作可能
AND R48913               ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
AND @T3                  ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND @T5                  ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANB R7004                ; 加圧治具ﾌﾟｯｼｬー押込み後退SOL
ANB R7006                ; 加圧治具ﾌﾟｯｼｬー早送り後退SOL
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
ANB @MR301               ; 加圧治具J8軸手動供給位置移動
ANB @MR302               ; 加圧治具J8軸手動治具1位置移動
ANB @MR303               ; 加圧治具J8軸手動治具2位置移動
ANB @MR304               ; 加圧治具J8軸手動治具3位置移動
ANB @MR305               ; 加圧治具J8軸手動治具4位置移動
ANB @MR307               ; 加圧治具J8軸手動原点位置移動
OUT @MR300               ; 加圧治具J8軸動作可能
LDB R66200               ; 加圧治具J8軸手動供給位置移動SW
ANB R66100               ; 加圧治具J8軸手動供給位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR301                ; 加圧治具J8軸手動供給位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR301               ; 加圧治具J8軸手動供給位置移動
@MOV #1 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB R66201               ; 加圧治具J8軸手動治具1位置移動SW
ANB R66102               ; 加圧治具J8軸手動治具1位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR302                ; 加圧治具J8軸手動治具1位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR302               ; 加圧治具J8軸手動治具1位置移動
@MOV #2 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB R66202               ; 加圧治具J8軸手動治具2位置移動SW
ANB R66104               ; 加圧治具J8軸手動治具2位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR303                ; 加圧治具J8軸手動治具2位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR303               ; 加圧治具J8軸手動治具2位置移動
@MOV #3 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB R66203               ; 加圧治具J8軸手動治具3位置移動SW
ANB R66106               ; 加圧治具J8軸手動治具3位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR304                ; 加圧治具J8軸手動治具3位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR304               ; 加圧治具J8軸手動治具3位置移動
@MOV #4 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB R66204               ; 加圧治具J8軸手動治具4位置移動SW
ANB R66108               ; 加圧治具J8軸手動治具4位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR305                ; 加圧治具J8軸手動治具4位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR305               ; 加圧治具J8軸手動治具4位置移動
@MOV #5 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB R66101               ; 加圧治具J8軸手動原点位置移動SW
INV
MEP
AND @MR300               ; 加圧治具J8軸動作可能
OR @MR307                ; 加圧治具J8軸手動原点位置移動
ANB @MR310               ; 加圧治具J8軸移動
OUT @MR307               ; 加圧治具J8軸手動原点位置移動
@MOV #10 DM2945          ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
LDB @MR301               ; 加圧治具J8軸手動供給位置移動
ANB @MR302               ; 加圧治具J8軸手動治具1位置移動
ANB @MR303               ; 加圧治具J8軸手動治具2位置移動
ANB @MR304               ; 加圧治具J8軸手動治具3位置移動
ANB @MR305               ; 加圧治具J8軸手動治具4位置移動
ANB @MR307               ; 加圧治具J8軸手動原点位置移動
ANB @MR410
ANB @MR607
ANB @MR613
ANB @MR704
INV
OUT @MR309               ; 加圧治具J8軸移動指示集合
LD @MR309                ; 加圧治具J8軸移動指示集合
ANB @MR310               ; 加圧治具J8軸移動
OR @MR310                ; 加圧治具J8軸移動
ANB MR30011              ; 治具供給＆排出全異常
ANB @MR315               ; 加圧治具J8軸移動完了
OUT @MR310               ; 加圧治具J8軸移動
TMH @11 #5               ; 加圧治具J8軸移動開始ﾃﾞｨﾚｰ
LDP @T11                 ; 加圧治具J8軸移動開始ﾃﾞｨﾚｰ
SET @MR311               ; 加圧治具J8軸移動ﾎﾟｲﾝﾄ指示
STG @MR311               ; 加圧治具J8軸移動ﾎﾟｲﾝﾄ指示
@U_WRPPNT #4 #4 DM2945   ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
TMH @12 #5               ; 加圧治具J8軸起動指令ﾃﾞｨﾚｰ
CON
AND @T12                 ; 加圧治具J8軸起動指令ﾃﾞｨﾚｰ
JMP @MR312               ; 加圧治具J8軸起動指令
STG @MR312               ; 加圧治具J8軸起動指令
MPS
OUT R42108               ; 加圧治具J8軸位置決め制御開始
MPP
AND R42908               ; 加圧治具J8軸位置決め制御開始完了
JMP @MR313               ; 加圧治具J8軸移動完了
STG @MR313               ; 加圧治具J8軸移動完了
ANB R42910               ; 加圧治具J8軸位置決め完了
JMP @MR314               ; 加圧治具J8軸移動終了
STG @MR314               ; 加圧治具J8軸移動終了
AND R42910               ; 加圧治具J8軸位置決め完了
JMP @MR315               ; 加圧治具J8軸移動完了
STG @MR315               ; 加圧治具J8軸移動完了
ANB @MR310               ; 加圧治具J8軸移動
ENDS
LDB @MR310               ; 加圧治具J8軸移動
ZRES @MR311 @MR315       ; 加圧治具J8軸移動ﾎﾟｲﾝﾄ指示  加圧治具J8軸移動完了
;**** 加圧治具J8軸　ｴﾘｱｾﾝｻｰ異常処理 / 加圧治具J8軸　ｴﾘｱｾﾝｻｰ異常処理 ****
LDP MR3308               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
ORP MR3808               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
OR @MR900                ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常処理
AND MR40005              ; 全起動中
ANB @MR915               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常終了
OUT @MR900               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常処理
LDP @MR900               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常処理
SET @MR901
STG @MR901
ANB MR3308               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
ANB MR3808               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
AND MR9915               ; 異常なし
JMP @MR902
STG @MR902
OUT MR30500              ; ｲﾝﾃﾞｯｸｽSTARTﾗﾝﾌﾟ点滅
CON
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
ANF R4803                ; ｲﾝﾃﾞｯｸｽ確認SW
JMP @MR903               ; 加圧治具J8軸位置決め運転再開
STG @MR903               ; 加圧治具J8軸位置決め運転再開
AND R48405               ; 加圧治具J8軸位置決め運転再開
AND R48804               ; 加圧治具J8軸制御中
JMP @MR915               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常終了
STG @MR915               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常終了
ANB @MR900               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常処理
ENDS
LDB @MR900               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常処理
ZRES @MR901 @MR915       ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常終了
;**** 加圧治具J8軸　原点復帰 / 加圧治具J8軸　原点復帰 ****
LD R44800                ; 位置決めﾕﾆｯﾄ2動作可能
AND R48913               ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
AND R4603                ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND R4605                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANB R7004                ; 加圧治具ﾌﾟｯｼｬー押込み後退SOL
ANB R7006                ; 加圧治具ﾌﾟｯｼｬー早送り後退SOL
AND MR7005               ; 排出ﾌﾟｯｼｬー押込み前進SOL補助
AND MR7007               ; 排出ﾌﾟｯｼｬー早送り前進SOL補助
ANB R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT @MR200               ; 加圧治具J8軸動作可能
LDB R66206               ; 加圧治具J8軸原点復帰SW
ANB @MR407
INV
AND @MR200               ; 加圧治具J8軸動作可能
ANB @MR210               ; 加圧治具J8軸原点復帰
OR @MR210                ; 加圧治具J8軸原点復帰
ANB @MR205               ; 加圧治具J8軸原点復帰完了
ANB MR30011              ; 治具供給＆排出全異常
OUT @MR210               ; 加圧治具J8軸原点復帰
TMH @10 #5               ; 加圧治具J8軸原点復帰ﾃﾞｨﾚｰ
LDP @T10                 ; 加圧治具J8軸原点復帰ﾃﾞｨﾚｰ
@SET @MR201              ; 加圧治具J8軸原点復帰開始
STG @MR201               ; 加圧治具J8軸原点復帰開始
SET MR2115               ; 加圧治具J8軸原点復帰HOME補助
CON
ANB R1111                ; 加圧治具　J8軸原点復帰完了HEND
JMP @MR202               ; 加圧治具J8軸原点復帰完了待ち
STG @MR202               ; 加圧治具J8軸原点復帰完了待ち
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
RES MR2115               ; 加圧治具J8軸原点復帰HOME補助
CON
JMP @MR203               ; 加圧治具J8軸目標座標変更
STG @MR203               ; 加圧治具J8軸目標座標変更
U_WRCPOS.L #4 #4 +0
SET R42112               ; 加圧治具J8軸現在座標変更要求
JMP @MR204               ; 加圧治具J8軸現在座標変更完了待ち
STG @MR204               ; 加圧治具J8軸現在座標変更完了待ち
AND R42912               ; 加圧治具J8軸現在座標変更完了
RES R42112               ; 加圧治具J8軸現在座標変更要求
CON
JMP @MR205               ; 加圧治具J8軸原点復帰完了
STG @MR205               ; 加圧治具J8軸原点復帰完了
ANB @MR210               ; 加圧治具J8軸原点復帰
ENDS
LDB @MR210               ; 加圧治具J8軸原点復帰
ZRES @MR201 @MR209       ; 加圧治具J8軸原点復帰開始
;**** 治具供給＆排出 全原点復帰 / 治具供給＆排出 全原点復帰 Operation ****
LD R44800                ; 位置決めﾕﾆｯﾄ2動作可能
AND R48913               ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
OUT @MR400               ; 治具供給＆排出全原点復帰可
OUT MR30009              ; 治具供給＆排出全原点復帰可
LD MR40101               ; 各ﾕﾆｯﾄ原点復帰開始
ORP R66015               ; 手動治具供給＆排出全原点復帰SW
AND @MR400               ; 治具供給＆排出全原点復帰可
ANB MR30014              ; 治具供給＆排出全原点復帰開始
OR MR30014               ; 治具供給＆排出全原点復帰開始
ANB MR30011              ; 治具供給＆排出全異常
ANB @MR415
ANB MR30011              ; 治具供給＆排出全異常
OUT MR30014              ; 治具供給＆排出全原点復帰開始
OUT R86015               ; 治具供給＆排出全原点復帰PL
TMH @20 #5               ; 治具供給＆排出原点復帰開始ﾃﾞｨﾚｰ
LDP @T20                 ; 治具供給＆排出原点復帰開始ﾃﾞｨﾚｰ
SET @MR401
STG @MR401
ANB MR40101              ; 各ﾕﾆｯﾄ原点復帰開始
JMP @MR402
STG @MR402
MPS
LD R4603                 ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND R4605                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANL
JMP @MR405               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除OFF
MPP
LDB R4603                ; 加圧治具ﾌﾟｯｼｬー押込み前進端
ORB R4605                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANL
JMP @MR403               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除ON
STG @MR403               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除ON
SET MR7002               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL補助
CON
AND @T0                  ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除端
JMP @MR404               ; 排出ﾌﾟｯｼｬｰ前進
STG @MR404               ; 排出ﾌﾟｯｼｬｰ前進
RES MR7004               ; 排出ﾌﾟｯｼｬー押込み後退SOL補助
CON
SET MR7005               ; 排出ﾌﾟｯｼｬー押込み前進SOL補助
CON
RES MR7006               ; 排出ﾌﾟｯｼｬー早送り後退SOL補助
CON
SET MR7007               ; 排出ﾌﾟｯｼｬー早送り前進SOL補助
CON
AND @T3                  ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND @T5                  ; 加圧治具ﾌﾟｯｼｬー早送り前進端
JMP @MR405               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除OFF
STG @MR405               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除OFF
RES MR7004               ; 排出ﾌﾟｯｼｬー押込み後退SOL補助
CON
SET MR7005               ; 排出ﾌﾟｯｼｬー押込み前進SOL補助
CON
RES MR7006               ; 排出ﾌﾟｯｼｬー早送り後退SOL補助
CON
SET MR7007               ; 排出ﾌﾟｯｼｬー早送り前進SOL補助
CON
RES MR7002               ; ｲﾝﾃﾞｯｸｽｸﾗﾝﾌﾟ解除SOL補助
CON
JMP @MR406
STG @MR406
MPS
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
JMP @MR409
MPP
ANB R1111                ; 加圧治具　J8軸原点復帰完了HEND
JMP @MR407
STG @MR407
AND @MR210               ; 加圧治具J8軸原点復帰
JMP @MR408
STG @MR408
ANB @MR210               ; 加圧治具J8軸原点復帰
AND R86101               ; 加圧治具J8軸原点位置PL
JMP @MR409
STG @MR409
@MOV #1 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
JMP @MR410
STG @MR410
AND @MR310               ; 加圧治具J8軸移動
JMP @MR411
STG @MR411
ANB @MR310               ; 加圧治具J8軸移動
AND R86200               ; 加圧治具J8軸供給位置PL
JMP @MR412
STG @MR412
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
CON
AND @T7                  ; 加圧治具上昇端
JMP @MR415
STG @MR415
LD MR30300               ; 治具供給＆排出全原点復帰完了
ORB MR40014              ; 全原点復帰中
ANL
ANB MR30014              ; 治具供給＆排出全原点復帰開始
ENDS
LD @MR415
OR MR30300               ; 治具供給＆排出全原点復帰完了
ANB MR40102              ; 各原点復帰完了FLGOFF
AND MR40014              ; 全原点復帰中
OUT MR30300              ; 治具供給＆排出全原点復帰完了
LDB MR30014              ; 治具供給＆排出全原点復帰開始
ZRES @MR401 @MR415
;**** 治具供給＆排出　自動運転 / 治具供給＆排出　自動運転 ****
LDB R4610                ; 接着治具確認1
ANB R4611                ; 接着治具確認2
ANB R4612                ; 接着治具確認3
ANB R4613                ; 接着治具確認4
MPS
TMH @15 #15              ; 接着治具満杯確認
MPP
AND @T15                 ; 接着治具満杯確認
OUT MR30100              ; 接着治具満杯
LDB R4610                ; 接着治具確認1
ANB R4611                ; 接着治具確認2
ANB R4612                ; 接着治具確認3
ANB R4613                ; 接着治具確認4
INV
MPS
TMH @16 #15              ; 接着治具無し確認
MPP
AND @T16                 ; 接着治具無し確認
OUT MR30101              ; 接着治具空あり
LD CR2002                ; 常時ON
LDP R4803                ; ｲﾝﾃﾞｯｸｽ確認SW
OR T96                   ; ｲﾝﾃﾞｯｸｽ確認SWﾃﾞｨﾚｰ
ANL
AND MR40611              ; STEP後半選択
ANB MR40112              ; 自動ﾓｰﾄﾞ
OR MR40112               ; 自動ﾓｰﾄﾞ
OUT MR30008              ; STEPﾊﾟﾙｽ
LD R44800                ; 位置決めﾕﾆｯﾄ2動作可能
AND R48913               ; 加圧治具　J8軸ｻｰﾎﾞﾚﾃﾞｨｰ
AND R4603                ; 加圧治具ﾌﾟｯｼｬー押込み前進端
AND R4605                ; 加圧治具ﾌﾟｯｼｬー早送り前進端
ANB R7004                ; 加圧治具ﾌﾟｯｼｬー押込み後退SOL
ANB R7006                ; 加圧治具ﾌﾟｯｼｬー早送り後退SOL
AND R1111                ; 加圧治具　J8軸原点復帰完了HEND
OUT MR30010              ; 治具供給＆排出全原点
LDB MR30901              ; 加圧治具ｾﾝｻｰ異常一括
ANB MR30902              ; 加圧治具J8軸ﾛﾎﾞｯﾄｴﾗｰ一括
ANB MR3309               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
ANB MR3809               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
ANB CR2003               ; 常時OFF
INV
OUT MR30011              ; 治具供給＆排出全異常
LD MR40015               ; 主幹自動起動中
OR MR40611               ; STEP後半選択
AND MR30010              ; 治具供給＆排出全原点
ANB MR30011              ; 治具供給＆排出全異常
ANB MR30014              ; 治具供給＆排出全原点復帰開始
ANB MR30015              ; 治具供給＆排出全自動起動中
OUT MR30012              ; 治具供給＆排出起動条件
LD MR30011               ; 治具供給＆排出全異常
OR MR40910               ; 主幹異常一括
OUT MR30013
LD MR30008               ; STEPﾊﾟﾙｽ
AND MR30012              ; 治具供給＆排出起動条件
OR MR30015               ; 治具供給＆排出全自動起動中
ANB MR30013
LD MR40112               ; 自動ﾓｰﾄﾞ
OR MR40611               ; STEP後半選択
ANL
ANB @MR715
OUT MR30015              ; 治具供給＆排出全自動起動中
TMH @21 #5               ; 治具供給＆排出全自動起動ﾃﾞｨﾚｰ
LDP @T21                 ; 治具供給＆排出全自動起動ﾃﾞｨﾚｰ
SET @MR601
STG @MR601
JMP @MR602               ; SRB1　MG吸着確認
STG @MR602               ; SRB1　MG吸着確認
JMP @MR603               ; 接着治具有無確認
STG @MR603               ; 接着治具有無確認
MPS
AND @T8                  ; 加圧治具突き当て確認
JMP @MR604               ; 接着治具満杯確認
MPP
ANB @T8                  ; 加圧治具突き当て確認
JMP @MR605               ; 加圧治具J8軸供給位置確認
STG @MR604               ; 接着治具満杯確認
MPS
AND MR30100              ; 接着治具満杯
JMP @MR605               ; 加圧治具J8軸供給位置確認
MPP
AND MR30101              ; 接着治具空あり
JMP @MR611
STG @MR605               ; 加圧治具J8軸供給位置確認
MPS
AND R86200               ; 加圧治具J8軸供給位置PL
JMP @MR609
MPP
LDB R86200               ; 加圧治具J8軸供給位置PL
AND @MR300               ; 加圧治具J8軸動作可能
ANL
JMP @MR606
STG @MR606
MPS
@MOV #1 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MPP
LD R1411                 ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
AND MR30008              ; STEPﾊﾟﾙｽ
ANL
JMP @MR607
STG @MR607
AND @MR310               ; 加圧治具J8軸移動
JMP @MR608
STG @MR608
ANB @MR310               ; 加圧治具J8軸移動
AND R86200               ; 加圧治具J8軸供給位置PL
AND MR30008              ; STEPﾊﾟﾙｽ
JMP @MR609
STG @MR609
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
CON
AND @T7                  ; 加圧治具上昇端
AND MR30008              ; STEPﾊﾟﾙｽ
JMP @MR610
STG @MR610
OUT MR30110              ; 治具収納ｶｾｯﾄ供給要求
MPS
LD @T8                   ; 加圧治具突き当て確認
AND MR30105              ; 加圧治具無し確認
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
ANF R4803                ; ｲﾝﾃﾞｯｸｽ確認SW
AND MR9915               ; 異常なし
ANL
JMP @MR611
MPP
LDB MR40015              ; 主幹自動起動中
ANB MR28015              ; ｲﾝﾃﾞｯｸｽ全自動起動中
ANB MR40611              ; STEP後半選択
ANL
TMH @26 #10              ; 主幹起動OFF確認
CON
AND @T26                 ; 主幹起動OFF確認
JMP @MR715
LDB @T8                  ; 加圧治具突き当て確認
OR MR30105               ; 加圧治具無し確認
AND MR30015              ; 治具供給＆排出全自動起動中
OUT MR30105              ; 加圧治具無し確認
STG @MR611
SET MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
CON
AND @T6                  ; 加圧治具下降端
AND MR30008              ; STEPﾊﾟﾙｽ
JMP @MR612
STG @MR612
MPS
AND R4613                ; 接着治具確認4
@MOV #5 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MRD
LDB R4613                ; 接着治具確認4
AND R4612                ; 接着治具確認3
ANL
@MOV #4 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MRD
LDB R4613                ; 接着治具確認4
ANB R4612                ; 接着治具確認3
AND R4611                ; 接着治具確認2
ANL
@MOV #3 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MRD
LDB R4613                ; 接着治具確認4
ANB R4612                ; 接着治具確認3
ANB R4611                ; 接着治具確認2
AND R4610                ; 接着治具確認1
ANL
@MOV #2 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MRD
JMP @MR613
MPP
AND MR30100              ; 接着治具満杯
TMR @17 #10              ; 接着治具満杯
CON
AND @T17                 ; 接着治具満杯
JMP @MR715
STG @MR613
AND @MR310               ; 加圧治具J8軸移動
JMP @MR614
STG @MR614
ANB @MR310               ; 加圧治具J8軸移動
MPS
LD R4613                 ; 接着治具確認4
AND R86204               ; 加圧治具J8軸治具4位置PL
LDB R4613                ; 接着治具確認4
AND R4612                ; 接着治具確認3
AND R86203               ; 加圧治具J8軸治具3位置PL
ORL
LDB R4613                ; 接着治具確認4
ANB R4612                ; 接着治具確認3
AND R4611                ; 接着治具確認2
AND R86202               ; 加圧治具J8軸治具2位置PL
ORL
LDB R4613                ; 接着治具確認4
ANB R4612                ; 接着治具確認3
ANB R4611                ; 接着治具確認2
AND R4610                ; 接着治具確認1
AND R86201               ; 加圧治具J8軸治具1位置PL
ORL
ANL
JMP @MR615               ; CAV　T1X軸搬送位置移動完了
MPP
AND MR30100              ; 接着治具満杯
TMR @18 #10              ; 接着治具満杯
CON
AND @T18                 ; 接着治具満杯
JMP @MR715
STG @MR615               ; CAV　T1X軸搬送位置移動完了
OUT MR30000              ; 治具供給＆排出収納要求
MPS
AND MR28001              ; ｲﾝﾃﾞｯｸｽ治具排出完了
JMP @MR715
MRD
LDB MR40015              ; 主幹自動起動中
ANB MR40611              ; STEP後半選択
ANL
TMR @27 #3               ; 主幹起動OFF確認
CON
AND @T27                 ; 主幹起動OFF確認
MPS
ANB MR28015              ; ｲﾝﾃﾞｯｸｽ全自動起動中
JMP @MR715
MPP
ANB MR28310              ; ST1排出要求起動中
JMP @MR715
MPP
LD MR40900               ; 払出し動作
ANB DM2601.0             ; ｲﾝﾃﾞｯｸｽST1接着治具有り
ANB DM2602.0             ; ｲﾝﾃﾞｯｸｽST2接着治具有り
ANB DM2603.0             ; ｲﾝﾃﾞｯｸｽST3接着治具有り
ANB DM2504.0             ; ｲﾝﾃﾞｯｸｽST4接着治具有り
ANB R4700                ; ｲﾝﾃﾞｯｸｽ1ST下治具突当て確認
ANB R4703                ; ST1上治具確認
AND MR28201              ; ST1治具手供給作業指示１
AND MR28209              ; 各ST　起動OFF確認２
ANB MR40901              ; 上流工程ﾜｰｸ有り
ANL
TMR @25 #10              ; 払出しﾜｰｸ無し確認時間
CON
AND @T25                 ; 払出しﾜｰｸ無し確認時間
JMP @MR702
STG @MR700
JMP @MR715
STG @MR702
ANB R86200               ; 加圧治具J8軸供給位置PL
AND @MR300               ; 加圧治具J8軸動作可能
JMP @MR703
STG @MR703
MPS
@MOV #1 DM2945           ; 加圧治具　J8軸ﾎﾟｲﾝﾄNo.
MPP
LD R1411                 ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
AND MR30008              ; STEPﾊﾟﾙｽ
ANL
JMP @MR704
STG @MR704
AND @MR310               ; 加圧治具J8軸移動
JMP @MR705
STG @MR705
ANB @MR310               ; 加圧治具J8軸移動
AND R86200               ; 加圧治具J8軸供給位置PL
AND MR30008              ; STEPﾊﾟﾙｽ
JMP @MR706
STG @MR706
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
CON
AND @T7                  ; 加圧治具上昇端
AND MR30008              ; STEPﾊﾟﾙｽ
OUT MR30600
CON
JMP @MR707
STG @MR707
OUT MR30400              ; 払出し動作終了　自動終了
CON
ANB MR40015              ; 主幹自動起動中
JMP @MR708
STG @MR708
OUT MR30401              ; 払出しFLG　OFF
CON
ANB MR40902              ; 払出し受付
JMP @MR715
STG @MR715
ANB MR30015              ; 治具供給＆排出全自動起動中
ENDS
LDB MR30015              ; 治具供給＆排出全自動起動中
ZRES @MR600 @MR715
LDB MR3308               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
ANB MR3808               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
INV
OUT R48402               ; 加圧治具J8軸減速停止
LD @MR903                ; 加圧治具J8軸位置決め運転再開
OUT R48405               ; 加圧治具J8軸位置決め運転再開
;<h1/>治具供給排出　異常処理
LDB MR4606               ; 加圧治具下降端右側ｾﾝｻｰ異常
ANB MR4607               ; 加圧治具上昇端右側ｾﾝｻｰ異常
ANB MR4608               ; 加圧治具下降端左側ｾﾝｻｰ異常
ANB MR4609               ; 加圧治具上昇端左側ｾﾝｻｰ異常
INV
OUT MR30901              ; 加圧治具ｾﾝｻｰ異常一括
LDB MR9306               ; 加圧治具J8軸位置決めｴﾗｰ
ANB MR9307               ; 加圧治具J8軸ｻｰﾎﾞﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
INV
OUT MR30902              ; 加圧治具J8軸ﾛﾎﾞｯﾄｴﾗｰ一括
LDB MR3309               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
INV
OUT MR30903              ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常
LDB MR3308               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
INV
OUT MR30904              ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常自動
LD MR3310                ; 加圧治具ｾｯﾄﾐｽ警報
OUT MR30905              ; 加圧治具ｾｯﾄﾐｽ警報一括
LD MR40110               ; 自動ﾓｰﾄﾞSW選択
OR MR30014               ; 治具供給＆排出全原点復帰開始
OR= DM300 #61            ; 画面表示No.
AND T210                 ; 異常監視開始
OUT MR30915              ; 異常監視
LD R4606                 ; 加圧治具下降端右側
AND R4607                ; 加圧治具上昇端右側
LD R7008                 ; 加圧治具ｾｯﾄ下降SOL
ANB R4606                ; 加圧治具下降端右側
ORL
AND MR30915              ; 異常監視
TMR @30 #50              ; 加圧治具下降端右側ｾﾝｻｰ監視
CON
AND @T30                 ; 加圧治具下降端右側ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4606               ; 加圧治具下降端右側ｾﾝｻｰ異常
LD MR4606                ; 加圧治具下降端右側ｾﾝｻｰ異常
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
LDB R7008                ; 加圧治具ｾｯﾄ下降SOL
ANB R4607                ; 加圧治具上昇端右側
AND MR30915              ; 異常監視
TMR @31 #50              ; 加圧治具上昇端右側ｾﾝｻｰ監視
CON
AND @T31                 ; 加圧治具上昇端右側ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4607               ; 加圧治具上昇端右側ｾﾝｻｰ異常
LD R4608                 ; 加圧治具下降端左側
AND R4609                ; 加圧治具上昇端左側
LD R7008                 ; 加圧治具ｾｯﾄ下降SOL
ANB R4608                ; 加圧治具下降端左側
ORL
AND MR30915              ; 異常監視
TMR @32 #50              ; 加圧治具下降端左側ｾﾝｻｰ監視
CON
AND @T32                 ; 加圧治具下降端左側ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4608               ; 加圧治具下降端左側ｾﾝｻｰ異常
LD MR4608                ; 加圧治具下降端左側ｾﾝｻｰ異常
AND R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
RES MR7008               ; 加圧治具ｾｯﾄ下降SOL補助
LDB R7008                ; 加圧治具ｾｯﾄ下降SOL
ANB R4609                ; 加圧治具上昇端左側
AND MR30915              ; 異常監視
TMR @33 #50              ; 加圧治具上昇端左側ｾﾝｻｰ監視
CON
AND @T33                 ; 加圧治具上昇端左側ｾﾝｻｰ監視
AND MR9915               ; 異常なし
SET MR4609               ; 加圧治具上昇端左側ｾﾝｻｰ異常
LD T210                  ; 異常監視開始
AND R48800               ; 加圧治具J8軸位置決めｴﾗｰ
AND MR9915               ; 異常なし
SET MR9306               ; 加圧治具J8軸位置決めｴﾗｰ
LD T210                  ; 異常監視開始
AND R49105               ; 加圧治具J8軸ｻｰﾎﾞﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
AND MR9915               ; 異常なし
SET MR9307               ; 加圧治具J8軸ｻｰﾎﾞﾄﾞﾗｲﾊﾞｰｱﾗｰﾑ
LD T210                  ; 異常監視開始
ANB R42910               ; 加圧治具J8軸位置決め完了
ANB R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
AND MR40005              ; 全起動中
@SET MR3808              ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
LD MR3808                ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
AND MR9915               ; 異常なし
SET MR3308               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
LD @MR210                ; 加圧治具J8軸原点復帰
LD R1111                 ; 加圧治具　J8軸原点復帰完了HEND
ANB R42910               ; 加圧治具J8軸位置決め完了
ORL
ANB R1411                ; ｲﾝﾃﾞｯｸｽ部ｴﾘｱｾﾝｻｰ
LD T210                  ; 異常監視開始
AND>= DM300 #61          ; 画面表示No.
AND<= DM300 #62          ; 画面表示No.
OR MR30014               ; 治具供給＆排出全原点復帰開始
ANL
@SET MR3809              ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
LD MR3809                ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ異常補助
AND MR9915               ; 異常なし
SET MR3309               ; 加圧治具J8軸制御中ｴﾘｱｾﾝｻ-異常
;<h1/>警報:　加圧治具ｾｯﾄﾐｽ警報
LDB @T8                  ; 加圧治具突き当て確認
ORB MR30105              ; 加圧治具無し確認
AND MR30110              ; 治具収納ｶｾｯﾄ供給要求
ANP R4803                ; ｲﾝﾃﾞｯｸｽ確認SW
AND MR9915               ; 異常なし
SET MR3310               ; 加圧治具ｾｯﾄﾐｽ警報
;<h1/>ﾌﾟﾛｸﾞﾗﾑﾓﾆﾀｰ用　DM
LD @MR601
@MOV #601 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR602                ; SRB1　MG吸着確認
@MOV #602 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR603                ; 接着治具有無確認
@MOV #603 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR604                ; 接着治具満杯確認
@MOV #604 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR605                ; 加圧治具J8軸供給位置確認
@MOV #605 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR606
@MOV #606 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR607
@MOV #607 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR608
@MOV #608 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR609
@MOV #609 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR610
@MOV #610 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR611
@MOV #611 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR612
@MOV #612 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR613
@MOV #613 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR614
@MOV #614 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR615                ; CAV　T1X軸搬送位置移動完了
@MOV #615 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR700
@MOV #700 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR701
@MOV #701 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR702
@MOV #702 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR703
@MOV #703 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR704
@MOV #704 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR705
@MOV #705 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR706
@MOV #706 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR707
@MOV #707 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR708
@MOV #708 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR709
@MOV #709 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR710
@MOV #710 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR711
@MOV #711 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR712
@MOV #712 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR713
@MOV #713 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR714
@MOV #714 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LD @MR715
@MOV #715 DM514          ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
LDB MR30015              ; 治具供給＆排出全自動起動中
MOV #0 DM514             ; 治具供給＆排出ｽﾃｯﾌﾟﾓﾆﾀ
END
ENDH
