DEVICE:53
;MODULE:ｺﾝﾍﾞｱ
;MODULE_TYPE:0
;非常停止 / Emergency stop
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50010                ; 非常停止
ZRES @MR002 @MR3315
CON
SET MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
ZRES R52000 R52015       ; 給材ｺﾝﾍﾞｱ 取出し要求
CON
RES MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
RES MR2005               ; 供給側ｺﾝﾍﾞｱ ON
CON
RES MR2006               ; 戻し側ｺﾝﾍﾞｱ ON
CON
RES MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
RES MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
OUT @MR001
;条件ﾌﾗｸﾞ / Flug
LDB R4100                ; 供給2個前ｽﾄｯﾊﾟ 上昇端
AND R4101                ; 供給2個前ｽﾄｯﾊﾟ 下降端
ANB R4102                ; 供給1個前ｽﾄｯﾊﾟ 下降端
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
ANB R4104                ; 供給ｽﾄｯﾊﾟ 下降端
AND R4105                ; 供給ｽﾄｯﾊﾟ 上昇端
ANB R4106                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 上昇端
AND R4107                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 下降端
ANB R4108                ; 除材_位置決め下降端
AND R4109                ; 除材_位置決め上昇端
OUT @MR005               ; ｺﾝﾍﾞｱ部ｼﾘﾝﾀﾞ原位置
LDB R50300               ; PAUSE中
ANB R50111               ; ｺﾝﾍﾞｱｴﾗｰ中
ANB R50108               ; ｴﾗｰ発生中
OUT @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
;確認ｾﾝｻﾃﾞｨﾚｲ / Senser Delay
LD R4206                 ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @1 #4                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR #241 #4              ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
LDB R4206                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @2 #2                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
TMR #242 #2              ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
LD R4205                 ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
TMR @3 #2                ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
LDB R4205                ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
TMR @4 #2                ; 供給_1個前ﾜｰｸ確認ｾﾝｻ OFF
LD R4204                 ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
TMR @5 #2                ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
LDB R4204                ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
TMR @6 #2                ; 供給_2個前ﾜｰｸ確認ｾﾝｻ OFF
LD R4207                 ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @7 #2                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR #247 #2              ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
LDB R4207                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @8 #2                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
LDB R4209                ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ
TMR #240 #2              ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ有り
LD R4209                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ
TMR @10 #10              ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
TMR #250 #10             ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
;AUTO動作 / AUTO OPERATION
LD R50011                ; AUTO MODE
ANB R50008               ; 全体ｴﾗｰ発生
ANB R50300               ; PAUSE中
AND @T5                  ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
LD @T3                   ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
OR R6114                 ; 供給部_1個前ｽｯﾊﾟ 下降
ANL
OUT MR6201               ; 供給部_2個前ｽｯﾊﾟ 上昇
LD R50011                ; AUTO MODE
ANB MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
AND MR11001              ; ｺﾝﾍﾞｱstart
RES @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
CON
RES @MR2505
CON
RES MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
RES MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
SET MR2005               ; 供給側ｺﾝﾍﾞｱ ON
CON
SET MR2006               ; 戻し側ｺﾝﾍﾞｱ ON
CON
SET MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
CON
RES MR11001              ; ｺﾝﾍﾞｱstart
CON
SET @MR2000              ; AUTO START
STG @MR2600
SET MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
SET MR6112               ; 供給部_位置決め 下降
CON
SET MR6108               ; 除材部_位置決め 下降
CON
AND R4102                ; 供給1個前ｽﾄｯﾊﾟ 下降端
AND R4104                ; 供給ｽﾄｯﾊﾟ 下降端
AND R4108                ; 除材_位置決め下降端
JMP @MR2601
STG @MR2601
MPS
ANB R51000               ; 停止要求
JMP @MR2602
MPP
AND R51000               ; 停止要求
TMR @9 #10
CON
AND @T9
JMP @MR2603
STG @MR2602
TMR @0 #3                ; Wait Timer-00
CON
AND @T0                  ; Wait Timer-00
JMP @MR2600
STG @MR2603
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
RES MR6112               ; 供給部_位置決め 下降
CON
RES MR6108               ; 除材部_位置決め 下降
CON
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
AND R4105                ; 供給ｽﾄｯﾊﾟ 上昇端
AND R4109                ; 除材_位置決め上昇端
RES MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
ENDS
STG @MR2000              ; AUTO START
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB R50013               ; 払い出し動作
MPS
ANB R51000               ; 停止要求
MPS
AND @T1                  ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
JMP @MR2100
MPP
LD @T2                   ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
AND @T3                  ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
ANL
MPS
AND @T5                  ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
JMP @MR2200
MPP
AND @T6                  ; 供給_2個前ﾜｰｸ確認ｾﾝｻ OFF
SET @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
CON
JMP @MR2201
MPP
AND R51000               ; 停止要求
SET MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
JMP @MR2500
STG @MR2100
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
RES MR6112               ; 供給部_位置決め 下降
CON
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
AND R4105                ; 供給ｽﾄｯﾊﾟ 上昇端
JMP @MR2101
STG @MR2101
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6110               ; 供給部_ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4106                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 上昇端
SET R52000               ; 給材ｺﾝﾍﾞｱ 取出し要求
CON
JMP @MR2102
STG @MR2102
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R52001               ; 取出し完了
JMP @MR2103
MPP
LD CR2003                ; 常時OFF
ANB R52001               ; 取出し完了
AND R51000               ; 停止要求
ANL
MPS
ANB R52008               ; 給材中
JMP @MR2103
MPP
AND R52008               ; 給材中
JMP @MR2102
STG @MR2103
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R52001               ; 取出し完了
CON
ANB R52000               ; 給材ｺﾝﾍﾞｱ 取出し要求
RES MR6110               ; 供給部_ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4107                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 下降端
JMP @MR2000              ; AUTO START
STG @MR2200
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
CON
AND R4100                ; 供給2個前ｽﾄｯﾊﾟ 上昇端
JMP @MR2201
STG @MR2201
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
AND R4102                ; 供給1個前ｽﾄｯﾊﾟ 下降端
AND @T4                  ; 供給_1個前ﾜｰｸ確認ｾﾝｻ OFF
JMP @MR2202
STG @MR2202
MPS
LD @T1                   ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
OR R4206                 ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
ANL
JMP @MR2203
MPP
AND @T2                  ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
TMR #950 #100            ; ﾜｰｸ到着時間監視
CON
AND T950                 ; ﾜｰｸ到着時間監視
SET @MR3000              ; ﾜｰｸ到着異常
CON
ENDS
STG @MR2203
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
RES @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
CON
JMP @MR2204
STG @MR2204
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
CON
AND R4101                ; 供給2個前ｽﾄｯﾊﾟ 下降端
JMP @MR2000              ; AUTO START
LD @MR2315               ; 2個前ｽﾄｯﾊﾟ 上昇監視
LD @T5                   ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
OR R4204                 ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
ANL
SET MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
LDF @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
RES MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
STG @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND @T10                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
MPS
AND @T8                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
JMP @MR2401
MPP
AND @T7                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
JMP @MR2410
MPP
AND T240                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ有り
TMR @11 #20
CON
AND @T11
OUT MR4209               ; ｵｰﾊﾞｰﾌﾛｰ検出中
STG @MR2401
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6108               ; 除材部_位置決め 下降
CON
AND R4108                ; 除材_位置決め下降端
JMP @MR2402
STG @MR2402
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @T7                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
AND @T10                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
TMR #952 #20             ; Wait Timer 952
CON
AND T952                 ; Wait Timer 952
JMP @MR2403
STG @MR2403
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6108               ; 除材部_位置決め 下降
CON
AND R4109                ; 除材_位置決め上昇端
JMP @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
STG @MR2410
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @T7                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
AND @T10                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
SET R52010               ; ｺﾝﾍﾞｱ受け取り可
CON
JMP @MR2411
STG @MR2411
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
LD R52007                ; 受渡し完了
ANB R52006               ; 除材部ﾜｰｸ受取り可
ANL
RES R52007               ; 受渡し完了
CON
JMP @MR2412
MPP
LDB R52007               ; 受渡し完了
AND MR11406              ; 除材PPｻｲｸﾙ終了
ANL
SET @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
CON
ENDS
STG @MR2412
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
DIFU @MR2505
CON
MPS
ANB R54306               ; 取P無し
SET MR6108               ; 除材部_位置決め 下降
CON
AND R4108                ; 除材_位置決め下降端
JMP @MR2413
MPP
AND R54306               ; 取P無し
SET @MR3008              ; 取P無し異常
CON
RES R54306               ; 取P無し
CON
ENDS
STG @MR2413
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @T7                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
AND @T10                 ; 下流側ｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
TMR #953 #20             ; Wait Timer 953
CON
AND T953                 ; Wait Timer 953
RES @MR2505
CON
JMP @MR2414
STG @MR2414
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6108               ; 除材部_位置決め 下降
CON
AND R4109                ; 除材_位置決め上昇端
JMP @MR2410
STG @MR2500
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
RES R52100               ; 除材PP部AUTO動作完了
CON
RES @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
CON
JMP @MR2501
STG @MR2501
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R52010               ; ｺﾝﾍﾞｱ受け取り可
CON
RES MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
ENDS
;ｱﾗｰﾑ処理
STG @MR3000              ; ﾜｰｸ到着異常
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50111               ; ｺﾝﾍﾞｱｴﾗｰ中
CON
SET R51001               ; ｺﾝﾍﾞｱ部　表示
CON
SET R50515               ; ERR15
CON
JMP @MR3001
MOV #15 DM400            ; 表示画面No.
STG @MR3001
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3002
STG @MR3002
ANB R50008               ; 全体ｴﾗｰ発生
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
RES MR6112               ; 供給部_位置決め 下降
CON
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
AND R4105                ; 供給ｽﾄｯﾊﾟ 上昇端
SET @MR2000              ; AUTO START
CON
ENDS
STG @MR3008              ; 取P無し異常
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R50111               ; ｺﾝﾍﾞｱｴﾗｰ中
CON
SET R51001               ; ｺﾝﾍﾞｱ部　表示
CON
SET R50512               ; ERR12
CON
JMP @MR3009
MOV #12 DM400            ; 表示画面No.
STG @MR3009
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR3010
STG @MR3010
ANB R50008               ; 全体ｴﾗｰ発生
MPS
AND @T8                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
JMP @MR3008              ; 取P無し異常
MPP
AND @T7                  ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
JMP @MR2410
LD MR11201               ; ｺﾝﾍﾞｱｻｲｸﾙ中
AND CR2005               ; 100msｸﾛｯｸﾊﾟﾙｽ
@INC DM15034             ; ｻｲｸﾙﾀｲﾑ
LD MR11201               ; ｺﾝﾍﾞｱｻｲｸﾙ中
ANF @MR2505
MOV DM15034 DM15018      ; ｻｲｸﾙﾀｲﾑ
DW #0 DM15034            ; ｻｲｸﾙﾀｲﾑ
END
ENDH
