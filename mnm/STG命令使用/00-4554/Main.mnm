DEVICE:51
;MODULE:Main
;MODULE_TYPE:0
;<h1/>モード確認
LD= DM0 #0               ; TP表示頁No.
OUT MR1001               ; 自動MODE
LD= DM0 #1               ; TP表示頁No.
OUT MR1002               ; 周期MODE
LD= DM0 #2               ; TP表示頁No.
OUT MR1003               ; 手動MODE
;<h1/>自動運転 前準備
LDP MR1100               ; Tape送りﾓｰﾀｰON_SW
OR MR400                 ; Tape送りﾓｰﾀｰON
LD MR1001                ; 自動MODE
OR MR1002                ; 周期MODE
ANL
ANB MR1101               ; Tape送りﾓｰﾀｰOFF_SW
ANB MR107                ; Tape送りT.O.
OUT MR400                ; Tape送りﾓｰﾀｰON
LD MR400                 ; Tape送りﾓｰﾀｰON
AND T3                   ; Tape巻き出し上限
SET MR401                ; Tape送り開始
LD MR400                 ; Tape送りﾓｰﾀｰON
AND T4                   ; Tape巻き出し下限
ORB MR400                ; Tape送りﾓｰﾀｰON
RES MR401                ; Tape送り開始
LDP MR1102               ; Tape巻取ﾓｰﾀｰON_SW
OR MR402                 ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
LD MR1001                ; 自動MODE
OR MR1002                ; 周期MODE
ANL
ANB MR1103               ; Tape巻取ﾓｰﾀｰOFF_SW
ANB MR108                ; Tape巻取りT.O.
OUT MR402                ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
LD MR402                 ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
AND T15                  ; Tape巻取り 下限
SET MR403                ; Tape巻取り開始
LD MR402                 ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
AND T14                  ; Tape巻取り 上限
ORB MR402                ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
RES MR403                ; Tape巻取り開始
;<h1/>自動運転
LDP T2                   ; Start両押し
AND< DM100 DM200         ; ﾌﾟﾚｽｶｳﾝﾄﾓﾆﾀ  [TP設定]ﾌﾟﾚｽｶｳﾝﾄ警報
AND T10                  ; ﾌﾟﾚｽ上限
AND MR1200               ; Tape送りﾓｰﾀｰON_SWﾗﾝﾌﾟ
AND MR402                ; Tape巻取ﾓｰﾀｰON_SWﾗﾝﾌﾟ
OR MR500                 ; 自動運転準備
ANB MR503                ; ｻｲｸﾙ停止
ANB MR613                ; [A]ｻｲｸﾙ停止
LD MR1001                ; 自動MODE
OR MR1002                ; 周期MODE
ANL
ANB MR300                ; 異常代表
OUT MR500                ; 自動運転準備
MPS
ANB MR612                ; [A]1ｻｲｸﾙ完了
TMR #30 #5               ; 自動運転Delay
MPP
AND T30                  ; 自動運転Delay
OUT MR501                ; 自動運転
LD MR1007                ; ｻｲｸﾙ停止SW
LD<> DM100 #0            ; ﾌﾟﾚｽｶｳﾝﾄﾓﾆﾀ
AND>= DM100 DM200        ; ﾌﾟﾚｽｶｳﾝﾄﾓﾆﾀ  [TP設定]ﾌﾟﾚｽｶｳﾝﾄ警報
ORL
OR MR502                 ; ｻｲｸﾙ停止保持
AND MR500                ; 自動運転準備
AND MR1001               ; 自動MODE
MPS
OUT MR502                ; ｻｲｸﾙ停止保持
MPP
AND MR612                ; [A]1ｻｲｸﾙ完了
OUT MR503                ; ｻｲｸﾙ停止
LDP MR500                ; 自動運転準備
RES LR010                ; 変位ｾﾝｻ確認
LDP MR501                ; 自動運転
MPS
LDA.L +0
CON
SUB.L DM212              ; [TP設定]   Tapeﾋﾟｯﾁ量
CON
STA.L DM300              ; Tapeﾋﾟｯﾁ量(→)
CON
MOV.L DM300 CM2060       ; Tapeﾋﾟｯﾁ量(→)  ﾎﾟｲﾝﾄ0 目標値/移動量
MPP
PLSX #0
LD MR501                 ; 自動運転
LD CR3009                ; X軸ﾊﾟﾙｽ出力中/緊急停止
ANB CR3006               ; X軸位置決め完了ﾘﾚｰ
OR MR600                 ; [A]Tape送り動作中
ANL
MPS
OUT MR600                ; [A]Tape送り動作中
MPP
LDB CR3009               ; X軸ﾊﾟﾙｽ出力中/緊急停止
AND CR3006               ; X軸位置決め完了ﾘﾚｰ
OR MR601                 ; [A]Tape送り位置決め完了
ANL
OUT MR601                ; [A]Tape送り位置決め完了
LD MR601                 ; [A]Tape送り位置決め完了
ANB LR010                ; 変位ｾﾝｻ確認
MPS
OUT MR602                ; [A]変位ｾﾝｻ下降
MPP
LD T13                   ; 変位ｾﾝｻ下限
OR MR603                 ; [A]変位ｾﾝｻ確認正常
OR MR604                 ; [A]変位ｾﾝｻ確認異常
ANL
MPS
LD T16                   ; 金型検出
OR T17                   ; 金型検出無
AND R007                 ; 変位ｾﾝｻ-Hi
OR MR603                 ; [A]変位ｾﾝｻ確認正常
ANB MR604                ; [A]変位ｾﾝｻ確認異常
ANL
OUT MR603                ; [A]変位ｾﾝｻ確認正常
MPP
LD T13                   ; 変位ｾﾝｻ下限
LD T16                   ; 金型検出
OR T17                   ; 金型検出無
ANL
LD R008                  ; 変位ｾﾝｻ-Go
OR R009                  ; 変位ｾﾝｻ-Lo
ANL
OR MR604                 ; [A]変位ｾﾝｻ確認異常
ANB MR603                ; [A]変位ｾﾝｻ確認正常
ANL
OUT MR604                ; [A]変位ｾﾝｻ確認異常
LD MR601                 ; [A]Tape送り位置決め完了
LDB LR010                ; 変位ｾﾝｻ確認
AND MR603                ; [A]変位ｾﾝｻ確認正常
OR MR605                 ; [A]変位ｾﾝｻ確認終了
OR LR010                 ; 変位ｾﾝｻ確認
ANL
AND T12                  ; 変位ｾﾝｻ上限
OUT MR605                ; [A]変位ｾﾝｻ確認終了
LD MR605                 ; [A]変位ｾﾝｻ確認終了
SET LR010                ; 変位ｾﾝｻ確認
MPS
LD R1002                 ; Tape巻き出し 終了
OR MR606                 ; [A]Tape有り確認
ANB MR607                ; [A]Tapeつなぎ無
ANL
OUT MR606                ; [A]Tape有り確認
MPP
LD T20                   ; つなぎ目無し
OR MR607                 ; [A]Tapeつなぎ無
ANB MR606                ; [A]Tape有り確認
ANL
OUT MR607                ; [A]Tapeつなぎ無
LD MR606                 ; [A]Tape有り確認
AND T35                  ; 完了確認
AND LR011                ; 位置ｽﾞﾚ有効/無効
MPS
LD LR012                 ; Color/BlackON:Color
AND R1003                ; 位置ｽﾞﾚ検出1(ｶﾗｰ)
AND R1004                ; 位置ｽﾞﾚ検出2(ｶﾗｰ)
LDB LR012                ; Color/BlackON:Color
AND R1014                ; 位置ｽﾞﾚ検出1(ﾌﾞﾗｯｸ)
AND R1015                ; 位置ｽﾞﾚ検出2(ﾌﾞﾗｯｸ)
ORL
OR MR608                 ; [A]位置ｽﾞﾚ確認OK
ANB MR609                ; [A]位置ｽﾞﾚ確認NG
ANL
OUT MR608                ; [A]位置ｽﾞﾚ確認OK
MPP
LD LR012                 ; Color/BlackON:Color
LDB R1003                ; 位置ｽﾞﾚ検出1(ｶﾗｰ)
ORB R1004                ; 位置ｽﾞﾚ検出2(ｶﾗｰ)
ANL
LDB LR012                ; Color/BlackON:Color
LDB R1013                ; 圧力ｾﾝｻ
ORB R1014                ; 位置ｽﾞﾚ検出1(ﾌﾞﾗｯｸ)
ANL
ORL
ANB MR608                ; [A]位置ｽﾞﾚ確認OK
ANL
OUT MR609                ; [A]位置ｽﾞﾚ確認NG
LD MR606                 ; [A]Tape有り確認
LD MR608                 ; [A]位置ｽﾞﾚ確認OK
ORB LR011                ; 位置ｽﾞﾚ有効/無効
ANL
MPS
OUT MR610                ; [A]ﾌﾟﾚｽ開始
MPP
LD T11                   ; ﾌﾟﾚｽ下限
LD CR2003                ; 常時OFF
AND MR1002               ; 周期MODE
LDB R000                 ; Start(右)SW
ORB R001                 ; Start(左)SW
ANL
ORL
OR MR611                 ; [A]ﾌﾟﾚｽ上昇
ANL
MPS
OUT MR611                ; [A]ﾌﾟﾚｽ上昇
MPP
AND T10                  ; ﾌﾟﾚｽ上限
MPS
AND MR1001               ; 自動MODE
OUT MR612                ; [A]1ｻｲｸﾙ完了
MRD
AND MR1002               ; 周期MODE
OUT MR613                ; [A]ｻｲｸﾙ停止
MRD
@INC DM100               ; ﾌﾟﾚｽｶｳﾝﾄﾓﾆﾀ
MPP
@INC DM102               ; 良品カウンタ
;<h1/>手動運転 
; パルス出力
LDP MR1111               ; １パルス 右移動SW
ANB R1000                ; Tape巻き出し 上限
ANB CR3009               ; X軸ﾊﾟﾙｽ出力中/緊急停止
AND MR1003               ; 手動MODE
MPS
LDA.L +0
CON
SUB.L DM210              ; [TP設定]Tapeｼﾌﾄ量
CON
STA.L DM300              ; Tapeﾋﾟｯﾁ量(→)
CON
MOV.L DM300 CM2060       ; Tapeﾋﾟｯﾁ量(→)  ﾎﾟｲﾝﾄ0 目標値/移動量
MPP
PLSX #0
LDP MR1110               ; １パルス 左移動SW
ANB R1010                ; Tape巻取り 上限
ANB CR3009               ; X軸ﾊﾟﾙｽ出力中/緊急停止
AND MR1003               ; 手動MODE
MOV.L DM210 CM2060       ; [TP設定]Tapeｼﾌﾄ量  ﾎﾟｲﾝﾄ0 目標値/移動量
PLSX #0
LDP MR1113               ; １ピッチ 右移動SW
ANB R1000                ; Tape巻き出し 上限
ANB CR3009               ; X軸ﾊﾟﾙｽ出力中/緊急停止
AND MR1003               ; 手動MODE
MPS
LDA.L +0
CON
SUB.L DM212              ; [TP設定]   Tapeﾋﾟｯﾁ量
CON
STA.L DM300              ; Tapeﾋﾟｯﾁ量(→)
CON
MOV.L DM300 CM2060       ; Tapeﾋﾟｯﾁ量(→)  ﾎﾟｲﾝﾄ0 目標値/移動量
MPP
PLSX #0
LDP MR1112               ; １ピッチ 左移動SW
ANB R1010                ; Tape巻取り 上限
ANB CR3009               ; X軸ﾊﾟﾙｽ出力中/緊急停止
AND MR1003               ; 手動MODE
MOV.L DM212 CM2060       ; [TP設定]   Tapeﾋﾟｯﾁ量  ﾎﾟｲﾝﾄ0 目標値/移動量
PLSX #0
LD MR1010                ; 仮)JOG+
LD MR1011                ; 仮)JOG-
LD MR1012                ; 仮)JOG High
JOGX
END
ENDH
