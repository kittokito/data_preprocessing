DEVICE:53
;MODULE:CHANGE_PAGE
;MODULE_TYPE:1
;OneSystem SupportMacro
;広丘
;-----------------------------------------
;Macro Name 	: 	CHANGE_PAGE
;Macro Type	:	Self Holding
;Function	:	ﾕｰｻﾞｰ画面ﾍﾟｰｼﾞ切替
;-----------------------------------------
;
;Copylight(C) 2011 SEIKO EPSON Corp. All rights reserved.
;引数：
;	V0	=	表示ｸﾞﾙｰﾌﾟ番号			#1〜8		
;	V1	=	切替ﾍﾟｰｼﾞ番号			#2000〜8999
;	P9	=	完了ﾘﾚｰ					ﾏｸﾛ完了時ON
;
;機能：
;	指定番号のﾕｰｻﾞｰ画面に切り替える
LD R59915                ; _運転ﾓｰﾄﾞ指定
SET P9                   ; Fin_Relay
CON
MEND
LD R46104                ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ZRES @R000 @R015         ; 排他占有  ﾏｸﾛ異常発生
CON
MEND
;<h1/>Argument Range Check/引数範囲ﾁｪｯｸ
LD< V0 #1                ; Group_No
OR> V0 #8                ; Group_No
OR< V1 #2000             ; Page_No
OR> V1 #8999             ; Page_No
SET R40008               ; _ｻﾎﾟｰﾄﾏｸﾛ引数異常発生
CON
SET @R015                ; ﾏｸﾛ異常発生
;<h1/>Exclusive/排他占有
LD @CR2008
ANB R59915               ; _運転ﾓｰﾄﾞ指定
RES P9                   ; Fin_Relay
CON
SET @R000                ; 排他占有
STG @R000                ; 排他占有
ANB R48700               ; _ﾕｰｻﾞｰﾍﾟｰｼﾞ切替排他
SET R48700               ; _ﾕｰｻﾞｰﾍﾟｰｼﾞ切替排他
CON
JMP @R001                ; 優先画面消去待ち
;<h1/>Proirity Screen Display Check/優先画面表示確認
;・優先ｴﾗｰ、通常ｴﾗｰ、選択系画面が表示されていたら消去待ちとする
STG @R001                ; 優先画面消去待ち
ANB R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
ANB R48000               ; _Group1　ｴﾗｰ処理排他
ANB R48008               ; _Group1　画面処理排他
JMP @R002                ; ﾕｰｻﾞｰﾍﾟｰｼﾞ切替
;<h1/>User Page Change Processing/ﾕｰｻﾞｰﾍﾟｰｼﾞ切替処理
;・ﾕｰｻﾞｰﾍﾟｰｼﾞ切替〜切替完了待ち〜排他開放
STG @R002                ; ﾕｰｻﾞｰﾍﾟｰｼﾞ切替
MPS
MOV V1 DM3008            ; Page_No  _MainTP　切替ﾍﾟｰｼﾞNo
MPP
AND= DM3000 V1           ; _MainTP　表示中ﾍﾟｰｼﾞNo  Page_No
RES R48700               ; _ﾕｰｻﾞｰﾍﾟｰｼﾞ切替排他
SET P9                   ; Fin_Relay
ENDS
MEND
END
ENDH
