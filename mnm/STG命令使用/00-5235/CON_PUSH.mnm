DEVICE:53
;MODULE:CON_PUSH
;MODULE_TYPE:1
;OneSystem SupportMacro
;広丘
;-----------------------------------------
;Macro Name 	: 	IAI_CON_PUSH
;Macro Type	:	Self Holding
;Ver.		:	1.4
;Function	:	IAI CON系直値PUSH動作ﾏｸﾛ
;-----------------------------------------
;1.2	ｺﾝﾄﾛｰﾗｴﾗｰ時のｴﾗｰ番号ｽﾄﾚｰｼﾞ修正
;1.3	START時のRES信号OFFﾀｲﾐﾝｸﾞの見直し
;1.4 引数書込みﾐｽ対策
;引数：
;V0	=	目標位置(0.01mm)				V0	=	Target Position Value
;V1	=	押付け幅(0.01mm)				V1	=	Push Wide
;V2	=	速度							V2	=	Speed
;V3	=	加減速(0.01G)					V3	=	G
;V4　=	押付け電流値(#0〜255)			V4	=	Push Power
;
;P0	=	RB入力先頭番地	DM番号で指定	P0	=	Input Leading Address for RB
;P1	=	RB出力先頭番地	DM番号で指定	P1	=	Output Leading Address for RB
;P2	=	完了コード						P2	=	Macro Complete Code
;			#20			=	正常終了				#20			=	No Error
;			#21			=	RB応答なし				#21			=	Non Responce
;			#22			=	ｽﾃｰﾀｽ異常				#22			=	Status Abnormal
;			#23			=	押付け空振り			#23			=	Push Fail
;			#24			=	引数変更失敗			#24			=	Argument failure
;			#Alarmcode	=	Alarm					#Alarmcode	=	Alarm
;
;機能：CON系 MSEP 数直移動押付け指令ﾏｸﾛ/ Robot Move Macro for Numerical Push Move Mode
;IN
BLD P0:#7 #0             ; RB_IN
OUT PEND                 ; IN PEND
BLD P0:#7 #1             ; RB_IN
OUT HEND                 ; IN HEND
BLD P0:#7 #2             ; RB_IN
OUT MOVE                 ; IN MOVE
BLD P0:#7 #3             ; RB_IN
OUT ALM                  ; IN ALM
BLD P0:#7 #4             ; RB_IN
OUT SV                   ; IN SV
BLD P0:#7 #5             ; RB_IN
OUT PSFL                 ; IN PSFL
BLD P0:#7 #7             ; RB_IN
OUT ALML                 ; IN ALML
BLD P0:#7 #8             ; RB_IN
OUT M_END                ; IN MEND
BLD P0:#7 #12            ; RB_IN
OUT ZONE1                ; IN ZONE1
BLD P0:#7 #13            ; RB_IN
OUT ZONE2                ; IN ZONE2
BLD P0:#7 #14            ; RB_IN
OUT CRDY                 ; IN CRDY
BLD P0:#7 #15            ; RB_IN
OUT EMGS                 ; IN EMGS
LD R59915                ; _運転ﾓｰﾄﾞ指定
ORP R46104               ; _非常停止中　ﾓｰﾄﾞﾌﾗｸﾞ
@SET @R012               ; 終了処理
;初期化　/ Initialize
LD @CR2008
SET @R000
;RBｽﾃｰﾀｽﾓﾆﾀｰ / Status Monitoring
LDB EMGS                 ; IN EMGS
AND SV                   ; IN SV
AND CRDY                 ; IN CRDY
AND HEND                 ; IN HEND
ANB MOVE                 ; IN MOVE
OUT @R014                ; RB Status OK
;Move Parameter storage
STG @R000
MPS
LDA.D V1                 ; Push Wide
CON
STA.D P1:#2              ; RB_OUT
CON
LDA.D V0                 ; POS
CON
STA.D P1:#0              ; RB_OUT
CON
LDA V2                   ; SPEED
CON
STA P1:#4                ; RB_OUT
CON
LDA V3                   ; G
CON
STA P1:#5                ; RB_OUT
MRD
SET DIR                  ; OUT Push ON=+Dir
CON
SET PUSH                 ; OUT Push
CON
LDA V4                   ; Push_Power
CON
STA P1:#6                ; RB_OUT
MRD
TMH @0 #300
MRD
LDB @T0
AND=.D V1 P1:#2          ; Push Wide  RB_OUT
AND=.D V0 P1:#0          ; POS  RB_OUT
AND= V2 P1:#4            ; SPEED  RB_OUT
AND= V3 P1:#5            ; G  RB_OUT
AND= V4 P1:#6            ; Push_Power  RB_OUT
AND DIR                  ; OUT Push ON=+Dir
AND PUSH                 ; OUT Push
ANL
JMP @R001
MPP
AND @T0
DW #24 @DM0
CON
JMP @R012                ; 終了処理
STG @R001
MPS
ANB ALM                  ; IN ALM
MPS
AND @R014                ; RB Status OK
MPS
LD PEND                  ; IN PEND
ANB PSFL                 ; IN PSFL
ANL
SET CSTR                 ; OUT START
CON
JMP @R002
MPP
LDB PEND                 ; IN PEND
OR PSFL                  ; IN PSFL
ANL
SET CSTR                 ; OUT START
CON
JMP @R004
MPP
ANB @R014                ; RB Status OK
DW #22 @DM0
CON
JMP @R012                ; 終了処理
MPP
AND ALM                  ; IN ALM
JMP @R011                ; Alarm
STG @R004
TMH @4 #30
CON
AND @T4
JMP @R002
STG @R002
MPS
ANB @T2
MPS
LDB ALM                  ; IN ALM
ANB PEND                 ; IN PEND
ANL
RES CSTR                 ; OUT START
CON
JMP @R003
MPP
AND ALM                  ; IN ALM
RES CSTR                 ; OUT START
CON
JMP @R011                ; Alarm
MPP
TMR @2 #30
CON
AND @T2
RES CSTR                 ; OUT START
CON
DW #21 @DM0
CON
JMP @R012                ; 終了処理
STG @R003
MPS
ANB ALM                  ; IN ALM
MPS
LDB PSFL                 ; IN PSFL
AND PEND                 ; IN PEND
ANL
DW #20 @DM0
CON
JMP @R012                ; 終了処理
MPP
AND PSFL                 ; IN PSFL
DW #23 @DM0
CON
JMP @R012                ; 終了処理
MPP
AND ALM                  ; IN ALM
JMP @R011                ; Alarm
;Alarm code Stroge
STG @R011                ; Alarm
LDA P0:#6                ; RB_IN
CON
STA @DM0
CON
JMP @R012                ; 終了処理
;終了処理
STG @R012                ; 終了処理
RES DIR                  ; OUT Push ON=+Dir
CON
RES PUSH                 ; OUT Push
CON
LDA @DM0
CON
STA P2                   ; C_CODE
CON
MPS
ZRES @R001 @R100         ; ﾏｸﾛ異常発生
CON
ENDS
MPP
MEND
;OUT    MEND命令の下に移動  160617 NAT kashiwabara
LD CSTR                  ; OUT START
BOUT P1:#7 #0            ; RB_OUT
LD HOME                  ; OUT HOME
BOUT P1:#7 #1            ; RB_OUT
LD STOP                  ; OUT STOP
BOUT P1:#7 #2            ; RB_OUT
LD RSET                  ; OUT RESET
BOUT P1:#7 #3            ; RB_OUT
LD JISL                  ; OUT ON=JOG
BOUT P1:#7 #5            ; RB_OUT
LD JOG_Minus             ; JOG-
BOUT P1:#7 #7            ; RB_OUT
LD JOG_Plus              ; JOG+
BOUT P1:#7 #8            ; RB_OUT
LD PUSH                  ; OUT Push
BOUT P1:#7 #12           ; RB_OUT
LD DIR                   ; OUT Push ON=+Dir
BOUT P1:#7 #13           ; RB_OUT
LD Incre                 ; OUT ON=INC
BOUT P1:#7 #14           ; RB_OUT
LD BKRL                  ; OUT Brake Release
BOUT P1:#7 #15           ; RB_OUT
;
END
ENDH
