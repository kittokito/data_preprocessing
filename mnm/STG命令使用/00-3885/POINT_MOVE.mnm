DEVICE:53
;MODULE:POINT_MOVE
;MODULE_TYPE:1
;初期化
;
LD @CR2008
RES P1                   ; END_FLG
CON
RES P2                   ; ERR_FLG
;先頭R参照(先頭 + 0)
LD @CR2008
ADRSET UR0 @EM0          ; UT_NO  軸先頭ﾘﾚｰ
LDA V0                   ; AXIS_NO
CON
SUB #1
CON
MUL #8
CON
STA @EM4                 ; 軸選択ｵﾌｾｯﾄ
CON
ADRADD.S @EM4 @EM0       ; 軸選択ｵﾌｾｯﾄ  軸先頭ﾘﾚｰ
;先頭R参照(先頭 + 4000)
LD @CR2008
ADRSET UR0 @EM2          ; UT_NO  軸先頭ﾘﾚｰ2
LDA V0                   ; AXIS_NO
CON
SUB #1
CON
MUL #128
CON
ADD #640
CON
STA @EM4                 ; 軸選択ｵﾌｾｯﾄ
CON
ADRADD.S @EM4 @EM2       ; 軸選択ｵﾌｾｯﾄ  軸先頭ﾘﾚｰ2
;ﾎﾟｲﾝﾄ選択&軸動作開始
LD @CR2008
ANB *@EM2:#68            ; 軸先頭ﾘﾚｰ2
U_WRPPNT #2 V0 V1        ; AXIS_NO  POINT_NO
SET *@EM0:#0             ; 軸先頭ﾘﾚｰ
;軸動作開始完了
LD @CR2007
AND *@EM0:#128           ; 軸先頭ﾘﾚｰ
RES *@EM0:#0             ; 軸先頭ﾘﾚｰ
;位置決め完了
LD *@EM0:#130            ; 軸先頭ﾘﾚｰ
ANB *@EM2:#68            ; 軸先頭ﾘﾚｰ2
SET *@EM0:#2             ; 軸先頭ﾘﾚｰ
;位置決めｸﾘｱ
LDB *@EM0:#130           ; 軸先頭ﾘﾚｰ
AND *@EM0:#2             ; 軸先頭ﾘﾚｰ
RES *@EM0:#2             ; 軸先頭ﾘﾚｰ
;終了処理
LD @CR2007
ANF *@EM0:#2             ; 軸先頭ﾘﾚｰ
SET P1                   ; END_FLG
;異常処理
LD @CR2007
LD *@EM2:#64             ; 軸先頭ﾘﾚｰ2
OR *@EM2:#65             ; 軸先頭ﾘﾚｰ2
ANL
RES *@EM0:#0             ; 軸先頭ﾘﾚｰ
CON
RES *@EM0:#2             ; 軸先頭ﾘﾚｰ
CON
SET P2                   ; ERR_FLG
END
ENDH
