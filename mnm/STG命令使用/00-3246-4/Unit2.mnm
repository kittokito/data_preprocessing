DEVICE:53
;MODULE:Unit2
;MODULE_TYPE:0
;非常停止 出力ｸﾘｱ
;Emergency Stop Output Clear
;
LDP CR2002               ; 常時ON
LDB MR915                ; ﾛｰｶﾙｸﾘｱ
ANB R50000               ; 非常停止停止(復帰不可ｴﾗｰ)
ANB R55003               ; 即停止ｴﾗｰ(復帰不可)
INV
ORL
ZRES @MR000 @MR4915      ; @M3000〜3115
;<h1/>終了
;END
STG @MR1014              ; ENDへ
RES MR3200               ; Unit2_BUSY
CON
SET MR3201               ; Unit2_END
CON
ANB MR3200               ; Unit2_BUSY
AND MR3201               ; Unit2_END
JMP @MR1015              ; END
STG @MR1015              ; END
ANB MR3101               ; Unit1_END
ANB MR3201               ; Unit2_END
ANB MR3301               ; Tank_END
ZRES @MR000 @MR4915      ; @M3000〜3115
CON
ENDS
;<h1/>HALT
LD MR3005                ; 装置ﾎﾟｰｽﾞ
LD @MR2000               ; ﾎﾟｰｽﾞ許可
OR R50005                ; 停止中
ANL
TMR @2 #2                ; 装置ﾎﾟｰｽﾞ確認ﾀｲﾏｰ
CON
AND @T2                  ; 装置ﾎﾟｰｽﾞ確認ﾀｲﾏｰ
@SET MR3202              ; Unit2_HALT
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
RES @MR2000              ; ﾎﾟｰｽﾞ許可
LD MR3007                ; ﾎﾟｰｽﾞ解除
RES MR3202               ; Unit2_HALT
CON
RES @MR2000              ; ﾎﾟｰｽﾞ許可
;<h1/>ｲﾝﾀｰﾛｯｸ関係
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD<> EM1302 #1           ; RB2現在POS
LD>= EM1302 #2           ; RB2現在POS
AND<= EM1302 #4          ; RB2現在POS
OR= EM1302 #6            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
LD= EM1302 #5            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
OR R50003                ; 原点復帰中
ANL
OUT @MR500               ; IAI_ｾｯﾄ位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD<> EM1302 #1           ; RB2現在POS
AND<> EM1302 #5          ; RB2現在POS
LD= EM1302 #1            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
LD= EM1302 #5            ; RB2現在POS
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
AND<> EM1302 #2          ; RB2現在POS
OUT @MR501               ; IAI_ﾉｽﾞﾙ位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD<> EM1302 #1           ; RB2現在POS
AND<> EM1302 #5          ; RB2現在POS
LD= EM1302 #1            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
LD= EM1302 #5            ; RB2現在POS
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
AND<> EM1302 #3          ; RB2現在POS
OUT @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD<> EM1302 #1           ; RB2現在POS
AND<> EM1302 #5          ; RB2現在POS
LD= EM1302 #1            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
LD= EM1302 #5            ; RB2現在POS
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
AND<> EM1302 #4          ; RB2現在POS
OUT @MR503               ; IAI_ｴｱﾌﾞﾛｰ停止位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD= EM1302 #1            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
LD>= EM1302 #2           ; RB2現在POS
AND<= EM1302 #4          ; RB2現在POS
OR= EM1302 #6            ; RB2現在POS
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
AND<> EM1302 #5          ; RB2現在POS
OUT @MR504               ; IAI_取出位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
LD<> EM1302 #1           ; RB2現在POS
AND<> EM1302 #5          ; RB2現在POS
LD= EM1302 #1            ; RB2現在POS
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
LD= EM1302 #5            ; RB2現在POS
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORL
ANL
AND<> EM1302 #6          ; RB2現在POS
OUT @MR505               ; IAI_ｴｱﾌﾞﾛｰ中間位置動作I/L
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
OUT @MR510               ; IAI_ｴｱﾌﾞﾛｰ原点復帰動作I/L
LDB DM10815.2            ; MOVE
AND= EM1302 #2           ; RB2現在POS
OUT @MR600               ; ﾉｽﾞﾙﾕﾆｯﾄ下降I/L
;<h1/>HOME/原点復帰
;[本STG内容]
;・STG @MR1000の続きから各Taskの原点復帰動作を記述する
;・動作終了時は@MR1515にJMPする事
LDP R50003               ; 原点復帰中
SET MR3200               ; Unit2_BUSY
CON
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
AND MR3200               ; Unit2_BUSY
ANB MR7211               ; ｴｱｰﾌﾞﾛｰ2
SET @MR1001              ; 原点復帰開始
STG @MR1001              ; 原点復帰開始
SET MR7405               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇
CON
RES MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
CON
JMP @MR1002              ; ﾉｽﾞﾙﾕﾆｯﾄ加圧上昇
STG @MR1002              ; ﾉｽﾞﾙﾕﾆｯﾄ加圧上昇
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
JMP @MR1003              ; ﾉｽﾞﾙﾕﾆｯﾄ治具上昇
STG @MR1003              ; ﾉｽﾞﾙﾕﾆｯﾄ治具上昇
SET MR7401               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇
CON
RES MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
CON
JMP @MR1004              ; ﾉｽﾞﾙﾕﾆｯﾄ治具上昇完了
STG @MR1004              ; ﾉｽﾞﾙﾕﾆｯﾄ治具上昇完了
AND R4203                ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
JMP @MR1005              ; ｾｯﾄ位置へ移動
STG @MR1005              ; ｾｯﾄ位置へ移動
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
AND @MR500               ; IAI_ｾｯﾄ位置動作I/L
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANL
SET MR3203               ; Unit2_速度変更FLG
CON
DW #1 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR1006              ; ｾｯﾄ位置移動完了
STG @MR1006              ; ｾｯﾄ位置移動完了
MPS
AND @MR4501              ; 搬送動作SUB完了
RES MR3203               ; Unit2_速度変更FLG
CON
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR1007
MPP
LDB B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR1013              ; 原点復帰完了
STG @MR1007
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1008              ; ﾜｰｸ押え開
MPP
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
RES MR7314               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7315               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1008              ; ﾜｰｸ押え開
STG @MR1008              ; ﾜｰｸ押え開
MPS
LDB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND R4001                ; 【S】洗浄ｼｬﾄﾙ1 ﾜｰｸ押え ｱﾝﾁｬｯｸ
ANL
JMP @MR1013              ; 原点復帰完了
MPP
LD LR100                 ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND R4312                ; 【B】洗浄2ｱﾗｲﾒﾝﾄX開端
AND R4314                ; 【B】洗浄2ｱﾗｲﾒﾝﾄY開端
ANL
JMP @MR1013              ; 原点復帰完了
STG @MR1013              ; 原点復帰完了
RES LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
CON
JMP @MR1014              ; ENDへ
;<h1/>AUTO/自動動作
;ﾜｰｸ状態確認
;ﾜｰｸ状態確認
LD B2036                 ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ1・3有
AND B2037                ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ2・4有
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
ANB R50111               ; 連動動作2
OUT @MR700               ; 搬送上ﾜｰｸ有
LDB B2036                ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ1・3有
AND B2037                ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ2・4有
LD B2036                 ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ1・3有
ANB B2037                ; ﾒｲﾝよりﾗｲﾝ2ﾜｰｸ2・4有
ORL
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
ANB R50111               ; 連動動作2
OUT @MR701               ; 搬送上ﾜｰｸ有(途中)
;自動運転
;Auto Operation Start
LD R50500                ; 洗浄機スタート
ANB MR3002               ; 停止要求ON
ANB MR3200               ; Unit2_BUSY
DIFU @MR3000             ; 自動運転開始PLS
LD @MR3000               ; 自動運転開始PLS
MPS
ANB LR011                ; 洗浄2動作不可
SET MR3200               ; Unit2_BUSY
CON
AND MR3200               ; Unit2_BUSY
SET @MR3001              ; 自動運転開始
MPP
AND LR011                ; 洗浄2動作不可
SET @MR1014              ; ENDへ
;自動開始時ｼｬﾄﾙ位置確認
;Point check at operation start
STG @MR3001              ; 自動運転開始
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MRD
AND MR3002               ; 停止要求ON
RES MR3208               ; Unit2_供給IF開始
CON
ANB MR3208               ; Unit2_供給IF開始
JMP @MR1014              ; ENDへ
MPP
LDB MR3002               ; 停止要求ON
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
LDB LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
ORB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
ANL
LDB LR000                ; Offline
ORP R40004               ; OFFLINE-2_START
ANL
ANL
MPS
ANB LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
MPS
AND= EM1302 #1           ; RB2現在POS
JMP @MR3004              ; ｾｯﾄ位置確認OK
MPP
AND<> EM1302 #1          ; RB2現在POS
JMP @MR3002              ; ｾｯﾄ位置にいない
MPP
LD LR301                 ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
AND @MR700               ; 搬送上ﾜｰｸ有
ANL
JMP @MR3100              ; 搬送動作開始
STG @MR3002              ; ｾｯﾄ位置にいない
AND @MR500               ; IAI_ｾｯﾄ位置動作I/L
DW #1 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3003              ; ｾｯﾄ位置へ移動
STG @MR3003              ; ｾｯﾄ位置へ移動
MPS
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3004              ; ｾｯﾄ位置確認OK
MPP
LDB B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR3903              ; 自動運転停止
;ｱﾗｲﾒﾝﾄ開閉状態確認
;Operation start permission to MainPLC
STG @MR3004              ; ｾｯﾄ位置確認OK
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
MPS
AND R4201                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ｱﾝﾁｬｯｸ
MPS
ANB R50111               ; 連動動作2
JMP @MR3005              ; ﾜｰｸｾｯﾄ要求
MPP
AND R50111               ; 連動動作2
JMP @MR3100              ; 搬送動作開始
MPP
AND R4200                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ﾁｬｯｸ
JMP @MR3010              ; ﾜｰｸ押え開き開始
MPP
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
MPS
LD R4312                 ; 【B】洗浄2ｱﾗｲﾒﾝﾄX開端
AND R4314                ; 【B】洗浄2ｱﾗｲﾒﾝﾄY開端
ANL
JMP @MR3005              ; ﾜｰｸｾｯﾄ要求
MPP
LDB R4312                ; 【B】洗浄2ｱﾗｲﾒﾝﾄX開端
ORB R4314                ; 【B】洗浄2ｱﾗｲﾒﾝﾄY開端
ANL
JMP @MR3012              ; ｱﾗｲﾒﾝﾄ開き開始
STG @MR3010              ; ﾜｰｸ押え開き開始
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR3011              ; ﾜｰｸ押え開き
STG @MR3011              ; ﾜｰｸ押え開き
AND R4201                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ｱﾝﾁｬｯｸ
MPS
ANB R50111               ; 連動動作2
JMP @MR3005              ; ﾜｰｸｾｯﾄ要求
MPP
AND R50111               ; 連動動作2
JMP @MR3910              ; 1ｻｲｸﾙ動作終了へ
STG @MR3012              ; ｱﾗｲﾒﾝﾄ開き開始
SET R36100               ; 【B】ｱﾗｲﾒﾝﾄ2開開始
CON
RES R36108               ; 【B】ｱﾗｲﾒﾝﾄ2閉開始
CON
JMP @MR3013              ; ｱﾗｲﾒﾝﾄ開
STG @MR3013              ; ｱﾗｲﾒﾝﾄ開
AND R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
AND B2072                ; ﾒｲﾝへLD反転仮置き台 上昇限
RES R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
CON
MPS
ANB R50111               ; 連動動作2
JMP @MR3005              ; ﾜｰｸｾｯﾄ要求
MPP
AND R50111               ; 連動動作2
JMP @MR3910              ; 1ｻｲｸﾙ動作終了へ
;<h1/>MAIN-PLCへﾜｰｸｾｯﾄ要求 
;Work set request to MAIN-PLC
;MainPLCへ動作開始許可
;Operation start permission to MainPLC
STG @MR3005              ; ﾜｰｸｾｯﾄ要求
RES MR3209               ; Unit2_供給IF完了
CON
ANB MR3209               ; Unit2_供給IF完了
JMP @MR3008
STG @MR3008
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MRD
AND MR3002               ; 停止要求ON
RES MR3208               ; Unit2_供給IF開始
CON
ANB MR3208               ; Unit2_供給IF開始
JMP @MR1014              ; ENDへ
MPP
LDB MR3002               ; 停止要求ON
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
ANB B201A                ; ﾒｲﾝより給材2ﾗｲﾝ側動作失敗
ANL
SET MR3208               ; Unit2_供給IF開始
CON
AND MR3208               ; Unit2_供給IF開始
JMP @MR3006              ; I/F中
;MainPLC ﾜｰｸ置動作完了
;MainPLC Operation is completed to put the work
STG @MR3006              ; I/F中
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MRD
AND MR3002               ; 停止要求ON
RES MR3208               ; Unit2_供給IF開始
CON
ANB MR3208               ; Unit2_供給IF開始
JMP @MR1014              ; ENDへ
MPP
LDB MR3002               ; 停止要求ON
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
AND MR3209               ; Unit2_供給IF完了
ANL
JMP @MR3007              ; I/F完了
STG @MR3007              ; I/F完了
SET LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
CON
RES MR3209               ; Unit2_供給IF完了
CON
AND LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
ANB MR3209               ; Unit2_供給IF完了
JMP @MR3100              ; 搬送動作開始
;搬送ﾁｬｯｸ閉
;Transport chuck close
;真空ON
;Vacuum on
STG @MR3100              ; 搬送動作開始
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
LD B2003                 ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
OR LR000                 ; Offline
ANL
ANL
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
SET MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
RES MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR3101              ; ﾜｰｸ押え閉じ
MPP
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
MPS
ANB CR2002               ; 常時ON
SET R36108               ; 【B】ｱﾗｲﾒﾝﾄ2閉開始
CON
JMP @MR3105              ; ｱﾗｲﾒﾝﾄ閉じ
MPP
JMP @MR3200              ; 搬送開始
STG @MR3101              ; ﾜｰｸ押え閉じ
MPS
LDB MR4008               ; 洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸErr
AND R4200                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ﾁｬｯｸ
ANL
MPS
ANB LR002                ; 搬送ﾓｰﾄﾞ
JMP @MR3200              ; 搬送開始
MPP
AND LR002                ; 搬送ﾓｰﾄﾞ
JMP @MR3600              ; ｴｱﾌﾞﾛｰ動作完了
MPP
LD MR4008                ; 洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸErr
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
STG @MR3105              ; ｱﾗｲﾒﾝﾄ閉じ
AND R36109               ; 【B】ｱﾗｲﾒﾝﾄ2閉完了
JMP @MR3106              ; ｱﾗｲﾒﾝﾄ閉完了
STG @MR3106              ; ｱﾗｲﾒﾝﾄ閉完了
SET MR3204               ; Unit2_真空ｾﾝｻｰ確認ﾀｲﾐﾝｸﾞ
CON
RES R36109               ; 【B】ｱﾗｲﾒﾝﾄ2閉完了
CON
SET MR3212               ; Unit2_真空ON_SUB開始
CON
JMP @MR3107              ; ｱﾗｲﾒﾝﾄ真空開始
STG @MR3107              ; ｱﾗｲﾒﾝﾄ真空開始
AND MR3213               ; Unit2_真空ON_SUB完了
JMP @MR3108              ; ｱﾗｲﾒﾝﾄ真空ON完了
STG @MR3108              ; ｱﾗｲﾒﾝﾄ真空ON完了
RES MR3204               ; Unit2_真空ｾﾝｻｰ確認ﾀｲﾐﾝｸﾞ
CON
RES MR3213               ; Unit2_真空ON_SUB完了
CON
JMP @MR3109              ; ｱﾗｲﾒﾝﾄ真空ON完了確認
STG @MR3109              ; ｱﾗｲﾒﾝﾄ真空ON完了確認
MPS
LDB R55005               ; 吸着ｴﾗｰ(復帰可能ｴﾗｰ)
AND R4204                ; ﾉｽﾞﾙﾕﾆｯﾄ2 真空吸着ﾊﾞﾙﾌﾞ 吸着
ANL
MPS
ANB LR002                ; 搬送ﾓｰﾄﾞ
JMP @MR3200              ; 搬送開始
MPP
AND LR002                ; 搬送ﾓｰﾄﾞ
JMP @MR3600              ; ｴｱﾌﾞﾛｰ動作完了
MPP
AND R55005               ; 吸着ｴﾗｰ(復帰可能ｴﾗｰ)
SET MR3214               ; Unit2_真空OFF_SUB開始
CON
JMP @MR3110              ; 吸着ｴﾗｰ発生
STG @MR3110              ; 吸着ｴﾗｰ発生
AND MR3215               ; Unit2_真空OFF_SUB完了
JMP @MR3111              ; 吸着ｴﾗｰ_真空OFF
STG @MR3111              ; 吸着ｴﾗｰ_真空OFF
SET R36100               ; 【B】ｱﾗｲﾒﾝﾄ2開開始
CON
RES MR3215               ; Unit2_真空OFF_SUB完了
CON
JMP @MR3112              ; 吸着ｴﾗｰ_ｱﾗｲﾒﾝﾄ開
STG @MR3112              ; 吸着ｴﾗｰ_ｱﾗｲﾒﾝﾄ開
AND R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
JMP @MR3113              ; 吸着ｴﾗｰ_ｱﾗｲﾒﾝﾄ開完了
STG @MR3113              ; 吸着ｴﾗｰ_ｱﾗｲﾒﾝﾄ開完了
RES R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
CON
JMP @MR3114              ; 吸着ｴﾗｰ_ON
STG @MR3114              ; 吸着ｴﾗｰ_ON
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
ANB R55005               ; 吸着ｴﾗｰ(復帰可能ｴﾗｰ)
ANL
MPS
AND MR3207               ; Unit2_
RES MR3207               ; Unit2_
CON
RES LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
CON
JMP @MR3001              ; 自動運転開始
MPP
AND MR3206               ; Unit2_
RES MR3206               ; Unit2_
CON
JMP @MR3100              ; 搬送動作開始
;PCONﾉｽﾞﾙ位置移動
;PCON move to nozzle position
STG @MR3200              ; 搬送開始
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
AND @MR501               ; IAI_ﾉｽﾞﾙ位置動作I/L
ANL
DW #2 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3201              ; ﾉｽﾞﾙ位置へ移動
STG @MR3201              ; ﾉｽﾞﾙ位置へ移動
MPS
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3202              ; ﾉｽﾞﾙ位置移動完了
MPP
LDB B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR3903              ; 自動運転停止
;<h1/>ﾉｽﾞﾙﾕﾆｯﾄ動作開始
;Nozzle unit operation start
;ﾉｽﾞﾙﾕﾆｯﾄ上治具昇降下降/液受け上昇
;Nozzle unit jig lift down/liquid receive up
STG @MR3202              ; ﾉｽﾞﾙ位置移動完了
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
AND @MR600               ; ﾉｽﾞﾙﾕﾆｯﾄ下降I/L
ANL
RES MR7401               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇
CON
SET MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
CON
JMP @MR3203              ; ﾉｽﾞﾙﾕﾆｯﾄ下降
STG @MR3203              ; ﾉｽﾞﾙﾕﾆｯﾄ下降
MPS
LDB MR4010               ; ﾉｽﾞﾙﾕﾆｯﾄ2液受けｼﾘﾝﾀﾞ上昇Err
ANB MR4013               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降Err
AND R4202                ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 上昇端
AND R4303                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 下降限
ANL
JMP @MR3204              ; 下降完了
MPP
LD MR4010                ; ﾉｽﾞﾙﾕﾆｯﾄ2液受けｼﾘﾝﾀﾞ上昇Err
OR MR4013                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降Err
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
;ﾉｽﾞﾙﾕﾆｯﾄ上治具加圧下降
;Nozzle unit press cylinder down
STG @MR3204              ; 下降完了
RES MR7405               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇
CON
SET MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
CON
JMP @MR3205              ; 加圧下降
STG @MR3205              ; 加圧下降
MPS
LDB MR4015               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降Err
AND R4305                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 下降限
ANL
JMP @MR3300              ; 洗浄動作開始
MPP
LD MR4015                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降Err
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
;洗浄開始
;Wash start
STG @MR3300              ; 洗浄動作開始
SET @MR4600              ; 洗浄動作SUB開始
CON
JMP @MR3301              ; 洗浄中
STG @MR3301              ; 洗浄中
AND @MR4601              ; 洗浄動作SUB完了
RES @MR4601              ; 洗浄動作SUB完了
CON
JMP @MR3400              ; 洗浄完了
;ﾉｽﾞﾙﾕﾆｯﾄ上治具加圧上昇
;Nozzle unit press cylinder down
STG @MR3400              ; 洗浄完了
ANB @MR4601              ; 洗浄動作SUB完了
SET MR7405               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇
CON
RES MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
CON
JMP @MR3401              ; ﾉｽﾞﾙﾕﾆｯﾄ加圧上昇
STG @MR3401              ; ﾉｽﾞﾙﾕﾆｯﾄ加圧上昇
MPS
LDB MR4014               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇Err
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
ANL
JMP @MR3402              ; 上昇完了
MPP
LD MR4014                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇Err
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
;ﾉｽﾞﾙﾕﾆｯﾄ上治具昇降上昇/液受け下降
;Nozzle unit jig lift up/liquid receive down
STG @MR3402              ; 上昇完了
SET MR7401               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇
CON
RES MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
CON
JMP @MR3403              ; ﾉｽﾞﾙﾕﾆｯﾄ上昇
STG @MR3403              ; ﾉｽﾞﾙﾕﾆｯﾄ上昇
MPS
LDB MR4011               ; ﾉｽﾞﾙﾕﾆｯﾄ2液受けｼﾘﾝﾀﾞ下降Err
ANB MR4012               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇Err
AND R4203                ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
ANL
JMP @MR3500              ; 上昇完了
MPP
LD MR4011                ; ﾉｽﾞﾙﾕﾆｯﾄ2液受けｼﾘﾝﾀﾞ下降Err
OR MR4012                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇Err
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
;<h1/>ｴｱﾌﾞﾛｰｼｰｹﾝｽ
;Air blow sequence
;PCONｴｱﾌﾞﾛｰ開始位置へ移動
;PCON move to air blow starting position
STG @MR3500              ; 上昇完了
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
LDB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
ANL
DW #3 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3501              ; ｴｱﾌﾞﾛｰ開始位置へ移動
MPP
LD LR100                 ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND @MR505               ; IAI_ｴｱﾌﾞﾛｰ中間位置動作I/L
ANL
DW #6 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3501              ; ｴｱﾌﾞﾛｰ開始位置へ移動
STG @MR3501              ; ｴｱﾌﾞﾛｰ開始位置へ移動
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3502              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
;ｴｱﾌﾞﾛｰON
;Air blow ON
STG @MR3502              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
DW #0 @DM0               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1
CON
SET MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
SET MR3203               ; Unit2_速度変更FLG
CON
AND R7211                ; ｴｱｰﾌﾞﾛｰ2
TMR @0 #1                ; ｴｱﾌﾞﾛｰ安定時間
CON
AND @T0                  ; ｴｱﾌﾞﾛｰ安定時間
JMP @MR3503              ; ｴｱﾌﾞﾛｰ開始確認
;PCONｴｱﾌﾞﾛｰ停止位置へ移動
;PCON move to air blow stop position
STG @MR3503              ; ｴｱﾌﾞﾛｰ開始確認
AND @MR503               ; IAI_ｴｱﾌﾞﾛｰ停止位置動作I/L
@INC @DM0                ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1
CON
DW #4 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3504              ; ｴｱﾌﾞﾛｰ停止位置へ移動
STG @MR3504              ; ｴｱﾌﾞﾛｰ停止位置へ移動
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3505              ; ｴｱﾌﾞﾛｰ停止位置へ移動完了
;PCONｴｱﾌﾞﾛｰ開始位置へ移動
;PCON move to air blow starting position
STG @MR3505              ; ｴｱﾌﾞﾛｰ停止位置へ移動完了
AND @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
DW #3 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3506              ; ｴｱﾌﾞﾛｰ開始位置へ移動
STG @MR3506              ; ｴｱﾌﾞﾛｰ開始位置へ移動
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3507              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
;往復回数確認
;Checking the number of round trips
STG @MR3507              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
MPS
LD<> @DM0 DM5008         ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1  ﾌﾞﾛｰ(往復)_回数設定値
AND<> DM5008 #0          ; ﾌﾞﾛｰ(往復)_回数設定値
ANL
JMP @MR3503              ; ｴｱﾌﾞﾛｰ開始確認
MPP
LD= @DM0 DM5008          ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1  ﾌﾞﾛｰ(往復)_回数設定値
OR= DM5008 #0            ; ﾌﾞﾛｰ(往復)_回数設定値
ANL
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
DW #0 @DM0               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1
CON
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
RES MR3203               ; Unit2_速度変更FLG
CON
JMP @MR3600              ; ｴｱﾌﾞﾛｰ動作完了
MPP
LD LR100                 ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND @MR505               ; IAI_ｴｱﾌﾞﾛｰ中間位置動作I/L
ANL
DW #6 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3508              ; ｴｱﾌﾞﾛｰ中間位置へ移動
STG @MR3508              ; ｴｱﾌﾞﾛｰ中間位置へ移動
AND @MR4501              ; 搬送動作SUB完了
DW #0 @DM0               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ1
CON
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
RES MR3203               ; Unit2_速度変更FLG
CON
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3600              ; ｴｱﾌﾞﾛｰ動作完了
;PCON取出位置へ移動
;PCON move to ULD position
STG @MR3600              ; ｴｱﾌﾞﾛｰ動作完了
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
AND @MR504               ; IAI_取出位置動作I/L
ANL
DW #5 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3601              ; 取出位置へ移動
STG @MR3601              ; 取出位置へ移動
MPS
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3602              ; 取出位置へ移動完了
MPP
LDB B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR3903              ; 自動運転停止
;搬送ﾁｬｯｸ開
;Transport chuck open
;真空OFF
;Vacuum off
STG @MR3602              ; 取出位置へ移動完了
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR3603              ; ﾜｰｸ押え開き
MPP
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
MPS
ANB CR2002               ; 常時ON
SET R36100               ; 【B】ｱﾗｲﾒﾝﾄ2開開始
CON
JMP @MR3604              ; ｱﾗｲﾒﾝﾄ開
MPP
JMP @MR3700              ; ﾜｰｸ押え開き完了
STG @MR3604              ; ｱﾗｲﾒﾝﾄ開
AND R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
JMP @MR3603              ; ﾜｰｸ押え開き
STG @MR3603              ; ﾜｰｸ押え開き
MPS
LDB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
ANB MR4009               ; 洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸErr
AND R4201                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ｱﾝﾁｬｯｸ
ANL
MPS
ANB R50111               ; 連動動作2
JMP @MR3700              ; ﾜｰｸ押え開き完了
MPP
AND R50111               ; 連動動作2
JMP @MR3910              ; 1ｻｲｸﾙ動作終了へ
MRD
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
RES R36101               ; 【B】ｱﾗｲﾒﾝﾄ2開完了
CON
JMP @MR3700              ; ﾜｰｸ押え開き完了
MPP
LD MR4009                ; 洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸErr
AND MR3005               ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
;<h1/>MAIN-PLCへﾜｰｸ取出要求 
;Work entry request to the MAIN-PLC
;MainPLCへ動作開始許可 
;Operation start permission to MainPLC
STG @MR3700              ; ﾜｰｸ押え開き完了
RES MR3211               ; Unit2_除材IF完了
CON
ANB MR3211               ; Unit2_除材IF完了
JMP @MR3704
STG @MR3704
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
LD B2045                 ; ﾒｲﾝへ除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB B202A                ; ﾒｲﾝより機種切替要求
OR LR000                 ; Offline
ANL
ANL
SET MR3210               ; Unit2_除材IF開始
CON
AND MR3210               ; Unit2_除材IF開始
JMP @MR3701              ; 除材I/F開始
;MainPLC ﾜｰｸ取動作完了確認
;MainPLC Completion confirmation take the work
STG @MR3701              ; 除材I/F開始
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MRD
AND MR3002               ; 停止要求ON
RES MR3210               ; Unit2_除材IF開始
CON
ANB MR3210               ; Unit2_除材IF開始
JMP @MR1014              ; ENDへ
MPP
LDB MR3002               ; 停止要求ON
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
AND MR3211               ; Unit2_除材IF完了
ANL
JMP @MR3702              ; 除材I/F完了
STG @MR3702              ; 除材I/F完了
RES LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
CON
RES MR3211               ; Unit2_除材IF完了
CON
ANB LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
ANB MR3211               ; Unit2_除材IF完了
JMP @MR3703
STG @MR3703
SET MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
DW #0 @DM1               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2
CON
MPS
AND<> DM5026 #0          ; ﾌﾞﾛｰ(往復)_回数設定値(取出後)
JMP @MR3800              ; ｶﾗでｴｱﾌﾞﾛｰ動作
MPP
AND= DM5026 #0           ; ﾌﾞﾛｰ(往復)_回数設定値(取出後)
JMP @MR3807              ; ｴｱﾌﾞﾛｰ停止位置へ移動完了
;<h1/>ｶﾗの状態でｴｱﾌﾞﾛｰｼｰｹﾝｽ
;Air blow sequence without the work
;PCONｴｱﾌﾞﾛｰ開始位置へ移動
;PCON move to air blow starting position
STG @MR3800              ; ｶﾗでｴｱﾌﾞﾛｰ動作
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
LDB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
ANL
DW #3 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3801              ; ｴｱﾌﾞﾛｰ開始位置へ移動
MPP
LD LR100                 ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
AND @MR505               ; IAI_ｴｱﾌﾞﾛｰ中間位置動作I/L
ANL
DW #6 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3801              ; ｴｱﾌﾞﾛｰ開始位置へ移動
STG @MR3801              ; ｴｱﾌﾞﾛｰ開始位置へ移動
MPS
AND @MR4501              ; 搬送動作SUB完了
DW #0 @DM1               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2
CON
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3802              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
MPP
LDB B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR3903              ; 自動運転停止
;ｴｱﾌﾞﾛｰON
;Air blow ON
STG @MR3802              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
@INC @DM1                ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2
CON
SET MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
SET MR3203               ; Unit2_速度変更FLG
CON
AND R7211                ; ｴｱｰﾌﾞﾛｰ2
MPS
ANB LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
JMP @MR3805              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
MPP
AND LR100                ; OFF:成型部品ON:ﾌﾞｯｼｭｼｰﾙ
JMP @MR3803              ; ｴｱﾌﾞﾛｰON
;PCONｴｱﾌﾞﾛｰ開始位置へ移動
;PCON move to air blow starting position
STG @MR3803              ; ｴｱﾌﾞﾛｰON
AND @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
SET MR3203               ; Unit2_速度変更FLG
CON
DW #3 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3804              ; ｴｱﾌﾞﾛｰ開始位置へ移動
STG @MR3804              ; ｴｱﾌﾞﾛｰ開始位置へ移動
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3805              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
;PCONｴｱﾌﾞﾛｰ停止位置へ移動
;PCON move to air blow stop position
STG @MR3805              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
AND @MR503               ; IAI_ｴｱﾌﾞﾛｰ停止位置動作I/L
DW #4 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3806              ; ｴｱﾌﾞﾛｰ停止位置へ移動
STG @MR3806              ; ｴｱﾌﾞﾛｰ停止位置へ移動
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3807              ; ｴｱﾌﾞﾛｰ停止位置へ移動完了
;往復回数確認
;Checking the number of round trips
STG @MR3807              ; ｴｱﾌﾞﾛｰ停止位置へ移動完了
MPS
AND<> @DM1 DM5026        ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2  ﾌﾞﾛｰ(往復)_回数設定値(取出後)
JMP @MR3802              ; ｴｱﾌﾞﾛｰ開始位置へ移動完了
MPP
AND= @DM1 DM5026         ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2  ﾌﾞﾛｰ(往復)_回数設定値(取出後)
DW #0 @DM1               ; ｴｱﾌﾞﾛｰｶｳﾝﾄ2
CON
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
RES MR3203               ; Unit2_速度変更FLG
CON
ANB MR3203               ; Unit2_速度変更FLG
JMP @MR3900              ; 搬送戻り開始
;PCON給材位置へ移動
;PCON move Set position
STG @MR3900              ; 搬送戻り開始
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LDB MR3005               ; 装置ﾎﾟｰｽﾞ
AND @MR500               ; IAI_ｾｯﾄ位置動作I/L
ANB LR301                ; 洗浄ﾕﾆｯﾄ2ﾜｰｸ有
ANL
SET MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
DW #1 EM1202             ; RB2Target Pos
CON
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR3901              ; ｾｯﾄ位置へ移動
STG @MR3901              ; ｾｯﾄ位置へ移動
MPS
AND @MR4501              ; 搬送動作SUB完了
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
RES MR3203               ; Unit2_速度変更FLG
CON
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR3902              ; ｾｯﾄ位置へ移動完了
MPP
LDB B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ORB B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
ANB LR000                ; Offline
ANL
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
RES MR3203               ; Unit2_速度変更FLG
CON
SET MR3205               ; Unit2_IAI移動中RB-IL_OFF
CON
JMP @MR3903              ; 自動運転停止
STG @MR3902              ; ｾｯﾄ位置へ移動完了
MPS
ANB R50111               ; 連動動作2
JMP @MR3904
MPP
AND R50111               ; 連動動作2
JMP @MR3910              ; 1ｻｲｸﾙ動作終了へ
;<h1/>停止要求出ているか？
;Check out stop request
;ｻｲｸﾙ終了
;End of cycle
STG @MR3904
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
ANB MR3002               ; 停止要求ON
JMP @MR3001              ; 自動運転開始
MPP
AND MR3002               ; 停止要求ON
JMP @MR3903              ; 自動運転停止
STG @MR3903              ; 自動運転停止
JMP @MR1014              ; ENDへ
STG @MR3910              ; 1ｻｲｸﾙ動作終了へ
SET @MR3911              ; 1ｻｲｸﾙ動作完了
CON
ENDS
;<h1/>MAINTE/ﾒﾝﾃﾅﾝｽ動作
;[本STG内容]
;・各TaskのMAINTE動作を記述して下さい
;・動作ﾓｰﾄﾞは「MAINTEAC*」に格納されています(1〜32)
;・動作終了時は@MR2915にJMPして下さい
LDP R50102               ; 手動ﾓｰﾄﾞ
AND R50002               ; 手動中
DIFU @MR1300             ; 手動ﾓｰﾄﾞPLS
LD @MR1300               ; 手動ﾓｰﾄﾞPLS
SET MR3200               ; Unit2_BUSY
CON
SET @MR1301              ; 手動ﾓｰﾄﾞ開始
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
AND B2003                ; ﾒｲﾝより給材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
AND B2005                ; ﾒｲﾝより除材2ﾗｲﾝ側ｲﾝﾀｰﾛｯｸ信号
MPS
AND= EM1302 #1           ; RB2現在POS
OUT @MR1310              ; 連動動作可能
MPP
AND= EM1302 #5           ; RB2現在POS
OUT @MR1311              ; 連動戻り動作可能
STG @MR1301              ; 手動ﾓｰﾄﾞ開始
MPS
AND MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
ANB MR3202               ; Unit2_HALT
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
LD @MR1303               ; M_ﾒﾝﾃﾓｰﾄﾞ停止条件
LD R40002                ; SET UP戻SW
OR R40008                ; ﾒﾝﾃﾓｰﾄﾞOFF
ANL
ANL
JMP @MR1014              ; ENDへ
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
MPS
ANP R48100               ; ﾁｬｯｸ2閉SW
JMP @MR1400              ; M_ﾁｬｯｸ閉
MRD
ANP R48101               ; ﾁｬｯｸ2開SW
JMP @MR1402              ; M_ﾁｬｯｸ開
MRD
ANP R48610               ; ｱﾗｲﾒﾝﾄ2X(閉)SW
JMP @MR1404              ; M_ｱﾗｲﾒﾝﾄX閉
MRD
ANP R48611               ; ｱﾗｲﾒﾝﾄ2X(開)SW
JMP @MR1406              ; M_ｱﾗｲﾒﾝﾄX開
MRD
ANP R48612               ; ｱﾗｲﾒﾝﾄ2Y(閉)SW
JMP @MR1408              ; M_ｱﾗｲﾒﾝﾄY閉
MRD
ANP R48613               ; ｱﾗｲﾒﾝﾄ2Y(開)SW
JMP @MR1410              ; M_ｱﾗｲﾒﾝﾄY開
MRD
ANP R48614               ; ｱﾗｲﾒﾝﾄ2真空ON_SW
JMP @MR1412              ; M_ｱﾗｲﾒﾝﾄ真空ON
MRD
ANP R48615               ; ｱﾗｲﾒﾝﾄ2真空OFF_SW
JMP @MR1414              ; M_ｱﾗｲﾒﾝﾄ真空OFF
MRD
ANP R48102               ; ﾉｽﾞﾙ治具2上昇SW
JMP @MR1500              ; M_ﾉｽﾞﾙ治具上昇
MRD
LDP R48103               ; ﾉｽﾞﾙ治具2下降SW
AND @MR600               ; ﾉｽﾞﾙﾕﾆｯﾄ下降I/L
ANL
JMP @MR1502              ; M_ﾉｽﾞﾙ治具下降
MRD
ANP R48104               ; ﾉｽﾞﾙ加圧2上昇SW
JMP @MR1504              ; M_ﾉｽﾞﾙ加圧上昇
MRD
LDP R48105               ; ﾉｽﾞﾙ加圧2下降SW
AND @MR600               ; ﾉｽﾞﾙﾕﾆｯﾄ下降I/L
ANL
JMP @MR1506              ; M_ﾉｽﾞﾙ加圧下降
MRD
ANP R48106               ; ｴｱﾌﾞﾛｰ2ON_SW
JMP @MR1508              ; M_ｴｱﾌﾞﾛｰON
MRD
ANP R48107               ; ｴｱﾌﾞﾛｰ2OFF_SW
JMP @MR1510              ; M_ｴｱﾌﾞﾛｰOFF
MRD
LDP R48300               ; PCON2_ｾｯﾄ位置SW
AND @MR500               ; IAI_ｾｯﾄ位置動作I/L
ANL
DW #1 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48301               ; PCON2_洗浄位置SW
AND @MR501               ; IAI_ﾉｽﾞﾙ位置動作I/L
ANL
DW #2 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48302               ; PCON2_ｴｱﾌﾞﾛｰ開始位置SW
AND @MR502               ; IAI_ｴｱﾌﾞﾛｰ開始位置動作I/L
ANL
DW #3 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48303               ; PCON2_ｴｱﾌﾞﾛｰ終了位置SW
AND @MR503               ; IAI_ｴｱﾌﾞﾛｰ停止位置動作I/L
ANL
DW #4 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48304               ; PCON2_取出位置SW
AND @MR504               ; IAI_取出位置動作I/L
ANL
DW #5 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48306               ; PCON2_ｴｱﾌﾞﾛｰ中間位置SW
AND @MR505               ; IAI_ｴｱﾌﾞﾛｰ中間位置動作I/L
ANL
DW #6 EM1202             ; RB2Target Pos
CON
JMP @MR1600              ; M_搬送動作
MRD
LDP R48305               ; PCON2_原点復帰SW
AND @MR510               ; IAI_ｴｱﾌﾞﾛｰ原点復帰動作I/L
ANL
JMP @MR1608              ; M_搬送HOME
MRD
LD R4202                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 上昇端
AND R4303                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 下降限
AND R4305                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 下降限
AND= EM1302 #2           ; RB2現在POS
ANP R48108               ; ﾉｽﾞﾙ2洗浄動作SW
ANL
JMP @MR1700              ; M_洗浄動作
MRD
LD @MR1310               ; 連動動作可能
ANP R48910               ; 洗浄ﾕﾆｯﾄ2連動SW
ANL
SET R48914               ; 洗浄ﾕﾆｯﾄ2連動PL
CON
JMP @MR1800              ; 洗浄ﾕﾆｯﾄ1ｻｲｸﾙ動作
MRD
LD @MR1311               ; 連動戻り動作可能
ANP R48911               ; 洗浄ﾕﾆｯﾄ2戻りSW
ANL
SET R48915               ; 洗浄ﾕﾆｯﾄ2戻りPL
CON
JMP @MR1808              ; 洗浄ﾕﾆｯﾄ戻り動作
MPP
LD @MR1303               ; M_ﾒﾝﾃﾓｰﾄﾞ停止条件
LD R40002                ; SET UP戻SW
OR R40008                ; ﾒﾝﾃﾓｰﾄﾞOFF
OR R55001                ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
ANL
ANL
JMP @MR1014              ; ENDへ
LDB MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
ANB MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
ANB MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
ANB MR7211               ; ｴｱｰﾌﾞﾛｰ2
ANB R35100               ; RB2 MOVE
ANB R35108               ; RB2 HOME
ANB R34000               ; Teaching
AND DM10815.4            ; SV
ANB DM10815.8            ; RMDS
AND R50002               ; 手動中
OUT @MR1303              ; M_ﾒﾝﾃﾓｰﾄﾞ停止条件
STG @MR1400              ; M_ﾁｬｯｸ閉
SET MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
RES MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1401              ; M_ﾁｬｯｸ閉動作中
STG @MR1401              ; M_ﾁｬｯｸ閉動作中
MPS
AND R4200                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ﾁｬｯｸ
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48101               ; ﾁｬｯｸ2開SW
JMP @MR1402              ; M_ﾁｬｯｸ開
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1402              ; M_ﾁｬｯｸ開
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1403              ; M_ﾁｬｯｸ開動作中
STG @MR1403              ; M_ﾁｬｯｸ開動作中
MPS
AND R4201                ; 【S】洗浄ｼｬﾄﾙ2 ﾜｰｸ押え ｱﾝﾁｬｯｸ
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48100               ; ﾁｬｯｸ2閉SW
JMP @MR1400              ; M_ﾁｬｯｸ閉
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1404              ; M_ｱﾗｲﾒﾝﾄX閉
SET MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
RES MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1405              ; M_ｱﾗｲﾒﾝﾄX動作中
STG @MR1405              ; M_ｱﾗｲﾒﾝﾄX動作中
MPS
AND R4313                ; 【B】洗浄2ｱﾗｲﾒﾝﾄX閉端
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48611               ; ｱﾗｲﾒﾝﾄ2X(開)SW
JMP @MR1406              ; M_ｱﾗｲﾒﾝﾄX開
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1406              ; M_ｱﾗｲﾒﾝﾄX開
RES MR7312               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7313               ; 【S】洗浄ｼｬﾄﾙ2ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1407              ; M_ｱﾗｲﾒﾝﾄX開動作中
STG @MR1407              ; M_ｱﾗｲﾒﾝﾄX開動作中
MPS
AND R4312                ; 【B】洗浄2ｱﾗｲﾒﾝﾄX開端
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48610               ; ｱﾗｲﾒﾝﾄ2X(閉)SW
JMP @MR1404              ; M_ｱﾗｲﾒﾝﾄX閉
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1408              ; M_ｱﾗｲﾒﾝﾄY閉
SET MR7314               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えﾁｬｯｸ
CON
RES MR7315               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1409              ; M_ｱﾗｲﾒﾝﾄY動作閉
STG @MR1409              ; M_ｱﾗｲﾒﾝﾄY動作閉
MPS
AND R4315                ; 【B】洗浄2ｱﾗｲﾒﾝﾄY閉端
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48613               ; ｱﾗｲﾒﾝﾄ2Y(開)SW
JMP @MR1410              ; M_ｱﾗｲﾒﾝﾄY開
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1410              ; M_ｱﾗｲﾒﾝﾄY開
RES MR7314               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えﾁｬｯｸ
CON
SET MR7315               ; 【S】洗浄ｼｬﾄﾙ1ﾜｰｸ押えｱﾝﾁｬｯｸ
CON
JMP @MR1411              ; M_ｱﾗｲﾒﾝﾄY開動作中
STG @MR1411              ; M_ｱﾗｲﾒﾝﾄY開動作中
MPS
AND R4314                ; 【B】洗浄2ｱﾗｲﾒﾝﾄY開端
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48612               ; ｱﾗｲﾒﾝﾄ2Y(閉)SW
JMP @MR1408              ; M_ｱﾗｲﾒﾝﾄY閉
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1412              ; M_ｱﾗｲﾒﾝﾄ真空ON
SET MR3212               ; Unit2_真空ON_SUB開始
CON
JMP @MR1413              ; M_ｱﾗｲﾒﾝﾄ真空ON動作中
STG @MR1413              ; M_ｱﾗｲﾒﾝﾄ真空ON動作中
AND MR3213               ; Unit2_真空ON_SUB完了
RES MR3213               ; Unit2_真空ON_SUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1414              ; M_ｱﾗｲﾒﾝﾄ真空OFF
SET MR3214               ; Unit2_真空OFF_SUB開始
CON
JMP @MR1415              ; M_ｱﾗｲﾒﾝﾄ真空OFF動作中
STG @MR1415              ; M_ｱﾗｲﾒﾝﾄ真空OFF動作中
AND MR3215               ; Unit2_真空OFF_SUB完了
RES MR3215               ; Unit2_真空OFF_SUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1500              ; M_ﾉｽﾞﾙ治具上昇
SET MR7401               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇
CON
RES MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
CON
JMP @MR1501              ; M_ﾉｽﾞﾙ治具上昇動作中
STG @MR1501              ; M_ﾉｽﾞﾙ治具上昇動作中
MPS
LD R4203                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 下降端
AND R4302                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 上昇限
ANL
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48103               ; ﾉｽﾞﾙ治具2下降SW
JMP @MR1502              ; M_ﾉｽﾞﾙ治具下降
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1502              ; M_ﾉｽﾞﾙ治具下降
RES MR7401               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具上昇
CON
SET MR7400               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具下降
CON
JMP @MR1503              ; M_ﾉｽﾞﾙ治具下降動作中
STG @MR1503              ; M_ﾉｽﾞﾙ治具下降動作中
MPS
LD R4202                 ; ﾉｽﾞﾙﾕﾆｯﾄ2 液受けｼﾘﾝﾀﾞ 上昇端
AND R4303                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具昇降ｼﾘﾝﾀﾞ 下降限
ANL
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48102               ; ﾉｽﾞﾙ治具2上昇SW
JMP @MR1500              ; M_ﾉｽﾞﾙ治具上昇
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1504              ; M_ﾉｽﾞﾙ加圧上昇
SET MR7405               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇
CON
RES MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
CON
JMP @MR1505              ; M_ﾉｽﾞﾙ加圧上昇動作中
STG @MR1505              ; M_ﾉｽﾞﾙ加圧上昇動作中
MPS
AND R4304                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 上昇限
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48105               ; ﾉｽﾞﾙ加圧2下降SW
JMP @MR1506              ; M_ﾉｽﾞﾙ加圧下降
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1506              ; M_ﾉｽﾞﾙ加圧下降
RES MR7405               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧上昇
CON
SET MR7404               ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧下降
CON
JMP @MR1507              ; M_ﾉｽﾞﾙ加圧下降動作中
STG @MR1507              ; M_ﾉｽﾞﾙ加圧下降動作中
MPS
AND R4305                ; ﾉｽﾞﾙﾕﾆｯﾄ2上治具加圧ｼﾘﾝﾀﾞ 下降限
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
MRD
ANP R48104               ; ﾉｽﾞﾙ加圧2上昇SW
JMP @MR1504              ; M_ﾉｽﾞﾙ加圧上昇
MPP
AND R55001               ; ｼﾘﾝﾀﾞｰｴﾗｰ(復帰可能ｴﾗｰ)
JMP @MR1014              ; ENDへ
STG @MR1508              ; M_ｴｱﾌﾞﾛｰON
SET MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1510              ; M_ｴｱﾌﾞﾛｰOFF
RES MR7211               ; ｴｱｰﾌﾞﾛｰ2
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1600              ; M_搬送動作
SET @MR4500              ; 搬送動作SUB開始
CON
JMP @MR1601              ; M_搬送動作中
STG @MR1601              ; M_搬送動作中
AND @MR4501              ; 搬送動作SUB完了
RES @MR4501              ; 搬送動作SUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1608              ; M_搬送HOME
SET R35108               ; RB2 HOME
CON
JMP @MR1609              ; M_搬送HOME中
STG @MR1609              ; M_搬送HOME中
AND R35109               ; RB2 HOME END
RES R35109               ; RB2 HOME END
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1700              ; M_洗浄動作
SET MR1102               ; ﾎﾟﾝﾌﾟON
CON
SET MR7201               ; ﾊﾞｲﾊﾟｽﾊﾞﾙﾌﾞ 開
CON
SET @MR4600              ; 洗浄動作SUB開始
CON
JMP @MR1701              ; M_洗浄動作中
STG @MR1701              ; M_洗浄動作中
AND @MR4601              ; 洗浄動作SUB完了
RES MR1102               ; ﾎﾟﾝﾌﾟON
CON
RES MR7201               ; ﾊﾞｲﾊﾟｽﾊﾞﾙﾌﾞ 開
CON
RES @MR4601              ; 洗浄動作SUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1800              ; 洗浄ﾕﾆｯﾄ1ｻｲｸﾙ動作
MPS
ANP R48906               ; 連動START_SW
SET MR1102               ; ﾎﾟﾝﾌﾟON
CON
SET MR7201               ; ﾊﾞｲﾊﾟｽﾊﾞﾙﾌﾞ 開
CON
SET R48907               ; 連動START_SW_PL
CON
SET @MR4700              ; 洗浄ﾕﾆｯﾄ連動SUB開始
CON
JMP @MR1801              ; 洗浄ﾕﾆｯﾄ1ｻｲｸﾙ動作完了
MRD
AND R48910               ; 洗浄ﾕﾆｯﾄ2連動SW
TMR @30 #10
CON
AND @T30
RES R48914               ; 洗浄ﾕﾆｯﾄ2連動PL
CON
JMP @MR1802
MPP
AND R40200               ; 連動画面戻りSW
RES R48914               ; 洗浄ﾕﾆｯﾄ2連動PL
CON
JMP @MR1802
STG @MR1802
ANB R48910               ; 洗浄ﾕﾆｯﾄ2連動SW
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1801              ; 洗浄ﾕﾆｯﾄ1ｻｲｸﾙ動作完了
AND @MR4701              ; 洗浄ﾕﾆｯﾄ1連動SUB完了
RES MR1102               ; ﾎﾟﾝﾌﾟON
CON
RES MR7201               ; ﾊﾞｲﾊﾟｽﾊﾞﾙﾌﾞ 開
CON
RES R48907               ; 連動START_SW_PL
CON
RES R48914               ; 洗浄ﾕﾆｯﾄ2連動PL
CON
RES @MR4701              ; 洗浄ﾕﾆｯﾄ1連動SUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1808              ; 洗浄ﾕﾆｯﾄ戻り動作
MPS
ANP R48906               ; 連動START_SW
SET R48907               ; 連動START_SW_PL
CON
SET @MR4800              ; 洗浄ﾕﾆｯﾄ連動戻りSUB開始
CON
JMP @MR1809
MRD
AND R48911               ; 洗浄ﾕﾆｯﾄ2戻りSW
TMR @31 #10
CON
AND @T31
RES R48915               ; 洗浄ﾕﾆｯﾄ2戻りPL
CON
JMP @MR1810
MPP
AND R40200               ; 連動画面戻りSW
RES R48915               ; 洗浄ﾕﾆｯﾄ2戻りPL
CON
JMP @MR1810
STG @MR1810
ANB R48911               ; 洗浄ﾕﾆｯﾄ2戻りSW
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
STG @MR1809
AND @MR4801              ; 洗浄ﾕﾆｯﾄ連動戻りSUB完了
RES R48907               ; 連動START_SW_PL
CON
RES R48915               ; 洗浄ﾕﾆｯﾄ2戻りPL
CON
RES @MR4801              ; 洗浄ﾕﾆｯﾄ連動戻りSUB完了
CON
JMP @MR1301              ; 手動ﾓｰﾄﾞ開始
;<h1/>SubRoutines/ｻﾌﾞﾙｰﾁﾝ
;[記述方法]
;・ｻﾌﾞﾙｰﾁﾝは本ﾌﾞｯｸﾏｰｸから下に記述していって下さい
;・ｻﾌﾞﾙｰﾁﾝは必ずﾌﾞｯｸﾏｰｸを挿入して処理内容を明確にする事
;PCON1動作SUB
;
STG @MR4500              ; 搬送動作SUB開始
SET R35100               ; RB2 MOVE
CON
JMP @MR4505              ; PCON_MOVE開始
STG @MR4505              ; PCON_MOVE開始
AND R35101               ; RB2 MOVE END
RES R35101               ; RB2 MOVE END
CON
JMP @MR4506              ; PCON_MOVE完了
STG @MR4506              ; PCON_MOVE完了
MPS
AND R35103               ; RB2 Alarm
SET @MR2000              ; ﾎﾟｰｽﾞ許可
CON
JMP @MR4507              ; PCON_ALM発生
MPP
ANB R35103               ; RB2 Alarm
JMP @MR4508              ; 搬送動作完了
STG @MR4507              ; PCON_ALM発生
MPS
LD MR3005                ; 装置ﾎﾟｰｽﾞ
ANB MR3202               ; Unit2_HALT
ANL
SET @MR2000              ; ﾎﾟｰｽﾞ許可
MPP
ANB MR3005               ; 装置ﾎﾟｰｽﾞ
JMP @MR4508              ; 搬送動作完了
STG @MR4508              ; 搬送動作完了
SET @MR4501              ; 搬送動作SUB完了
CON
ENDS
;洗浄動作SUB
;
STG @MR4600              ; 洗浄動作SUB開始
SET R48109               ; ﾉｽﾞﾙ2洗浄動作SWﾗﾝﾌﾟ
CON
SET MR7204               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給水
CON
SET MR7205               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給水
CON
AND R7204                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給水
AND R7205                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給水
TMR @104 #1              ; 水安定時間
CON
AND @T104                ; 水安定時間
SET MR7208               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
CON
SET MR7209               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
CON
AND R7208                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
AND R7209                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
JMP @MR4605              ; 洗浄水+ｴｱｰ
STG @MR4605              ; 洗浄水+ｴｱｰ
TMR @100 DM5000          ; 水+ｴｱｰ設定時間  水+ｴｱｰ_ﾀｲﾏｰ設定値
CON
AND @T100                ; 水+ｴｱｰ設定時間
RES MR7204               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給水
CON
RES MR7205               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給水
CON
ANB R7204                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給水
ANB R7205                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給水
JMP @MR4606              ; 洗浄ｴｱｰ@
STG @MR4606              ; 洗浄ｴｱｰ@
TMR @101 DM5002          ; ｴｱｰ@設定時間  ｴｱｰ1_ﾀｲﾏｰ設定値
CON
AND @T101                ; ｴｱｰ@設定時間
RES MR7208               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
CON
RES MR7209               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
CON
ANB R7208                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
ANB R7209                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
JMP @MR4607              ; 洗浄Wait
STG @MR4607              ; 洗浄Wait
TMR @102 DM5004          ; Wait設定時間  Wait_ﾀｲﾏｰ設定値
CON
AND @T102                ; Wait設定時間
SET MR7208               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
CON
SET MR7209               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
CON
AND R7208                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
AND R7209                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
JMP @MR4608              ; 洗浄ｴｱｰA
STG @MR4608              ; 洗浄ｴｱｰA
TMR @103 DM5006          ; ｴｱｰA設定時間  ｴｱｰ2_ﾀｲﾏｰ設定値
CON
AND @T103                ; ｴｱｰA設定時間
RES MR7208               ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
CON
RES MR7209               ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
CON
ANB R7208                ; 液供給ﾏﾆﾎｰﾙﾄﾞ3 給気
ANB R7209                ; 液供給ﾏﾆﾎｰﾙﾄﾞ4 給気
JMP @MR4609              ; 洗浄動作完了
STG @MR4609              ; 洗浄動作完了
RES R48109               ; ﾉｽﾞﾙ2洗浄動作SWﾗﾝﾌﾟ
CON
SET @MR4601              ; 洗浄動作SUB完了
CON
ENDS
;連動動作SUB
;水圧安定させるため、ﾀｲﾏｰにて3sec待つ
STG @MR4700              ; 洗浄ﾕﾆｯﾄ連動SUB開始
TMR @35 #20
CON
AND @T35
SET @MR3001              ; 自動運転開始
CON
SET R50111               ; 連動動作2
CON
JMP @MR4702
STG @MR4702
AND @MR3911              ; 1ｻｲｸﾙ動作完了
RES @MR3911              ; 1ｻｲｸﾙ動作完了
CON
JMP @MR4703
STG @MR4703
ANB @MR3911              ; 1ｻｲｸﾙ動作完了
RES R50111               ; 連動動作2
CON
SET @MR4701              ; 洗浄ﾕﾆｯﾄ1連動SUB完了
CON
ENDS
STG @MR4800              ; 洗浄ﾕﾆｯﾄ連動戻りSUB開始
SET @MR3703
CON
SET R50111               ; 連動動作2
CON
JMP @MR4802
STG @MR4802
AND @MR3911              ; 1ｻｲｸﾙ動作完了
RES @MR3911              ; 1ｻｲｸﾙ動作完了
CON
JMP @MR4803
STG @MR4803
ANB @MR3911              ; 1ｻｲｸﾙ動作完了
RES R50111               ; 連動動作2
CON
SET @MR4801              ; 洗浄ﾕﾆｯﾄ連動戻りSUB完了
CON
ENDS
END
ENDH
