DEVICE:53
;MODULE:Lamp_Buzzer
;MODULE_TYPE:0
;<<フリッカー>>
;<<Flicker>>
LD CR2002                ; 常時ON
FLIK #50 #50 MR005       ; ﾗﾝﾌﾟ点滅用(0.5S)
FLIK #20 #20 MR006       ; ﾗﾝﾌﾟ点滅用(0.2S)
;表示　/　Indixation
;<h1/>ｽﾀｰﾄswﾗﾝﾌﾟ/Start sw lamp
;点灯：動作中、結果不良
;点滅：動作開始要求
;Lit: In operation, poor result
;Flashing: Operation start request
LD MR002                 ; ｽﾀｰﾄSW点滅要求
OR MR202                 ; ｴﾗｰ解除要求
AND MR005                ; ﾗﾝﾌﾟ点滅用(0.5S)
LD MR30007               ; ｻｲｸﾙ動作中
ANB MR202                ; ｴﾗｰ解除要求
ORL
OUT MR7008               ; AUTO  Startsw lamp
LD MR010                 ; 自動ｽﾀｰﾄSW　点滅
ANB MR306                ; 自動運転中
OR MR20111               ; MANU Select Lamp
OR MR30510               ; TP_ｽﾀｰﾄSW　点滅
AND MR005                ; ﾗﾝﾌﾟ点滅用(0.5S)
LD MR306                 ; 自動運転中
ANB MR21014              ; TP_STOP　SW　点滅
ORL
OR MR7000                ; 安全扉開閉Start sw lamp
OUT MR21010              ; TP_自動ｽﾀｰﾄ表示
LD MR306                 ; 自動運転中
ANP MR21001              ; STOP　SW
SET MR21014              ; TP_STOP　SW　点滅
LD MR306                 ; 自動運転中
AND MR21014              ; TP_STOP　SW　点滅
ANB MR902                ; ｴﾗｰ
ANB MR315                ; 扉開処理中
ANB MR21100              ; 自動_ﾜｰｸｾｯﾄOK
AND R1014                ; ｴﾘｱｾﾝｻ状態(OFF=遮光)
RES MR21014              ; TP_STOP　SW　点滅
;<h1/>ﾌﾞｻﾞｰ/Buzzer
;異常中、結果不良
;
;3秒ON：結果良品
;During abnormality, result defect
;
;3 seconds ON:results good product
LDP MR902                ; ｴﾗｰ
OR MR401                 ; ﾌﾞｻﾞｰON要求
ORP MR20008              ; 溶着OKﾌﾞｻﾞｰ
SET MR2003               ; ﾀﾜｰﾗﾝﾌﾟ ﾌﾞｻﾞｰ
LDB MR902                ; ｴﾗｰ
ANB MR401                ; ﾌﾞｻﾞｰON要求
MEP
ORP MR114                ; NGﾘｾｯﾄ処理ﾌﾞｻﾞｰ解除
ORF MR20008              ; 溶着OKﾌﾞｻﾞｰ
RES MR2003               ; ﾀﾜｰﾗﾝﾌﾟ ﾌﾞｻﾞｰ
LD MR20008               ; 溶着OKﾌﾞｻﾞｰ
TMR @0 EM31200           ; 良品ﾌﾞｻﾞｰOFF
CON
AND @T0                  ; 良品ﾌﾞｻﾞｰOFF
RES MR20008              ; 溶着OKﾌﾞｻﾞｰ
;<h1/>ﾀﾜｰﾗﾝﾌﾟ赤/Tower lamp red
;点灯：重度異常中
;点滅：原点復帰要求、軽度異常中、異常中、結果不良
;On: Severe error
;Blinking: Home return request, mild abnormality, abnormality, result defect
;EM30025=1:原点復帰要求/EM 30025 = 1: Home return request
LD MR900                 ; 重度異常：電源再投入必要
LD MR902                 ; ｴﾗｰ
LD MR304                 ; 原点復帰中
AND= EM30000 #0          ; (TP)現在表示中画面No.
AND= EM30025 #1          ; (TP)初期画面_ｺﾒﾝﾄ表示用
ORL
OR MR004                 ; ﾜｰｸ不良品
AND MR005                ; ﾗﾝﾌﾟ点滅用(0.5S)
ORL
OUT MR2000               ; ﾀﾜｰﾗﾝﾌﾟ 赤
;<h1/>ﾀﾜｰﾗﾝﾌﾟ黄/Tower lamp yellow
;点灯：動作中、軽度異常中、異常中、結果不良
;点滅：結果良品
;Lighting: Operating, Mildly Abnormal, Failure, Result Failure
;Blinking: Good results
LDB MR304                ; 原点復帰中
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR004                ; ﾜｰｸ不良品
INV
LD MR902                 ; ｴﾗｰ
ANB MR900                ; 重度異常：電源再投入必要
ORL
LD MR003                 ; ﾜｰｸ良品
AND MR005                ; ﾗﾝﾌﾟ点滅用(0.5S)
ORL
OUT MR2001               ; ﾀﾜｰﾗﾝﾌﾟ 黄
;<h1/>ﾀﾜｰﾗﾝﾌﾟ緑/Tower lamp green
;点灯：原点復帰要求、動作開始要求、定停止中、待機中
;/Lit: Home return request, operation start request, constant stop, waiting
;EM30025=1:原点復帰要求/EM 30025 = 1: Home return request
LD MR304                 ; 原点復帰中
AND= EM30000 #0          ; (TP)現在表示中画面No.
AND= EM30025 #1          ; (TP)初期画面_ｺﾒﾝﾄ表示用
LDB MR304                ; 原点復帰中
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ORL
OUT MR2002               ; ﾀﾜｰﾗﾝﾌﾟ 緑
END
ENDH
