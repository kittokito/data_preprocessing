DEVICE:53
;MODULE:Touch_panel_display
;MODULE_TYPE:0
;ﾒﾓﾘｶｰﾄﾞ書込中(TP_GW表示）/Memory card writing in progress (TP_GW display)
;TP画面600：ｴﾗｰ画面
;601−650：ｴﾗｰ詳細画面
;TP screen 600: error screen
;601 - 650: Error detail screen
LD MR111                 ; ﾒﾓﾘｶｰﾄﾞ書込中
LD<> EM30000 #600        ; (TP)現在表示中画面No.
AND< EM30000 #601        ; (TP)現在表示中画面No.
OR> EM30000 #650         ; (TP)現在表示中画面No.
ANL
OUT MR110                ; ﾒﾓﾘｶｰﾄﾞ書込中(TP_GW表示）
;Language select
LD<> EM30016 #0          ; Language(0:JPN 1:ENG)
AND CR2003               ; 常時OFF
MOV #0 EM30016           ; Language(0:JPN 1:ENG)
;Display
LD= EM30000 #500         ; (TP)現在表示中画面No.
MPS
LDB MR304                ; 原点復帰中
AND MR305                ; 原点復帰完了
ANB MR20101              ; MANU  Select
ANB MR20102              ; HOME  Select
AND MR20100              ; AUTO  Select
ANL
OUT MR20110              ; AUTO Select Lamp
MRD
LDB MR304                ; 原点復帰中
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR20102              ; HOME  Select
ANB MR20100              ; AUTO  Select
AND MR20101              ; MANU  Select
ANL
MPS
OUT MR20111              ; MANU Select Lamp
MPP
ANP MR21000              ; TP_自動ｽﾀｰﾄSW
RES MR305                ; 原点復帰完了
CON
MOV #700 EM30001         ; (TP)ﾍﾟｰｼﾞ画面切替用No.
MPP
LDB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR20100              ; AUTO  Select
ANB MR20101              ; MANU  Select
AND MR20102              ; HOME  Select
ANL
OUT MR20112              ; HOME Select Lamp
LD CR2002                ; 常時ON
MOV.L DM10600 DM22000    ; RB1現在値L  TP_RB_NOW_Pos
MOV DM2020 DM22002       ; RBT RB Position No
LDB MR304                ; 原点復帰中
ANB MR305                ; 原点復帰完了
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR315                ; 扉開処理中
OUT MR30004              ; 原点復帰待ち
LD MR304                 ; 原点復帰中
ANB MR305                ; 原点復帰完了
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR315                ; 扉開処理中
OUT MR30003              ; 原点復帰中
LDB MR304                ; 原点復帰中
AND MR305                ; 原点復帰完了
ANB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR315                ; 扉開処理中
OUT MR30000              ; 定停止中
LDB MR304                ; 原点復帰中
AND MR305                ; 原点復帰完了
AND MR306                ; 自動運転中
ANB MR315                ; 扉開処理中
OUT MR30001              ; 自動運転中
LDB MR306                ; 自動運転中
ANB MR307                ; ﾒﾝﾃ動作中
ANB MR304                ; 原点復帰中
INV
OUT MR30006              ; 装置動作中
LD MR315                 ; 扉開処理中
OUT MR30005              ; 一時停止中
LD= EM30000 #540         ; (TP)現在表示中画面No.
ANP MR30310              ; ﾊﾟﾗﾒｰﾀ確定ｷｰ
SPWR #0 #2 #1 #805 DM20114
SPWR #0 #2 #2 #805 DM20124
END
ENDH
