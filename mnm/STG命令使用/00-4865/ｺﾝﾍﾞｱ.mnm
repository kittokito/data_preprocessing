DEVICE:53
;MODULE:ｺﾝﾍﾞｱ
;MODULE_TYPE:0
;非常停止
LD R53010                ; 非常停止
OR R54200                ; TRAP
ZRES @MR100 @MR2915      ; 除材部 固体ﾊﾟﾚｯﾄ除材要求 
CON
ZRES R54500 R54510       ; ｺﾝﾍﾞｱAUTO ｽﾀｰﾄ  給材部ｵｰﾊﾞｰﾌﾛｰ中
LD R55515                ; ﾓｼﾞｭｰﾙｽﾃｰｼﾞﾘｾｯﾄ
ZRES @MR100 @MR2915      ; 除材部 固体ﾊﾟﾚｯﾄ除材要求 
;条件ﾌﾗｸﾞ / Flug
LD R4109                 ; 供給部ｸﾗﾝﾌﾟ後退端
ANB R4108                ; 供給部ｸﾗﾝﾌﾟ前進端
AND R4101                ; 供給2個前ｽﾄｯﾊﾟ 下降端
ANB R4100                ; 供給2個前ｽﾄｯﾊﾟ 上昇端
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
ANB R4102                ; 供給1個前ｽﾄｯﾊﾟ 下降端
AND R4105                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 下降端
ANB R4104                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 上昇端
AND R4107                ; 供給ｽﾄｯﾊﾟ 上昇端
ANB R4106                ; 供給ｽﾄｯﾊﾟ 下降端
AND R4111                ; 中間ｽﾄｯﾊﾟ前進端
ANB R4110                ; 中間ｽﾄｯﾊﾟ後退端
AND R4201                ; 除材部ｸﾗﾝﾌﾟ 後退端
ANB R4200                ; 除材部ｸﾗﾝﾌﾟ 前進端
AND R4113                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 下降端
ANB R4112                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇端
AND R4115                ; 除材_位置決め上昇端
ANB R4114                ; 除材_位置決め下降端
OUT @MR005               ; ｺﾝﾍﾞｱ部ｼﾘﾝﾀﾞ原位置
LDB R53300               ; PAUSE中
ANB R53111               ; ｺﾝﾍﾞｱｴﾗｰ中
ANB R53108               ; ｴﾗｰ発生中
OUT @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
;確認ｾﾝｻﾃﾞｨﾚｲ / Senser Delay
LD R4202                 ; 供給側2個前ﾜｰｸ確認ｾﾝｻ
TMH @2 #0                ; 供給側2個前ﾜｰｸ確認補助
LD R4203                 ; 供給側1個前ﾜｰｸ確認ｾﾝｻ
TMR @3 #2                ; 供給側1個前ﾜｰｸ確認補助
LD R4204                 ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @4 #2                ; 給材側位置決め ﾜｰｸ確認補助
LDB R4207                ; 給材側ﾜｰｸ満杯（通過）
TMR @7 #2                ; 給材側ﾜｰｸ満杯（通過）補助
LDB R4205                ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
OUT MR4205               ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
LD MR4205                ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
TMR @5 #2                ; 除材側1個前ﾜｰｸ確認補助
LD R4206                 ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @6 #2                ; 除材側位置決め ﾜｰｸ確認補助
LDB R4208                ; 除材側ﾜｰｸ満杯（通過）
TMR @8 #2                ; 除材側ﾜｰｸ満杯（通過）補助
;AUTO動作 / AUTO OPERATION
LD R53011                ; AUTO MODE
ANB R54501               ; ｺﾝﾍﾞｱAUTO 動作中
AND R54500               ; ｺﾝﾍﾞｱAUTO ｽﾀｰﾄ
RES R54502               ; ｺﾝﾍﾞｱAUTO 給材動作終了
CON
SET MR2001               ; 供給側ｺﾝﾍﾞｱ ON
CON
SET MR2002               ; 戻り側ｺﾝﾍﾞｱ ON
CON
SET R54501               ; ｺﾝﾍﾞｱAUTO 動作中
CON
SET @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
CON
RES R54500               ; ｺﾝﾍﾞｱAUTO ｽﾀｰﾄ
CON
SET @MR2000              ; AUTO START
;-------
STG @MR2000              ; AUTO START
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
ANB R54207               ; 停止要求
MPS
AND @T4                  ; 給材側位置決め ﾜｰｸ確認補助
JMP @MR2100
MPP
LDB R4204                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
AND @T3                  ; 供給側1個前ﾜｰｸ確認補助
ANL
MPS
AND @T2                  ; 供給側2個前ﾜｰｸ確認補助
JMP @MR2200
MPP
ANB @T2                  ; 供給側2個前ﾜｰｸ確認補助
SET @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
CON
JMP @MR2202
MPP
AND R54207               ; 停止要求
SET R54502               ; ｺﾝﾍﾞｱAUTO 給材動作終了
CON
JMP @MR2700              ; 終了処理へ
;-------
STG @MR2100
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6110               ; 供給部_位置決 下降
CON
RES MR6112               ; 供給部_ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
RES MR6108               ; 供給部ｸﾗﾝﾌﾟ 前進
CON
AND R4107                ; 供給ｽﾄｯﾊﾟ 上昇端
AND R4105                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 下降端
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
AND R4109                ; 供給部ｸﾗﾝﾌﾟ後退端
JMP @MR2101
STG @MR2101
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6112               ; 供給部_ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4104                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 上昇端
JMP @MR2102
STG @MR2102
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6108               ; 供給部ｸﾗﾝﾌﾟ 前進
CON
AND R4108                ; 供給部ｸﾗﾝﾌﾟ前進端
SET R54505               ; 供給ｺﾝﾍﾞｱ取りP取出し要求
CON
JMP @MR2105
STG @MR2105
AND R54605               ; 取りP除材完了
SET R54603               ; 固体ﾊﾟﾚｯﾄ排出中
CON
JMP @MR2106
STG @MR2106
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R54605               ; 取りP除材完了
CON
ANB R54505               ; 供給ｺﾝﾍﾞｱ取りP取出し要求
RES MR6108               ; 供給部ｸﾗﾝﾌﾟ 前進
CON
ANB R4108                ; 供給部ｸﾗﾝﾌﾟ前進端
RES MR6112               ; 供給部_ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4109                ; 供給部ｸﾗﾝﾌﾟ後退端
AND R4105                ; 供給_ﾊﾞｯｸｱｯﾌﾟ 下降端
JMP @MR2107
STG @MR2107
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4204                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
MPS
LD R4207                 ; 給材側ﾜｰｸ満杯（通過）
ANB MR4205               ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
AND R4111                ; 中間ｽﾄｯﾊﾟ前進端
ANL
TMR @14 #1               ; ﾃﾞｨﾚｰﾀｲﾏ14
CON
AND @T14                 ; ﾃﾞｨﾚｰﾀｲﾏ14
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
SET MR6110               ; 供給部_位置決 下降
CON
JMP @MR2110
MPP
LDB R4207                ; 給材側ﾜｰｸ満杯（通過）
OR MR4205                ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
ORB R4111                ; 中間ｽﾄｯﾊﾟ前進端
ANL
OUT R54510               ; 給材部ｵｰﾊﾞｰﾌﾛｰ中
MPP
ANB R4204                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @12 #2
CON
AND @T12
@INC DM1111
CON
JMP @MR2109
STG @MR2110
AND R4106                ; 供給ｽﾄｯﾊﾟ 下降端
JMP @MR2108
STG @MR2108
ANB R4204                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
AND R4207                ; 給材側ﾜｰｸ満杯（通過）
LDB LR1200               ; 4号機以降
AND MR4205               ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
OR LR1200                ; 4号機以降
ANL
TMR @10 #1               ; ﾃﾞｨﾚｰﾀｲﾏ10
CON
AND @T10                 ; ﾃﾞｨﾚｰﾀｲﾏ10
JMP @MR2109
STG @MR2109
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
RES MR6110               ; 供給部_位置決 下降
CON
JMP @MR2115
STG @MR2115
AND R4107                ; 供給ｽﾄｯﾊﾟ 上昇端
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB R4204                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
MPS
ANB LR41007              ; 自動ﾊﾟﾗﾒｰﾀ7　基準測定ﾓｰﾄﾞ
TMR #841 #1
CON
AND T841
RES R54603               ; 固体ﾊﾟﾚｯﾄ排出中
CON
JMP @MR2000              ; AUTO START
MPP
AND LR41007              ; 自動ﾊﾟﾗﾒｰﾀ7　基準測定ﾓｰﾄﾞ
SET R55415               ; 基準測定モード終了
CON
TMR #843 #5
CON
AND T843
RES R54603               ; 固体ﾊﾟﾚｯﾄ排出中
CON
JMP @MR2000              ; AUTO START
;-------
STG @MR2200
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
CON
JMP @MR2201
STG @MR2201
AND R4100                ; 供給2個前ｽﾄｯﾊﾟ 上昇端
JMP @MR2202
STG @MR2202
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R4107                ; 供給ｽﾄｯﾊﾟ 上昇端
SET MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
JMP @MR2203
STG @MR2203
AND R4102                ; 供給1個前ｽﾄｯﾊﾟ 下降端
ANB R4203                ; 供給側1個前ﾜｰｸ確認ｾﾝｻ
TMR #842 #2
CON
AND T842
RES MR6114               ; 供給部_1個前ｽｯﾊﾟ 下降
CON
JMP @MR2204
STG @MR2204
AND @T4                  ; 給材側位置決め ﾜｰｸ確認補助
AND R4107                ; 供給ｽﾄｯﾊﾟ 上昇端
AND R4103                ; 供給1個前ｽﾄｯﾊﾟ 上昇端
RES @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
CON
RES MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
CON
JMP @MR2215
STG @MR2215
AND R4101                ; 供給2個前ｽﾄｯﾊﾟ 下降端
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
JMP @MR2000              ; AUTO START
LD @MR2315               ; 2個前ｽﾄｯﾊﾟ 上昇監視
AND @T2                  ; 供給側2個前ﾜｰｸ確認補助
SET MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
LDF @MR2315              ; 2個前ｽﾄｯﾊﾟ 上昇監視
RES MR6200               ; 供給部_2個前ｽｯﾊﾟ 上昇
;-------　（除材側）
STG @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND @T6                  ; 除材側位置決め ﾜｰｸ確認補助
JMP @MR2500
MRD
LDB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
AND @T5                  ; 除材側1個前ﾜｰｸ確認補助
ANL
JMP @MR2600              ; 中間ｽﾄｯﾊﾟ動作
MPP
ANB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
MPS
LDB R53000               ; 自動運転中
ANB MR4205               ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
AND R55002               ; L側ﾜｰｸｾｯﾄ部 AUTO 動作終了
AND R55102               ; R側ﾜｰｸｾｯﾄ部 AUTO 動作終了
ANB R54705               ; L側取りP除材中
ANB R54706               ; R側取りP除材中
ANL
SET R54509               ; 除材処理ｻｲｸﾙ終了
CON
ENDS
MPP
LD R53000                ; 自動運転中
AND R54702               ; 除材PP AUTO 動作終了
ANB MR4205               ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
AND R55002               ; L側ﾜｰｸｾｯﾄ部 AUTO 動作終了
AND R55102               ; R側ﾜｰｸｾｯﾄ部 AUTO 動作終了
ANB R54705               ; L側取りP除材中
ANB R54706               ; R側取りP除材中
ANL
SET R54509               ; 除材処理ｻｲｸﾙ終了
CON
ENDS
;-------
STG @MR2500
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6102               ; 除材部位置決 下降
CON
RES MR6104               ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
RES MR6100               ; 除材部ｸﾗﾝﾌﾟ 前進
CON
AND R4115                ; 除材_位置決め上昇端
AND R4113                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 下降端
AND R4201                ; 除材部ｸﾗﾝﾌﾟ 後退端
JMP @MR2501
STG @MR2501
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6104               ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4112                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇端
JMP @MR2502
STG @MR2502
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6100               ; 除材部ｸﾗﾝﾌﾟ 前進
CON
AND R4200                ; 除材部ｸﾗﾝﾌﾟ 前進端
RES R54604               ; 取りPｺﾝﾍﾞｱ排出完了
CON
SET R54507               ; 取りP除材準備OK
CON
JMP @MR2505
STG @MR2505
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
LD @T6                   ; 除材側位置決め ﾜｰｸ確認補助
AND R54604               ; 取りPｺﾝﾍﾞｱ排出完了
ANL
JMP @MR2506
MPP
ANB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
RES R54507               ; 取りP除材準備OK
CON
JMP @MR2511
STG @MR2511
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R54604               ; 取りPｺﾝﾍﾞｱ排出完了
CON
RES MR6100               ; 除材部ｸﾗﾝﾌﾟ 前進
CON
ANB R4200                ; 除材部ｸﾗﾝﾌﾟ 前進端
LD R4306                 ; PZT除材PP 前進端
AND R4309                ; PZT除材PP ﾊﾝﾄﾞ上昇端
ORB R4306                ; PZT除材PP 前進端
ANL
RES MR6104               ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4201                ; 除材部ｸﾗﾝﾌﾟ 後退端
AND R4113                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 下降端
JMP @MR2507
STG @MR2506
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R54604               ; 取りPｺﾝﾍﾞｱ排出完了
CON
ANB R54507               ; 取りP除材準備OK
RES MR6100               ; 除材部ｸﾗﾝﾌﾟ 前進
CON
ANB R4200                ; 除材部ｸﾗﾝﾌﾟ 前進端
LD R4306                 ; PZT除材PP 前進端
AND R4309                ; PZT除材PP ﾊﾝﾄﾞ上昇端
ORB R4306                ; PZT除材PP 前進端
ANL
RES MR6104               ; 除材部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4201                ; 除材部ｸﾗﾝﾌﾟ 後退端
AND R4113                ; 除材部ﾊﾞｯｸｱｯﾌﾟ 下降端
JMP @MR2507
STG @MR2507
MPS
LDB R55502               ; 除材部固体ﾊﾟﾚｯﾄ除材要求
OR CR2002                ; 常時ON
ANL
MPS
AND @T6                  ; 除材側位置決め ﾜｰｸ確認補助
JMP @MR2508
MPP
ANB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR @13 #1               ; ﾃﾞｨﾚｰﾀｲﾏ13
CON
AND @T13                 ; ﾃﾞｨﾚｰﾀｲﾏ13
JMP @MR2515
MPP
LD R55502                ; 除材部固体ﾊﾟﾚｯﾄ除材要求
ANB CR2002               ; 常時ON
ANL
JMP @MR2300              ; 2個前ｽﾄｯﾊﾟ 上昇監視
STG @MR2300              ; 2個前ｽﾄｯﾊﾟ 上昇監視
RES R55502               ; 除材部固体ﾊﾟﾚｯﾄ除材要求
CON
SET @MR100               ; 除材部 固体ﾊﾟﾚｯﾄ除材要求 
CON
JMP @MR2302
STG @MR2302
AND @MR107               ; ｴﾗｰ解除
RES @MR107               ; ｴﾗｰ解除
CON
JMP @MR2507
STG @MR2508
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4208                ; 除材側ﾜｰｸ満杯（通過）
TMR @15 #1               ; ﾃﾞｨﾚｰﾀｲﾏ15
CON
AND @T15                 ; ﾃﾞｨﾚｰﾀｲﾏ15
SET MR6102               ; 除材部位置決 下降
CON
AND R4114                ; 除材_位置決め下降端
JMP @MR2509
MPP
ANB R4208                ; 除材側ﾜｰｸ満杯（通過）
OUT R54511               ; 除材部ｵｰﾊﾞｰﾌﾛｰ中
STG @MR2509
ANB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
AND R4208                ; 除材側ﾜｰｸ満杯（通過）
TMR @11 #3               ; ﾃﾞｨﾚｰﾀｲﾏ11
CON
AND @T11                 ; ﾃﾞｨﾚｰﾀｲﾏ11
JMP @MR2510
STG @MR2510
RES MR6102               ; 除材部位置決 下降
CON
AND R4115                ; 除材_位置決め上昇端
DIFU R54515              ; 完成品排出ｻｲｸﾙｶｳﾝﾄ用
CON
JMP @MR2515
STG @MR2515
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB R4206                ; 除材側位置決め ﾜｰｸ確認ｾﾝｻ
TMR #840 #1
CON
AND T840
RES R55502               ; 除材部固体ﾊﾟﾚｯﾄ除材要求
CON
JMP @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
;-------
STG @MR2600              ; 中間ｽﾄｯﾊﾟ動作
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6106               ; 中間ｽﾄｯﾊﾟ 前進
CON
AND R4110                ; 中間ｽﾄｯﾊﾟ後退端
JMP @MR2602
STG @MR2602
MPS
LD @T6                   ; 除材側位置決め ﾜｰｸ確認補助
AND CR2003               ; 常時OFF
ANL
JMP @MR2603
MPP
AND R4205                ; 除材側1個前ﾜｰｸ確認ｾﾝｻ
TMH @0 #10
CON
AND @T0
JMP @MR2603
STG @MR2603
RES MR6106               ; 中間ｽﾄｯﾊﾟ 前進
CON
AND R4111                ; 中間ｽﾄｯﾊﾟ前進端
JMP @MR2615
STG @MR2615
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB MR913
JMP @MR2400              ; 除材ｽﾄｯﾊﾟ処理開始
;-------　（ｺﾝﾍﾞｱｻｲｸﾙ終了処理）
STG @MR2700              ; 終了処理へ
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R54509               ; 除材処理ｻｲｸﾙ終了
JMP @MR2701
STG @MR2701
RES R54509               ; 除材処理ｻｲｸﾙ終了
CON
JMP @MR2702
STG @MR2702
RES R54604               ; 取りPｺﾝﾍﾞｱ排出完了
CON
RES R54505               ; 供給ｺﾝﾍﾞｱ取りP取出し要求
CON
RES R54507               ; 取りP除材準備OK
CON
RES R54501               ; ｺﾝﾍﾞｱAUTO 動作中
CON
SET R54503               ; ｺﾝﾍﾞｱAUTO 給除材動作完了
CON
ENDS
;ｱﾗｰﾑ / ALARM
;-------ERR12
STG @MR100               ; 除材部 固体ﾊﾟﾚｯﾄ除材要求 
ANB R53008               ; 全体ｴﾗｰ発生
SET R53008               ; 全体ｴﾗｰ発生
CON
SET R53111               ; ｺﾝﾍﾞｱｴﾗｰ中
CON
SET R54301               ; ｺﾝﾍﾞｱｴﾗｰ 表示
CON
SET R53513               ; ERR13
CON
RES R210                 ; RETRY SW　目隠し解除
CON
JMP @MR101
MOV #13 DM400            ; 表示画面No.
STG @MR101
AND R53008               ; 全体ｴﾗｰ発生
JMP @MR102
STG @MR102
ANB R53008               ; 全体ｴﾗｰ発生
SET @MR107               ; ｴﾗｰ解除
CON
ENDS
;-------　（PACK管理）
LD @MR2900               ; 一時停止禁止
OUT R54400               ; ｺﾝﾍﾞｱ部 PACK
END
ENDH
