DEVICE:53
;MODULE:PANEL
;MODULE_TYPE:0
;ﾌﾘｯｶ
LD CR2002                ; 常時ON
FLIK #30 #30 @MR000      ; ﾌﾘｯｶ1
;初期画面表示
LD CR2008                ; 運転開始時1ｽｷｬﾝON
SET MR000                ; 立上げｾｯﾄ
CON
MOV #1 DM3008            ; 画面切替
MOV #1000 DM3101
;ﾒｲﾝ画面表示
LD MR000                 ; 立上げｾｯﾄ
TMR #100 #20             ; 初期画面表示
CON
AND T100                 ; 初期画面表示
RES MR000                ; 立上げｾｯﾄ
CON
@MOV #2 DM3008           ; 画面切替
;ｴﾗｰ発生時表示
LD R53008                ; 全体ｴﾗｰ発生
AND R53108               ; ｴﾗｰ発生中
@MOV DM3000 DM3020       ; 表示画面
@MOV DM3100 DM3022       ; 背面ﾊﾟﾈﾙ 表示画面No.  REAR表示画面一時保管
@MOV #110 DM3008         ; 画面切替
@MOV #1020 DM3101
LD CR2002                ; 常時ON
LDF R53008               ; 全体ｴﾗｰ発生
OR MR002                 ; ｴﾗｰ解除後保持時間補助
ANL
MPS
AND T101                 ; 遅延ﾀｲﾏ
OUT MR002                ; ｴﾗｰ解除後保持時間補助
MRD
MOV DM3020 DM3008        ; 画面切替
MRD
AND R205                 ; 背面ﾊﾟﾈﾙ無効
MOV DM3022 DM3101        ; REAR表示画面一時保管
MRD
ANB R205                 ; 背面ﾊﾟﾈﾙ無効
MOV #1000 DM3101
MRD
RES R53015               ; ｴﾗｰﾘｾｯﾄSW ON
MPP
TMR #101 #1              ; 遅延ﾀｲﾏ
LDP R44900               ; TP/ｴﾗｰﾘｾｯﾄSW
ANB R53015               ; ｴﾗｰﾘｾｯﾄSW ON
SET R53015               ; ｴﾗｰﾘｾｯﾄSW ON
LD R53206                ; ｴﾗｰﾘｾｯﾄ
RES R53015               ; ｴﾗｰﾘｾｯﾄSW ON
CON
RES R53206               ; ｴﾗｰﾘｾｯﾄ
LD R53811                ; ERR59
OR R53812                ; ERR60
OR R53813                ; ERR61
OR R53814                ; ERR62
OR R53815                ; ERR63
OR R53900                ; ERR64
OUT R44901               ; ｼﾘﾝﾀﾞｾﾝｻｴﾗｰ発生中
;画面　STATUS表示
LD= DM3000 #2            ; 表示画面
OR= DM3000 #3            ; 表示画面
OR= DM3000 #4            ; 表示画面
OR= DM3000 #5            ; 表示画面
MPS
LD R53000                ; 自動運転中
LDB R53001               ; 手動動作中
ORB R53002               ; STEP動作中
ANL
ANB R53003               ; 原点復帰中
ANB R53300               ; PAUSE中
ANL
OUT R44902               ; TP/STATUS_自動運転中
MRD
LDB R53000               ; 自動運転中
LD R53001                ; 手動動作中
OR R53002                ; STEP動作中
ANL
ANB R53003               ; 原点復帰中
ANB R53300               ; PAUSE中
ANL
OUT R44903               ; TP/STATUS_手動運転中
MRD
LDB R53315               ; 原点復帰完了
ANB R53300               ; PAUSE中
ANB R53003               ; 原点復帰中
ANL
OUT R44905               ; TP/STATUS_原点復帰待ち
MRD
LD R53315                ; 原点復帰完了
ANB R53000               ; 自動運転中
ANB R53001               ; 手動動作中
ANB R53002               ; STEP動作中
ANB R53300               ; PAUSE中
ANL
OUT R44906               ; TP/STATUS_定停止
MPP
LD R53315                ; 原点復帰完了
ANB R53000               ; 自動運転中
ANB R53001               ; 手動動作中
ANB R53002               ; STEP動作中
ORB R53315               ; 原点復帰完了
OR R53003                ; 原点復帰中
OR R53000                ; 自動運転中
OR R53001                ; 手動動作中
OR R53002                ; STEP動作中
AND R53300               ; PAUSE中
ANL
OUT R44907               ; TP/STATUS_一時停止中
;ﾒｯｾｰｼﾞ表示
LDB R53315               ; 原点復帰完了
LD R206                  ; TP MANU切替SW
OR R207                  ; TP RBﾎﾟｲﾝﾄ設定
ANL
MOV #3000 DM150
LDB R53315               ; 原点復帰完了
LDF R206                 ; TP MANU切替SW
ORF R207                 ; TP RBﾎﾟｲﾝﾄ設定
ANL
MOV #0 DM150
LD= DM3000 #2            ; 表示画面
OR= DM3000 #3            ; 表示画面
OR= DM3000 #4            ; 表示画面
OR= DM3000 #5            ; 表示画面
ANB R53315               ; 原点復帰完了
ANB R53003               ; 原点復帰中
LDB R14113               ; 軸1 ｻｰﾎﾞON
ORB R14913               ; 軸2 ｻｰﾎﾞON
ORB R15713               ; 軸3 ｻｰﾎﾞON
ORB R16513               ; 軸4 ｻｰﾎﾞON
ORB R22113               ; 軸1 ｻｰﾎﾞON
ORB R22913               ; 軸2 ｻｰﾎﾞON
ORB R23713               ; 軸3 ｻｰﾎﾞON
ORB R24513               ; 軸4 ｻｰﾎﾞON
LDB R30113               ; 軸1 ｻｰﾎﾞON
ORB R30913               ; 軸2 ｻｰﾎﾞON
ORB R31713               ; 軸3 ｻｰﾎﾞON
ORB R38113               ; 軸1 ｻｰﾎﾞON
ORB R38913               ; 軸2 ｻｰﾎﾞON
ORB R39713               ; 軸3 ｻｰﾎﾞON
AND CR2003               ; 常時OFF
ORL
ANL
ANB R53214               ; 原点復帰待ち
MOV #3002 DM150
LD= DM3000 #2            ; 表示画面
OR= DM3000 #3            ; 表示画面
OR= DM3000 #4            ; 表示画面
OR= DM3000 #5            ; 表示画面
ANB R53315               ; 原点復帰完了
ANB R53003               ; 原点復帰中
AND R14113               ; 軸1 ｻｰﾎﾞON
AND R14913               ; 軸2 ｻｰﾎﾞON
AND R15713               ; 軸3 ｻｰﾎﾞON
AND R16513               ; 軸4 ｻｰﾎﾞON
AND R22113               ; 軸1 ｻｰﾎﾞON
AND R22913               ; 軸2 ｻｰﾎﾞON
AND R23713               ; 軸3 ｻｰﾎﾞON
AND R24513               ; 軸4 ｻｰﾎﾞON
LD R30113                ; 軸1 ｻｰﾎﾞON
AND R30913               ; 軸2 ｻｰﾎﾞON
AND R31713               ; 軸3 ｻｰﾎﾞON
AND R38113               ; 軸1 ｻｰﾎﾞON
AND R38913               ; 軸2 ｻｰﾎﾞON
AND R39713               ; 軸3 ｻｰﾎﾞON
OR CR2002                ; 常時ON
ANL
AND R53214               ; 原点復帰待ち
@MOV #0 DM150
LD R44915                ; TP 設定切替
AND R53315               ; 原点復帰完了
MPS
AND R53011               ; AUTO MODE
@MOV #11 DM3008          ; 画面切替
MPP
ANB R53011               ; AUTO MODE
@MOV #12 DM3008          ; 画面切替
LDB R53315               ; 原点復帰完了
OR R53011                ; AUTO MODE
OR LR200                 ; 自動ﾓｰﾄﾞ　   選択
OUT R53104               ; 手動ﾓｰﾄﾞ切替不可ﾌﾗｸﾞ
;背面ﾊﾟﾈﾙ/画面表示
LDB R205                 ; 背面ﾊﾟﾈﾙ無効
MOV #100 DM152           ; 背面ﾊﾟﾈﾙ_ﾒｯｾｰｼﾞ表示
LDB R205                 ; 背面ﾊﾟﾈﾙ無効
@MOV #0 DM150
LD R205                  ; 背面ﾊﾟﾈﾙ無効
AND R53315               ; 原点復帰完了
LD= DM3100 #1000         ; 背面ﾊﾟﾈﾙ 表示画面No.
OR= DM3100 #1001         ; 背面ﾊﾟﾈﾙ 表示画面No.
ANL
MOV #101 DM152           ; 背面ﾊﾟﾈﾙ_ﾒｯｾｰｼﾞ表示
LD<> DM3100 #1000        ; 背面ﾊﾟﾈﾙ 表示画面No.
MEP
MOV #0 DM152             ; 背面ﾊﾟﾈﾙ_ﾒｯｾｰｼﾞ表示
LD R205                  ; 背面ﾊﾟﾈﾙ無効
AND<> DM3100 #1000       ; 背面ﾊﾟﾈﾙ 表示画面No.
MOV #3006 DM150
END
ENDH
