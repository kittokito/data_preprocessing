DEVICE:53
;MODULE:Index
;MODULE_TYPE:0
;Index 制御
;Index Control 
;
;非常停止
;Emergency Stop
LD R50010                ; 非常停止
OR @MR001
ZRES @MR002 @MR3915      ; STEP END
CON
OUT @MR001
TMR @0 #5
LD @T0
MDSTOP Index
;<h1/>HOME
LD R50003                ; HOMEﾓｰﾄﾞ
ANB R50200               ; INDEX Do
SET R50200               ; INDEX Do
CON
SET @MR1003
STG @MR1003
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
RES MR8411               ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ上昇
CON
SET MR8410               ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ下降
CON
AND R5414                ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ下降限
ANB R8411                ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ上昇
JMP @MR1004
STG @MR1004
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
MPS
ANB LR000                ; Step/Manual 操作記憶
JMP @MR1112
MPP
AND LR000                ; Step/Manual 操作記憶
JMP @MR1005
LDB R4800                ; INDEX ﾁｬｯｸ1-1 ﾜｰｸ有無
ANB R4801                ; INDEX ﾁｬｯｸ1-2 ﾜｰｸ有無
ANB R4802                ; INDEX ﾁｬｯｸ2-1 ﾜｰｸ有無
ANB R4803                ; INDEX ﾁｬｯｸ2-2 ﾜｰｸ有無
ANB R4804                ; INDEX ﾁｬｯｸ3-1 ﾜｰｸ有無
ANB R4805                ; INDEX ﾁｬｯｸ3-2 ﾜｰｸ有無
ANB R4806                ; INDEX ﾁｬｯｸ4-1 ﾜｰｸ有無
ANB R4807                ; INDEX ﾁｬｯｸ4-2 ﾜｰｸ有無
ANB R4808                ; INDEX ﾁｬｯｸ5-1 ﾜｰｸ有無
ANB R4809                ; INDEX ﾁｬｯｸ5-2 ﾜｰｸ有無
ANB R4810                ; INDEX ﾁｬｯｸ6-1 ﾜｰｸ有無
ANB R4811                ; INDEX ﾁｬｯｸ6-2 ﾜｰｸ有無
ANB R4812                ; INDEX ﾁｬｯｸ7-1 ﾜｰｸ有無
ANB R4813                ; INDEX ﾁｬｯｸ7-2 ﾜｰｸ有無
ANB R4814                ; INDEX ﾁｬｯｸ8-1 ﾜｰｸ有無
ANB R4815                ; INDEX ﾁｬｯｸ8-2 ﾜｰｸ有無
ANB R5200                ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-1閉限
ANB R5202                ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-2閉限
ANB R5204                ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-1閉限
ANB R5206                ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-2閉限
INV
OUT @MR1000              ; ｲﾝﾃﾞｯｸｽ上にﾜｰｸ有
STG @MR1005
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
MPS
ANB @MR1000              ; ｲﾝﾃﾞｯｸｽ上にﾜｰｸ有
JMP @MR1112
MPP
AND @MR1000              ; ｲﾝﾃﾞｯｸｽ上にﾜｰｸ有
SET MR8201               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-1開
CON
RES MR8200               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-1閉
CON
SET MR8203               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-2開
CON
RES MR8202               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ1-2閉
CON
SET MR8205               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-1開
CON
RES MR8204               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-1閉
CON
SET MR8207               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-2開
CON
RES MR8206               ; ｷｬｯﾌﾟｾｯﾄ ﾁｬｯｸ2-2閉
CON
SET @MR100               ; ALL st ﾜｰｸ除材要求
CON
JMP @MR1006
STG @MR1006
ANB R50300               ; Pause
AND R50110               ; INDEX異常
JMP @MR1005
STG @MR1112
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
AND MR3200               ; ﾒｲﾝINDEX 旋回IL
LDB MR001                ; 上流 H/S 無
AND B2012                ; 上流より ｲﾝﾀｰﾛｯｸ
ANB B2013                ; 上流より 動作開始通知
OR MR001                 ; 上流 H/S 無
ANL
SET R50100               ; PACK INDEX
CON
JMP @MR1113
STG @MR1113
ANB MR1003               ; ﾒｲﾝINDEX 回転完了
MPS
ANB LR000                ; Step/Manual 操作記憶
LDA EM1206               ; ﾒｲﾝINDEX 自動POS記憶
CON
STA EM1200               ; ﾒｲﾝINDEX 指令POS
CON
SET MR1002               ; ﾒｲﾝINDEX 回転要求
CON
JMP @MR1114
MPP
AND LR000                ; Step/Manual 操作記憶
LDA EM1208               ; ﾒｲﾝINDEX Step POS記憶
CON
STA EM1200               ; ﾒｲﾝINDEX 指令POS
CON
SET MR1002               ; ﾒｲﾝINDEX 回転要求
CON
JMP @MR1114
STG @MR1114
AND MR1003               ; ﾒｲﾝINDEX 回転完了
RES MR1003               ; ﾒｲﾝINDEX 回転完了
CON
RES R50100               ; PACK INDEX
CON
JMP @MR1115
STG @MR1115
MPS
ANB LR000                ; Step/Manual 操作記憶
JMP @MR1200
MPP
AND LR000                ; Step/Manual 操作記憶
@MOV EM1208 EM1206       ; ﾒｲﾝINDEX Step POS記憶  ﾒｲﾝINDEX 自動POS記憶
@FMOV #0 DM20100 #900    ; UT1-1在荷
JMP @MR1200
STG @MR1200
RES LR000                ; Step/Manual 操作記憶
CON
SET R51900               ; INDEX 1ｻｲｸﾙ終了
CON
ENDS
;<h1/>AUTO
LD R50001                ; AUTOﾓｰﾄﾞ
ANB R50300               ; Pause
ANB R50200               ; INDEX Do
ANB @MR2001              ; 自動運転中
AND= EM1202 EM1206       ; ﾒｲﾝINDEX 現在POS  ﾒｲﾝINDEX 自動POS記憶
SET R50200               ; INDEX Do
CON
SET @MR2000              ; INDEX AUTO START
STG @MR2000              ; INDEX AUTO START
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
JMP @MR2100
STG @MR2100
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
RES MR7801               ; ﾒｲﾝINDEX ﾁｬｯｸ1開
CON
RES MR7803               ; ﾒｲﾝINDEX ﾁｬｯｸ2開
CON
RES MR7805               ; ﾒｲﾝINDEX ﾁｬｯｸ3開
CON
RES MR7807               ; ﾒｲﾝINDEX ﾁｬｯｸ4開
CON
RES MR7809               ; ﾒｲﾝINDEX ﾁｬｯｸ5開
CON
RES MR7811               ; ﾒｲﾝINDEX ﾁｬｯｸ6開
CON
RES MR7813               ; ﾒｲﾝINDEX ﾁｬｯｸ7開
CON
RES MR7815               ; ﾒｲﾝINDEX ﾁｬｯｸ8開
CON
SET MR7800               ; ﾒｲﾝINDEX ﾁｬｯｸ1閉
CON
SET MR7802               ; ﾒｲﾝINDEX ﾁｬｯｸ2閉
CON
SET MR7804               ; ﾒｲﾝINDEX ﾁｬｯｸ3閉
CON
SET MR7806               ; ﾒｲﾝINDEX ﾁｬｯｸ4閉
CON
SET MR7808               ; ﾒｲﾝINDEX ﾁｬｯｸ5閉
CON
SET MR7810               ; ﾒｲﾝINDEX ﾁｬｯｸ6閉
CON
SET MR7812               ; ﾒｲﾝINDEX ﾁｬｯｸ7閉
CON
SET MR7814               ; ﾒｲﾝINDEX ﾁｬｯｸ8閉
CON
JMP @MR2101
STG @MR2101
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
AND T500                 ; ﾒｲﾝINDEX 全ﾁｬｯｸ閉
LDB MR001                ; 上流 H/S 無
AND B2012                ; 上流より ｲﾝﾀｰﾛｯｸ
ANB B2013                ; 上流より 動作開始通知
OR MR001                 ; 上流 H/S 無
ANL
SET R50100               ; PACK INDEX
CON
JMP @MR2102
STG @MR2102
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
ANB MR1003               ; ﾒｲﾝINDEX 回転完了
ANB MR106                ; 6UT ｲﾝﾃﾞｯｸｽ旋回禁止
MPS
AND<= EM1200 #8          ; ﾒｲﾝINDEX 指令POS
@INC EM1200              ; ﾒｲﾝINDEX 指令POS
MRD
AND> EM1200 #8           ; ﾒｲﾝINDEX 指令POS
@DW #1 EM1200            ; ﾒｲﾝINDEX 指令POS
MRD
AND R50001               ; AUTOﾓｰﾄﾞ
@MOV EM1200 EM1206       ; ﾒｲﾝINDEX 指令POS  ﾒｲﾝINDEX 自動POS記憶
MRD
AND R50002               ; STEPﾓｰﾄﾞ
SET LR000                ; Step/Manual 操作記憶
CON
@MOV EM1200 EM1208       ; ﾒｲﾝINDEX 指令POS  ﾒｲﾝINDEX Step POS記憶
MPP
SET MR1002               ; ﾒｲﾝINDEX 回転要求
CON
JMP @MR2103
STG @MR2103
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
AND MR1003               ; ﾒｲﾝINDEX 回転完了
RES MR1003               ; ﾒｲﾝINDEX 回転完了
CON
JMP @MR2104
STG @MR2104
ANB MR104                ; 4UT ｲﾝﾃﾞｯｸｽﾃﾞｰﾀｼﾌﾄ禁止
@WSR DM20100 #700 #100   ; UT1-1在荷
RES R50100               ; PACK INDEX
CON
JMP @MR2105
STG @MR2105
MPS
ANB R50002               ; STEPﾓｰﾄﾞ
JMP @MR2115
MPP
AND R50002               ; STEPﾓｰﾄﾞ
JMP @MR2106
STG @MR2106
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
SET MR7801               ; ﾒｲﾝINDEX ﾁｬｯｸ1開
CON
SET MR7803               ; ﾒｲﾝINDEX ﾁｬｯｸ2開
CON
SET MR7805               ; ﾒｲﾝINDEX ﾁｬｯｸ3開
CON
SET MR7807               ; ﾒｲﾝINDEX ﾁｬｯｸ4開
CON
SET MR7809               ; ﾒｲﾝINDEX ﾁｬｯｸ5開
CON
SET MR7811               ; ﾒｲﾝINDEX ﾁｬｯｸ6開
CON
SET MR7813               ; ﾒｲﾝINDEX ﾁｬｯｸ7開
CON
SET MR7815               ; ﾒｲﾝINDEX ﾁｬｯｸ8開
CON
RES MR7800               ; ﾒｲﾝINDEX ﾁｬｯｸ1閉
CON
RES MR7802               ; ﾒｲﾝINDEX ﾁｬｯｸ2閉
CON
RES MR7804               ; ﾒｲﾝINDEX ﾁｬｯｸ3閉
CON
RES MR7806               ; ﾒｲﾝINDEX ﾁｬｯｸ4閉
CON
RES MR7808               ; ﾒｲﾝINDEX ﾁｬｯｸ5閉
CON
RES MR7810               ; ﾒｲﾝINDEX ﾁｬｯｸ6閉
CON
RES MR7812               ; ﾒｲﾝINDEX ﾁｬｯｸ7閉
CON
RES MR7814               ; ﾒｲﾝINDEX ﾁｬｯｸ8閉
CON
SET @MR3915              ; STEP END
CON
ENDS
;Operation is completed
;動作終了
STG @MR2115
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
RES LR100                ; 4st ﾒｲﾝｲﾝﾃﾞｯｸｽ旋回完了FLG
CON
SET R51900               ; INDEX 1ｻｲｸﾙ終了
CON
ENDS
;<h1/>STEP
LD R50002                ; STEPﾓｰﾄﾞ
ANB R50300               ; Pause
ANB R50205               ; INDEX Do(STEP)
SET R50205               ; INDEX Do(STEP)
CON
SET @MR3600
STG @MR3600
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
MPS
AND LR40100              ; STEP INDEX-0 selection lamp
JMP @MR3601
MPP
ANB LR40100              ; STEP INDEX-0 selection lamp
SET @MR3915              ; STEP END
CON
JMP @MR3609
STG @MR3601
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
MPS
LDB R4814                ; INDEX ﾁｬｯｸ8-1 ﾜｰｸ有無
ANB R4815                ; INDEX ﾁｬｯｸ8-2 ﾜｰｸ有無
ANL
JMP @MR3608
MPP
LD R4814                 ; INDEX ﾁｬｯｸ8-1 ﾜｰｸ有無
OR R4815                 ; INDEX ﾁｬｯｸ8-2 ﾜｰｸ有無
ANL
SET @MR108               ; 8st ﾜｰｸ除材要求
CON
JMP @MR3607
STG @MR3607
ANB R50300               ; Pause
AND R50110               ; INDEX異常
JMP @MR3600
STG @MR3608
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
SET @MR2000              ; INDEX AUTO START
CON
JMP @MR3609
STG @MR3609
ANB R50300               ; Pause
ANB R50110               ; INDEX異常
AND @MR3915              ; STEP END
RES @MR3915              ; STEP END
CON
SET R51900               ; INDEX 1ｻｲｸﾙ終了
CON
ENDS
;サブルーチン
;Subroutine
;<h1/>INDEX sub
;The starting point return of an index is program No.5.  
;By 5 division specification, it moves to a starting point position by the shortest. 
;/ ｲﾝﾃﾞｯｸｽの原点復帰は、ﾌﾟﾛｸﾞﾗﾑNo.10　8分割指定で、近周りで原点位置へ移動する
;72 degrees positive rotation of an index chooses program No.0 to 3 and 7 in order, and starts it. 
;By 5 division specification, it moves to the following position of five positions. 
;If movement is completed, the following specification will be written in DM. 
;ﾌﾟﾛｸﾞﾗﾑNo.1から8を順に選択し起動する
;8分割指定で、移動が完了したらDMに次の指定を書込む
STG MR1002               ; ﾒｲﾝINDEX 回転要求
RES MR8411               ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ上昇
CON
SET MR8410               ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ下降
CON
AND R5414                ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ下降限
ANB R8411                ; ﾒｲﾝINDEX ﾌﾞﾚｰｷ上昇
AND MR3200               ; ﾒｲﾝINDEX 旋回IL
RES DM10850.11           ; ﾒｲﾝINDEX ｱﾝｻ
CON
SET MR1011               ; ﾒｲﾝINDEX 移動中
CON
JMP @MR3000
STG @MR3000
AND DM10650.10           ; ﾒｲﾝINDEX 起動入力待ち
AND<> EM1200 #0          ; ﾒｲﾝINDEX 指令POS
AND<> EM1204 #0          ; ﾒｲﾝINDEX 現在指令POS
AND= EM1200 EM1204       ; ﾒｲﾝINDEX 指令POS  ﾒｲﾝINDEX 現在指令POS
DW #0 EM1202             ; ﾒｲﾝINDEX 現在POS
CON
SET DM10850.8            ; ﾒｲﾝINDEX 起動入力
CON
JMP @MR3001
STG @MR3001
ANB DM10650.10           ; ﾒｲﾝINDEX 起動入力待ち
RES DM10850.8            ; ﾒｲﾝINDEX 起動入力
CON
JMP @MR3002
STG @MR3002
AND DM10650.9            ; ﾒｲﾝINDEX 位置決め完了
SET DM10850.11           ; ﾒｲﾝINDEX ｱﾝｻ
CON
JMP @MR3003
STG @MR3003
ANB DM10650.9            ; ﾒｲﾝINDEX 位置決め完了
RES DM10850.11           ; ﾒｲﾝINDEX ｱﾝｻ
CON
JMP @MR3004
STG @MR3004
AND DM10651.0            ; ﾒｲﾝINDEX 分割位置 ｽﾄﾛｰﾌﾞ
TMH @2 #10
CON
AND @T2
LDA DM10650              ; ﾒｲﾝINDEX ｽﾃｰﾀｽ1
CON
ANDA $00FF
CON
STA EM1202               ; ﾒｲﾝINDEX 現在POS
CON
SET DM10850.11           ; ﾒｲﾝINDEX ｱﾝｻ
CON
JMP @MR3005
STG @MR3005
ANB DM10651.0            ; ﾒｲﾝINDEX 分割位置 ｽﾄﾛｰﾌﾞ
RES DM10850.11           ; ﾒｲﾝINDEX ｱﾝｻ
CON
AND= EM1200 EM1202       ; ﾒｲﾝINDEX 指令POS  ﾒｲﾝINDEX 現在POS
JMP @MR3006
STG @MR3006
JMP @MR3007
STG @MR3007
RES MR1011               ; ﾒｲﾝINDEX 移動中
CON
SET MR1003               ; ﾒｲﾝINDEX 回転完了
CON
ENDS
;ﾒｲﾝINDEX 現在指令POS読込
LD CR2002                ; 常時ON
LDA DM10850              ; ﾒｲﾝINDEX 指令1
CON
ANDA $003F
CON
STA EM1204               ; ﾒｲﾝINDEX 現在指令POS
;ﾒｲﾝINDEX 指令POS出力
LD CR2002                ; 常時ON
MPS
AND EM1200.0             ; ﾒｲﾝINDEX 指令POS0
OUT DM10850.0            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号0
MRD
AND EM1200.1             ; ﾒｲﾝINDEX 指令POS1
OUT DM10850.1            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号1
MRD
AND EM1200.2             ; ﾒｲﾝINDEX 指令POS2
OUT DM10850.2            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号2
MRD
AND EM1200.3             ; ﾒｲﾝINDEX 指令POS3
OUT DM10850.3            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号3
MRD
AND EM1200.4             ; ﾒｲﾝINDEX 指令POS4
OUT DM10850.4            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号4
MPP
AND EM1200.5             ; ﾒｲﾝINDEX 指令POS5
OUT DM10850.5            ; ﾒｲﾝINDEX ﾌﾟﾛｸﾞﾗﾑ番号5
;<h1/>Error Display
;ｴﾗｰ表示
;
;Error07 7st ﾜｰｸ除材要求
;Error07 7st Remove Works Request
STG @MR100               ; ALL st ﾜｰｸ除材要求
ANB R50008               ; Whole ERROR
SET @MR102
CON
SET R50008               ; Whole ERROR
CON
SET R50110               ; INDEX異常
CON
SET R50506               ; 7.7st ﾜｰｸ除材要求
CON
JMP @MR103
MOV #7 DM400             ; ﾒｯｾｰｼﾞNo.
STG @MR103
AND R50008               ; Whole ERROR
JMP @MR104
STG @MR104
ANB R50008               ; Whole ERROR
JMP @MR105
STG @MR105
RES @MR102
CON
ENDS
;<h1/>Error Display
;ｴﾗｰ表示
;
;Error08 8st ﾜｰｸ除材要求
;Error08 8st  Remove Works Request
STG @MR108               ; 8st ﾜｰｸ除材要求
ANB R50008               ; Whole ERROR
SET @MR110
CON
SET R50008               ; Whole ERROR
CON
SET R50110               ; INDEX異常
CON
SET R50507               ; 8.8st ﾜｰｸ除材要求
CON
JMP @MR111
MOV #8 DM400             ; ﾒｯｾｰｼﾞNo.
STG @MR111
AND R50008               ; Whole ERROR
JMP @MR112
STG @MR112
ANB R50008               ; Whole ERROR
JMP @MR113
STG @MR113
RES @MR110
CON
ENDS
END
ENDH
