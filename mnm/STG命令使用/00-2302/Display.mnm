DEVICE:51
;MODULE:Display
;MODULE_TYPE:0
;フリッカー
;Ｆｌｉｃｋｅｒ
LD CR2002                ; 常時ON
FLIK #30 #30 @MR000      ; 0.3s Flicker PLS
LD CR2002                ; 常時ON
FLIK #15 #15 @MR001      ; 0.15s Flicker PLS
LD CR2002                ; 常時ON
FLIK #150 #20 @MR005     ; 0.9s Flicker PLS
LD @MR005                ; 0.9s Flicker PLS
AND R50309               ; ERR Rset under input
OUT @MR006               ; Pause lamp control
;ｽﾀｰﾄﾗﾝﾌﾟ点灯制御
;START Lamp ON Control
LDP R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
LDP R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
;スタートランプ
;Ｓｔａｒｔ ｌａｍｐ
LD CR2002                ; 常時ON
LDA DM3000               ; 現在表示中   画面
CON
CMP #111
CON
AND CR2010               ; 演算結果がｾﾞﾛ
OUT R49204               ; 確認 画面表示中
LDB R49201               ; MAIN 画面表示中
ANB R49204               ; 確認 画面表示中
OUB R49206               ; ﾗﾝﾌﾟ発光許可(PANEL)
LD R50014                ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R50103               ; 警報ｾｯﾄﾌﾗｸﾞ
LDB R50110               ; Err ﾄﾚｲﾁｪﾝｼﾞｬｰ
ANB R50301               ; Pause
LD R50110                ; Err ﾄﾚｲﾁｪﾝｼﾞｬｰ
ANB R50306               ; PAUSE中
ORL
ANL
LD R50103                ; 警報ｾｯﾄﾌﾗｸﾞ
AND R1000                ; ｴｱｰ元圧
ORL
OR R50511                ; ﾊﾟﾈﾙ選択完　ST_SW要求
LD R51004                ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
ANB R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
ANB R50006               ; 異常発生中
ANB R50301               ; Pause
LD R015                  ; ﾃｰﾌﾞﾙ引き込み確認
ORB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
ANL
ORL
LD LR201                 ; 手動ﾓｰﾄﾞ　   選択
AND R51011               ; SETP選択
LD R50400                ; Call Post
ORB R50002               ; 手動中
ANL
AND R49206               ; ﾗﾝﾌﾟ発光許可(PANEL)
ANB R50306               ; PAUSE中
ANB R50108               ; ｴﾗｰ中
ORL
OR R50301                ; Pause
LD R49201                ; MAIN 画面表示中
AND R49812               ; ｴﾘｱｾﾝｻ 　全OK
AND LR200                ; 自動ﾓｰﾄﾞ　   選択
ANB R50001               ; 自動中
AND R49811               ; 安全ｶﾊﾞｰ  全OK
AND MR100                ; ST原点FLG
ORL
AND @MR000               ; 0.3s Flicker PLS
LD LR201                 ; 手動ﾓｰﾄﾞ　   選択
AND R51011               ; SETP選択
AND R50004               ; 動作中
ANB R50400               ; Call Post
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
LDB R51605               ; 作業者ｸﾗﾝﾌﾟ解除待ち中
LD R51605                ; 作業者ｸﾗﾝﾌﾟ解除待ち中
AND @MR005               ; 0.9s Flicker PLS
ORL
ANL
LD R51005                ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
ANB R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
ANB R50006               ; 異常発生中
ANB R50301               ; Pause
ANB R50002               ; 手動中
LDB R51603               ; 固定完了→作業完了待ち
ANB R51605               ; 作業者ｸﾗﾝﾌﾟ解除待ち中
ANB R51604               ; 作業者T引出し待ち中
LD R51603                ; 固定完了→作業完了待ち
OR R51605                ; 作業者ｸﾗﾝﾌﾟ解除待ち中
AND @MR005               ; 0.9s Flicker PLS
ORL
ANL
ORL
LD R51101                ; 原点復帰
ANB R50109               ; Err MAIN
ANB R50108               ; ｴﾗｰ中
ANB R50009               ; RB4動作前 ｴﾘｱｾﾝｻ遮光
ORL
ANB R50301               ; Pause
ORL
LD R50011                ; 自動      動作選択済
OR R50012                ; 手動   動作選択済
ORB R51100               ; 原点復帰完了
OR R50007                ; 手動制御中
OR R50301                ; Pause
ANL
OUT MR501                ; START sw Lamp
;タワーランプ　赤
;Ｔｏｗｅｒ ｌａｍｐ ｒｅｄ
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R49915                ; 表示用　　 TRAP
LD R50006                ; 異常発生中
OR R50103                ; 警報ｾｯﾄﾌﾗｸﾞ
OR R50901                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OR R50902                ; Trapしない   異常
LDB R51100               ; 原点復帰完了
ANB R50004               ; 動作中
ORL
AND @MR000               ; 0.3s Flicker PLS
ORL
OUT MR6315               ; ﾀﾜｰﾗﾝﾌﾟ(R)
;タワーランプ　黄
;Ｔｏｗｅｒ ｌａｍｐ ｙｅｌｌｏｗ
;
LD R50004                ; 動作中
AND R55000               ; 即停止/TRAP 入力無し
LD R53901                ; ｷｬﾘﾔ交換中
ANB R53903               ; ｷｬﾘｱ交換確認
ORL
OUT MR6314               ; ﾀﾜｰﾗﾝﾌﾟ(Y)
;タワーランプ　緑
;Ｔｏｗｅｒ ｌａｍｐ ｇｒｅｅｎ
LDB R50301               ; Pause
OUB R50306               ; PAUSE中
LDB R50004               ; 動作中
ANB R50306               ; PAUSE中
LD R50301                ; Pause
AND @MR000               ; 0.3s Flicker PLS
ORL
AND R55000               ; 即停止/TRAP 入力無し
OUT MR6313               ; ﾀﾜｰﾗﾝﾌﾟ(G)
;位置調整ﾗﾝﾌﾟ
;Position alignment Lamp
LD R51500                ; 位置調整中
LD R51501                ; ねじ締め中
AND @MR000               ; 0.3s Flicker PLS
ORL
OUT MR1100               ; 調整開始ﾗﾝﾌﾟ
;ブザー
;Ｂｕｚｚｅｒ
;
LDB R50000               ; 即停止
ANB R50900               ; TRAP
ANB R50013               ; 異常発生    表示
ANB R50915               ; 原点復帰終了ﾌﾞｻﾞｰON
ANB R51007               ; 固定完了時ﾌﾞｻﾞｰON
ANB R51008               ; 自動終了   ﾌﾞｻﾞｰON
ANB R51009               ; 自動動作中ﾌﾞｻﾞｰON
ANB R51010               ; 手動終了   ﾌﾞｻﾞｰON
INV
OUT MR500                ; ﾌﾞｻﾞｰ
;エリアセンサー表示灯
;Ａｒｅａ ｓｅｎｓｏｒ ｄｉｓｐｌａｙ ｌａｍｐ
LD R51201                ; ｴﾘｱｾﾝｻｰ有効FLG
ANB R50400               ; Call Post
OUT MR6300               ; ｴﾘｱｾﾝｻｰ　出力
;Panel sw
;
LD R50308                ; PACK中
FLIK #50 #50 @MR004      ; Pause lamp Blink
LD R50301                ; Pause
LD @MR004                ; Pause lamp Blink
ORB R50308               ; PACK中
ANL
OUT R50314               ; PAUSEswﾗﾝﾌﾟ
LD R50212                ; START ﾗﾝﾌﾟ点滅FLG(PAUSE release)
AND @MR000               ; 0.3s Flicker PLS
ANB R50308               ; PACK中
ANB R49008               ; ｴﾘｱｾﾝｻ遮光
LD R50309                ; ERR Rset under input
AND CR2003               ; 常時OFF
ORL
OUT R50315               ; STARTswﾗﾝﾌﾟ
LD R50301                ; Pause
OUT R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
LD R50005                ; 停止中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
OUT R49909               ; 停止中     ﾀｯﾁﾊﾟﾈﾙ
LD R50001                ; 自動中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
ANB R49914               ; 引出し待ち  ﾀｯﾁﾊﾟﾈﾙ
ANB R49904               ; 解除待ち  ﾀｯﾁﾊﾟﾈﾙ
ANB R49903               ; ねじ締め  ﾀｯﾁﾊﾟﾈﾙ
ANB R49902               ; 調整      ﾀｯﾁﾊﾟﾈﾙ
OUT R49910               ; 自動中     ﾀｯﾁﾊﾟﾈﾙ
LD R50002                ; 手動中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
OUT R49911               ; 手動中     ﾀｯﾁﾊﾟﾈﾙ
LD R50003                ; 原点復帰中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
OUT R49913               ; 原点復帰中  ﾀｯﾁﾊﾟﾈﾙ
LD R50001                ; 自動中
AND R51604               ; 作業者T引出し待ち中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
OUT R49914               ; 引出し待ち  ﾀｯﾁﾊﾟﾈﾙ
LD R50001                ; 自動中
AND R51605               ; 作業者ｸﾗﾝﾌﾟ解除待ち中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
OUT R49904               ; 解除待ち  ﾀｯﾁﾊﾟﾈﾙ
LD R50001                ; 自動中
AND R51501               ; ねじ締め中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
OUT R49903               ; ねじ締め  ﾀｯﾁﾊﾟﾈﾙ
LD R50001                ; 自動中
AND R51500               ; 位置調整中
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
ANB R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
OUT R49902               ; 調整      ﾀｯﾁﾊﾟﾈﾙ
LDB R50001               ; 自動中
ANB R50002               ; 手動中
ANB R50003               ; 原点復帰中
OUT R49300               ; 設定sw 許可
LD R50009                ; RB4動作前 ｴﾘｱｾﾝｻ遮光
ANB R49912               ; 一時停止中  ﾀｯﾁﾊﾟﾈﾙ
OUT R49905               ; ｴﾘｱｾﾝｻ遮光中 ﾀｯﾁﾊﾟﾈﾙ
END
ENDH
