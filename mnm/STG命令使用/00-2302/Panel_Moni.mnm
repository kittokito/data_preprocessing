DEVICE:51
;MODULE:Panel_Moni
;MODULE_TYPE:0
;SCRIPT_TYPE:
LDP T200                 ; 動作中遅延
LDB R51100               ; 原点復帰完了
ANB R50003               ; 原点復帰中
ANB R50007               ; 手動制御中
LDF R50006               ; 異常発生中
ORF R50901               ; ｼﾘﾝﾀﾞｰｴﾗｰ
ANL
ORL
MOV #100 DM3001          ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R40004                ; 原点復帰完了解除
RES R51100               ; 原点復帰完了
CON
RES MR50007              ; 原点復帰　完了
LDB R50004               ; 動作中
ANB R40006               ; 停止中表示
SET R50005               ; 停止中
LD R50004                ; 動作中
RES R50005               ; 停止中
LD R50000                ; 即停止
OR R50900                ; TRAP
ANB R50004               ; 動作中
OUT R40006               ; 停止中表示
LD R50001                ; 自動中
OUT R40007               ; ﾊﾟﾈﾙ用自動動作中表示
;即停止　で、非常停止画面を表示
;Ｅｍｅｒｇｅｎｃｙ  ｓｃｒｅｅｎ  ｉｓ  ｄｉｓｐｌａｙｅｄ   ａｔ ｔｈｅ  ｔｉｍｅ  ｏｆ  ｃｏｎｔｒｏｌ ｓｔｏｐ.
LD R50000                ; 即停止
MOV #105 DM3001          ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;原点復帰完了で、メイン画面に変わる。
;ＭＡＩＮ ｓｃｒｅｅｎ ｉｓ ｄｉｓｐｌａｙｅｄ ａｆｔｅｒ ｒｅｔｕｒｎ ｈｏｍｅ ｐｏｓｉｔｉｏｎ ｆｉｎｉｓｈ
;自動中は、メイン画面にＳＴＡＴＵＳに、自動中表示
;Ｄｉｓｐｌａｙｓ ｏｆ ＡＵＴＯ，ｄｕｒｉｎｇ ａｕｔｏ ｍｏｄｅ ｏｐｅｒａｔｉｏｎ
;異　常
;Ｅｒｒｏｒ
;
LD R55000                ; 即停止/TRAP 入力無し
LD T200                  ; 動作中遅延
OR R49915                ; 表示用　　 TRAP
ANL
LD R50006                ; 異常発生中
OR R50103                ; 警報ｾｯﾄﾌﾗｸﾞ
OR R50901                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OR R50902                ; Trapしない   異常
OR R49915                ; 表示用　　 TRAP
ANL
MOV #110 DM3001          ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LDP R50003               ; 原点復帰中
LD R50003                ; 原点復帰中
LDF R50006               ; 異常発生中
ORF R49110               ; ｾﾝｻｴﾗｰ
ANL
ORL
@MOV #101 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R55000                ; 即停止/TRAP 入力無し
AND T200                 ; 動作中遅延
ANB R50006               ; 異常発生中
ANB R50103               ; 警報ｾｯﾄﾌﾗｸﾞ
ANB R50901               ; ｼﾘﾝﾀﾞｰｴﾗｰ
ANB R50902               ; Trapしない   異常
ANB R49915               ; 表示用　　 TRAP
ANB R51101               ; 原点復帰
ANB R52101
MPS
LDB R50007               ; 手動制御中
AND R51100               ; 原点復帰完了
ANL
@MOV #102 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
AND R50007               ; 手動制御中
@MOV #180 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R50901                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OUT R50200               ; ｼﾘﾝﾀﾞｰｴﾗｰ  ｳｨﾝﾄﾞｳ表示
LD<> #90 DM500           ; ｴﾗｰ内容表示
AND<> #91 DM500          ; ｴﾗｰ内容表示
AND<> #92 DM500          ; ｴﾗｰ内容表示
AND<> #93 DM500          ; ｴﾗｰ内容表示
AND<> #94 DM500          ; ｴﾗｰ内容表示
OUB R55200               ; Retry/Removeｳｨﾝﾄﾞｳ表示
;エラータイトル表示
;DM510 = DM500/16
;DM511 = DM500-(DM510*16)
;Z5 = DM510
;BSET (DM2500:Z5,DM511)
LD R50006                ; 異常発生中
OR R49110                ; ｾﾝｻｴﾗｰ
NCJ #1000
;DM510 = DM500/16
;
LD CR2002                ; 常時ON
LDA DM500                ; ｴﾗｰ内容表示
CON
EXT
CON
EXT.L
CON
DIV.L +16
CON
STA DM510                ; ｴﾗｰNo.表示用(ﾌﾗｸﾞｱﾄﾞﾚｽﾄ)
;DM511 = DM500-(DM510*16)
;
LD CR2002                ; 常時ON
LDA DM510                ; ｴﾗｰNo.表示用(ﾌﾗｸﾞｱﾄﾞﾚｽﾄ)
CON
EXT
CON
MUL.L +16
CON
STA.L @VM0
CON
LDA DM500                ; ｴﾗｰ内容表示
CON
EXT
CON
SUB.L @VM0
CON
STA DM511                ; ｴﾗｰNo.表示用(ﾌﾗｸﾞﾋﾟｯﾄ)
;Z5 = DM510
;
LD CR2002                ; 常時ON
LDA DM510                ; ｴﾗｰNo.表示用(ﾌﾗｸﾞｱﾄﾞﾚｽﾄ)
CON
EXT
CON
STA.L Z5
;BSET (DM2500:Z5,DM511)
LD CR2002                ; 常時ON
BSET DM2500:Z5 DM511     ; ERR 1-16  ｴﾗｰNo.表示用(ﾌﾗｸﾞﾋﾟｯﾄ)
LABEL #1000
;
LDB R50006               ; 異常発生中
ANB R49110               ; ｾﾝｻｴﾗｰ
ZRES DM2500 DM2531       ; ERR 1-16
;パラメーター選択画面
;Ｐａｒａｍｅｔｅｒ  Ｓｅｌｅｃｔｉｏｎ 
LD R40000                ; SET UP SW
AND R40005               ; SET UP 　　表示許可
@MOV #103 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
LD R50005                ; 停止中
OUT R40005               ; SET UP 　　表示許可
LD R50007                ; 手動制御中
@RES R51100              ; 原点復帰完了
LD R40002                ; SET UP戻SW
MPS
AND R51100               ; 原点復帰完了
@MOV #102 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
MPP
ANB R51100               ; 原点復帰完了
MOV #100 DM3001          ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;STEP動作選択画面
;STEP Operation Selection 
LD LR201                 ; 手動ﾓｰﾄﾞ　   選択
ANF R50101               ; ﾓｰﾄﾞSW
AND R50005               ; 停止中
AND R51100               ; 原点復帰完了
@MOV #124 DM3001         ; VTﾍﾟｰｼﾞ　　変更ｱﾄﾞﾚｽ
;パネル表示言語選択
;Ｐａｎｅｌ  Ｌａｎｇｕｅｇｅ  Ｓｅｌｅｃｔｉｏｎ
;0:Japanese     1:English     2:Indonesian
LDP R50208               ; 日本語選択    SW
DW #0 DM502              ; 選択言語No.
LDP R50209               ; 英語選択    SW
DW #1 DM502              ; 選択言語No.
LDP R50210               ; 中国語選択 SW
DW #2 DM502              ; 選択言語No.
;生産ｶｳﾝﾀﾘｾｯﾄ / product counter reset
LDP R40008               ; 生産ｶｳﾝﾀﾘｾｯﾄ
DW #0 DM470              ; 生産ｶｳﾝﾀ
CON
DW #0 DM471              ; 投入ｶｳﾝﾀ
;エラー
;ＴＲＡＰしないで、異常信号のみ出力
LD CR2003                ; 常時OFF
AND R50503               ; 真空吸着ｴﾗｰ表示中
OR R50606                ; Trapしない   異常
OUT R50902               ; Trapしない   異常
LD R50901                ; ｼﾘﾝﾀﾞｰｴﾗｰ
OR R50902                ; Trapしない   異常
OUT R55100               ; ERROR
END
ENDH
