DEVICE:51
;MODULE:MAIN_ST
;MODULE_TYPE:0
LD R50000                ; 即停止
OR R50900                ; TRAP
OR R50914                ; HOMEsw ON
ZRES @MR000 @MR5015
LDB LR200                ; 自動ﾓｰﾄﾞ　   選択
LDB R51011               ; SETP選択
ANB R52011
ORB LR201                ; 手動ﾓｰﾄﾞ　   選択
ANL
ANB R50003               ; 原点復帰中
ZRES R50500 R50815       ; AUTO動作     終了
;***** PACK FLAG 処理  / PACK FLAG   ******
;
LD @MR2900               ; Pack1　MAIN
OUT R50612               ; Pack (MAIN)
;<h1/>■■■  HOME  ■■■
;
;**************************************************************
;******* 原点復帰  / HOME operation ********
;**************************************************************
;
LD R51101                ; 原点復帰
TMR @10 #5               ; 原点復帰開始ﾃﾞｨﾚｲ
CON
AND @T10                 ; 原点復帰開始ﾃﾞｨﾚｲ
ANB R51102               ; 原点復帰中
DIFU @MR1000             ; 原点復帰 開始
LD @MR1000               ; 原点復帰 開始
RES R51600               ; 自動動作  初回
CON
SET @MR1100              ; 原点復帰
STG @MR1100              ; 原点復帰
AND R51101               ; 原点復帰
SET @MR2600              ; M1 M2 M3
CON
SET R51102               ; 原点復帰中
CON
JMP @MR1101
STG @MR1101
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR505                ; 押し上げｸﾗﾝﾌﾟ上昇
CON
RES MR507                ; 上下ｸﾗﾝﾌﾟ下降
CON
ANB R007                 ; 押し上げｸﾗﾝﾌﾟ上昇限
AND R008                 ; 押し上げｸﾗﾝﾌﾟ下降限
ANB R011                 ; 上下ｸﾗﾝﾌﾟ下降限
AND R012                 ; 上下ｸﾗﾝﾌﾟ上昇限
JMP @MR1102
STG @MR1102
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR503                ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進
CON
RES MR504                ; 前後ｸﾗﾝﾌﾟ前進
CON
ANB R003                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進限
AND R004                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ後退限
ANB R005                 ; 前後ｸﾗﾝﾌﾟ前進限
AND R006                 ; 前後ｸﾗﾝﾌﾟ後退限
JMP @MR1103
STG @MR1103
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR502                ; ﾃｰﾌﾞﾙﾛｯｸ下降
CON
ANB R001                 ; ﾃｰﾌﾞﾙﾛｯｸ下降限
AND R002                 ; ﾃｰﾌﾞﾙﾛｯｸ上昇限
JMP @MR1104
STG @MR1104
ANB R50301               ; Pause
ANB R50111               ; Err 全体
MPS
ANB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
RES MR506                ; ﾛｰﾗ前進
CON
ANB R009                 ; ﾛｰﾗ前進限
AND R010                 ; ﾛｰﾗ後退限
JMP @MR1615              ; 原点復帰終了処理
MPP
AND R015                 ; ﾃｰﾌﾞﾙ引き込み確認
JMP @MR1615              ; 原点復帰終了処理
;******* 原点復帰終了操作  / HOME End operation ********
;
STG @MR1615              ; 原点復帰終了処理
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
SET R51100               ; 原点復帰完了
CON
RES R51102               ; 原点復帰中
CON
SET MR50007              ; 原点復帰　完了
CON
ENDS
LDF R51101               ; 原点復帰
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
RES R51102               ; 原点復帰中
;<h1/>■■■  STEP  ■■■
;********* STEP動作ﾓｰﾄﾞ選択 *********
;********* STEP MODE SELECTION *********
LDB LR000                ; 通常動作  ｽﾃｯﾌﾟ選択
OUB R51011               ; SETP選択
LD R50002                ; 手動中
AND R50012               ; 手動   動作選択済
ANB R50006               ; 異常発生中
AND R51011               ; SETP選択
RES R51600               ; 自動動作  初回
CON
DIFU @MR3000             ; 手動ｽﾀｰﾄPLS
LD @MR3000               ; 手動ｽﾀｰﾄPLS
AND R50012               ; 手動   動作選択済
MPS
LD LR000                 ; 通常動作  ｽﾃｯﾌﾟ選択
ANB R50700               ; 通常動作  ｽﾃｯﾌﾟ実行中
ANL
@SET R50700              ; 通常動作  ｽﾃｯﾌﾟ実行中
CON
@SET @MR3100             ; 通常動作  STEP
MPP
ANB R51011               ; SETP選択
@SET @MR3915             ; STEP 終了へ
;********* 通常動作 STEP *********
;********* Normal Operation STEP *********
STG @MR3100              ; 通常動作  STEP
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET R50700               ; 通常動作  ｽﾃｯﾌﾟ実行中
CON
JMP @MR4203
;///// Work Lock /////
STG @MR3101
ANB R50301               ; Pause
ANB R50111               ; Err 全体
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4303              ; ﾜｰｸ固定解除開始
;///// Work Release /////
STG @MR3102
ANB R50301               ; Pause
ANB R50111               ; Err 全体
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR3915              ; STEP 終了へ
;********* 手動動作異常終了操作  *********
;********* STEP Error End process *********
STG @MR3900              ; STEP異常終了処理へ
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR505                ; 押し上げｸﾗﾝﾌﾟ上昇
CON
RES MR507                ; 上下ｸﾗﾝﾌﾟ下降
CON
ANB R007                 ; 押し上げｸﾗﾝﾌﾟ上昇限
AND R008                 ; 押し上げｸﾗﾝﾌﾟ下降限
ANB R011                 ; 上下ｸﾗﾝﾌﾟ下降限
AND R012                 ; 上下ｸﾗﾝﾌﾟ上昇限
JMP @MR3901
STG @MR3901
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR503                ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進
CON
RES MR504                ; 前後ｸﾗﾝﾌﾟ前進
CON
ANB R003                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進限
AND R004                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ後退限
ANB R005                 ; 前後ｸﾗﾝﾌﾟ前進限
AND R006                 ; 前後ｸﾗﾝﾌﾟ後退限
JMP @MR3902
STG @MR3902
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR502                ; ﾃｰﾌﾞﾙﾛｯｸ下降
CON
ANB R001                 ; ﾃｰﾌﾞﾙﾛｯｸ下降限
AND R002                 ; ﾃｰﾌﾞﾙﾛｯｸ上昇限
JMP @MR3903
STG @MR3903
ANB R50301               ; Pause
ANB R50109               ; Err MAIN
JMP @MR3915              ; STEP 終了へ
;********* 手動動作終了操作  *********
;********* STEP End process *********
STG @MR3915              ; STEP 終了へ
OUT @MR2914              ; 手動終了   ﾌﾞｻﾞｰON
CON
ZRES R50700 R50707       ; 通常動作  ｽﾃｯﾌﾟ実行中
CON
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
MPS
ANB R42503               ; 連続STEP許可
RES LR201                ; 手動ﾓｰﾄﾞ　   選択
CON
ENDS
MPP
AND R42503               ; 連続STEP許可
ENDS
;<h1/>■■■  AUTO  ■■■
;********* AUTO動作ﾓｰﾄﾞ選択 *********
;********* AUTO MODE SELECTION *********
LD R50001                ; 自動中
AND R50011               ; 自動      動作選択済
ANB R51600               ; 自動動作  初回
DIFU @MR4000             ; 自動ｽﾀｰﾄPLS
;***** 定停止 ﾌﾗｸﾞﾘｾｯﾄ / Cycle stop Flag reset *****
;
LDF R49807               ; 全ﾕﾆｯﾄ払出し完了
RES R51600               ; 自動動作  初回
;***** ﾊﾟﾗﾒｰﾀ選択 / parametar sellection *****
;
LD @MR4000               ; 自動ｽﾀｰﾄPLS
AND R50011               ; 自動      動作選択済
MPS
LD LR1000                ; AUTO PARA0 通常動作選択
OR LR1001                ; AUTO PARA1位置調整時押え有効
ANL
SET R51600               ; 自動動作  初回
CON
SET MR50209              ; 動作開始
CON
SET MR50005              ; MC動作中
CON
MPS
AND R1000                ; ｴｱｰ元圧
MPS
AND R015                 ; ﾃｰﾌﾞﾙ引き込み確認
@SET @MR4200             ; ﾜｰｸ固定開始
MPP
ANB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
@SET @MR4100             ; 動作開始 入力待ち
MPP
ANB R1000                ; ｴｱｰ元圧
@SET @MR4103             ; 元圧空低下
MPP
LDB LR1000               ; AUTO PARA0 通常動作選択
ANB LR1001               ; AUTO PARA1位置調整時押え有効
ANL
@SET @MR4001             ; ﾊﾟﾗﾒｰﾀ異常　→　終了
;***** ﾊﾟﾗﾒｰﾀ選択異常の時 / when parametar error *****
STG @MR4001              ; ﾊﾟﾗﾒｰﾀ異常　→　終了
SET R49802               ; MAIN     払出し完了
CON
SET R49805               ; ﾄﾚｲﾁｪﾝ   払出し完了
CON
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
RES R51600               ; 自動動作  初回
CON
SET R50500               ; AUTO動作     終了
CON
JMP @MR4002              ; 4002
STG @MR4002              ; 4002
AND R49807               ; 全ﾕﾆｯﾄ払出し完了
RES R49802               ; MAIN     払出し完了
CON
ENDS
;***** Start 確認 / Start check *****
;
LDB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R50111               ; Err 全体
ANB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
TMH @20 #2               ; 引込確認ﾃﾞｨﾚｲ
CON
AND @T20                 ; 引込確認ﾃﾞｨﾚｲ
OUT @MR109               ; ｽﾀｰﾄ条件
STG @MR4100              ; 動作開始 入力待ち
ANB R50301               ; Pause
ANB R50111               ; Err 全体
MPS
AND @MR109               ; ｽﾀｰﾄ条件
SET R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
ANP R51001               ; ｽﾀｰﾄsw ON
SET MR50209              ; 動作開始
CON
SET MR50005              ; MC動作中
CON
MPS
AND R1000                ; ｴｱｰ元圧
JMP @MR4200              ; ﾜｰｸ固定開始
MPP
ANB R1000                ; ｴｱｰ元圧
JMP @MR4103              ; 元圧空低下
MPP
AND R49800               ; ｻｲｸﾙ停止・払出し開始
JMP @MR5000              ; 動作終了処理
;***** 元圧ｴﾗｰの時 / Air Pressure Down Error  *****
;
STG @MR4103              ; 元圧空低下
SET R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
TMR @21 #2               ; 元圧空低下
CON
AND @T21                 ; 元圧空低下
SET MR202                ; 元圧空異状
CON
SET @MR108               ; 元圧空異常
CON
JMP @MR4104              ; 4104
STG @MR4104              ; 4104
AND @MR108               ; 元圧空異常
AND R50111               ; Err 全体
AND R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
SET R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR4105              ; 4105
STG @MR4105              ; 4105
ANB MR202                ; 元圧空異状
AND R51300               ; ｴﾗｰﾘｾｯﾄsw ON
JMP @MR4106              ; 4106
STG @MR4106              ; 4106
ANB R50111               ; Err 全体
ANB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R51300               ; ｴﾗｰﾘｾｯﾄsw ON
RES @MR108               ; 元圧空異常
CON
MPS
AND R50001               ; 自動中
JMP @MR4100              ; 動作開始 入力待ち
MPP
AND R50002               ; 手動中
JMP @MR3309
;***** ﾜｰｸ固定 / Work Lock *****
;
STG @MR4200              ; ﾜｰｸ固定開始
@SET R51005              ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
SET R51602               ; ﾜｰｸ固定処理 実行中
CON
JMP @MR4203
STG @MR4203
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR506                ; ﾛｰﾗ前進
CON
ANB R009                 ; ﾛｰﾗ前進限
AND R010                 ; ﾛｰﾗ後退限
JMP @MR4204
STG @MR4204
ANB R50301               ; Pause
ANB R50111               ; Err 全体
TMR @23 #2
CON
AND @T23
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4205
STG @MR4205
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET MR504                ; 前後ｸﾗﾝﾌﾟ前進
CON
ANB R006                 ; 前後ｸﾗﾝﾌﾟ後退限
AND R005                 ; 前後ｸﾗﾝﾌﾟ前進限
JMP @MR4206
STG @MR4206
ANB R50301               ; Pause
ANB R50111               ; Err 全体
TMR @24 #2
CON
AND @T24
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4207
STG @MR4207
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET MR503                ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進
CON
ANB R004                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ後退限
AND R003                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進限
JMP @MR4208
STG @MR4208
ANB R50301               ; Pause
ANB R50111               ; Err 全体
TMR @25 #2
CON
AND @T25
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4209
STG @MR4209
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET MR505                ; 押し上げｸﾗﾝﾌﾟ上昇
CON
ANB R008                 ; 押し上げｸﾗﾝﾌﾟ下降限
AND R007                 ; 押し上げｸﾗﾝﾌﾟ上昇限
JMP @MR4210
STG @MR4210
OUT @MR4215              ; 固定完了ﾌﾞｻﾞｰ
CON
MPS
AND R50001               ; 自動中
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
OUT @MR4214              ; ﾛｰﾗ押さえ完了ﾌﾞｻﾞｰ
CON
JMP @MR4211              ; ﾜｰｸ起動    確認へ
MPP
AND R50002               ; 手動中
JMP @MR3101
LDB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R50111               ; Err 全体
AND R015                 ; ﾃｰﾌﾞﾙ引き込み確認
TMH @30 #2
CON
AND @T30
OUT @MR110               ; ﾃｰﾌﾞﾙ引き込み確認
;***** ﾛｰﾗ後退（ﾜｰｸ押さえ込み） / Roller Back *****
;
STG @MR4211              ; ﾜｰｸ起動    確認へ
ANB R50301               ; Pause
ANB R50111               ; Err 全体
AND @MR110               ; ﾃｰﾌﾞﾙ引き込み確認
@SET R51004              ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
ANP R51001               ; ｽﾀｰﾄsw ON
JMP @MR4201
STG @MR4201
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
SET MR502                ; ﾃｰﾌﾞﾙﾛｯｸ下降
CON
ANB R002                 ; ﾃｰﾌﾞﾙﾛｯｸ上昇限
AND R001                 ; ﾃｰﾌﾞﾙﾛｯｸ下降限
JMP @MR4202
STG @MR4202
ANB R50301               ; Pause
ANB R50111               ; Err 全体
TMR @22 #2
CON
AND @T22
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4213
STG @MR4213
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET MR507                ; 上下ｸﾗﾝﾌﾟ下降
CON
ANB R012                 ; 上下ｸﾗﾝﾌﾟ上昇限
AND R011                 ; 上下ｸﾗﾝﾌﾟ下降限
JMP @MR4212
STG @MR4212
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR506                ; ﾛｰﾗ前進
CON
ANB R009                 ; ﾛｰﾗ前進限
AND R010                 ; ﾛｰﾗ後退限
MPS
ANB LR1001               ; AUTO PARA1位置調整時押え有効
JMP @MR4300              ; ﾜｰｸ解除待ち
MPP
AND LR1001               ; AUTO PARA1位置調整時押え有効
SET R51501               ; ねじ締め中
CON
RES R51603               ; 固定完了→作業完了待ち
CON
JMP @MR4302              ; ねじ締め完了待ち
;***** ﾜｰｸ固定解除 / Work Release *****
;
STG @MR4300              ; ﾜｰｸ解除待ち
ANB R50301               ; Pause
ANB R50111               ; Err 全体
SET MR506                ; ﾛｰﾗ前進
CON
ANB R010                 ; ﾛｰﾗ後退限
AND R009                 ; ﾛｰﾗ前進限
SET R51500               ; 位置調整中
CON
@SET R51004              ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR4301
STG @MR4301
ANB R50301               ; Pause
ANB R50111               ; Err 全体
ANP R51001               ; ｽﾀｰﾄsw ON
RES R51500               ; 位置調整中
CON
SET R51501               ; ねじ締め中
CON
RES R51603               ; 固定完了→作業完了待ち
CON
JMP @MR4302              ; ねじ締め完了待ち
STG @MR4302              ; ねじ締め完了待ち
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR506                ; ﾛｰﾗ前進
CON
AND R010                 ; ﾛｰﾗ後退限
ANB R009                 ; ﾛｰﾗ前進限
ANP R51001               ; ｽﾀｰﾄsw ON
RES R51501               ; ねじ締め中
CON
JMP @MR4303              ; ﾜｰｸ固定解除開始
STG @MR4303              ; ﾜｰｸ固定解除開始
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR502                ; ﾃｰﾌﾞﾙﾛｯｸ下降
CON
RES MR507                ; 上下ｸﾗﾝﾌﾟ下降
CON
ANB R001                 ; ﾃｰﾌﾞﾙﾛｯｸ下降限
AND R002                 ; ﾃｰﾌﾞﾙﾛｯｸ上昇限
ANB R011                 ; 上下ｸﾗﾝﾌﾟ下降限
AND R012                 ; 上下ｸﾗﾝﾌﾟ上昇限
JMP @MR4304
STG @MR4304
ANB R50301               ; Pause
ANB R50111               ; Err 全体
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR4305
LDB R50014               ; 異常発生ｴﾗｰﾘｾｯﾄsw ON
ANB R50111               ; Err 全体
ANB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
TMH @29 #2               ; 引込確認ﾃﾞｨﾚｲ
CON
AND @T29                 ; 引込確認ﾃﾞｨﾚｲ
OUT @MR111               ; ﾃｰﾌﾞﾙ引き出し確認
;***** 作業者ﾃｰﾌﾞﾙ引き出し待ち / Waiting for table pull out *****
;
STG @MR4305
ANB R50301               ; Pause
ANB R50111               ; Err 全体
OUT R51604               ; 作業者T引出し待ち中
CON
AND @MR111               ; ﾃｰﾌﾞﾙ引き出し確認
SET R51605               ; 作業者ｸﾗﾝﾌﾟ解除待ち中
CON
SET R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
JMP @MR4306
STG @MR4306
ANB R50301               ; Pause
ANB R50111               ; Err 全体
ANP R51001               ; ｽﾀｰﾄsw ON
RES R51605               ; 作業者ｸﾗﾝﾌﾟ解除待ち中
CON
JMP @MR4307
STG @MR4307
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR505                ; 押し上げｸﾗﾝﾌﾟ上昇
CON
RES MR503                ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進
CON
RES MR504                ; 前後ｸﾗﾝﾌﾟ前進
CON
ANB R007                 ; 押し上げｸﾗﾝﾌﾟ上昇限
AND R008                 ; 押し上げｸﾗﾝﾌﾟ下降限
ANB R003                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ前進限
AND R004                 ; ｻｲﾄﾞｸﾗﾝﾌﾟ後退限
ANB R005                 ; 前後ｸﾗﾝﾌﾟ前進限
AND R006                 ; 前後ｸﾗﾝﾌﾟ後退限
JMP @MR4308
STG @MR4308
ANB R50301               ; Pause
ANB R50111               ; Err 全体
OUT R50400               ; Call Post
CON
AND R50402               ; Post Ok
JMP @MR4309
STG @MR4309
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR502                ; ﾃｰﾌﾞﾙﾛｯｸ下降
CON
ANB R001                 ; ﾃｰﾌﾞﾙﾛｯｸ下降限
AND R002                 ; ﾃｰﾌﾞﾙﾛｯｸ上昇限
MPS
ANB R50002               ; 手動中
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
OUT @MR4315              ; 解除完了ﾌﾞｻﾞｰ処理
CON
JMP @MR4400              ; 作業者取出待ち
MPP
AND R50002               ; 手動中
JMP @MR4400              ; 作業者取出待ち
STG @MR4400              ; 作業者取出待ち
MPS
LD R50001                ; 自動中
ANB R015                 ; ﾃｰﾌﾞﾙ引き込み確認
ANL
JMP @MR4401
MPP
AND R50002               ; 手動中
TMR @1 #5
CON
AND @T1
JMP @MR4401
STG @MR4401
TMR #0 DM0               ; ﾛｰﾗ後退ﾃﾞｨﾚｲ  TIM00設定値
CON
AND T0                   ; ﾛｰﾗ後退ﾃﾞｨﾚｲ
JMP @MR4402
STG @MR4402
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES MR506                ; ﾛｰﾗ前進
CON
ANB R009                 ; ﾛｰﾗ前進限
AND R010                 ; ﾛｰﾗ後退限
JMP @MR4403
STG @MR4403
ANB R50301               ; Pause
ANB R50111               ; Err 全体
MPS
AND R50001               ; 自動中
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
JMP @MR4600              ; ｻｲｸﾙ終了処理
MPP
AND R50002               ; 手動中
JMP @MR3915              ; STEP 終了へ
;ｻｲｸﾙ終了処理 / cycle end process
STG @MR4600              ; ｻｲｸﾙ終了処理
ANB R50301               ; Pause
ANB R50111               ; Err 全体
MPS
AND R50001               ; 自動中
INC DM470                ; 生産ｶｳﾝﾀ
CON
JMP @MR4601              ; 4501
MPP
AND R50002               ; 手動中
JMP @MR3315
STG @MR4601              ; 4501
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES R51602               ; ﾜｰｸ固定処理 実行中
CON
RES MR50209              ; 動作開始
CON
RES MR50005              ; MC動作中
CON
MPS
ANB R49800               ; ｻｲｸﾙ停止・払出し開始
JMP @MR4100              ; 動作開始 入力待ち
MPP
AND R49800               ; ｻｲｸﾙ停止・払出し開始
JMP @MR5000              ; 動作終了処理
;***** 処理終了処理  /  Squeegee process end routine *****
;
STG @MR5000              ; 動作終了処理
ANB R50301               ; Pause
ANB R50111               ; Err 全体
RES R51602               ; ﾜｰｸ固定処理 実行中
CON
SET R49802               ; MAIN     払出し完了
CON
JMP @MR5001
STG @MR5001
ANB R50301               ; Pause
ANB R50111               ; Err 全体
AND R49807               ; 全ﾕﾆｯﾄ払出し完了
RES R49802               ; MAIN     払出し完了
CON
RES R51005               ; ｽﾀｰﾄﾗﾝﾌﾟ点灯
CON
RES R51004               ; ｽﾀｰﾄﾗﾝﾌﾟ点滅
CON
@SET R49808              ; ｻｲｸﾙ停止 処理FLG.
CON
ENDS
;<h1/>■■■  SUBROUTINE  ■■■
;******* ﾜｰｸ固定完了時ﾌﾞｻﾞｰ制御  / Buzzer Control when Work Hold complete ********
;
LDP @MR4215              ; 固定完了ﾌﾞｻﾞｰ
ORP @MR4214              ; ﾛｰﾗ押さえ完了ﾌﾞｻﾞｰ
ANB R50003               ; 原点復帰中
OR R51007                ; 固定完了時ﾌﾞｻﾞｰON
ANB T104                 ; 固定完了ﾌﾞｻﾞｰON
OUT R51007               ; 固定完了時ﾌﾞｻﾞｰON
TMH #104 #5              ; 固定完了ﾌﾞｻﾞｰON
;******* ﾜｰｸ解除完了時ﾌﾞｻﾞｰ制御  / Buzzer Control when Work release complete ********
;
LDP @MR4315              ; 解除完了ﾌﾞｻﾞｰ処理
ORP @MR4515              ; 解除完了ﾌﾞｻﾞｰ処理2
ANB R50003               ; 原点復帰中
OR R51009                ; 自動動作中ﾌﾞｻﾞｰON
ANB T101                 ; 解除完了   ﾌﾞｻﾞｰON
OUT R51009               ; 自動動作中ﾌﾞｻﾞｰON
TMR #101 #4              ; 解除完了   ﾌﾞｻﾞｰON
;******* STEP終了時ﾌﾞｻﾞｰ制御  / Buzzer Control when STEP End ********
;
LDP @MR2914              ; 手動終了   ﾌﾞｻﾞｰON
ANB R50003               ; 原点復帰中
OR R51010                ; 手動終了   ﾌﾞｻﾞｰON
ANB T102                 ; 手動終了   ﾌﾞｻﾞｰON
OUT R51010               ; 手動終了   ﾌﾞｻﾞｰON
TMR #102 #4              ; 手動終了   ﾌﾞｻﾞｰON
END
ENDH
