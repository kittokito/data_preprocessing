DEVICE:53
;MODULE:Conveyer
;MODULE_TYPE:0
;非常停止 / Emergency stop
LD R50000                ; 即停止
OR R50415                ; TRAP
OR R50010                ; 非常停止
ZRES @MR002 @MR3515
;条件ﾌﾗｸﾞ / Flug
LDB R4308                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降限
AND R4309                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 上昇限
ANB R4310                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇限
AND R4311                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 下降限
ANB R4312                ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 閉限
AND R4313                ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 開限
ANB R4314                ; 供給部位置決め 下降限
AND R4315                ; 供給部位置決め 上昇限
ANB R4400                ; 供給部ﾊﾞｯｸｱｯﾌﾟ 上昇限
AND R4401                ; 供給部ﾊﾞｯｸｱｯﾌﾟ 下降限
ANB R4402                ; 供給部1個前ｽﾄｯﾊﾟ 下降限
AND R4403                ; 供給部1個前ｽﾄｯﾊﾟ 上昇限
ANB R4404                ; 供給部2個前ｽﾄｯﾊﾟ 上昇限
AND R4405                ; 供給部2個前ｽﾄｯﾊﾟ 下降限
OUT @MR005               ; ｺﾝﾍﾞｱ部ｼﾘﾝﾀﾞ原位置
LDB R50300               ; PAUSE中
ANB R50108               ; ｴﾗｰ発生中
ANB R51001               ; 全体･ｼｽﾃﾑ部ｴﾗｰ中
ANB R51002               ; ｺﾝﾍﾞﾔ部ｴﾗｰ中
OUT @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
;確認ｾﾝｻﾃﾞｨﾚｲ / Senser Delay
LD R4102                 ; 給材位置決め部ﾊﾟﾚｯﾄ検出
TMH @1 #50               ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
LDB R4102                ; 給材位置決め部ﾊﾟﾚｯﾄ検出
TMR @2 #2                ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ OFF
LD R4101                 ; ｺﾝﾍﾞﾔ1個前ﾊﾟﾚｯﾄ検出
TMR @3 #2                ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
LDB R4101                ; ｺﾝﾍﾞﾔ1個前ﾊﾟﾚｯﾄ検出
TMR @4 #2                ; 供給_1個前ﾜｰｸ確認ｾﾝｻ OFF
LD R4100                 ; ｺﾝﾍﾞﾔ2個前ﾊﾟﾚｯﾄ検出
TMR @5 #5                ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
LDB R4100                ; ｺﾝﾍﾞﾔ2個前ﾊﾟﾚｯﾄ検出
TMR @6 #5                ; 供給_2個前ﾜｰｸ確認ｾﾝｻ OFF
LD R4105                 ; ｴﾚﾍﾞｰﾀ1個前ﾊﾟﾚｯﾄ検出
TMR @7 #2                ; ｴﾚﾍﾞｰﾀ1個前 ﾜｰｸ確認ｾﾝｻ
LDB R4105                ; ｴﾚﾍﾞｰﾀ1個前ﾊﾟﾚｯﾄ検出
TMR @8 #20               ; ｴﾚﾍﾞｰﾀ1個前 ﾜｰｸ確認ｾﾝｻ OFF
LD R4104                 ; ｴﾚﾍﾞｰﾀ2個前ﾊﾟﾚｯﾄ検出
TMR @9 #2                ; ｴﾚﾍﾞｰﾀ2個前 ﾜｰｸ確認ｾﾝｻ
LDB R4104                ; ｴﾚﾍﾞｰﾀ2個前ﾊﾟﾚｯﾄ検出
TMR @10 #2               ; ｴﾚﾍﾞｰﾀ2個前 ﾜｰｸ確認ｾﾝｻ OFF
LD R4103                 ; ｴﾚﾍﾞｰﾀ前ﾊﾟﾚｯﾄﾌﾙ検出　ON=無し
TMR @15 #2               ; ｴﾚﾍﾞｰﾀｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
LD R4106                 ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
TMR @20 #20              ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ
LDB R4106                ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
TMR @21 #2               ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ OFF
LDB R4107                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
TMR @22 #20              ; ｴﾚﾍﾞｰﾀ満杯未検出
LDB R4107                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
TMR @23 #2               ; ｴﾚﾍﾞｰﾀ満杯確認ｾﾝｻ OFF
;<h1/>■■■　AUTO動作 / AUTO OPERATION
LD R50011                ; AUTO MODE
ANB MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
AND MR11001              ; ｺﾝﾍﾞｱstart
RES @MR3505              ; ｺﾝﾍﾞﾔｻｲｸﾙ計測？
CON
RES MR11001              ; ｺﾝﾍﾞｱstart
CON
MPS
ANB LR41008              ; 自動ﾊﾟﾗﾒｰﾀ9  PASS
RES MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
RES MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
SET MR3111               ; 供給側ｺﾝﾍﾞｱ ON
CON
SET MR3112               ; 戻し側ｺﾝﾍﾞｱ ON
CON
SET MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET @MR2400              ; ｴﾚﾍﾞｰﾀ処理開始
CON
SET @MR2000              ; AUTO START
MPP
AND LR41008              ; 自動ﾊﾟﾗﾒｰﾀ9  PASS
SET MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET @MR2600              ; PASS処理
;<h1/>/// 供給部
;PASSﾓｰﾄﾞ
STG @MR2600              ; PASS処理
SET MR6202               ; 供給部1個前ｽﾄｯﾊﾟ 下降
CON
SET MR6114               ; 供給部位置決め 下降
CON
SET MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4102                ; 給材位置決め部ﾊﾟﾚｯﾄ検出
AND R4104                ; ｴﾚﾍﾞｰﾀ2個前ﾊﾟﾚｯﾄ検出
AND R4108                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
JMP @MR2601
STG @MR2601
MPS
ANB R51000               ; 停止要求
JMP @MR2602
MPP
AND R51000               ; 停止要求
JMP @MR2603
STG @MR2602
TMR @0 #3                ; Wait Timer-00
CON
AND @T0                  ; Wait Timer-00
JMP @MR2600              ; PASS処理
STG @MR2603
RES MR6114               ; 供給部位置決め 下降
CON
RES MR6112               ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 閉
CON
RES MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4103                ; ｴﾚﾍﾞｰﾀ前ﾊﾟﾚｯﾄﾌﾙ検出　ON=無し
AND R4105                ; ｴﾚﾍﾞｰﾀ1個前ﾊﾟﾚｯﾄ検出
AND R4109                ; OK_A級品 空ﾄﾚｲ満杯検出
RES MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
ENDS
;通常ﾓｰﾄﾞ
STG @MR2000              ; AUTO START
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB R50013               ; 払い出し動作
MPS
ANB R51000               ; 停止要求
MPS
AND @T1                  ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
JMP @MR2100              ; 取P取出動作
MPP
LDB R4102                ; 給材位置決め部ﾊﾟﾚｯﾄ検出
AND @T3                  ; 供給_1個前ﾜｰｸ確認ｾﾝｻ
ANL
MPS
AND @T5                  ; 供給_2個前ﾜｰｸ確認ｾﾝｻ
JMP @MR2200              ; 取出位置へﾊﾟﾚｯﾄ送り動作
MPP
AND @T6                  ; 供給_2個前ﾜｰｸ確認ｾﾝｻ OFF
SET @MR2001              ; 2個前通過監視中
CON
JMP @MR2201              ; 2個前上げないﾊﾟﾚｯﾄ送り
MPP
AND R51000               ; 停止要求
SET MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
JMP @MR3500              ; ｻｲｸﾙ終了処理
;取り出し動作
STG @MR2100              ; 取P取出動作
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6202               ; 供給部1個前ｽﾄｯﾊﾟ 下降
CON
RES MR6114               ; 供給部位置決め 下降
CON
AND R4403                ; 供給部1個前ｽﾄｯﾊﾟ 上昇限
AND R4315                ; 供給部位置決め 上昇限
JMP @MR2101
STG @MR2101
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6200               ; 供給部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4400                ; 供給部ﾊﾞｯｸｱｯﾌﾟ 上昇限
TMH @26 #10
CON
AND @T26
JMP @MR2102
STG @MR2102
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6112               ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 閉
CON
AND R4312                ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 閉限
SET R52000               ; 給材ｺﾝﾍﾞｱ 取出し要求
CON
JMP @MR2103
STG @MR2103
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND R52001               ; 取出し完了
JMP @MR2104
STG @MR2104
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R52001               ; 取出し完了
CON
ANB R52000               ; 給材ｺﾝﾍﾞｱ 取出し要求
RES MR6112               ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 閉
CON
AND R4313                ; 供給部ﾜｰｸｸﾗﾝﾌﾟ 開限
JMP @MR2105
STG @MR2105
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6200               ; 供給部ﾊﾞｯｸｱｯﾌﾟ 上昇
CON
AND R4401                ; 供給部ﾊﾞｯｸｱｯﾌﾟ 下降限
JMP @MR2106
STG @MR2106
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND @T15                 ; ｴﾚﾍﾞｰﾀｵｰﾊﾞｰﾌﾛｰｾﾝｻ ﾜｰｸ無し
JMP @MR2107
MPP
ANB R4103                ; ｴﾚﾍﾞｰﾀ前ﾊﾟﾚｯﾄﾌﾙ検出　ON=無し
TMR @13 #20
CON
AND @T13
OUT MR4209               ; ｵｰﾊﾞｰﾌﾛｰ検出中
STG @MR2107
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6114               ; 供給部位置決め 下降
CON
AND R4314                ; 供給部位置決め 下降限
JMP @MR2108
STG @MR2108
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
ANB R4102                ; 給材位置決め部ﾊﾟﾚｯﾄ検出
MPS
AND R4103                ; ｴﾚﾍﾞｰﾀ前ﾊﾟﾚｯﾄﾌﾙ検出　ON=無し
TMR #952 #50             ; Wait Timer 952
CON
AND T952                 ; Wait Timer 952
JMP @MR2109
MPP
ANB R4103                ; ｴﾚﾍﾞｰﾀ前ﾊﾟﾚｯﾄﾌﾙ検出　ON=無し
JMP @MR2109
STG @MR2109
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6114               ; 供給部位置決め 下降
CON
AND R4315                ; 供給部位置決め 上昇限
JMP @MR2000              ; AUTO START
;取り出し位置へﾊﾟﾚｯﾄ1個装填
STG @MR2200              ; 取出位置へﾊﾟﾚｯﾄ送り動作
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6204               ; 供給部2個前ｽﾄｯﾊﾟ 上昇
CON
AND R4404                ; 供給部2個前ｽﾄｯﾊﾟ 上昇限
TMH @27 #20
CON
AND @T27
JMP @MR2201              ; 2個前上げないﾊﾟﾚｯﾄ送り
LD @MR2001               ; 2個前通過監視中
AND R4100                ; ｺﾝﾍﾞﾔ2個前ﾊﾟﾚｯﾄ検出
SET MR6204               ; 供給部2個前ｽﾄｯﾊﾟ 上昇
CON
RES @MR2001              ; 2個前通過監視中
STG @MR2201              ; 2個前上げないﾊﾟﾚｯﾄ送り
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6202               ; 供給部1個前ｽﾄｯﾊﾟ 下降
CON
AND R4402                ; 供給部1個前ｽﾄｯﾊﾟ 下降限
JMP @MR2202
STG @MR2202
MPS
AND @T1                  ; 給材側位置決め ﾜｰｸ確認ｾﾝｻ
JMP @MR2300
MPP
ANB R4102                ; 給材位置決め部ﾊﾟﾚｯﾄ検出
TMR #950 #100            ; ﾜｰｸ到着時間監視
CON
AND T950                 ; ﾜｰｸ到着時間監視
RES @MR2001              ; 2個前通過監視中
CON
SET @MR208               ; ERR15ﾜｰｸ到着異常
CON
JMP @MR2203
;☆ﾜｰｸ到達異常処理
STG @MR2203
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR215               ; ERR15解除
RES @MR215               ; ERR15解除
CON
JMP @MR2204
STG @MR2204
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6204               ; 供給部2個前ｽﾄｯﾊﾟ 上昇
CON
RES MR6202               ; 供給部1個前ｽﾄｯﾊﾟ 下降
CON
AND R4405                ; 供給部2個前ｽﾄｯﾊﾟ 下降限
AND R4403                ; 供給部1個前ｽﾄｯﾊﾟ 上昇限
JMP @MR2000              ; AUTO START
;☆☆
STG @MR2300
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES @MR2001              ; 2個前通過監視中
CON
RES MR6202               ; 供給部1個前ｽﾄｯﾊﾟ 下降
CON
AND R4403                ; 供給部1個前ｽﾄｯﾊﾟ 上昇限
JMP @MR2301
STG @MR2301
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6204               ; 供給部2個前ｽﾄｯﾊﾟ 上昇
CON
AND R4405                ; 供給部2個前ｽﾄｯﾊﾟ 下降限
JMP @MR2000              ; AUTO START
;<h1/>/// ｴﾚﾍﾞｰﾀ部処理
;初期化
STG @MR2400              ; ｴﾚﾍﾞｰﾀ処理開始
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
RES MR6106               ; ｴﾚﾍﾞｰﾀ 下降
CON
SET MR6107               ; ｴﾚﾍﾞｰﾀ 上昇
CON
RES MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
RES MR6110               ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇
CON
AND R4307                ; ｴﾚﾍﾞｰﾀ 上昇限
AND R4309                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 上昇限
AND R4311                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 下降限
JMP @MR2401
STG @MR2401
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
TMR @30 #10
CON
AND @T30
RES MR3114               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ 回転方向逆向き
CON
JMP @MR2402
STG @MR2402
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
TMR @31 #5               ; 吸着破壊時間
CON
AND @T31                 ; 吸着破壊時間
SET MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
JMP @MR2403
STG @MR2403
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
TMR @32 #10              ; ﾊﾟﾚｯﾄ状態安定化wait
CON
AND @T32                 ; ﾊﾟﾚｯﾄ状態安定化wait
JMP @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
;動作判定
STG @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
ANB MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
MPS
LD R4107                 ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
AND R4108                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
ANL
JMP @MR2700              ; ﾊﾟﾚｯﾄﾘﾀｰﾝ処理
MPP
AND @T22                 ; ｴﾚﾍﾞｰﾀ満杯未検出
MPS
LDB @T7                  ; ｴﾚﾍﾞｰﾀ1個前 ﾜｰｸ確認ｾﾝｻ
AND R4108                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
ANL
TMR @28 #200
CON
AND @T28
JMP @MR2700              ; ﾊﾟﾚｯﾄﾘﾀｰﾝ処理
MPP
AND @T7                  ; ｴﾚﾍﾞｰﾀ1個前 ﾜｰｸ確認ｾﾝｻ
JMP @MR3000              ; ｴﾚﾍﾞｰﾀへﾊﾟﾚｯﾄ1個装填
MPP
LD R51000                ; 停止要求
AND MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
ANL
SET @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
CON
ENDS
;■　ﾊﾟﾚｯﾄﾘﾀｰﾝ動作
STG @MR2700              ; ﾊﾟﾚｯﾄﾘﾀｰﾝ処理
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4309                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 上昇限
MPS
AND R4106                ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
SET @MR2703              ; EV逆回転準備
CON
JMP @MR2708              ; EV下降
MPP
ANB R4106                ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
TMR #954 #50
CON
AND T954
SET @MR300               ; ERR16ｴﾚﾍﾞｰﾀ部ﾊﾟﾚｯﾄ異常
CON
JMP @MR2702
;Err16処理
STG @MR2702
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR307               ; ERR16解除
RES @MR307               ; ERR16解除
CON
JMP @MR2700              ; ﾊﾟﾚｯﾄﾘﾀｰﾝ処理
;ｴﾚﾍﾞｰﾀｺﾝﾍﾞﾔ停止→逆回転準備
STG @MR2703              ; EV逆回転準備
RES MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
TMR @33 #10
CON
AND @T33
JMP @MR2704
STG @MR2704
SET MR3114               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ 回転方向逆向き
CON
TMR @34 #5
CON
AND @T34
SET @MR2707              ; EV逆回転準備完了
CON
ENDS
;EV下降　→　ﾊﾟﾚｯﾄ排出
STG @MR2708              ; EV下降
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6107               ; ｴﾚﾍﾞｰﾀ 上昇
CON
SET MR6106               ; ｴﾚﾍﾞｰﾀ 下降
CON
AND R4306                ; ｴﾚﾍﾞｰﾀ 下降限
JMP @MR2709
STG @MR2709
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR2707              ; EV逆回転準備完了
SET MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
RES @MR2707              ; EV逆回転準備完了
CON
JMP @MR2710
STG @MR2710
MPS
AND @T21                 ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ OFF
JMP @MR2711
MPP
ANB @T21                 ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ OFF
TMR #955 #50
CON
AND T955
JMP @MR2712
STG @MR2711
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND @T20                 ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ
JMP @MR2800              ; CV3停止 → EV上昇
MPP
ANB @T20                 ; ｴﾚﾍﾞｰﾀ前通過確認ｾﾝｻ
TMR #956 #50
CON
AND T956
JMP @MR2712
;☆　ｴﾚﾍﾞｰﾀ部ﾊﾟﾚｯﾄ異常処理
STG @MR2712
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
SET @MR300               ; ERR16ｴﾚﾍﾞｰﾀ部ﾊﾟﾚｯﾄ異常
CON
JMP @MR2713
STG @MR2713
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR307               ; ERR16解除
RES @MR307               ; ERR16解除
CON
JMP @MR2714
STG @MR2714
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
LD R4106                 ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
ANB R4107                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
ANB R4108                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
ANL
JMP @MR2800              ; CV3停止 → EV上昇
MRD
LDB R4106                ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
ANB R4107                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
ANB R4108                ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
ANL
SET MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
JMP @MR2711
MPP
LD R4106                 ; ｴﾚﾍﾞｰﾀﾊﾟﾚｯﾄ通過検出　ON=無
LD R4107                 ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ4枚目検出
OR R4108                 ; ｴﾚﾍﾞｰﾀ上ﾊﾟﾚｯﾄ1枚目検出
ANL
ANL
SET MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
JMP @MR2710
;☆
STG @MR2800              ; CV3停止 → EV上昇
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET @MR2801              ; CV3正回転準備
CON
JMP @MR2808
;ｴﾚﾍﾞｰﾀｺﾝﾍﾞﾔ停止→正回転準備
STG @MR2801              ; CV3正回転準備
RES MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
TMR @35 #10
CON
AND @T35
JMP @MR2802
STG @MR2802
RES MR3114               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ 回転方向逆向き
CON
TMR @36 #5
CON
AND @T36
SET @MR2807              ; EV正回転準備完了
CON
ENDS
;EV上昇
STG @MR2808
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6106               ; ｴﾚﾍﾞｰﾀ 下降
CON
SET MR6107               ; ｴﾚﾍﾞｰﾀ 上昇
CON
AND R4307                ; ｴﾚﾍﾞｰﾀ 上昇限
JMP @MR2809
STG @MR2809
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR2807              ; EV正回転準備完了
SET MR3113               ; ｴﾚﾍﾞｰﾀｺﾝﾍﾞｱ ON
CON
RES @MR2807              ; EV正回転準備完了
CON
JMP @MR2810
STG @MR2810
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
LD @MR2900
AND @MR2901
ANL
RES @MR2901
CON
JMP @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
MPP
ANB @MR2900
JMP @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
;■　ｴﾚﾍﾞｰﾀへﾊﾟﾚｯﾄ1個装填動作
STG @MR3000              ; ｴﾚﾍﾞｰﾀへﾊﾟﾚｯﾄ1個装填
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
MPS
AND R4115                ; 取P残検出
MPS
AND @T9                  ; ｴﾚﾍﾞｰﾀ2個前 ﾜｰｸ確認ｾﾝｻ
JMP @MR3002
MPP
AND @T10                 ; ｴﾚﾍﾞｰﾀ2個前 ﾜｰｸ確認ｾﾝｻ OFF
SET @MR015               ; ｴﾚﾍﾞｰﾀ2個前監視中
CON
JMP @MR3003
MPP
ANB R4115                ; 取P残検出
SET @MR200               ; ERR14取P残異常
CON
JMP @MR3001
STG @MR3001
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR207               ; ERR14解除
RES @MR207               ; ERR14解除
CON
JMP @MR3000              ; ｴﾚﾍﾞｰﾀへﾊﾟﾚｯﾄ1個装填
LD @MR015                ; ｴﾚﾍﾞｰﾀ2個前監視中
AND R4104                ; ｴﾚﾍﾞｰﾀ2個前ﾊﾟﾚｯﾄ検出
SET MR6110               ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇
CON
RES @MR015               ; ｴﾚﾍﾞｰﾀ2個前監視中
STG @MR3002
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6110               ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇
CON
AND R4310                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇限
JMP @MR3003
STG @MR3003
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
SET MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4308                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降限
JMP @MR3004
STG @MR3004
MPS
AND @T8                  ; ｴﾚﾍﾞｰﾀ1個前 ﾜｰｸ確認ｾﾝｻ OFF
JMP @MR3008
MPP
AND R4105                ; ｴﾚﾍﾞｰﾀ1個前ﾊﾟﾚｯﾄ検出
TMR #957 #100
CON
AND T957
RES @MR015               ; ｴﾚﾍﾞｰﾀ2個前監視中
CON
SET @MR300               ; ERR16ｴﾚﾍﾞｰﾀ部ﾊﾟﾚｯﾄ異常
CON
JMP @MR3005
;☆ﾜｰｸ到達異常処理
STG @MR3005
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR307               ; ERR16解除
RES @MR307               ; ERR16解除
CON
JMP @MR3006
STG @MR3006
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6110               ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇
CON
RES MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4311                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 下降限
AND R4309                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 上昇限
JMP @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
;☆☆
STG @MR3008
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR6108               ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 下降
CON
AND R4309                ; ｴﾚﾍﾞｰﾀ1個前ｽﾄｯﾊﾟ 上昇限
JMP @MR3009
STG @MR3009
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES @MR015               ; ｴﾚﾍﾞｰﾀ2個前監視中
CON
RES MR6110               ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 上昇
CON
AND R4311                ; ｴﾚﾍﾞｰﾀ2個前ｽﾄｯﾊﾟ 下降限
JMP @MR2408              ; ｴﾚﾍﾞｰﾀ動作Main
;<h1/>/// ｺﾝﾍﾞﾔｻｲｸﾙ終了処理
STG @MR3500              ; ｻｲｸﾙ終了処理
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
AND @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
AND MR11404              ; 給材PPｻｲｸﾙ終了
JMP @MR3501
STG @MR3501
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES R52100               ; 除材PP部AUTO動作完了
CON
RES @MR2415              ; ｺﾝﾍﾞｱ除材部動作完了
CON
JMP @MR3502
STG @MR3502
AND @MR010               ; ｺﾝﾍﾞｱ 一時停止ｴﾗｰ無し
RES MR11407              ; ｺﾝﾍﾞｱ給材ｻｲｸﾙ終了
CON
RES R52010               ; ｺﾝﾍﾞｱ受け取り可
CON
RES MR11201              ; ｺﾝﾍﾞｱｻｲｸﾙ中
CON
SET MR11400              ; ｺﾝﾍﾞｱｻｲｸﾙ終了
CON
ENDS
;<h1/>/// ｱﾗｰﾑ処理
;ERR14
STG @MR200               ; ERR14取P残異常
ANB R50008               ; 全体ｴﾗｰ発生
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R51002               ; ｺﾝﾍﾞﾔ部ｴﾗｰ中
CON
SET R50514               ; ERR14
CON
JMP @MR201
MOV #14 DM400            ; 表示画面No.
STG @MR201
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR202
STG @MR202
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR207               ; ERR14解除
CON
ENDS
;ERR15
STG @MR208               ; ERR15ﾜｰｸ到着異常
ANB R50008               ; 全体ｴﾗｰ発生
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R51002               ; ｺﾝﾍﾞﾔ部ｴﾗｰ中
CON
SET R50515               ; ERR15
CON
JMP @MR209
MOV #15 DM400            ; 表示画面No.
STG @MR209
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR210
STG @MR210
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR215               ; ERR15解除
CON
ENDS
;ERR16
STG @MR300               ; ERR16ｴﾚﾍﾞｰﾀ部ﾊﾟﾚｯﾄ異常
ANB R50008               ; 全体ｴﾗｰ発生
SET R50008               ; 全体ｴﾗｰ発生
CON
SET R51002               ; ｺﾝﾍﾞﾔ部ｴﾗｰ中
CON
SET R50600               ; ERR16
CON
JMP @MR301
MOV #16 DM400            ; 表示画面No.
STG @MR301
AND R50008               ; 全体ｴﾗｰ発生
JMP @MR302
STG @MR302
ANB R50008               ; 全体ｴﾗｰ発生
SET @MR307               ; ERR16解除
CON
ENDS
END
ENDH
